apply plugin: 'com.android.application'

def doSkipNdk = project.hasProperty('SKIP_NDK') && SKIP_NDK.equals('true')
def isNdkArguments = project.hasProperty('NDK_ARGUMENTS') && !NDK_ARGUMENTS.isEmpty()
def ignoreAssets = project.hasProperty('IGNORE_ASSETS') && !IGNORE_ASSETS.isEmpty()
def linkGooglePlay = project.hasProperty('LINK_GOOGLE_PLAY_LIB') && LINK_GOOGLE_PLAY_LIB.equals('true')
def linkLocalytics = project.hasProperty('LINK_LOCALYTICS') && LINK_LOCALYTICS.equals('true')
def doIncludeSigning =  project.hasProperty('key.store') && project.hasProperty('key.store.password') &&
                        project.hasProperty('key.alias') && project.hasProperty('key.alias.password')

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId PACKAGE_ID
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION

        if (!doSkipNdk) {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }

            if (isNdkArguments) {
                externalNativeBuild {
                    ndkBuild {
                        arguments NDK_ARGUMENTS
                    }
                }
            }
        } else {
            sourceSets.main.jni.srcDirs = []
        }
    }

    if (!linkGooglePlay) {
        sourceSets.main.java.exclude '**/dk/area9/flowrunner/FlowGooglePlayServices.java'
        sourceSets.main.java.exclude '**/dk/area9/flowrunner/FlowGooglePlayServicesLocationListener.java'
        sourceSets.main.java.exclude '**/dk/area9/flowrunner/FlowFirebaseMessagingService.java'
        sourceSets.main.java.exclude '**/dk/area9/flowrunner/FlowFirebaseInstanceIDService.java'
    } else {
        sourceSets.main.java.exclude '**/dk/area9/flowrunner/FlowGooglePlayServicesStub.java'
    }

    if (doIncludeSigning) {
        signingConfigs {
            sign {
                def props = project.getProperties()

                storeFile file("../" + props['key.store'])
                storePassword props['key.store.password']
                keyAlias props['key.alias']
                keyPassword props['key.alias.password']
            }
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            if (ignoreAssets) {
                aaptOptions {
                    ignoreAssetsPattern IGNORE_ASSETS
                }
            }

            if (doIncludeSigning) {
                signingConfig signingConfigs.sign
            }
        }

        debug {
            if (doIncludeSigning) {
                signingConfig signingConfigs.sign
            }
        }
    }

    if (!doSkipNdk) {
        externalNativeBuild {
            ndkBuild {
                path 'src/main/jni/Android.mk'
            }
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'io.getstream:stream-webrtc-android:1.0.4'
    implementation('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }
    implementation 'org.java-websocket:Java-WebSocket:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.annotation:annotation:1.7.0"
    if (linkLocalytics) {
        implementation files('libs/localytics.jar')
        implementation 'com.android.support:support-compat:28.0.0'
    }
    if (linkGooglePlay) {
        implementation 'com.google.firebase:firebase-core:18.0.3'
        implementation 'com.google.firebase:firebase-messaging:21.1.0'
        implementation 'com.google.android.gms:play-services-location:17.0.0'
        implementation 'com.google.android.gms:play-services-gcm:17.0.0'
        implementation 'com.android.support:multidex:1.0.3'
        implementation 'com.android.support:multidex-instrumentation:1.0.3'
    }
}

if (linkGooglePlay) {
    apply plugin: 'com.google.gms.google-services'
}
