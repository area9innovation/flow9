import material/material2tropic;

main() {
	list = make(["Emil, Hans", "Mustermann, Max", "Tisch, Roman"]);
	filterPrefix = make("");

	filteredList = fselect2(list, filterPrefix, FLift2(\l, p -> {
		filter(l, \n -> startsWith(toLowerCase(n), toLowerCase(p)))
	}));

	listSelection = make([]);
	hasSelection = fneq(listSelection, []);
	getSelected = \ -> elementAt(getValue(listSelection), 0, -1);

	name = make("");
	surname = make("");
	mrender(makeMaterialManager([]), true, MLines([
		MTextInput(filterPrefix, [MLabel("Filter prefix")], []),
		MCols([
			MDynamicList(
				fmap(filteredList, \n -> MSingleLine(n, [])),
				[],
				[MListSelection(const(true), false, true, listSelection)]
			),
			MConstruct([
					// When we select an item, update the names
					makeSubscribe(listSelection, \sel -> {
						if (sel != []) {
							element = getValue(list)[sel[0]];
							parts = strSplit(element, ", ");
							next(surname, elementAt(parts, 0, ""));
							next(name, elementAt(parts, 1, ""));
						}
					})
				],
				MLines([
					MTextInput(name, [MLabel("Name")], []),
					MTextInput(surname, [MLabel("Surname")], []),
				])
			)
		]),
		MCols([
			MTextButton("CREATE", \ -> {
				element = getValue(surname) + ", " + getValue(name);
				next(list, arrayPush(getValue(list), element));
			}, [], []),
			MTextButton("UPDATE", \ -> {
				element = getValue(surname) + ", " + getValue(name);
				next(list, replace(getValue(list), getSelected(), element));
			}, [], [
				MEnabled(hasSelection)
			]),
			MTextButton("DELETE", \ -> {
				next(list, removeIndex(getValue(list), getSelected()));
			}, [], [
				MEnabled(hasSelection)
			])
		])
	]));
}
