import material/material2tropic;

main() {
	elapsedTime = make(0.0);
	duration = make(25.0);

	timer(100, \ -> timerTick(elapsedTime, duration));

	mrender(makeMaterialManager([]), true, MLines([
		MBaselineCols([
			MText("Elapsed time: ", []),
			drawProgress(fselect2(elapsedTime, duration, FLift2(\e, d -> if (d != 0.0 && d >= e) e / d else 1.0)))
		]),
		MSelect(elapsedTime, \e -> MText(d2st(e, 2) + "s", [])),
		MBaselineCols([
			MText("Duration", []),
			MSlider(duration, [MSliderRange(0.0, 50.0)]),
		]),
		MTextButton("RESET", \ -> {
			next(elapsedTime, 0.0)
		}, [], [])
	]));
}

drawProgress(t : Transform<double>) -> Material {
	MScale(fselect(t, FLift(\tt -> Factor(tt, 1.0))),
		TRectangle([MFill(MBlue(500))], TFixed(256.0, 16.0))
	)
}

timerTick(time : DynamicBehaviour<double>, duration : DynamicBehaviour<double>) -> void {
	nt = getValue(time) + 0.1;
	next(time, min(getValue(duration), nt));
	timer(100, \ -> timerTick(time, duration))
}
