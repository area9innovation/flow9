import fabric/fabric;

main() -> void {
	ui = BLetd("duration", 25.0,
		BLetd("elapsedTime", 0.0,
			BLines([
				BTimer("elapsedTime", 0.1),
				BBCols([
					BText("Elapsed time: ", []), 
					BSelectdd("elapsedTime", "duration", \env, elapsedTime, duration -> {
						progress = if (duration != 0.0 && duration >= elapsedTime) elapsedTime / duration else 1.0;
						BGroup([
							BMaterial(TRectangle([Fill(0xc0c0c0)], TFixed(256.0, 16.0))),
							BMaterial(TRectangle([Fill(0x0000c0)], TFixed(256.0 * progress, 16.0)))
						])
					})
				]),
				BSelectdd("elapsedTime", "duration", \env, elap, dur -> BText(d2s(min(elap, dur)) + "s", [])),
				BBCols([BText("Duration", []), BSlider("duration", [MSliderRange(0.0, 50.0)])]),
				BTextButton("RESET", \env -> {
					nextFabricValue(env, "elapsedTime", 0.0);
				}, [], [])
			])
		)
	);
	brender(ui);
	{}
}