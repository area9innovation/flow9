import demos/helloworld;
import demos/hellouser;
import demos/tictactoe;
import demos/matchthree;
import demos/schedule;
import demos/calculator;
import demos/subscribe_size;
import demos/dynamic_paging;


main() {
	mManager = makeMaterialManager([]);

	// Main window of the application
	demoAppB : DynamicBehaviour<Material> = make(TEmpty());

	demoMakeMainView(mManager, demoAppB);

	mrender(mManager, true, MMutable(demoAppB))
}

demoMakeMainView(mManager : MaterialManager, demoAppB : DynamicBehaviour<Material>) -> void {
	makeView = \mat -> {
		next(demoAppB, mat);
	};

	logo = MFrame(30.0, 0.0, [], MZoom(
			MAvailable(
				MLines([
					MCols2(MFillX(), MText(_("Material"), [MCustomFont(30.0, "RobotoMedium", 0.2), MBlue(200)])),
					MCols2(MFillX(), MText(_("demo"), [MCustomFont(15.0, "Roboto", 0.4), MBlue(200)])),
				]),
				MFixed(100.0, 500.0)
			),
			MGroup2(MFillX(), MFillY()),
			true
		));
	
	mainView = ref MEmpty();
	displayMainView = \ -> {
		makeView(^mainView);
	}

	menu = MCenterY(MLines([
		MTextButton(_("Hello, world!"), \ -> makeView(demoMakeHelloWorld(displayMainView)), [], []),
		MTextButton(_("Hello, user!"), \ -> makeView(demoMakeHelloUser(mManager, displayMainView)), [], []),
		MTextButton(_("Tic tac toe"), \ -> makeView(demoMakeTicTacToe(displayMainView)), [], []),
		MTextButton(_("Match 3"), \ -> makeView(demoMakeMatchThree(mManager, displayMainView)), [], []),
		MTextButton(_("Schedule demo"), \ -> makeView(demoMakeSchedule(mManager, displayMainView)), [], []),
		MTextButton(_("Calculator demo"), \ -> makeView(demoMakeCalculator(mManager, displayMainView)), [], []),
		MTextButton(_("Subscribe size"), \ -> makeView(demoMakeSubscribeSize(displayMainView)), [], []),
		MTextButton(_("Dynamic paging"), \ -> makeView(demoMakeDynamicPaging(displayMainView)), [], []),
	]));

	space = MFixedX(30.0);
	vl = MCenterY(MFrame(0.0, 0.0, [FillOpacity(0.5), Fill(0x0088FF)], MFixed(2.0, 300.0)));
	view = MCols([logo, space, vl, space, menu, MFillX()]);

	mainView := view;
	
	makeView(view);
}
