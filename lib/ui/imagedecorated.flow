import text/serialize;
import formats/html/html;
import formats/html/html2form;
import form/paragraph;
import form/paragraphalignments;
import ui/imagedecoratedstructs;
import form/form;
import ui/activityindicator;

import material/material2tropic;
import material/material_gui;
/*
 * The code below is full of callbacks and other tricks that makes it hard readable.
 * The reason is requirements of editors. Initially all the decorations were solid and scaled as whole.
 * Now it is required to copyright / caption / footer do not be downscaled with the image.
 * Also we have to avoid double zoom if ImageDecorated is put into ColumnPicture of SlideTemplate.
 * Also WigiImage has zoom option scale to fit with some hardcoded availables for places where available height is not given -
 * we have to pass height added by external decorations toi their zoom calculator.
 * !!! Use regression testing tool !!!
*/
export {

	verticalAlign2d(va : VerticalAlignment) -> double;

	makeImageDecorated2(
		filename : string,
		width : double,
		height : double,
		imageOnlyZoomFn : Maybe<((Form) -> Form) -> (Form) -> Pair<Form, (Form) -> Form>>,
		trustedSizes : bool,
		decorations : [ImageDecoration],
		makeParagraphElements : (string, [CharacterStyle]) -> [ParagraphElement],
		preScaleCrop : bool,
		noScalingDecorations : bool,
		onLoaded : () -> void,
		onWarning : () -> void,
		hideLoadingSpinnerB : Behaviour<bool>,
		requestHeaders : [KeyValue]
	) -> Form;

	makeImagePreZoomDecorationsForms(decorations : [PreZoomDecoration], width : double, height : double) -> [Form];
	makeImagePreZoomDecorationsForm(decorations : [PreZoomDecoration], width : double, height : double) -> Form;

	splitImageDecorations(
		noScaleDecorations : bool,
		decorations : [ImageDecoration]
	) -> ImageDecoration4;


	ImageDecoration4(
		preZoom : [PreZoomDecoration],
		postZoom : [PostZoomDecoration],
		sizeIndependent : [SizeIndependentDecoration],
		crop : Maybe<ImageCrop>
	);

	url2ImageDecorated(url : string) -> ImageDecorated;

	// makeZoomAdded and ImageDecoration are strictly coupled. Decorations should be added to
	// full size popup also. So, result is given a function to add decorations to popup (Form) -> Form and
	// returns final (Form) -> Form which is given undecorated full size image and returns downscaled image
	// without decorations.
	// [-TODO-] simplify
	// #41879 Now Pair<zoomed form, transform to be applied after crop is returned>
	makeZoomAdder(zoom : ZoomDescription) -> ((Form) -> Form) -> (Form) -> Form;

	makeZoomAdder3(
		zoom : ZoomDescription,
		virtualScreenInfoM : Maybe<VirtualScreenInfo>
	) -> ((Form) -> Form) -> (Form) -> Pair<Form, (Form) -> Form>;

	// fn is given the image to generate small preview.
	// only .mousezoom from ZoomDescription is checked.
	makeZoomAdder2(
		zoom : ZoomDescription,
		fn : (Form) -> Form,
		virtualScreenInfoM : Maybe<VirtualScreenInfo>,
		backcolor : Maybe<Color>
	) -> ((Form) -> Form) -> (Form) -> Pair<Form, (Form) -> Form>;

	makeDimmerShape(
		type : ImageDimmerType,
		leftTopPoint : Point,
		rightBotPoint : Point
	) -> [StaticGraphicOp];

	calculateAdaptiveCropValues(
		crop : ImageAdaptiveCrop,
		formSize : WidthHeight,
		givenSize : WidthHeight,
		loading : bool,
		availableWH : WidthHeight,
		squareness : double
	) -> Quadruple<double, double, double, double>;
}

verticalAlign2d(va : VerticalAlignment) -> double {
	switch(va) {
		TopAlign(): 0.0;
		CenterAlign(): 0.5;
		BottomAlign(): 1.0;
	}
}

calculateAdaptiveCropValues(crop : ImageAdaptiveCrop, formSize : WidthHeight, givenSize : WidthHeight, loading : bool, availableWH : WidthHeight, squareness : double) -> Quadruple<double, double, double, double> {
	if (loading || formSize.width <= availableWH.width && formSize.height <= availableWH.height) {
		Quadruple(0.0, 0.0, formSize.width, formSize.height);
	} else {
		awhRatio = if (availableWH.height > 0.) {
			availableWH.width / availableWH.height;
		} else {
			1.;
		};

		k = (awhRatio - 1.0) * (1.0 - squareness);

		spaceWH = if (availableWH.width <= 0.0) {
			givenSize
		} else {
			WidthHeight(availableWH.height + availableWH.height * k, availableWH.height);
		};

		scale = if (spaceWH != zeroWH) {
			max3(crop.width / spaceWH.width, crop.height / spaceWH.height, 1.0)
		} else {
			1.;
		};

		newCropW = min(max(spaceWH.width * scale, crop.width), formSize.width);
		newCropH = min(max(spaceWH.height * scale, crop.height), formSize.height);

		newCropX = min(max(0.0, crop.x + crop.width / 2.0 - newCropW / 2.0), formSize.width - newCropW);
		newCropY = min(max(0.0, crop.y + crop.height / 2.0 - newCropH / 2.0), formSize.height - newCropH);

		Quadruple(newCropX, newCropY, newCropW, newCropH);
	}
}

// Width and height are serialized on OK click in the image decorations editor. So, we have information about picture size until it is loaded.
// But we also  have some legacy sources with width = height = 0. The reason is unclear. Probably it was a bug in the editor.
makeImageDecorated2(
	filename : string,
	_width : double,
	_height : double,
	imageOnlyZoomFn : Maybe<((Form) -> Form) -> (Form) -> Pair<Form, (Form) -> Form>>,
	trustedSizes : bool,
	decorations : [ImageDecoration],
	makeParagraphElements : (string, [CharacterStyle]) -> [ParagraphElement],
	preScaleCrop : bool,
	noScalingDecorations : bool,
	onLoaded : () -> void,
	onWarning : () -> void,
	hideLoadingSpinnerB : Behaviour<bool>,
	requestHeaders : [KeyValue]
) -> Form {

	givenSize = WidthHeight(_width, _height);
	savedSize = either(getCachedPictureSize(filename), givenSize);

	pictureIsLoading = make(true);
	pictureWarning = make(false);

	pictureWithWarning = makeImageWithWarning(filename, pictureIsLoading, pictureWarning, hideLoadingSpinnerB, requestHeaders);

	wholeImageWH : DynamicBehaviour<WidthHeight> = makeWH();
	savedOrRealSize = makeWH();

	// Learnsmart SMEs replaces image files and do not update decorations.	We can trust only to aspect ratio.

	replacedImageScale = make(1.0);

	splittedDecorations = splitImageDecorations(noScalingDecorations, decorations);
	preZoomDecorations = splittedDecorations.preZoom;
	postZoomDecorations = splittedDecorations.postZoom;
	sizeIndependentDecorations = splittedDecorations.sizeIndependent;
	cropMB = splittedDecorations.crop;
	postZoomCrop = if (preScaleCrop) None() else cropMB;

	pictureWithZoomableDecorations = Group([
		Inspect([ISize(wholeImageWH)], pictureWithWarning.picture),
		Scale(replacedImageScale, replacedImageScale, Select(wholeImageWH, \wh -> makeImagePreZoomDecorationsForm(preZoomDecorations, wh.width, wh.height)))
	]);

	addDecorations = \scaleConnector, zoom -> \image -> { //is applied both to full size image and downscaled one
		// #39271 zoom is added to control copyright size in different contexts.
		// zoom : bool might be replaced with style : [DecorationStyle] if more is needed
		scaledWH = makeWH();
		scaledFixed = make(0.0);
		addPostZoomDecorations = makePostZoomDecorationsAdder(scaledFixed, postZoomDecorations, postZoomCrop, noScalingDecorations, _width, _height);
		addSizeIndependentDecorations = makeSizeIndependentDecorationsAdder(
			sizeIndependentDecorations,
			makeParagraphElements,
			savedSize,
			zoom
		);

		Constructor(
			Inspect([ISize(scaledWH)], image) |> addPostZoomDecorations |> addSizeIndependentDecorations,
			\ -> scaleConnector(scaledWH, scaledFixed)
		);
	}

	noScaleDecorationsFixUp = make(1.0);

	fullSizeScaleConnector = \from, to -> {
		connectSelect2Distinctu(noScaleDecorationsFixUp, replacedImageScale, to, \fixUp, ris -> {
			if (noScalingDecorations && fixUp != 0.0) {
				// in case of PictureScaleBeforeDecorations we need to upscale PostZoom decorations
				ris / fixUp;
			} else {
				ris;
			}
		});
	}

	zoomedImageScaleConnector = \from, to -> {
		connectSelect3Distinctu(from, savedOrRealSize, replacedImageScale, to, \swh, rwh, ris -> {
			v = if (0.5 < rwh.width) ris * swh.width / rwh.width else 0.0; // We expect zoom to preserve aspect ratio
			if (noScalingDecorations) {
				nextDistinct(noScaleDecorationsFixUp, v);
				ris;
			} else {
				v;
			}
		})
	}

	//Takes a function to add decorations to full size image and returns (Form) -> Pair<Form, (Form) -> Form). It takes full size image and returns downscaled one and then second of the pair is applied.
	zoommer : ((Form) -> Form) -> (Form) -> Pair<Form, (Form) -> Form> = either(imageOnlyZoomFn, \fn -> \form -> Pair(form, idfn));

	useSavedSize = \f -> {
		Size2(savedOrRealSize, f) |> NonZeroSize
	}

	defPaddingFn = idfn;
	addPadding = extract(sizeIndependentDecorations, defPaddingFn, \d -> switch (d : SizeIndependentDecoration) {
		ImagePadding(l, t, r, b) : {
			fn = \f -> Border(l, t, r, b, f);
			Some(fn)
		}
		default : {
			None()
		};
	});

	applyCrop = eitherMap(cropMB,
		\c -> \f -> {
			sc = getValue(replacedImageScale);
			cropX_ = make(c.x * sc);
			cropY_ = const(c.y * sc);
			cropW_ = const(c.width * sc);
			cropH_ = const(c.height * sc);
			Crop(cropX_, cropY_, cropW_, cropH_, f)
		},
		idfn
	);

	defaultImageAdaptiveCrop = makeDefaultAdaptiveCrop();

	adaptiveCropping = \form : Form -> {
		if (containsStruct(decorations, defaultImageAdaptiveCrop)) {
			crop = extractStruct(decorations, defaultImageAdaptiveCrop);
			tweaks = extractStruct(decorations, ImageAdaptiveCropTweaks([]));
			squareness = extractStruct(tweaks.style, Squareness(0.0)).value;

			initValues = calculateAdaptiveCropValues(crop, getValue(savedOrRealSize), givenSize, getValue(pictureIsLoading), getValue(crop.availableWHB), squareness);

			cropX = make(initValues.first);
			cropY = make(initValues.second);
			cropW = make(initValues.third);
			cropH = make(initValues.fourth);

			Constructor(
				Crop(cropX, cropY, cropW, cropH, form),
					\ -> {
						select3u(pictureIsLoading, crop.availableWHB, savedOrRealSize, \loading, awh, formSize -> {
							curValues = calculateAdaptiveCropValues(crop, formSize, givenSize, loading, awh, squareness);
							nextDistinct(cropX, curValues.first);
							nextDistinct(cropY, curValues.second);
							nextDistinct(cropW, curValues.third);
							nextDistinct(cropH, curValues.fourth);
						}).second;
				}
			)
		} else {
			form
		};
	};

	thePicture =
		pictureWithZoomableDecorations
		|> useSavedSize
		|> adaptiveCropping
		|> (\f -> if (preScaleCrop) applyCrop(f) else f)
		|> zoommer(addDecorations(fullSizeScaleConnector, true))
		|> unpairC(\f, t -> f |> addDecorations(zoomedImageScaleConnector, false) |> t)
		|> addPadding;

	decoratedPicSize = makeWH();
	Constructor(
		Group([
			if (isUrlParameterTrue("media_form"))
				Visibleb(
					pictureIsLoading,
					Available2(
						decoratedPicSize,
						Center(pictureWithWarning.warning)
					)
				)
			else
				Select(pictureIsLoading, \l ->
					if (l) Available2(
						decoratedPicSize,
						Center(pictureWithWarning.warning)
					) else Empty()
				),
			Inspect([ISize(decoratedPicSize)], thePicture)
		]),
		\ -> {
			uns0 = if (trustedSizes || givenSize.width < 0.5)
				nop
			else
				//#37892 saved size is replaced with given
				connectSelectDistinctu(savedOrRealSize, replacedImageScale, \as -> as.width / givenSize.width);

			uns1 = connectSelect2Distinctu(pictureIsLoading, wholeImageWH, savedOrRealSize, \l, wh -> {(if (!l && wh.width > 0.0) wh else savedSize)});
			uns2 = selectDistinctu(pictureIsLoading, \loading -> if (!loading) onLoaded()).second;
			uns3 = selectDistinctu(pictureWarning, \warning -> if (warning) onWarning()).second;

			\ -> {
				uns0();
				uns1();
				uns2();
			}
		}
	);
}

makeImageRectangle(iRectangle : ImageRectangle, _width : double, _height : double) -> Form {
	getRectangle = \style -> {
		Graphics(rectangle(iRectangle.x, iRectangle.y, iRectangle.width, iRectangle.height), style)
	}
	getInvertedRectangle = \ -> {
		backStyle = [extractStruct(iRectangle.style, Fill(0x000000)), extractStruct(iRectangle.style, FillOpacity(0.0))];
		borderStyle = [extractStruct(iRectangle.style, Stroke(0x000000)), extractStruct(iRectangle.style, StrokeOpacity(0.0))];
		fullPath = concat(
			rectangle(iRectangle.x, iRectangle.y, iRectangle.width, iRectangle.height),
			[
				LineTo(iRectangle.x, 0.0),
				LineTo(0.0, 0.0),
				LineTo(0.0, _height),
				LineTo(_width, _height),
				LineTo(_width, 0.0),
				LineTo(iRectangle.x, 0.0)
			]
		);
		Group([
			Graphics(fullPath, backStyle),
			getRectangle(borderStyle)
		])
	}
	if (iRectangle.inverted)
		getInvertedRectangle()
	else
		getRectangle(iRectangle.style)
}

makeImageTextBox(ib : ImageTextBox) -> Form {
	recW = ib.rectangle.width;
	recH = ib.rectangle.height;
	hAlignN = switch(ib.hAlign) {
		LeftAlign(): 0.0;
		CenterAlign(): 0.5;
		RightAlign(): 1.0;
		StartAlign(): b2d(getDefaultRtl());
		EndAlign(): b2d(!getDefaultRtl());
	};
	widthManager = make(100000.0) |> \w -> ParagraphWidthManager(w, w, false);
	scaleX = make(1.0);
	Group([
		makeImageRectangle(ib.rectangle, 0.0, 0.0),  // Only first parameter effective in the case.
		Translate(
			const(ib.rectangle.x),
			const(ib.rectangle.y),
			Available(
				const(recW), const(recH),
				Align2(hAlignN, verticalAlign2d(ib.vAlign), Constructor(
					Scale(
						scaleX, scaleX,
						Border(8.0, 8.0, 8.0, 8.0, renderParagraph(
							[TextFragment(ib.label.text, ib.label.style)],
							[ib.hAlign, ParagraphSingleLine()],
							getDefaultRtl(), widthManager
						)
					)),
					\ -> {
						[
							connectSelectu(
								widthManager.width, scaleX,
								\w -> if (w < 1.0) 0.0 else min((recW - 16.0/*borders*/) / w, 1.0)
							)
						] |> \uns -> \-> applyall(uns)
					}
				))
			)
		),
	]);
}


makeImageTextColumn(textCol : ImageTextColumn) -> Form {
	heightB = make(0.);
	topLeft = Point(textCol.x, textCol.y);
	fontSize = extractStruct(textCol.label.style, FontSize(36.)).size;
	textColor = extractStruct(textCol.label.style, Fill(0xFFFFFF)).color;
	isItalicFont = isItalic(textCol.label.style);
	isBoldFont = isBold(textCol.label.style);
	backColor = extractStruct(textCol.style, Fill(0));
	backOpacity = extractStruct(textCol.style, FillOpacity(0.5));

	material2nativeForm(
		makeMaterialManager([]),
		MTranslate(
			const(
				topLeft
			),
			MGroup([
				TRectangle(
					[backColor, backOpacity],
					TSizedHeight(textCol.width, heightB)
				),
				MAttachHeight(
					MAvailableWidth(
						MBorder4(8.,
							MParagraph(textCol.label.text, [
								MCustomColor(textColor),
								MCustomFont(
									fontSize,
									switchFontFaceStyle(switchFontFaceStyle("Roboto", SItalic(), Some(isItalicFont)), SBold(), Some(isBoldFont)),
									0.87
								),
								textCol.hAlign,
								ParagraphMargin(0., 0.)
							])
						),
						const(textCol.width)
					),
					heightB
				)
			])
		)
	)
}

makeImageCircle(iCircle : ImageCircle, _width : double, _height : double) -> Form {
	getCircle = \style -> Circle(iCircle.x, iCircle.y, iCircle.radius, style);
	getInvertedCircle = \ -> {
		backStyle = [extractStruct(iCircle.style, Fill(0x000000)), extractStruct(iCircle.style, FillOpacity(0.0))];
		borderStyle = [extractStruct(iCircle.style, Stroke(0x000000)), extractStruct(iCircle.style, StrokeOpacity(0.0))];
		circlePath = SectorPath(iCircle.x, iCircle.y, iCircle.radius, 0.0, 360.0, false, false, false);
		squareAroundPath = [
			LineTo(iCircle.x, 0.0),
			LineTo(0.0, 0.0),
			LineTo(0.0, _height),
			LineTo(_width, _height),
			LineTo(_width, 0.0),
			LineTo(iCircle.x, 0.0)
		];
		Group([
			Graphics(concat(circlePath, squareAroundPath), backStyle),
			getCircle(borderStyle)
		])
	}
	if (iCircle.inverted)
		getInvertedCircle()
	else
		getCircle(iCircle.style)

}

makeImagePreZoomDecorationsForms(decorations : [PreZoomDecoration], _width : double, _height : double) -> [Form] {
	fold(decorations, [], \prevD, d -> {
		concat(prevD,
			switch (d: PreZoomDecoration) {
				ImageLabel(x, y, label): {
					// We use a border to force the size to adjust accordingly
					// We cut two pixel border of TextField, so we add them here to match view in Annotate
					switch (label : Form) {
						Text(text, style) : Offset(x, y, Border(2.0, 2.0, 0.0, 0.0, Text(text, style))) |> v2a;
						default : {
							reportImageLabelError(d);
							[]
						}

					}
				}
				ImageArrow(x, y, rotate, len, color, thickness, label): {
					// (x, y) is a point to where the arrow points
					angleRad = rotate * PI / 180.0;

					arrow = FixSize(6.0, 9.0, Graphics(makeArrowGraphics(len, thickness), [Stroke(color), StrokeWidth(max(thickness, 1.0))]));
					[
						Offset(x, y, Rotate(const(-rotate - 90.0), arrow)),
						Offset(x + len * cos(angleRad), y - len * sin(angleRad), label)
					]
				}
				ImageCircle(x, y, radius, style, inverted): {
					[ makeImageCircle(d, _width, _height) ]
				}
				ImageRectangle(x, y, width, height, style, inverted) : {
					[ makeImageRectangle(d, _width, _height) ]
				}
				ImageTextBox(label, hAlign, vAlign, rect) : {
					[ makeImageTextBox(d) ];
				}
				ImageTextColumn(__, __, __, __, __, __, __) : {
					[ makeImageTextColumn(d) ]
				}
				ImageDimmer(type, lt, rb, dimmerColor, borderColor, dimmerOpacity, borderOpacity) : {
					shape = makeDimmerShape(type, lt, rb);
					boxAround = [LineTo(0.0, 0.0), LineTo(0.0, _height), LineTo(_width, _height), LineTo(_width, 0.0), LineTo(0.0, 0.0)];
					[
						Group([
							Graphics(concat(shape, boxAround), [Fill(dimmerColor), FillOpacity(dimmerOpacity)]),
							Graphics(shape, [Stroke(borderColor), StrokeOpacity(borderOpacity)])
						])
					]
				}
			}
		)
	})
}

makeImagePreZoomDecorationsForm(decorations : [PreZoomDecoration], _width : double, _height : double) -> Form {
	if (decorations != []) {
		makeImagePreZoomDecorationsForms(decorations, _width, _height) |> Group |> \f -> Size2(zeroSize, f)
	} else {
		Empty()
	}
}


PictureWithWarning(picture : Form, warning : Form);
makeImageWithWarning(filename:string, pictureIsLoading : DynamicBehaviour<bool>, pictureWarning : DynamicBehaviour<bool>, hideLoadingSpinnerB : Behaviour<bool>, requestHeaders : [KeyValue]) -> PictureWithWarning {
	filenameUrl = unescapeHtml(filename);
	warning = make(Empty());
	setWarning = \t, isBrokenImage -> {
		if (isBrokenImage) println(t);
		nextDistinct(pictureWarning, isBrokenImage);
		next(
			warning,
			Tooltip(
				Iffe(
					isBrokenImage,
					\ -> Scale(make(1.5), make(1.5), Picture("images/broken_image.png", [])),
					\ -> {
						Select(hideLoadingSpinnerB, \hide -> {
							if (hide) Empty()
							else SpinnerActivityIndicator(50.0, 0xAAAAAA)
						})
					}
				),
				t,
				[]
			)
		)
	};
	setWarning(_("Loading..."), false);

	PictureWithWarning(
		Picture(
			filenameUrl,
			concat(
				[
					OnLoadingError(\e -> { setWarning(_("Couldn't load image"), true) } ),
					OnLoaded(\ -> nextDistinct(pictureIsLoading, false))
				],
				if (requestHeaders == []) [] else [RequestHeaders(requestHeaders)]
			)
		),
		Mutable(warning)
	)
}

url2ImageDecorated(url : string) -> ImageDecorated {
	ImageDecorated(url, "", 0.0, 0.0, [])
}

makeZoomAdder(zoom) {
	z = makeZoomAdder3(zoom, None());
	\fn -> {
		fn2 = z(fn);
		\f -> {
			f |> fn2 |> unpairC(\form, transform -> transform(form))
		}
	}
}

makeZoomAdder3(zoom, virtualScreenInfoM : Maybe<VirtualScreenInfo>) {
	fn = eitherMap(
		zoom.percent,
		\percent -> \form -> Scale(const(percent), const(percent), form),
		{
			width = either(zoom.toWidth, 0.0);
			height = either(zoom.toHeight, 0.0);
			resizeFunctions = [
				[
					idfn, //noZoom
					\form -> resizeToHeight(height, form) //only height is specified
				],
				[
					\form -> resizeToWidth(width, form), //only width is specified
					\form -> resizeTo(width, height, form)  //width and heigth are specified
				]
			];
			resizeFunctions[b2i(isSome(zoom.toWidth))][b2i(isSome(zoom.toHeight))];
		}
	);

	makeZoomAdder2(zoom, fn, virtualScreenInfoM, None())
}

makeZoomAdder2(
	zoom: ZoomDescription,
	fn: (Form) -> Form,
	virtualScreenInfoM : Maybe<VirtualScreenInfo>,
	backcolor : Maybe<Color>
) -> ((Form) -> Form) -> (Form) -> Pair<Form, (Form) -> Form>
{
	if (zoom.mouseZoom) {
		\makeExtDecForm -> \form -> {
			decoratedForm = makeExtDecForm(form);// second Behaviour<WidthHeight> with height added by decorations outside the form borders. Ignored here
			sizeB = make(zoom.realSize);
			embedded = Inspect([ISize(sizeB)], fn(form));
			validSize = zoom.realSize.width > 0.0 && zoom.realSize.height > 0.0;

			magnifier = eitherFn(zoom.zoomButtonM,
				\zoomBtn -> getHtmlPictureMagnifyCustom(
					decoratedForm,
					embedded,
					zoom.toWidth,
					zoom.percent,
					backcolor,
					\onClickFn -> zoomBtn.formFn(onClickFn, None()),
					zoomBtn.align,
					zoom.zoomCustomUIFnM,
					zoom.closeButtonFn,
					zoom.closeButtonAlign,
					virtualScreenInfoM
				),
				\ -> getHtmlPictureMagnifyWithCloseButton(
					decoratedForm,
					embedded,
					"",
					TreeEmpty(),
					zoom.toWidth,
					zoom.percent,
					zoom.zoomCustomUIFnM,
					zoom.extraZoom,
					backcolor,
					zoom.closeButtonAlign,
					zoom.closeButtonFn,
					virtualScreenInfoM
				)
			);

			zoomFn = ref idfn;
			Pair(
				Select(sizeB, \sz -> {
					if (validSize && sz.width > 0.0 && sz.height > 0.0 && sz.width / zoom.realSize.width > 0.9 && sz.height / zoom.realSize.height > 0.9) {
						zoomFn := idfn;
						embedded;
					} else {
						zoomFn := magnifier.second;
						magnifier.first;
					}
				}),
				^zoomFn
			);
		}
	} else {
		\__ -> \f -> Pair(fn(f), idfn)
	}
}

splitImageDecorations(
	noScaleDecorations : bool,
	decorations : [ImageDecoration]
	) -> ImageDecoration4
{
	graphics = \q : ImageDecoration4, d : PreOrPostZoomDecoration -> {
		if (noScaleDecorations)
			ImageDecoration4(q.preZoom, arrayPush(q.postZoom, d), q.sizeIndependent, q.crop)
		else
			ImageDecoration4(arrayPush(q.preZoom, d), q.postZoom, q.sizeIndependent, q.crop);
	}

	independent = \q : ImageDecoration4, d : SizeIndependentDecoration -> {
		ImageDecoration4(q.preZoom, q.postZoom, arrayPush(q.sizeIndependent, d), q.crop);
	}

	fold(decorations, ImageDecoration4([], [], [], None()),
		\acc : ImageDecoration4, d : ImageDecoration -> {

		if (isSameStructType(d, IllegalStruct())) {
			println("imagedecorated: splitImageDecorations([..., IllegalStruct(), ...])");
			acc;
		} else switch (d : ImageDecoration) {
			ImageLabel(__, __, __) : graphics(acc, d);
			ImageCircle(__, __, __, __, __) : graphics(acc, d);
			ImageArrow(__, __, __, __, __, __, __) : graphics(acc, d);
			ImageRectangle(__, __, __, __, __, __) : graphics(acc, d);
			ImageTextBox(__, __, __, __) : graphics(acc, d);
			ImageTextColumn(__, __, __, __, __, __, __) : graphics(acc, d);
			ImageCaption(__, __, __, __, __): independent(acc, d);
			ImagePadding(__, __, __, __): independent(acc, d);
			ImageBorder(__, __, __): independent(acc, d);
			ImageHeader(__, __, __): independent(acc, d);
			ImageFooter(__, __, __): independent(acc, d);
			ImageCopyright(__, __): independent(acc, d);
			ImageCopyrightHidable(): independent(acc, d);
			ImageAltText(__, __): independent(acc, d);
			ImageShadow() : independent(acc, d);
			ImageLabel2(__, __, __, __, __, __, __, __):
				ImageDecoration4(acc.preZoom, arrayPush(acc.postZoom, d), acc.sizeIndependent, acc.crop);
			ImageCrop(x, y, w, h):
				ImageDecoration4(acc.preZoom, acc.postZoom, acc.sizeIndependent, Some(d));
			ImageAdaptiveCrop(__, __, __, __, __): acc;
			ImageThumbnailCrop(x, y, w, h) : ImageDecoration4(
				acc.preZoom,
				acc.postZoom,
				acc.sizeIndependent,
				Some(ImageCrop(x, y, w, h))
			);
			ImageAdaptiveCropTweaks(__): acc;
			ImageDimmer(__, __, __, __, __, __, __):
				ImageDecoration4(arrayPush(acc.preZoom, d), acc.postZoom, acc.sizeIndependent, acc.crop);
		}
	})
}


makePostZoomDecorationsAdder(
	scale : Behaviour<double>,
	decorations : [PostZoomDecoration],
	crop : Maybe<ImageCrop>, noScaleDecorations : bool,
	_width : double, _height : double
	) -> (Form) -> Form
{
	subConnectToScale = \beh : DynamicBehaviour<double>, v : double -> {
		connectSelectDistinctu(scale, beh, \s -> s * v);
	}
	cropX = eitherMap(crop, \c -> c.x, 0.0);
	cropY = eitherMap(crop, \c -> c.y, 0.0);
	applyCrop = eitherMap(crop,  \c -> \f -> {
			cropX_ = make(0.0);
			cropY_ = make(0.0);
			cropW_ = make(0.0);
			cropH_ = make(0.0);
			Constructor(
				Crop(cropX_, cropY_, cropW_, cropH_, f),
				\ -> {
					uns = [
						subConnectToScale(cropX_, c.x),
						subConnectToScale(cropY_, c.y),
						subConnectToScale(cropW_, c.width),
						subConnectToScale(cropH_, c.height)
					];
					\ -> applyall(uns);
				}
			)
		},
		idfn
	);
	forms0 = map(decorations, \d -> switch(d : PostZoomDecoration) {
		ImageLabel2(x, y, ha, va, cs, text, rotation, stable) : {
			addScale = if (stable) idfn else \f -> Scale(scale, scale, f);
			translateX = make(0.0);
			translateY = make(0.0);
			Constructor(
				Translate(translateX, translateY,
					Rotate(const(rotation), addScale(Text(text, cs)))
				),
				\ -> {
					uns = [
						subConnectToScale(translateX, x - cropX),
						subConnectToScale(translateY, y - cropY)
					];
					\ -> applyall(uns);
				}
			);
		}
		ImageLabel(x, y, label): {
			// We use a border to force the size to adjust accordingly
			// We cut two pixel border of TextField, so we add them here to match view in Annotate
			if (noScaleDecorations)
				switch (label : Form) {
					Text(text, style) :
						Scale(scale, scale,
							Offset(x, y, Border(2.0, 2.0, 0.0, 0.0, Text(text, style)))
						);
					default : {
						reportImageLabelError(d);
						Empty();
					}

				}
			else
				Empty()
		}
		ImageArrow(x, y, rotate, len, color, thickness, label): {
			// (x, y) is a point to where the arrow points
			if (noScaleDecorations) {
				angleRad = rotate * PI / 180.0;

				arrow = FixSize(6.0, 9.0, Graphics(makeArrowGraphics(len, thickness), [Stroke(color), StrokeWidth(max(thickness, 1.0))]));
				Scale(scale, scale,
					Group(
						[
							Offset(x, y, Rotate(const(-rotate - 90.0), arrow)),
							Offset(x + len * cos(angleRad), y - len * sin(angleRad), label)
						]
					)
				);
			} else
				Empty();
		}
		ImageCircle(x, y, radius, style, inverted): {
			if (noScaleDecorations)
				Scale(scale, scale,
					makeImageCircle(d, _width, _height)
				)
			else
				Empty()
		}
		ImageRectangle(x, y, width, height, style, inverted) : {
			if (noScaleDecorations)
				Scale(scale, scale, makeImageRectangle(d, _width, _height))
			else
				Empty()
		}
		ImageTextBox(label, halign, valign, rec) : {
			if (noScaleDecorations)
				Scale(scale, scale, makeImageTextBox(d))
			else
				Empty()
		}
		ImageTextColumn(__,__, __, __, __, __, __) : Empty()
	});
	forms1 = filter(forms0, neq(Empty()));
	forms = Group(forms1);

	addForms = \f -> {
		Group([f, Size2(zeroSize, forms)])
	}

	if (noScaleDecorations)
		\f -> f |> addForms |> applyCrop
	else
		\f -> f |> applyCrop |> addForms
}

makeSizeIndependentDecorationsAdder(
	decorations : [SizeIndependentDecoration],
	makeParagraphElements : (string, [CharacterStyle]) -> [ParagraphElement],
	imgSize: WidthHeight,
	zoom : bool
	) -> (Form) -> Form
{
	imageWH = makeWH();
	paragraphSizer = \l, t, r, b -> \f -> {
		paragraphWH = makeWH();
		Constructor(
			Available2(
				paragraphWH,
				Border(l, t, r, b, f)
			), //We need it to force Paragraph recalculation.
			\ -> {
				connectSelectDistinctu(imageWH, paragraphWH, \wh -> WidthHeight(max(wh.width, 0.0), 0.0));
			}
		)
	}
	addInspector = \size -> \f : Form -> Inspect([ISize(size)], f);
	addShadow = \pic -> if (containsStruct(decorations, ImageShadow())) {
		if (js) CustomShadowAround2(black, 0.4, 5., 5., true, pic)
		else ShadowFilter2(0.4, pic)
	} else pic;

	defaultDecoration = idfn;
	addBorder = eitherMap(
		tryExtractStruct(decorations, ImageBorder(0., 0, 0.)),
		\d ->  {
			\f -> Rounded(d.radius, [Fill(white), Stroke(d.color), StrokeWidth(d.thickness)], f);
		},
		idfn
	);
	addHeader = eitherMap(
		tryExtractStruct(decorations, ImageHeader("", 0., 0)),
		\d -> {
			\f : Form -> {
				Lines([
					Paragraph(
						makeParagraphElements(d.label, [Fill(d.color), FontSize(d.fontsize)]),
						[CenterAlign()]
					) |> paragraphSizer(5.0, 5.0, 5.0, 5.0),
					f
				])
			}
		},
		idfn
	);
	addFooter = eitherMap(
		tryExtractStruct(decorations, ImageFooter("", 0., 0)),
		\d -> {
			\f : Form -> {
				Lines([
					f,
					Paragraph(
						makeParagraphElements(d.label, [Fill(d.color), FontSize(d.fontsize)]),
						[CenterAlign()]
					) |> paragraphSizer(5.0, 5.0, 5.0, 5.0)
				])
			}
		},
		idfn

	);
	addCopyright = eitherMap(
		tryExtractStruct(decorations, ImageCopyright("", [])),
		\d -> {
			copyright = d.copyright;
			style = d.style;
			text = getImageCopyrightPair(copyright).first;
			\f -> {
				zoomFs = max(12.0, 12.0 * sqrt(imgSize.width * imgSize.width + imgSize.height * imgSize.height) / 1000.0);
				newStyle = ifArrayPush(style, !containsStruct(style, Fill(0x525252)), Fill(0x525252));

				needsHide = !zoom && contains(decorations, ImageCopyrightHidable());
				availableH = make(0.);
				showCopyrightB = make(false);
				Lines([
					f,
					Paragraph(makeParagraphElements(text, concat(newStyle, [FontSize(if (zoom) zoomFs else 8.0)])), [RightAlign()])
						|> paragraphSizer(5.0, 5.0, 5.0, 7.0)
						|> (if (needsHide) \p -> Visibleb(showCopyrightB, p) else idfn)
				])
				|> if (needsHide) \l -> Constructor(
					Inspect([AvailableHeight(availableH)], l),
					\ -> subscribe(availableH, \h -> nextDistinct(showCopyrightB, h >= 300. || h == 0.))
				) else idfn
			}
		},
		idfn
	);
	addCaption = eitherMap(
		tryExtractStruct(decorations, ImageCaption("", 0., 0, [], [])),
		\d -> {
			\f : Form -> {
				Lines([
					f,
					Paragraph(
						makeParagraphElements(
							d.text,
							replaceStructMany(d.characterStyle, [Fill(d.color), FontSize(d.fontsize)])
						),
						d.paragraphStyle
					) |> paragraphSizer(10.0, 10.0, 10.0, 12.0)
				])
			}
		},
		idfn
	);
	addAltText = eitherMap(
		tryExtractStruct(decorations, ImageAltText("", [])),
		\d ->  {
			\f -> Access([AccessRole("img"), AccessDescription(d.label)], f);
		},
		idfn
	);
	steps = [addBorder, addHeader, addFooter, addShadow, addCopyright, addCaption, addAltText];

	\f -> fold(steps, Inspect([ISize(imageWH)], f), \acc, decorate -> decorate(acc));
}

makeDimmerShape(
	type : ImageDimmerType,
	leftTopPoint : Point,
	rightBotPoint : Point
) -> [StaticGraphicOp] {
	w = rightBotPoint.x - leftTopPoint.x;
	h = rightBotPoint.y - leftTopPoint.y;
	switch (type : ImageDimmerType) {
		ImageDimmerCircle(): {
			radius = min(w, h) / 2.0;
			SectorPath(leftTopPoint.x + radius, leftTopPoint.y + radius, radius, 0.0, 360.0, false, false, false);
		}
		ImageDimmerRectangle(): {
			if (js) {
				cast(rectangle(leftTopPoint.x, leftTopPoint.y, w, h) : [GRect] -> [StaticGraphicOp]);
			} else {
				RectanglePath(leftTopPoint, WidthHeight(w, h));
			}
		}
	}
}
