// Â© Copyright 2013 Area9 Innovation. All rights reserved

import api_filesystem;
import filesystem;
import string;

export {
	setLocalFilesystemAPI() -> void;
}

localFilesystemAPI() -> FilesystemAPI {
	FilesystemAPI(
		//readDirectory : (path : string, onDone : (string) -> void) -> void
		\path, onDone -> {
			if (fileExists(path)) {
				content = readDirectory(path);
				dirs = concat([".."], filter(content, \name -> isDirectory(path + "/" + name)));
				files = filter(content, \name -> !isDirectory(path + "/" + name));
				onDone(strGlue(dirs, ",") + "\n" + strGlue(files, ","))
			} else {
				onDone("")
			}
		},
		//isDirectory : (string, onDone : (bool) -> void) -> void
		\path, onDone -> {
			onDone(isDirectory(path))
		},
		// resolveRelativePath : (string) -> string
		resolveRelativePath,
		// fileExists : (string, onDone : (bool) -> void) -> void
		\path, onDone -> {
			onDone(fileExists(path))
		},
		// deleteFile : (string, onDone : (string) -> void) -> void
		\path, onDone -> {
			onDone(deleteFile(path))
		},
		// getFileContent : (string, onDone : (string) -> void, onError : (string) -> void) -> void,
		\filename, onDone, onError -> {
			onDone(getFileContent(filename))
		},
		// setFileContent : (string, string, onDone : (string) -> void, onError : (string) -> void) -> void
		\filename, content, onDone, onError -> {
			if (setFileContent(filename, content)) onDone("OK") else onError("Can't save file '"+filename+"'");
		}
	)
}

setLocalFilesystemAPI() -> void {
	setFilesystemAPI(localFilesystemAPI());
}
