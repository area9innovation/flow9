import string;

export {
	deserializeString(s : string) -> Pair<string, string>;
	deserializeString2(s : string, start : int) -> Pair<string, int>;

	// These are useful for JSON parsing
	deserializeRestOfString2(s : string, start : int) -> Pair<string, int>;
	deserializeRestOfString(s : string) -> Pair<string, string>;
}


deserializeString2(s : string, start : int) -> Pair<string, int> {
	if (substring(s, start, 2) == "\"\"") {
		// Common case of empty string
		Pair("", start+2);
	} else if (getCharCodeAt(s, start) == 34) {  // "\""
		deserializeRestOfStringTail(s, start+1);
	} else {
		deserializeRestOfStringTail(s, start);
	}
}

deserializeString(s : string) -> Pair<string, string> {
	rv = deserializeString2(s, 0);
	Pair(rv.first, strRight(s, rv.second))
}

deserializeRestOfString2(s : string, start : int) -> Pair<string, int> {
	deserializeRestOfStringTail(s, start);
}

deserializeRestOfString(s : string) -> Pair<string, string> {
	rv = deserializeRestOfStringTail(s, 0);
	Pair(rv.first, strRight(s, rv.second))
}

deserializeRestOfStringTail(s : string, start : int) -> Pair<string, int> {
	rv = deserializeRestOfStringTail2(s, makeList(), start, -1);
	Pair(cloneString(list2string(rv.first)), rv.second)
}

deserializeRestOfStringTail2(s : string, acc : List<string>, start : int, quote0 : int) -> Pair<List<string>, int> {
	strLength = strlen(s);
	// When " is met far away from the beginning of a large string, containing lots of \n and \t (i.e. a source file), if the code
	//    `strRangeIndexOf(s, "\"", start, strLength)`
	// is invoked at each occurrence of \n or \t, and at each such occurrence the same position of far-away " is re-computed,
	// thus O(n^2) complexity occurrs.
	// There exists an example (file of 1.4 megabytes) being de-escaped in roughly 40 seconds, because there's very few of " symbols, but lots of new lines.
	quote = if (start > quote0) strRangeIndexOf(s, "\"", start, strLength) else quote0;
	// Search for escape only in the candidate string,
	// because no escape is more common:
	escape = strRangeIndexOf(s, "\\", start, quote);
	if (escape != -1 && escape < quote) {
		// Handle escape
		first = substring(s, start, escape - start);
		escapedCharCode = getCharCodeAt(s, escape + 1);
		if (escapedCharCode == 34) {			// "\""
			deserializeRestOfStringTail2(s, Cons("\"", Cons(first, acc)), escape + 2, quote);
		} else if (escapedCharCode == 92) {		// "\"
			deserializeRestOfStringTail2(s, Cons("\\", Cons(first, acc)), escape + 2, quote);
		} else if (escapedCharCode == 110) {	// "n"
			deserializeRestOfStringTail2(s, Cons("\n", Cons(first, acc)), escape + 2, quote);
		} else if (escapedCharCode == 117) {	// "u"
			// Unicode hex escape
			hex = substring(s, escape + 2, 4);
			code = parseHex(hex);
			deserializeRestOfStringTail2(s, Cons(fromCharCode(code), Cons(first, acc)), escape + 6, quote);
		} else if (escapedCharCode == 120) {	// "x"
			// Ascii hex escape
			hex = substring(s, escape + 2, 2);
			code = parseHex(hex);
			deserializeRestOfStringTail2(s, Cons(fromCharCode(code), Cons(first, acc)), escape + 4, quote);
		} else {
			descaped =
				if (escapedCharCode == 116) "\t"			// "t"
				else if (escapedCharCode == 114) "\u000d"	// "r"
				else fromCharCode(escapedCharCode);

			deserializeRestOfStringTail2(s, Cons(descaped, Cons(first, acc)), escape + 2, quote);
		}
	} else if (quote == -1) {
		Pair(Cons(if (start >= strLength) "" else substring(s, start, strLength - start), acc), strLength); // inlined strRight
	} else {
		Pair(Cons(substring(s, start, quote - start), acc), quote + 1);
	}
}
