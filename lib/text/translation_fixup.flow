import formats/xml;
import text/translation_tags;

export {
	fixupTranslations(s : string) -> string;
}

//from <g id=0 ctype='bold'> to <g id="0" ctype="bold"> , the same for "x" tag
fixupTranslations(s : string) -> string {
	ss = xmlUnescape(s);
	xml = parseXml2WithStrategy(ss, [XmlParseLeadingSpaces(true, true), StandardEscaping()]);
	fixedXml = switch(xml : XmlNode) {
		XmlElement(name, attrs, children): {
			tag2str(xml)
		}
		default: {
			ss
		}
	}

	fixedXml
}

tag2str(element : XmlElement) -> string {
	processChildren = \-> fold(element.children, "", \r, ch -> r +
		switch (ch : XmlNode) {
			XmlElement(tag_, attr_, children_): tag2str(ch);
			XmlText(t): t;	//plain text, without tags
		}
	);

	attr = element.attributes;
	text = processChildren();
	if (element.tag == "g") {
		gTag2string(getXmlAttribute(attr, "id", "0"), getXmlAttribute(attr, "ctype", ""), text)
	} else if (element.tag == "x") {
		xTag2string(getXmlAttribute(attr, "id", "0"), getXmlAttribute(attr, "ctype", ""))
	} else
		text; // another tag?
}
