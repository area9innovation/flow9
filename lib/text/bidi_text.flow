import ds/arrayutils;
import text/unicodecharacters;
import ui/fontmapping;
import formats/html/html;
import loop;

export {
	// TODO refactor due these are used to partial functionality, do not bring
	// semantically complete functionality, but used in some places, which
	// themselves may duplicate each other.
	// Seems text breaking algo needs to be implemented to replace
	// existing in paragraph.flow and tparagraph.flow.

	// Choose leftmost valid string position from two given.
	nextValidStrPos(left: int, right: int) -> int;

	// Determine where to split the text, based on unicode categories.
	getCharCatSplitPos(data: CharCatSplitPosText, i: int, endPos: int) -> int;
	// END TODO

	DirectionAndMajority(d: int, m: int);

		dummy_majority = 0;
		minor_majority = 1; // Majority for uc_decimalDigitNumber
		major_majority = 2; /* Majority for Letters,
											Zero-Width Joiner,
		 									Right-To-Left Embedding,
		 									Right-To-Left Override */
		highest_majority = 3; // Majority for Isolated and Marks

	getTextDirectionAndMajority(t: string) -> DirectionAndMajority;

	reflowTextToBiDi(
		text: [[?]],
		isEmpty: (?) -> bool,
		getText: (?) -> string,
		setText: (?, string) -> ?,
		setRTL: (?, bool) -> ?,
		rtl: bool
	) -> [[?]];

	// Returns a reversed list with elements of original list wrapped into a pair with text flow direction
	markListToBiDiReverse(list: List<?>, getText: (?) -> string) -> List<Pair<?, int>>;
	// this is internal tuning structure to avoid multiple creation of copies of string possible large and converting them into ints
	// use makeCharCatSplitPosText helper function to get it
	CharCatSplitPosText(
		text : string, // this is original string of text
		text_asInt : [int], // it is pre-converted into ints
		text_space_asInt : [int], // same as text_asInt but with 32 in the end
		space_space_text_asInt : [int], // same as text_asInt but with 32, 32 in front of
		space_text_asInt : [int] // same as text_asInt but with 32 in front of
	);

	makeCharCatSplitPosText(t : string) -> CharCatSplitPosText;
}

makeCharCatSplitPosText(t : string) -> CharCatSplitPosText {
	lenText = strlen(t);
	text_asInt = s2a(t);

	CharCatSplitPosText(
		t,
		text_asInt,
		arrayPush(text_asInt, 32),
		concat([32, 32], text_asInt),
		concat([32], text_asInt),
	)
}

nextValidStrPos(left: int, right: int) -> int {
	if (left >= 0 && (right < 0 || left < right)) left else right;
}

charCatSplitPos_sep_cats = [
	uc_spaceSeparator, uc_lineSeparator, uc_paragraphSeparator, uc_control, uc_format,
	uc_connectorPunctuation, uc_dashPunctuation, uc_openPunctuation, uc_closePunctuation,
	uc_initialQuotePunctuation, uc_finalQuotePunctuation, uc_otherPunctuation
];

charCatSplitPos_codes = [0x27, 0x2C, 0x2D, 0x2E, 0x2F, 0x5C];

foldRangeWhileNotFound(start : int, end : int, acc : int, fn : (int, int) -> int) -> int {
	if (start <= end && acc == -1) {
		foldRangeWhileNotFound(start + 1, end, fn(acc, start), fn)
	} else {
		acc;
	}
}

getCharCatSplitPos(data: CharCatSplitPosText, i: int, endPos: int) -> int {
	// TODO make these refs part of accumulator in stringFoldi.
	htmlEntity: ref int = ref -1;
	htmlTag: ref int = ref -1;
	text = data.text_asInt;
	t_space = data.text_space_asInt;
	space2_t = data.space_space_text_asInt;
	space_t = data.space_text_asInt;
	endLoop = nextValidStrPos(endPos, length(text))-1;

	foldRangeWhileNotFound(i, endLoop, -1, \pspacc, cur_pspi -> {
		if (pspacc == -1) {
			pre_pspcode = space_t[cur_pspi];
			if (^htmlEntity != -1) {
				if (pre_pspcode==59) {
					htmlEntity := -1;
					nextValidStrPos(pspacc, cur_pspi + i)
				} else if ((pre_pspcode != 35 && pre_pspcode != 38 && pre_pspcode < 48) || (pre_pspcode > 57 && pre_pspcode < 65) || (pre_pspcode > 90 && pre_pspcode < 97) || pre_pspcode > 122) {
					nextValidStrPos(pspacc, cur_pspi + i + 1)
				} else {
					pspacc
				}
			} else if (pre_pspcode == 38) {
				htmlEntity := cur_pspi + i;
				pspacc;
			} else if (^htmlTag != -1) {
				if (pre_pspcode == 62) {
					nextValidStrPos(pspacc, cur_pspi + i)
				} else {
					pspacc;
				}
			} else if (pre_pspcode == 60) {
				htmlTag := cur_pspi + i;
				pspacc;
			} else {
				cur_pspcode = text[cur_pspi];
				pre_cat = getUnicodeCategory(pre_pspcode);
				cur_cat = getUnicodeCategory(cur_pspcode);
				nxt_cat = getUnicodeCategory(t_space[cur_pspi + 1]);
				pre_alp = getUnicodeAlphabet(pre_pspcode);
				cur_alp = getUnicodeAlphabet(cur_pspcode);
				pre_scl = getAlphabetScaling(pre_alp, "");
				cur_scl = getAlphabetScaling(cur_alp, "");
				pre_dir = getUnicodeDirection(pre_pspcode);
				cur_dir = getUnicodeDirection(cur_pspcode);
				pre2curBrk = pre_scl != cur_scl || (pre_dir == uc_dir_ltr && cur_dir == uc_dir_rtl) || (pre_dir == uc_dir_rtl && cur_dir == uc_dir_ltr);
				// It is possible that pre2curBrk is the only condition needed after all, for JS at least.
				isPunct_cur_cat = isPunct(cur_cat);
				if (
					(pre_alp != cur_alp) ||
					(pre_cat == uc_spaceSeparator) ||
					(cur_cat == uc_spaceSeparator && pre2curBrk) ||
					(isPunct_cur_cat && pre_cat == uc_decimalDigitNumber && (contains(charCatSplitPos_codes, cur_pspcode) && contains(charCatSplitPos_sep_cats, nxt_cat))) ||
					(pre2curBrk && isPunct_cur_cat && (pre_cat != uc_decimalDigitNumber || nxt_cat != uc_decimalDigitNumber)) ||
					(pre2curBrk && isPunct(pre_cat) && (cur_cat != uc_decimalDigitNumber || getUnicodeCategory(space2_t[cur_pspi]) != uc_decimalDigitNumber)) ||
					false
				) {
					nextValidStrPos(pspacc, cur_pspi)
				} else {
					pspacc;
				}
			}
		} else {
			pspacc;
		}
	})
}

BiDiWord(word: ?, dm: DirectionAndMajority);
BiDiWordAddress(lno: int, wno: int);

BiDiContext(rtl: bool, formattingStack : ref List<BiDiFormatting>);
BiDiFormatting(kind : BiDiFormattingKind, met: BiDiWordAddress, dm : DirectionAndMajority);
	BiDiFormattingKind ::=
		BiDiDirectionMark, BiDiDirectionEmbedded,
		BiDiDirectionIsolated, BiDiDirectionOverridden;

		BiDiDirectionMark();
		BiDiDirectionEmbedded();
		BiDiDirectionIsolated();
		BiDiDirectionOverridden();  // Not supported yet.

makeBiDiContext(rtl : Maybe<bool>) -> BiDiContext {
	BiDiContext(eitherFn(rtl, idfn, getDefaultRtl), ref EmptyList())
}

_isLetter(ipcat) {
	ipcat == uc_uppercaseLetter ||
	ipcat == uc_lowercaseLetter ||
	ipcat == uc_titlecaseLetter ||
	ipcat == uc_modifierLetter ||
	ipcat == uc_otherLetter ||
	ipcat == uc_nonSpacingMark;
}

isPunct(ipcat) {
	ipcat == uc_openPunctuation ||
	ipcat == uc_closePunctuation ||
	ipcat == uc_initialQuotePunctuation ||
	ipcat == uc_finalQuotePunctuation ||
	ipcat == uc_otherPunctuation;
}

mirrorWordGlyphsForRtl(s: string, rtl: bool) -> string {
	// We have our strings HTML-encoded, so additional replacements needed.
	COUPLED_MULTI_CHARS = if (js)
		// "<" by even index and ">" by odd index.
		["&lt;", "&gt;", "&#60;", "&#62;", "&#x3C;", "&#x3E;", "&#x3c;", "&#x3e;"]
	else
		[];

	if (rtl) {
		concatStrings(
			map(
				strSplit2(s, COUPLED_MULTI_CHARS),
				\frag -> eitherMap(
					findi(COUPLED_MULTI_CHARS, \el -> el == frag),
					\i -> COUPLED_MULTI_CHARS[if (0 == i % 2) i + 1 else i - 1], /* No XOR in flow for a pity */
					frag
				)
			)
		)
	} else s;
}

getCharDirectionAndMajority(code: int) -> DirectionAndMajority {
	cat = getUnicodeCategory(code);
	additionalMajorChars = [
		zeroWidthJoinerCode,
		0x202B, // Right-To-Left Embedding
		0x202E, // Right-To-Left Override
	];
	DirectionAndMajority(
		getUnicodeDirection(code),
		if (_isLetter(cat) || contains(additionalMajorChars, code)) major_majority
		else if(cat == uc_decimalDigitNumber) minor_majority
		else dummy_majority
	);
}

// Makes one DM pair from two, this function is fold helper.
fold2DMs(first: DirectionAndMajority, second: DirectionAndMajority) -> DirectionAndMajority {
	if (first.m > second.m) first else
	if (second.m > first.m) second else
	DirectionAndMajority(
		if (contains(uc_strong_dirs, first.d)) first.d
		else second.d,
		max(first.m, second.m)
	);
}

// Makes one DM pair from three, third is default when first two are conflicting.
// Assuming third has RTL or LTR direction so the case is solved.
fold3DMs(first: DirectionAndMajority, second: DirectionAndMajority, third: DirectionAndMajority) -> DirectionAndMajority {
	if (first.m > second.m) first else
	if (second.m > first.m || second.d == first.d) second
	else DirectionAndMajority(
		if (contains(uc_strong_dirs, first.d))
			if (contains(uc_strong_dirs, second.d))
				third.d
			else
				first.d
		else
			if (contains(uc_strong_dirs, second.d))
				second.d
			else
				third.d,
		max(first.m, second.m)
	);
}

// Returns word direction and majority flag (meaning this direction to be inherited
// by adjacent words with no majority).
getTextDirectionAndMajority(t: string) -> DirectionAndMajority {
	stringFold(
		unescapeHtml(t), DirectionAndMajority(uc_dir_flow, dummy_majority), \acc: DirectionAndMajority, code: int ->
			fold2DMs(acc, getCharDirectionAndMajority(code))
	);
}

debugWord(w: BiDiWord<?>, getText: (?) -> string) -> string {
	ddm = \dm -> getCharAt("→~←:", dm.d) + getCharAt("⓪①②③", dm.m);
	getText(w.word) + ddm(w.dm);
}

groupByCounterDirReverse(ws: [BiDiWord<?>], rtl: bool, getText: (?) -> string) -> [[BiDiWord<?>]] {
	defaultDir = if (rtl) uc_dir_rtl else uc_dir_ltr;
	counterDir = if (rtl) uc_dir_ltr else uc_dir_rtl;
	if (length(ws) < 2) [ws] else fold(
		tail(ws), Pair([[ws[0]]], ws[0].dm), \acc: Pair<[[BiDiWord<?>]], DirectionAndMajority>, w: BiDiWord<?> -> {
			wdm = w.dm;
			Pair(
				if (acc.second.d == counterDir && acc.second.m >= major_majority && wdm.d != defaultDir)
					insertArray(
						tail(acc.first),
						0,
						if (rtl) arrayPush(acc.first[0], w)
						else insertArray(acc.first[0], 0, w)
					)
				else
					insertArray(acc.first, 0, [w]),
				if (wdm.m >= major_majority) wdm
				else acc.second
			)
		}
	).first;
}

reflowWordsToBiDi(
	ws: [BiDiWord<?>],
	isEmpty: (?) -> bool,
	getText: (?) -> string,
	setText: (?, string) -> ?,
	rtl: bool
) -> [BiDiWord<?>] {
	// Grouping words with counter-direction into subarrays, and reversing order.
	ltrGrouping = groupByCounterDirReverse(ws, rtl, getText);

	// Repeating same for each group and reverse them back — to cut group trailing punctuation, and counter-directed groups now in reverse order.
	ltrCleanGrouping = concatA(map(ltrGrouping, \lgws -> reverseA(groupByCounterDirReverse(lgws, rtl, getText))));

	// Filtering out empty fragments and joining all groups back together.
	r = filter(
		concatA(
			map(ltrCleanGrouping, \grp -> {
				grpdm = DirectionAndMajority(
					if ((length(grp) == 1) == rtl) uc_dir_rtl
					else uc_dir_ltr,
					dummy_majority
				);
				map(grp, \w -> BiDiWord(setText(w.word, mirrorWordGlyphsForRtl(getText(w.word), (fold2DMs(grpdm, w.dm).d != uc_dir_ltr) == rtl)), w.dm));
			})
		),
		\w -> !isEmpty(w.word)
	);

	// Globally, text remains in reverse order after first operation, which only good for RTL.
	if (rtl) r else reverseA(r)
}

unwrapLine(l: [BiDiWord<?>], setRTL: (?, bool) -> ?) -> [?] {
	map(l, \w ->
		if (contains(uc_strong_dirs, w.dm.d)) setRTL(w.word, w.dm.d == uc_dir_rtl)
		else w.word
	);
}

PairingStackItem(kind : int, addr : BiDiWordAddress, foundDirections : int);

reflowTextToBiDi(
	text: [[?]],
	isEmpty: (?) -> bool,
	getText: (?) -> string,
	setText: (?, string) -> ?,
	setRTL: (?, bool) -> ?,
	rtl: bool
) -> [[?]] {
	if (isUrlParameterTrue("old_bidi")) {
		println("old-bidi");
		are2AddrsOrdered = \a: BiDiWordAddress, b: BiDiWordAddress -> a.lno < b.lno || (a.lno == b.lno && a.wno <= b.wno);

		defaultDM = DirectionAndMajority(if (rtl) uc_dir_rtl else uc_dir_ltr, 2);
		oppositeDM = DirectionAndMajority(if (rtl) uc_dir_ltr else uc_dir_rtl, 2);

		context = makeBiDiContext(Some(rtl));
		pairings : ref Tree<BiDiWordAddress, Pair<DirectionAndMajority, BiDiWordAddress>> = ref makeTree();
		stackPasser = \keyChar, dir, kind : [BiDiFormattingKind], fn -> Pair(
			keyChar,
			\pos, stack -> fn(dir, kind, pos, stack)
		);
		formattingMark = \dm, __, pos, stack : List<BiDiFormatting> -> Cons(BiDiFormatting(BiDiDirectionMark(), pos, dm), switch(stack) {
			Cons(head, t): switch(head.kind) {
				BiDiDirectionMark(): t;
				default: stack
			};
			EmptyList(): stack
		});
		formattingPush = \dm, kind : [BiDiFormattingKind], pos, stack -> {
			Cons(BiDiFormatting(kind[0], pos, dm), stack);
		};
		formattingPop = \dm, kind : [BiDiFormattingKind], pos, stack -> tailList(updateWhile(stack, \s -> switch(s : List<BiDiFormatting>) {
			EmptyList(): None();
			Cons(head, t): if (contains(kind, head.kind)) None() else Some(t);
		}));
		foundDirections = ref 0;  // Bit will be set for each character direction encountered.
		formatHandlers = pairs2tree([
			// Implemented isolated and marks as most useful yet.
			stackPasser("\u200E", DirectionAndMajority(uc_dir_ltr, 3), [BiDiDirectionMark()], formattingMark),
			stackPasser("\u200F", DirectionAndMajority(uc_dir_rtl, 3), [BiDiDirectionMark()], formattingMark),
			stackPasser("\u2066", DirectionAndMajority(uc_dir_ltr, 3), [BiDiDirectionIsolated()], formattingPush),
			stackPasser("\u2067", DirectionAndMajority(uc_dir_rtl, 3), [BiDiDirectionIsolated()], formattingPush),
			stackPasser("\u2069", DirectionAndMajority(uc_dir_flow, 3), [BiDiDirectionIsolated()], formattingPop),
			// TODO no good support yet, the Overridden behaves the same, but also shows chars in memory order,
			//      Embedded must also somehow differ from Isolated.
			//stackPasser("\u202A", uc_dir_ltr, [BiDiDirectionEmbedded()], formattingPush),
			//stackPasser("\u202B", uc_dir_rtl, [BiDiDirectionEmbedded()], formattingPush),
			//stackPasser("\u202C", uc_dir_flow, [BiDiDirectionEmbedded(), BiDiDirectionOverridden()], formattingPop),
			//stackPasser("\u202D", uc_dir_ltr, [BiDiDirectionOverridden()], formattingPush),
			//stackPasser("\u202E", uc_dir_rtl, [BiDiDirectionOverridden()], formattingPush),
		]);
		pairingO = ["{", "(", "["];
		pairingL = length(pairingO);
		pairingC = ["}", ")", "]"];
		pairingStack : ref List<PairingStackItem> = ref makeList();

		previousDM = ref defaultDM;
		lines0 = mapi(text, \lno, l -> mapi(l, \wno, w -> {
			t = getText(w);
			currentAddr = BiDiWordAddress(lno, wno);
			tdm = getTextDirectionAndMajority(t);
			foundDirections := bitOr(^foundDirections, bitShl(1, tdm.d));

			nearestCloser : Pair<[string], Cons<PairingStackItem>> = switch (^pairingStack) {
				EmptyList(): Pair(pairingO, Cons(PairingStackItem(-1, BiDiWordAddress(length(text), 0), 0), ^pairingStack));
				Cons(hd, tl): Pair(arrayPush(pairingO, pairingC[hd.kind]), cast(^pairingStack : List -> Cons));
			};
			// Assuming word splitting avoids more than one paired character in a word.
			switch(strFindFirstOfStrings(t, nearestCloser.first)) {
				Some(p): pairingStack := if (p.second == pairingL) {
					// http://www.unicode.org/reports/tr9/#Resolving_Neutral_Types
					// previous strong direction checked in passes below.
					dm = if (i2b(bitAnd(bitUshr(^foundDirections, defaultDM.d), 1))) defaultDM
						else if (i2b(bitAnd(bitUshr(^foundDirections, oppositeDM.d), 1))) oppositeDM
						else tdm;
					pairings := setTree(^pairings, nearestCloser.second.head.addr, Pair(dm, currentAddr));
					foundDirections := bitOr(^foundDirections, nearestCloser.second.head.foundDirections);
					nearestCloser.second.tail
				} else {
					newStack = Cons(PairingStackItem(p.second, currentAddr, ^foundDirections), ^pairingStack);
					foundDirections := 0;
					newStack
				}
				None(): {};
			};

			previousDM := if (tdm.d == uc_dir_coupled) ^previousDM else tdm;
			Pair(t, BiDiWord(w, tdm))
		}));

		previousDM := defaultDM;
		lines1 = mapi(lines0, \lno, l -> mapi(l, \wno, tw -> BiDiWord(tw.second with dm = {
			t = tw.first;
			currentAddr = BiDiWordAddress(lno, wno);
			// Splitters go to separate word, so the following is safe we don't miss any char.
			if (containsKeyTree(formatHandlers, strsubsmart(t, 0, 1))) {
				context.formattingStack := stringCharsFold(
					t, ^(context.formattingStack), \s, c -> lookupTreeDef(formatHandlers, c, \__, stack -> stack)(BiDiWordAddress(lno, wno), s)
				);
			};
			tdm = eitherMap(
				lookupTree(^pairings, currentAddr),
				\pair -> {
					pairings := removeFromTree(^pairings, currentAddr);
					if (are2AddrsOrdered(currentAddr, pair.second)) {
						// Opening pair
						dm = if(pair.first == oppositeDM && ^previousDM.d == oppositeDM.d) oppositeDM else pair.first;
						pairings := setTree(^pairings, pair.second, Pair(dm, currentAddr));
						dm
					} else {
						// Closing pair, DM is already calculated in opening one.
						pair.first
					}
				},
				tw.second.dm
			);
			tdmAgg = switch(^(context.formattingStack)) {
				Cons(head, __): if (tdm.m == 2) tdm else fold3DMs(head.dm, ^previousDM, tdm);
				EmptyList(): tdm;
			};
			if (contains([uc_dir_ltr, uc_dir_rtl], tdmAgg.d)) previousDM := tdmAgg;
			if (tdmAgg.d == uc_dir_coupled) ^previousDM else tdmAgg;
		})));

		// TODO: accumulate only those, which change the direction, not each major word.
		forwardMajorWords : ref List<Pair<BiDiWordAddress, DirectionAndMajority>> = ref foldi(
			reverseA(lines1),
			cast(Cons(Pair(BiDiWordAddress(length(lines1), 0), defaultDM), EmptyList()) : Cons<Pair<BiDiWordAddress, DirectionAndMajority>> -> List<Pair<BiDiWordAddress, DirectionAndMajority>>),
			\rlno, lacc, line -> {
				lno = length(lines0) - 1 - rlno;
				foldi(reverseA(line), lacc, \rwno, wacc : List<Pair<BiDiWordAddress, DirectionAndMajority>>, word -> {
					wno = length(line) - 1 - rwno;
					curr = word.dm;
					if (curr.m == 2 && contains([uc_dir_ltr, uc_dir_rtl], curr.d))
						cast(Cons(Pair(BiDiWordAddress(lno, wno), curr), wacc) : Cons<Pair<BiDiWordAddress, DirectionAndMajority>> -> List<Pair<BiDiWordAddress, DirectionAndMajority>>)
					else wacc
				})
			}
		);

		previousDM := defaultDM;
		mapi(
			lines1, \lno, line -> unwrapLine(
				reflowWordsToBiDi(
					mapi(line, \wno, word -> {
						currentAddr = BiDiWordAddress(lno, wno);
						forwardMajorWords := switch(^forwardMajorWords) {
							EmptyList(): EmptyList();
							Cons(h, t): if (are2AddrsOrdered(h.first, currentAddr)) t else ^forwardMajorWords;
						};
						previousDM := if (word.dm.m >= 2) word.dm else ^previousDM;
						majorDM = switch(^forwardMajorWords) {
							EmptyList(): defaultDM;
							Cons(h, t): h.second;
						};
						if (word.dm.m == 2) word else BiDiWord(word with dm=fold3DMs(^previousDM, majorDM, defaultDM));
					}),
					isEmpty, getText, setText, rtl
				), setRTL
			)
		)
	} else {
		are2AddrsOrdered = \a: BiDiWordAddress, b: BiDiWordAddress -> a.lno < b.lno || (a.lno == b.lno && a.wno <= b.wno);

		defaultDM = DirectionAndMajority(if (rtl) uc_dir_rtl else uc_dir_ltr, major_majority);
		oppositeDM = DirectionAndMajority(if (rtl) uc_dir_ltr else uc_dir_rtl, major_majority);

		context = makeBiDiContext(Some(rtl));
		pairings : ref Tree<BiDiWordAddress, Pair<DirectionAndMajority, BiDiWordAddress>> = ref makeTree();
		stackPasser = \keyChar, dir, kind : [BiDiFormattingKind], fn -> Pair(
			keyChar,
			\pos, stack -> fn(dir, kind, pos, stack)
		);
		formattingMark = \dm, __, pos, stack : List<BiDiFormatting> -> Cons(BiDiFormatting(BiDiDirectionMark(), pos, dm), switch(stack) {
			Cons(head, t): switch(head.kind) {
				BiDiDirectionMark(): t;
				default: stack
			};
			EmptyList(): stack
		});
		formattingPush = \dm, kind : [BiDiFormattingKind], pos, stack -> {
			Cons(BiDiFormatting(kind[0], pos, dm), stack);
		};
		formattingPop = \dm, kind : [BiDiFormattingKind], pos, stack -> tailList(updateWhile(stack, \s -> switch(s : List<BiDiFormatting>) {
			EmptyList(): None();
			Cons(head, t): if (contains(kind, head.kind)) None() else Some(t);
		}));
		foundDirections = ref 0;  // Bit will be set for each character direction encountered.
		formatHandlers = pairs2tree([
			// Implemented isolated and marks as most useful yet.
			stackPasser(
				"\u200E", // Left-To-Right Mark
				DirectionAndMajority(uc_dir_ltr, highest_majority),
				[BiDiDirectionMark()],
				formattingMark
			),
			stackPasser(
				"\u200F", // Right-To-Left Mark
				DirectionAndMajority(uc_dir_rtl, highest_majority),
				[BiDiDirectionMark()],
				formattingMark
			),
			stackPasser(
				"\u2066", // Left-To-Right Isolate
				DirectionAndMajority(uc_dir_ltr, highest_majority),
				[BiDiDirectionIsolated()],
				formattingPush
			),
			stackPasser(
				"\u2067", // Right-To-Left Isolate
				DirectionAndMajority(uc_dir_rtl, highest_majority),
				[BiDiDirectionIsolated()],
				formattingPush
			),
			stackPasser(
				"\u2069", // Pop Directional Isolate
				DirectionAndMajority(uc_dir_flow, highest_majority),
				[BiDiDirectionIsolated()],
				formattingPop
			),
			// TODO no good support yet, the Overridden behaves the same, but also shows chars in memory order,
			//      Embedded must also somehow differ from Isolated.
			//stackPasser("\u202A", uc_dir_ltr, [BiDiDirectionEmbedded()], formattingPush),
			//stackPasser("\u202B", uc_dir_rtl, [BiDiDirectionEmbedded()], formattingPush),
			//stackPasser("\u202C", uc_dir_flow, [BiDiDirectionEmbedded(), BiDiDirectionOverridden()], formattingPop),
			//stackPasser("\u202D", uc_dir_ltr, [BiDiDirectionOverridden()], formattingPush),
			//stackPasser("\u202E", uc_dir_rtl, [BiDiDirectionOverridden()], formattingPush),
		]);
		pairingO = ["{", "(", "["];
		pairingL = length(pairingO);
		pairingC = ["}", ")", "]"];
		pairingStack : ref List<PairingStackItem> = ref makeList();

		previousDM = ref defaultDM;
		lines0 = mapi(text, \lno, l -> mapi(l, \wno, w -> {
			t = getText(w);
			currentAddr = BiDiWordAddress(lno, wno);
			textDM = getTextDirectionAndMajority(t);
			foundDirections := bitOr(^foundDirections, bitShl(1, textDM.d));

			nearestCloser : Pair<[string], Cons<PairingStackItem>> = switch (^pairingStack) {
				EmptyList(): Pair(pairingO, Cons(PairingStackItem(-1, BiDiWordAddress(length(text), 0), 0), ^pairingStack));
				Cons(hd, tl): Pair(arrayPush(pairingO, pairingC[hd.kind]), cast(^pairingStack : List -> Cons));
			};
			// Assuming word splitting avoids more than one paired character in a word.
			switch(strFindFirstOfStrings(t, nearestCloser.first)) {
				Some(p): pairingStack := if (p.second == pairingL) {
					// http://www.unicode.org/reports/tr9/#Resolving_Neutral_Types
					// previous strong direction checked in passes below.
					dm = if (i2b(bitAnd(bitUshr(^foundDirections, defaultDM.d), 1))) defaultDM
						else if (i2b(bitAnd(bitUshr(^foundDirections, oppositeDM.d), 1))) oppositeDM
						else textDM;
					pairings := setTree(^pairings, nearestCloser.second.head.addr, Pair(dm, currentAddr));
					foundDirections := bitOr(^foundDirections, nearestCloser.second.head.foundDirections);
					nearestCloser.second.tail
				} else {
					newStack = Cons(PairingStackItem(p.second, currentAddr, ^foundDirections), ^pairingStack);
					foundDirections := 0;
					newStack
				}
				None(): {};
			};

			previousDM := if (textDM.d == uc_dir_coupled) ^previousDM else textDM;
			Pair(t, BiDiWord(w, textDM))
		}));

		previousDM := defaultDM;
		lines1 = mapi(lines0, \lineNumber, line -> mapi(line, \wordNumber, tw -> BiDiWord(tw.second with dm = {
			t = tw.first;
			currentAddr = BiDiWordAddress(lineNumber, wordNumber);
			// Splitters go to separate word, so the following is safe we don't miss any char.
			if (containsKeyTree(formatHandlers, strsubsmart(t, 0, 1))) {
				context.formattingStack := stringCharsFold(
					t, ^(context.formattingStack), \s, c -> lookupTreeDef(formatHandlers, c, \__, stack -> stack)(BiDiWordAddress(lineNumber, wordNumber), s)
				);
			};
			textDM = eitherMap(
				lookupTree(^pairings, currentAddr),
				\pair -> {
					pairings := removeFromTree(^pairings, currentAddr);
					if (are2AddrsOrdered(currentAddr, pair.second)) {
						// Opening pair
						dm = if (pair.first == oppositeDM && ^previousDM.d == oppositeDM.d) oppositeDM
						else pair.first;
						pairings := setTree(^pairings, pair.second, Pair(dm, currentAddr));
						dm
					} else {
						// Closing pair, DM is already calculated in opening one.
						pair.first
					}
				},
				tw.second.dm
			);
			tdmAgg = switch (^(context.formattingStack)) {
				Cons(head, __):
					if (textDM.m == major_majority) textDM
					else fold3DMs(head.dm, ^previousDM, textDM);
				EmptyList(): textDM;
			};
			if (contains(uc_strong_dirs, tdmAgg.d)) previousDM := tdmAgg;
			if (tdmAgg.d == uc_dir_coupled) ^previousDM
			else tdmAgg;
		})));

		// TODO: accumulate only those, which change the direction, not each major word.
		forwardMajorWords : ref List<Pair<BiDiWordAddress, DirectionAndMajority>> = ref foldri(
			lines1,
			Cons(
				Pair(BiDiWordAddress(length(lines1), 0), defaultDM),
				EmptyList()
			),
			\lineNumber, lineAcc, line -> {
				foldri(
					line,
					lineAcc,
					\wordNumber, wordAcc : List<Pair<BiDiWordAddress, DirectionAndMajority>>, word -> {
						currentWordDM = word.dm;
						if (currentWordDM.m == major_majority && contains(uc_strong_dirs, currentWordDM.d))
							Cons(
								Pair(BiDiWordAddress(lineNumber, wordNumber), currentWordDM),
								wordAcc
							)
						else wordAcc
					}
				)
			}
		);

		previousDM := defaultDM;
		mapi(
			lines1, \lineNumber, line -> unwrapLine(
				reflowWordsToBiDi(
					mapi(line, \wordNumber, word -> {
						currentAddr = BiDiWordAddress(lineNumber, wordNumber);
						forwardMajorWords := switch (^forwardMajorWords) {
							EmptyList(): EmptyList();
							Cons(h, t): if (are2AddrsOrdered(h.first, currentAddr)) t else ^forwardMajorWords;
						};
						previousDM := if (word.dm.m >= major_majority) word.dm
						else ^previousDM;
						majorDM = switch (^forwardMajorWords) {
							EmptyList(): defaultDM;
							Cons(h, t): h.second;
						};
						if (word.dm.m == major_majority) word
						else BiDiWord(word with dm = fold3DMs(^previousDM, majorDM, defaultDM));
					}),
					isEmpty, getText, setText, rtl
				), setRTL
			)
		)
	}
}

getListDM(l: List<?>, getText: (?) -> string) -> DirectionAndMajority {
	rtl = getDefaultRtl();
	defaultDir = if (rtl) uc_dir_rtl else uc_dir_ltr;
	defaultDM = DirectionAndMajority(defaultDir, major_majority);
	switch(l) {
		EmptyList(): defaultDM;
		Cons(h, t): getTextDirectionAndMajority(getText(h))
	};
}

// Prepends ituple.first with a reversed list fragment from ituple.third to iiter.
markRange(iiter: List<?>, ituple: Triple<List<Pair<?, int>>, DirectionAndMajority, List<?>>, getText: (?) -> string)
-> Triple<List<Pair<?, int>>, DirectionAndMajority, List<?>> {
	rtl : bool= getDefaultRtl();
	defaultDir : int = if (rtl) uc_dir_rtl else uc_dir_ltr;
	fdir : int = ituple.second.d;
	lDM : DirectionAndMajority = getListDM(iiter, getText);
	dir : int = if (fdir == lDM.d) fdir else defaultDir;

	Triple(
		foldpList(
			ituple.third, Pair(true, ituple.first), \jiter : List<?>, jtuple : Pair<bool, List<Pair<?, int>>>, jw : ? -> {
				if (jtuple.first) {
					jwdm = getTextDirectionAndMajority(getText(jw));
					Pair(
						jiter != iiter,
						Cons(Pair(jw, if (jiter == iiter) lDM.d else if (jwdm.m<1) dir else jwdm.d), jtuple.second)
					)
				} else
					jtuple
			}
		).second,
		lDM,
		switch(iiter) {
			EmptyList(): EmptyList();
			Cons(__, nx): nx;
		},
	)
}

markListToBiDiReverse(list: List<?>, getText: (?) -> string) -> List<Pair<?, int>> {
	rtl = getDefaultRtl();
	defaultDir = if (rtl) uc_dir_rtl else uc_dir_ltr;
	defaultDM = DirectionAndMajority(defaultDir, major_majority);
	markRange(
		EmptyList(),
		foldpList(
			list,
			Triple(
				EmptyList(),
				defaultDM,
				list,
			),
			\iiter, ituple, iw -> {
				if (getTextDirectionAndMajority(getText(iw)).m <= minor_majority) ituple else markRange(iiter, ituple, getText)
			}
		),
		getText
	).first;
}
