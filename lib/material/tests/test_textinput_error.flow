import material/material;
import material/material_manager;
import material/material2tropic;
import material/material_dialog;
import material/material_manager;

MNumericTextInput(value : DynamicBehaviour<double>, name : string, minimal : double, maximal : double) -> Material {
	strValue = make("");
	MConstruct([
			\ -> bidirectionalLink(value, strValue, d2s, s2d)
		],
		MTextInput(strValue, [MWidth(120.), MCondensed(true), MLabel(name), MFloatingLabel()], [
			MInputError(fselect(strValue, FLift(\v -> {
				t = trim(v);
				rt = d2s(s2d(t));
				// stolen from flowschema gui
				// TODO: We do not accept things like
				// "0." ".5", "12.0000" as correct
				if (rt != t && rt != ltrim2(t, "0")) {
					Some(Pair("Expected a number", true));
				} else {
					if (s2d(t) < minimal) {
						Some(Pair("Expected a number\nnot smaller than " + d2s(minimal), true));
					} else if (s2d(t) > maximal) {
						Some(Pair("Expected a number\nnot bigger than " + d2s(maximal), true));
					} else {
						None();
					}
				}
			})), [MMaxLines(2)])
		])
	)
}

main() {
	mrender(makeMaterialManager([]), false,
		MCenter(MLines([
			MNumericTextInput(make(52.0), "", 50.0, 800.0),
			MNumericTextInput(make(10.0), "", doubleMin, doubleMax),
		]))
	);
}