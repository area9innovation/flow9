import material/extra/filesystem/localfilesystem_extended_api;
import material/extra/filesystem/filesystem_extended_api;
import material/extra/filebrowser/material_filebrowser;
import material/material_dialog;

main() {
	manager = makeMaterialManager([]);
	setMFileBrowserAPI(
		manager,
		Some(Pair(const(""), false)),
		Some(Pair(const(""), false)),
		None(),
		[
			FbTopPanelCustomItem(\__, enabled ->
				MTextButton(
					"UPLOAD",
					\-> ShowMConfirmation(makeMaterialManager([]), "Uploading", "OK", "enter", MText("Used in the DB to upload files", [])), 
					[MButtonRaised()],
					[MEnabled(enabled)]
				),
				PathAccessRight(false, true, false, false)
			),
			FbFolderSelection()
		]
	);
	registerMResolveContentAPI([".jpg", ".jpeg", ".png"], \filename -> MPicture(filename, []), []);
	setLocalFilesystemExtAPI();
	
	textB = make("");
	
	onClick = \-> {
		next(textB, "Nothing selected");

		CallMFileBrowser(
			MediaFileDialog(), 
			[".jpg", ".jpeg", ".png", ".txt", ".sql", ".flow", ".form"], 
			[],
			\objects : [FbObject] -> next(textB, "Selected files: " + toString(map(objects, \o -> o.fullPath))),
			nop
		)
	};

	mrender(
		manager,
		false,
		MCenter(
			MLines2(
				MCenterX(MSelect(textB, \text -> MText(text, []))),
				MCenterX(MTextButton("OPEN FILEBROWSER", onClick, [], []))
			)
		)
	);

	onClick();
}