import material/material2tropic;

ShowcaseView ::= ButtonsView, TextView;
	ButtonsView();
	TextView();

main() {
	view = make(TextView());
	manager = makeMaterialManager([]);
	expander = make(false);

	MAppStructure([
		MToolbar(expander, MText("Material Showcase", []), [MSwitchControl(manager.white, [MOnOffText("LIGHT", "DARK"), MWhite()])], [])
		|> MAppBar,
		MSideNav(
			MSNCardHead("", MSNHeadPhoto(const(""), nop), [], const("user"), const("")), 
			MList([
					MSingleLine("Buttons", [MOnClick(\ -> next(view, ButtonsView()))]),
					MSingleLine("Text", [MOnClick(\ -> next(view, TextView()))])
				], [], []), expander, [])
		|> (\f -> MLeftNav(f, expander, [])),
		MContent(
			MSelect(view, \v ->
				switch (v) {
					ButtonsView(): {
						MLines([
							MText("MTextButton", [MHeadline(), getPrimaryColor(manager.focus)])
							|> (\f -> MBorder(0., 0., 0., 36., f)),
							MColsEven([
								MTextButton("FLAT DISABLED", \ -> println("flat click"), [], []),
								MTextButton("FLAT", \ -> println("flat click"), [], []),
								MTextButton("FLAT FULLWIDTH", \ -> println("flat click"), [MFullWidth()], [])
							]),
							MColsEven([
								MTextButton("RAISED DISABLED", \ -> println("raised click"), [MButtonRaised()], []),
								MTextButton("RAISED", \ -> println("raised click"), [MButtonRaised()], []),
								MTextButton("RAISED FULLWIDTH", \ -> println("raised click"), [MButtonRaised(), MFullWidth()], [])
							])
						])
					}
					TextView(): {
						MLines([
							MText("MText", [MHeadline(), getPrimaryColor(manager.focus)])
							|> (\f -> MBorder(0., 0., 0., 36., f)),
							MText("MDisplay2", [MDisplay2()]),
							MText("MDisplay1", [MDisplay1()]),
							MText("MDisplay1Medium", [MDisplay1Medium()]),
							MText("MHeadline", [MHeadline()]),
							MText("MSubheading", [MSubheading()]),
							MText("MTitle", [MTitle()]),
							MText("MBody", [MBody()]),
							MText("MListBody", [MListBody()]),
							MText("MBodyBold", [MBodyBold()]),
							MText("MCaption", [MCaption()]),
							MText("MButtonStyle", [MButtonStyle()]),
							MText("MCaptionSolid", [MCaptionSolid()]),
							MText("MCaptionColumn", [MCaptionColumn()]),
							MText("MCaptionColumnSelected", [MCaptionColumnSelected()]),
							MText("MDataRow", [MDataRow()]),
							MText("MTooltipDesktop", [MTooltipDesktop()]),
						])
					}
				}
			)
			|> (\f -> MBorder(64., 36., 64., 24., f))
		)
		], [])
	|> (\f -> mrender(manager, true, f))
}

MColsEven(items : [Material]) -> MCols {
	MCols(map(items, \it -> MGroup2(TFillX(), MBorder(8., 8., 8., 8., MCenterX(it)))))
}