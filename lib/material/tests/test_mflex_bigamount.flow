import flowutils;
import material/material_ui;

main() {
	setRendererType("html");

	elementsCount = 500;
	hardToRender = false;
	isSameSize = false;

	start = ref 0.;

	showAdditionalText = make(false);

	makeItem = \i, d -> {
		size = TSized(make(d.second));
		MGroup2(
			MCenterIn(MText(i2s(i) + if (i == 2 && getValue(showAdditionalText)) "ADD" else "", [MDisplay1(), MWhite()]), size),
			MGroup2(
				size,
				TFillXY()
			)
		)
		|> (\m ->
			if (i != elementsCount - 1 && i != 14 && i != 2 && i != elementsCount) m else
			MConstruct([\ -> {
				if (hardToRender) generate(0, 1000000, \j -> j * j) |> ignore;
				deferUntilNextFrameRendered(\ -> println("CONS " + i2s(i) + ",  " + d2s(timestamp() - ^start) + " ms "));
				\ -> println("DISP " + i2s(i))
			}], m)
		)
		|> addMBackground(d.first)
	}

	data = generate(0, elementsCount, \i -> 
		Pair(MGreen(200 * (i % 4 + 1)), if  (isSameSize) WidthHeight(200., 200.) else WidthHeight(200. + (random() - 0.5) * 100.0, 200. + (random() - 0.5) * 100.0))
	);

	items = mapi(data, makeItem);

	additionalItem = makeItem(elementsCount, Pair(MBlue(200), WidthHeight(200., 200.)));

	itemsB = make([]);
	showText = make(true);

	showItemsFn = \ -> {
		println("START");
		start := timestamp();
		nextDistinct(showText, false);
		timer(10, \ -> nextDistinct(itemsB, items));
	};
	
	resetFn = \ -> {
		println("RESET");
		start := timestamp();
		nextDistinct(itemsB, []);
		nextDistinct(showText, true);
	}

	addItemStartFn = \ -> {
		println("ADD AT START");
		start := timestamp();
		nextDistinct(itemsB, concat3([getValue(itemsB)[0]], [additionalItem], tail(getValue(itemsB))));
	}

	addItemEndFn = \ -> {
		println("ADD AT END");
		start := timestamp();
		nextDistinct(itemsB, concat(getValue(itemsB), [additionalItem]));
	}

	showAdditionalTextFn = \ -> {
		println("CHANGE ITEM 2");
		start := timestamp();
		nextDistinct(showAdditionalText, true);
		timer(10, \ -> nextDistinct(
			itemsB,
			replace(
				getValue(itemsB),
				2,
				makeItem(2, Pair(MGreen(600), WidthHeight(200., 200.)))
			))
		);
	}

	content = 
		MGroup2(
			MFlex(itemsB, ifArrayPush([MOrdered(true)], isSameSize, MSameSize())),
			MShow(showText, MCenter(MText("EMPTY", [MHeadline()])))
		)
		|> (\m -> MScroll(m, TFillXY(), []))
		|> addMBackground(MGrey(400))
		|> MBorderLeftRight(200.)
		|> (\m -> MGroup2(m, MLines([
				MTextButton("SHOW ITEMS", showItemsFn, [], []),
				MTextButton("RESET", resetFn, [], []),
				MTextButton("ADD ITEM START", addItemStartFn, [], []),
				MTextButton("ADD ITEM END", addItemEndFn, [], []),
				MTextButton("CHANGE ITEM 2", showAdditionalTextFn, [], []),
			])));

	mrender(makeMaterialManager([]), true, content);
}