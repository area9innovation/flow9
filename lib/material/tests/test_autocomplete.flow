import material/material2tropic;

main() {
	manager = makeMaterialManager([]);

	makeAutocomplete : (string, DynamicBehaviour<string>) -> Material = \tag, selectedB -> {
		fpprint(tag)(selectedB);

		MAutoComplete(
			selectedB,
			[
				"test a",
				"test b",
				"test c",
				"another test"
			],
			[MWidth(350.0)],
			[]
		)
		|> (\m -> MBorder4(20., m))
	};

	showAutocompleteB : DynamicBehaviour<bool> = make(false);
	showAutocompleteBM : DynamicBehaviour<Maybe<bool>> = make(None());

	content = MLines([
		MText("Direct MAutoComplete", []),
		MFixedY(10.0),
		makeAutocomplete("Direct MAutoComplete", make("")),
		MSeparator(true),
		MText("MAutoComplete within MSelect", []),
		MFixedY(10.0),
		MSelect(
			showAutocompleteB,
			\__ -> makeAutocomplete("MAutoComplete within MSelect", make("")),
		),
		MFixedY(5.0),
		MSeparator(true),
		MBorder4(
			5.0,
			MCols([
				MFixedX(10.0),
				MSwitchControl(
					showAutocompleteB,
					[
						MOnOffText("Shown", "Hidden")
					]
				)
			])
		),
		MFixedY(5.0),
		MText("MAutoComplete within IF within MSelect", []),
		MFixedY(10.0),
		MSelect(
			showAutocompleteB,
			\showAutocomplete -> {
				if (showAutocomplete) makeAutocomplete("MAutoComplete within IF within MSelect", make("")) else MText("Toggle the Switch", [MRed(200)])
			},
		),
		MFixedY(5.0),
		MSeparator(true),
		MFixedY(5.0),
		MText("MAutoComplete within 'switch' within MSelect", []),
		MFixedY(5.0),
		MConstruct(
			[
				\-> fconnectSelect(showAutocompleteB, showAutocompleteBM, \v -> if (v) Some(v) else None())
			],
			MSelect(
				showAutocompleteBM,
				\showAutocompleteM -> {
					switch (showAutocompleteM : Maybe<bool>) {
						None(): MText("Toggle the Switch", [MRed(200)]);
						Some(__): makeAutocomplete("MAutoComplete within 'switch' within MSelect", make(""))
					}
				},
			)
		)
	])
	|> (\m -> MBorder4(10.0, m));

	mrender(manager, true, content);
}