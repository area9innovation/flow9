import flowutils;
import material/material_ui;

main() {

	elementsCount = 300;
	hardToRender = false;
	isSameSize = false;

	start = ref 0.;

	showAdditionalText = make(false);

	makeItem = \i, d -> {
		size = TSized(const(d.second));
		MGroup2(
			MCenterIn(MText(i2s(i) + if (i == 2 && getValue(showAdditionalText)) "ADD" else "", [MDisplay1(), MWhite()]), size),
			size
		)
		// |> (\m -> if (i == 2) MGroup2(m, MShow(showAdditionalText, MText("Additional text", []))) else m)
		|> (\m ->
			if (i != elementsCount - 1 && i != 14 && i != 2) m else
			MConstruct([\ -> {
				if (hardToRender) generate(0, 1000000, \j -> j * j) |> ignore;
				deferUntilNextFrameRendered(\ -> println("CONS " + i2s(i) + ",  " + d2s(timestamp() - ^start) + " ms "));
				\ -> println("DISP " + i2s(i))
			}], m)
		)
		|> addMBackground(d.first)
	}

	data = generate(0, elementsCount, \i -> 
		Pair(MGreen(200 * (i % 4 + 1)), WidthHeight(200., 200.))
	);

	items = mapi(data, makeItem);

	additionalItem = makeItem(elementsCount, Pair(MBlue(200), WidthHeight(200., 200.)));

	itemsB = make([]);
	showText = make(true);

	showItemsFn = \ -> {
		println("START");
		start := timestamp();
		nextDistinct(showText, false);
		timer(10, \ -> nextDistinct(itemsB, items));
	};
	
	resetFn = \ -> {
		println("RESET");
		start := timestamp();
		nextDistinct(itemsB, []);
		nextDistinct(showText, true);
	}

	addItemStartFn = \ -> {
		println("ADD AT START");
		start := timestamp();
		nextDistinct(itemsB, concat([additionalItem], getValue(itemsB)));
	}

	addItemEndFn = \ -> {
		println("ADD AT END");
		start := timestamp();
		nextDistinct(itemsB, concat(getValue(itemsB), [additionalItem]));
	}

	showAdditionalTextFn = \ -> {
		println("CHANGE ITEM 2");
		start := timestamp();
		nextDistinct(showAdditionalText, true);
		timer(10, \ -> nextDistinct(
			itemsB,
			replace(
				getValue(itemsB),
				2,
				makeItem(2, Pair(MGreen(600), WidthHeight(200., 200.)))
			))
		);
	}

	content = 
		MGroup2(
			MFlexibleGrid(itemsB, ifArrayPush([MKeepOrder()], isSameSize, MSameSize())),
			MShow(showText, MCenter(MText("EMPTY", [MHeadline()])))
		)
		|> (\m -> MScroll(m, TFillXY(), []))
		|> addMBackground(MGrey(400))
		|> MBorderLeftRight(200.)
		|> (\m -> MGroup2(m, MLines([
				MTextButton("SHOW ITEMS", showItemsFn, [], []),
				MTextButton("RESET", resetFn, [], []),
				MTextButton("ADD ITEM START", addItemStartFn, [], []),
				MTextButton("ADD ITEM END", addItemEndFn, [], []),
				MTextButton("CHANGE ITEM 2", showAdditionalTextFn, [], []),
			])));

	mrender(makeMaterialManager([]), true, content);
}