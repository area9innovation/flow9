import flowutils;
import material/material_ui;

main() {

	elementsCount = 399;
	hardToRender = false;
	isSameSize = true;

	start = ref 0.;

	data = generate(0, elementsCount, \i -> 
		Pair(MGreen(200 * (i % 4 + 1)), WidthHeight(200., 200.))
	);

	items = mapi(data, \i, d -> {
		size = TSized(const(d.second));
		MGroup2(
			MCenterIn(MText(i2s(i), [MDisplay1(), MWhite()]), size),
			size
		)
		|> (\m -> MConstruct([\ -> {
				if (hardToRender) generate(0, 1000000, \j -> j * j) |> ignore;
				deferUntilNextFrameRendered(\ -> println("CONS " + i2s(i) + ",  " + d2s(timestamp() - ^start) + " ms "));
				\ -> println("DISP " + i2s(i))
			}], m))
		|> addMBackground(d.first)
	});

	itemsB = make([]);
	showText = make(true);

	showItemsFn = \ -> {
		println("START");
		start := timestamp();
		nextDistinct(showText, false);
		timer(10, \ -> nextDistinct(itemsB, items));
	};
	
	resetFn = \ -> {
		println("RESET");
		nextDistinct(itemsB, []);
		nextDistinct(showText, true);
	}

	content = 
		MGroup2(
			MFlexibleGrid(itemsB, ifArrayPush([MKeepOrder()], isSameSize, MSameSize())),
			MShow(showText, MCenter(MText("EMPTY", [MHeadline()])))
		)
		|> (\m -> MScroll(m, TFillXY(), []))
		|> addMBackground(MGrey(400))
		|> MBorderLeftRight(200.)
		|> (\m -> MGroup2(m, MLines2(
				MTextButton("SHOW ITEMS", showItemsFn, [], []),
				MTextButton("RESET", resetFn, [], [])
			)));

	mrender(makeMaterialManager([]), true, content);
}