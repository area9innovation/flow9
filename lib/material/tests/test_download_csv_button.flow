import material/material_ui;

main() {
	setRendererType("html");
	manager = makeMaterialManager([]);

	content =
		MDownloadCSVButton([], println)
		|> MBorderRight(50.)
		|> MAlignRight;

	mrender(manager, true, content);
}

MDownloadCSVButton(state: [MButtonState], handler : (sep : string) -> void) -> Material {
	button = MTextButton(_("DOWNLOAD CSV (,)"), \-> handler(","), [MButtonPaddings(12.0, 12.0, 12.0, 12.0)], state);
	iconButton =
		MIconButton("arrow_drop_down", nop, [MPrimaryColor()],
			arrayPush(state, MTooltipText(const(_("Choose another separator for export"))))
		);
	choices = map(
		[
			Pair(_("Semicolon-separated"), ";"),
			Pair(_("Tab-separated"), "\t")
		],
		\ch -> MMenuSingleLine(ch.first, [MOnClick(\-> handler(ch.second))])
	);

	MSeparatedButton(button, iconButton, choices, false);
}