import material/material2tropic;
import material/material_ui;

main() {
	manager = makeMaterialManager([]);

	boxWidth = make(200.);
	boxHeight = make(200.);

	overlap = make(0);
	filler = make(true);

	scrollContent =
		fif(filler,
			const(TFillXH(300.)),
			const(TFixed(200., 300.))
		);

	scroll =
		MSelect(overlap, \ov ->
			MScroll(
				MGroup2(
					TMutable(scrollContent),
					MText("Some test text for scroll content", [])
				)
				|> addMBackground(MGreen(200)),

				TSizedWidthHeight(boxWidth, boxHeight),
				
				if (ov == 0) []
				else if (ov == 1) [TScrollbarOverlapContent(true)]
				else [TScrollbarOverlapContent(false)]
			)
		)
		|> MDebugPurple;

	content =
		MLines([
			MBaselineCols2(
				MText("Box width :  ", []),
				MSlider(boxWidth, [MSliderRange(10., 400.), MWidth(200.)])
			),
			MBaselineCols2(
				MText("Box height : ", []),
				MSlider(boxHeight, [MSliderRange(10., 400.), MWidth(200.)])
			),
			MBaselineCols2(
				MText("Content : ", []),
				MSwitchControl(filler, [MOnOffText("TFillXH(300.)", "TFixed(200., 300.)")])
			),
			// 
			MRadios(overlap, [], [
				MText("Default", []),
				MText("TScrollbarOverlapContent(true)", []),
				MText("TScrollbarOverlapContent(false)", []),
			])
			|> MCols,
			scroll
		]);
	
	mrender(manager, true, MBorder4(20., content));
}