import material/material2tropic;

main() {
	manager = makeMaterialManager([]);
	setRendererType("html");

	n = 10000;
	nextN = make(n);

	inputArray = generate(1, n, \i -> i);
	inputArrayB = make(inputArray);

	isFFold = make(true);
	useHeavyCalculations = make(true);
	
	foldFn = \acc, v -> {
		r = acc * v;
		if (getValue(useHeavyCalculations)) {
			generate(0, 10000, \i -> i * i) |> ignore;
		}
		r
	}

	folded1 = ffold(inputArrayB, 1, foldFn);
	folded2 = fselectLift(inputArrayB, \arr -> fold(arr, 1, foldFn));

	addValue = \ -> {
		start = timestamp();

		dynArrayPush(inputArrayB, getValue(nextN));

		end = timestamp();
		println("________");
		pprint("ffold is used : ")(getValue(isFFold));
		pprint("heavy calculations : ")(getValue(useHeavyCalculations));
		println("on added new value : " + d2s(end - start) + "ms");

		nextDistinct(nextN, getValue(nextN) + 1);
	}

	changeValues = \ -> {
		start = timestamp();

		increment = getValue(nextN);
		nextDistinct(inputArrayB, map(getValue(inputArrayB), \v -> v + increment));

		end = timestamp();
		println("________");
		pprint("ffold is used : ")(getValue(isFFold));
		pprint("heavy calculations : ")(getValue(useHeavyCalculations));
		println("on changed all values : " + d2s(end - start) + "ms");

		nextDistinct(nextN, getValue(nextN) * 2);
	}

	content =
		MLines([
			MText("n : " + i2s(n), []) |> MBorderLeft(16.),
			MTextButton("ADD SINGLE VALUE", addValue, [], []),
			MTextButton("CHANGE ALL VALUES", changeValues, [], []),
			MSwitchControl(isFFold, [MOnOffText("Use ffold", "Use fselect + fold")]),
			MCheckBox(MText("Heavy calculations", []), useHeavyCalculations, [])
		])
		|> (\m -> MConstruct([makeSubscribe(fif(isFFold, folded1, folded2), nop1)], m));

	mrender(manager, true, content);
}