import material/material2tropic;

main() {
	manager = makeMaterialManager([]);

	expander = make(false);
	showBS = make(false);

	bottomNav =
		MBottomNav([
				MBottomNavAction("android", "Android", [MOnClick(\ -> println("android"))]),
				MBottomNavAction("close", "Close", [MOnClick(\ -> println("close"))]),
				MBottomNavAction("menu", "Menu", [MOnClick(\ -> println("menu")), MIconCounter(const(8), [])]),
				MBottomNavAction("add", "Add", [MOnClick(\ -> println("add"))]),
			],
			[
				Fill(0xadadad),
				FillOpacity(0.6),
				Blur([Spread(10.)]),
				MActiveIconColor(MBlack())
			],
			make(-1),
			make(true)
		);

	content =
		MLines([
			MCols([
				MText("CONTENT", []),
				MTextButton("OPEN MODAL", \ -> reverseBehaviour(showBS), [], []),
				TFillX(),
				MText("RIGHT SIDE", [])
			]),
			TRectangle([GradientFill(90.0, [GradientPoint(blue, 1.0, 0.0), GradientPoint(yellow, 1.0, 1.0),])], TFixed(100., 1000.)),
			TRectangle([MFill(MBlack())], TFixed(20., 20.))
		]);

	toolbar = MToolbar(
		expander,
		MText("TEXT", []),
		[],
		[]
	);

	app = MAppStructure([
			MContent(content),
			MAppBar(toolbar),
			MBottomSheet(\ex ->
				MGroup2(
					TFillX(),
					MLines2(
						MDynamicText(fif(ex, const("EXPANDED"), const("HALF-EXPANDED")), []),
						generate(0, 30, \i -> MText(i2s(i), []) |> MBorderTopBottom(16.)) |> MLines
					)
				),
				[MSwipeEnabled(const(true)), MModalBottomSheet(showBS), MAboveFloatingContent()]
			),
			MFloatingContent(bottomNav)
		], [
			// MAppStructureFlexible([]),
			// MNoScroll()
		]);
	
	mrender(manager, true, app);
}