import material/material;
import material/material_manager;
import material/material2tropic;
import material/material_dialog;
import material/material_manager;
main() {
	manager = makeMaterialManager([]);
	selected = make([1, 2]);
	fpprint("selected")(selected);
	opened = make(false);

	items = ["item 0", "item 1", "item 2", "item 3", "item 4", "item 5", "item 6", "item 7", "item 8"];
	
	dropDown1 =
		MMultiSelectDropDown(
			selected,
			items,
			[
				MOpenMenu(opened),
				MAdditionalButtons([
					MTextButton("CLEAR", \ -> nextDistinct(selected, []), [], []),
					TFillX(),
					MTextButton("APPLY", \ -> nextDistinct(opened, false), [MContained()], []),
				]),
				MLeftDecorations(
					mapi(items, \i, item -> {
						checked = make(false);
						MConstruct(
							[\ -> fBidirectionalLink(
								fselect(selected, FLift(\s -> contains(s, i))),
								checked,
								\ch -> nextDistinct(checked, ch),
								\ch -> nextDistinct(selected, if (ch) uniq(arrayPush(fgetValue(selected), i)) else removeAll(fgetValue(selected), i))
							)],
							MCheckBox(TEmpty(), checked, [MIconButtonBorder(4.)])
						)
					})
				),
				MShowSelectedIcon(false),
				// MCustomButton(MSelect2(flength(selected), opened, \len, op ->
				// 	MTextButton("Custom Buttom (" + i2s(len) + ")", nop, [MWidth(300.), MRightIcon(if (op) "arrow_drop_up" else "arrow_drop_down", [])], [])
				// ))
				MCustomButton(
					MColsYCenterA([
						MSelect(flength(selected), \len -> MText("Custom Buttom (" + i2s(len) + ")", [])),
						TFixed(48., 0.),
						MDropDownIcon(opened, [])
					])
					|> (\m -> MBorder4(8., m))
				),
				MMenuLineBorders(32.),
				MBelowButton(),
				MWidth(500.)
			]
		);


	mrender(manager, true, dropDown1 |> MCenter);
}
