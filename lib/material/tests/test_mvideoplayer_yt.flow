import material/material2tropic;
import material/material_ui;

main() {
	setWindowTitle("YT Tester");
	setRendererType("html");

	setAppUrl("./");
	manager = makeMaterialManager([]);

	yt = "https://www.youtube.com/watch?v=9xmLpHZG5gI&rel=0&autoplay=1&fs=0&loop=1&start=45&end=52&controls=2&mute=0";

	availWH = makeWH();
	playB = make(false);
	lengthB = make(0.0);
	positionB = make(0.0);
	fullscreenB = make(false);
	loopB = make(false);
	volumeB = make(1.0);

	ytPlayer = MVideoPlayer(yt, availWH, [
		FVideoPlay(playB),
		FVideoPosition(positionB),
		FVideoFullscreen(fullscreenB),
		FVideoLoop(loopB),
		FVideoVolume(volumeB),
		FVideoLength(lengthB),
		FVideoControls([FullScreenPlayer()]),
		MPlayerYoutubeJsApi()
	]);

	content = MCols([
		ytPlayer
			|> (\m -> MAttachAvailable(m, availWH))
			|> (\m -> MFrame(16., 0., [MStroke(MGrey(500))], m)),

		MLines([
			MIf(playB,
				MTextButton("PAUSE", \ -> next(playB, false), [], []),
				MTextButton("PLAY", \ -> next(playB, true), [], []),
			) |> MBorderBottom(8.0),

			MSlider(volumeB, [
				MSliderRange(0.0, 1.0),
				MSliderStep(0.05),
			]) |> MBorderBottom(8.0),

			MSelect(lengthB, \vl ->
				MSlider(positionB, [
					MSliderRange(0.0, vl),
					MSliderStep(0.05),
				])
			) |> MBorderBottom(8.0),
		])
			|> (\m -> MAvailable(m, TFixed(400., 400.)))
			|> (\m -> MBorder4(8.0, m)),
	]);

	mrender(manager, true, content);
}

