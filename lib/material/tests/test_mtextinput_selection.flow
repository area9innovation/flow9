import material/material2tropic;

main() {
	manager = makeMaterialManager([]);
	setRendererType("html");

	content = generate(0, 3, makeInput) |> MLines;

	mrender(manager, true, content);
}


makeInput(id : int) -> Material {
	defaultValue = "This is some text text text";
	text = make(defaultValue);

	textPosition = make(-1);
	fpprint(i2s(id) + ". textPosition")(textPosition);

	textSelection = make(-1);
	fpprint(i2s(id) + ". textSelection")(textSelection);

	selections = make([]);

	MTextInput(
		text,
		[],
		[
			TTextInputPosition(textPosition),
			TTextInputSelection(textSelection),
			FInputOnSelect(\start, end -> dynArrayPush(selections, "Selected from " + i2s(start) + " to " + i2s(end))),
			FInputOnSelectAll(\ -> dynArrayPush(selections, "Selected all"))
		]
	)
	|> (\m -> MBorder4(8., m))
	|> (\m -> MLines([
			m,
			MCols2(
				MText("POSITION : ", []),
				MDynamicText(fselect(textPosition, FLift(i2s)), [])
			),
			MCols2(
				MText("SELECTION : ", []),
				MDynamicText(fselect(textSelection, FLift(i2s)), [])
			),
			MSelect(selections, \selects ->
				map(selects, \selection ->
					MText(selection, [])
				)
				|> MLines
			)
		]));
}