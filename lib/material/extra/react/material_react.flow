import material/material2tropic;

export {
	// This component imports /js/react_bundle.js file, which should be created from 'react', 'react-dom' and any number of different libraries, bundled together.
	// The easiest way to make this bundle by hand is copying content of .js file, distributed by CDN
	// (for example, for 'react'/'react-dom' - https://unpkg.com/react/umd/react.production.min.js and https://unpkg.com/react-dom/umd/react-dom.production.min.js)
	// If you use your custom .jsx, transpile it it to .js beforehand, using Babel. For example, you can use https://babeljs.io/repl
	// Make sure, that library/component is available from global object(window).

	// As 'element' you can use 
	// 1. Simple DOM element (lowercase)
	// 2. Name of React-component, exported from library by default (uppercase)
	// 3. <Lib>.<Component> format is also supported
	// See test_mreact.flow for usage examples.
	
	MReact(
		element : string,
		props : JsonObject,
		state : Tree<string, DynamicBehaviour<Json>>,
		listeners : [ReactListener]
	) -> Material;

	ReactListener(
		name : string,
		fn : (event : native) -> void // Use getReactEventAttribute to have an access to event's attributes
	);
}

MReact(element : string, props : JsonObject, state : Tree<string, DynamicBehaviour<Json>>, listeners : [ReactListener]) -> Material {
	wh = makeWH();
	TFForm(
		FReact(wh, element, props, state, listeners),
		TFormMetrics(
			fwidth(wh),
			fheight(wh),
			fheight(wh)
		)
	)
}

FReact(wh : DynamicBehaviour<WidthHeight>, element : string, props : JsonObject, state : Tree<string, DynamicBehaviour<Json>>, listeners : [ReactListener]) -> FForm {
	metrics = make(FormMetrics(0., 0., 0., 0.));
	blockResponse = ref false;

	FNativeForm(
		FEmpty(),
		metrics,
		\ -> FEmpty(),
		\__, __, __ -> {
			propsStr = json2string(props);
			stateInit = foldTree(state, [], \k, v, acc -> arrayPush(acc, Pair(k, getValue(v))));
			stateInitStr = json2string(JsonObject(stateInit));

			onStateChange = \str -> {
				blockResponse := true;
				stateJson = parseJsonSafe(str);
				fields = getJsonObjectValue(stateJson, []);
				iter(fields, \field -> {
					maybeApply(lookupTree(state, field.first), \stateValue -> nextDistinct(stateValue, field.second))
				});
				blockResponse := false;
			}

			container = makeReactContainer(element, propsStr, stateInitStr, onStateChange);
			
			uns = makeSubscribe(metrics, \mt -> nextDistinct(wh, WidthHeight(mt.width, mt.height)))();
			unsTree = mapTree2(state, \key, val -> {
				makeSubscribe2(val, \v -> {
					if (!^blockResponse) {
						updateReactState(container, key, json2string(v));
					}
				}
				)()
			});

			iter(listeners, \listener -> setReactListener(container, listener.name, listener.fn));

			unsResizeListener = addExtendedEventListener(container, "resize", \met -> {
				wd = met[0];
				hgt = met[1];
				nextDistinct(metrics, FormMetrics(wd, hgt, hgt, hgt));
			});

			NativeRenderResult(
				[container],
				\ -> {
					uns();
					traverseInOrder(unsTree, \__, unsFn -> unsFn());
					unsResizeListener();
				}
			)
		}
	)
}