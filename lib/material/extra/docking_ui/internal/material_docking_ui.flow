import material/material;
import material/internal/types;
import material/internal/material_util;

import material/extra/docking_ui/internal/material_docking_state;
import material/extra/docking_ui/internal/material_docking_styles;
import material/extra/docking_ui/internal/material_docking_theme;
import material/extra/docking_ui/internal/material_docking_areas;

export {

	// USAGE:
	// - Set up a dockable UI defining two main elements, "panels" and "layout", and stores layout changes into a "state".
	// - Define your panels as of array of MDockingPanels, each one differs for the content type (MDockingPanelItem)
	// - Inizialize the MDockingState with panel's information using "makeMDockingState" function
	// - Design your layout using MDockingAreas as dockable areas.

	// NOTE: example in flow9/lib/material/tests/test_docking_ui.flow

	MDockingUI(state : MDockingState, panelsA : [MDockingPanel], layout : Material, style : [MDockingStyle]) -> Material;
}


MDockingUI(state : MDockingState, panelsA : [MDockingPanel], layout : Material, style : [MDockingStyle]) -> Material {
	// Update state
	uiStyle = concat(style, state.style);
	cacheEnabled = extractStruct(uiStyle, MDockingEnableLoadAndStoreCache("", false)).cacheEnabled;
	updateMDockingState(state, panelsA, cacheEnabled, uiStyle);

	MSelect(state.redraw, \__ -> {
		manager = state.manager;
		titleA = state.title;
		panelIconA = state.panelIcon;
		contentA = state.content;
		dockA = state.dock;
		groupA = state.group;
		sizeA = state.size;
		mPositionsA = state.positions;
		expandedA = state.expanded;
		panelsStyleA = state.panelStyle;

		onDrop = make(-1);
		localPositionA = make([]);
		minimizedA = make([]);
		visibleA = make([]);
		defSizeA = make([]);
		start = make(false);
		positionScale = make(zeroPositionScale);
		zorder = getValue(manager.manager.zorder);
		isDragging = manager.manager.dragging;
		isDraggingT = fselect(isDragging, FLift(\dr -> (dr >= 0) && (dr < length(panelsA)) && getValue(dockA)[dr].areaId >= 0 && zorder >= getValue(manager.manager.zorder)));

		iteri(panelsA, \i, p -> {
			next(localPositionA, replace(getValue(localPositionA), i , zeroPoint));
		});

		//overlay layout
		movableDropArea = 
			MConstruct([
				// Redraw UI
				makeSubscribe2(state.loadWorkspace, \__ -> {
					updateMDockingState(state, panelsA, cacheEnabled, uiStyle);
				}),
				// Update borwser cache
				make4Subscribe2(
					state.dock,
					fmerge(map(getValue(mPositionsA), \p -> p)),	
					fmerge(map(getValue(state.enabled), \e -> e)),
					fmerge(map(getValue(state.expanded), \ex -> ex)),
					// fmerge(map(getValue(state.sideExpanded), \se -> se)),
					\__, __, __, __ -> {
						if (cacheEnabled) setMDockingStateKeyValue(state) |> ignore;
					}
				),
				// makeSubscribe(state.dock, \d -> {println(d)}),
				makeSubscribe(onDrop, \d -> {
					if (d >= 0) {
						pstyle = panelsA[d].style;
						width = getValue(sizeA)[d].width;
						height = getValue(sizeA)[d].height;
						posCorrection = if (getValue(dockA)[d].areaId >= 0) {
							localPos = elementAt(getValue(localPositionA), d, zeroPoint);
							dx = if (d >= 0) {
								localPos.x;
							} else {
								0.0;
							}; 
							dy = 4.0; 
							Point(dx, dy);
						} else {
							getValue(localPositionA)[d];
						}
						next(getValue(mPositionsA)[d], Point(
							manager.manager.xy().x - posCorrection.x - getValue(positionScale).pos.x,
							manager.manager.xy().y - posCorrection.y - getValue(positionScale).pos.y
						));

						next(getValue(state.enabled)[d], true);
						if (getValue(dockA)[d].areaId >= 0) undockPanel(state, d); //undock panel
						next(localPositionA, replace(getValue(localPositionA), d, zeroPoint));
					}
				})
			],
			MShow(isDraggingT,
				MDropSpot2(make(-1), onDrop, TRectangle(interactiveRectangleStyle, TFillXY()), false)
			));

		movablePanelsLayer = mapi(panelsA, \i, p -> makeMovablePanel(state, i, localPositionA));

		layoutBoxed = layout 
			|> (\m -> { 
				b = extractStruct(uiStyle, MDEditorBorders(dockingGap, dockingGap, dockingGap, dockingGap));
				MFrame(0.0, 0.0, [MFill(editorBgColorFn(true, state.style))], 
					MBorder(b.left, b.top, b.right, b.bottom, m)
				)
			});

		MInteractive([TMouseXY(state.mouseXy)],
			MGroup(concatA([
				// [makeMDockingDebugPrints(state)],        // Uncomment for debug prints
				[TPositionScale(positionScale, TRectangle([MFill(MGrey(100))], TFillXY()))],
				[MScroll(layoutBoxed, TFillXY(), [])],
				movablePanelsLayer,
				[movableDropArea]
			]))
		)
	})
}



// Update MDockingState collecting data from panels and areas
updateMDockingState(state : MDockingState, panelsA : [MDockingPanel], cacheEnabled : bool, style : [MDockingStyle]) -> void {
	titleA = state.title;
	panelIconA = state.panelIcon;
	contentA = state.content;
	dockA = state.dock;
	groupA = state.group;
	mPositionsA = state.positions;
	enabledA = state.enabled;
	expandedA = state.expanded;
	panelsStyleA = state.panelStyle;
	offsetP = extractStruct(style, MDMobileOffset(Point(72.0, 72.0))).offsetPosition;

	// Cache
	keyGroupName = extractStruct(style, MDockingEnableLoadAndStoreCache("", false)).keyGroupName;
	savedString = getKeyValue(keyGroupName |> addDockingSuffix, "");
	currentWorkspace = getJsonFieldValue(parseJson(savedString), "Current", JsonNull());
	cachedPanelA = mapi(panelsA, \panelId, __ -> 
		getJsonFieldValue(parseJson(getJsonStringValue(currentWorkspace, "")), i2s(panelId), JsonNull())
	);


	// End Alignment
	alignToolbar = \pId : int  -> {
		pstyle = getValue(state.panelStyle)[pId];
		isEndAligned = getValue(extractStruct(pstyle, MDToolbarEndAlign(make(false))).endAlign);
		cEndAligned = getJsonBoolValue(getJsonFieldValue(cachedPanelA[pId], "endAligned", JsonNull()), isEndAligned);
		endAligned = if (cacheEnabled) cEndAligned else isEndAligned;

		nextDistinct(state.panelStyle, replace(getValue(panelsStyleA), pId , 
			replaceStruct(getValue(panelsStyleA)[pId], MDToolbarEndAlign(make(endAligned)))
		));
	}

	// Panel's size
	getPanelSize = \p : MDockingPanel, pId : int -> {
		cWidth = getJsonDoubleValue(getJsonFieldValue(cachedPanelA[pId], "w", JsonNull()), doubleMax);
		cHeight = getJsonDoubleValue(getJsonFieldValue(cachedPanelA[pId], "h", JsonNull()), doubleMax);
		pWidth = if (cacheEnabled && (cWidth != doubleMax)) cWidth else p.size.width;
		pHeight = if (cacheEnabled && (cHeight != doubleMax)) cHeight else p.size.height;

		cDockedWidth = getJsonDoubleValue(getJsonFieldValue(cachedPanelA[pId], "dockedW", JsonNull()), doubleMax);
		cDockedHeight = getJsonDoubleValue(getJsonFieldValue(cachedPanelA[pId], "dockedH", JsonNull()), doubleMax);
		dockedWidth = if (cacheEnabled && (cDockedWidth != doubleMax)) cDockedWidth else p.size.width;
		dockedHeight = if (cacheEnabled && (cDockedHeight != doubleMax)) cDockedHeight else p.size.height;

		cMobileWidth = getJsonDoubleValue(getJsonFieldValue(cachedPanelA[pId], "mobileW", JsonNull()), doubleMax);
		cMobileHeight = getJsonDoubleValue(getJsonFieldValue(cachedPanelA[pId], "mobileH", JsonNull()), doubleMax);
		mobileWidth = if (cacheEnabled && (cMobileWidth != doubleMax)) cMobileWidth else p.size.width;
		mobileHeight = if (cacheEnabled && (cMobileHeight != doubleMax)) cMobileHeight else p.size.height;

		next(state.size, replace(getValue(state.size), pId , WidthHeight(pWidth, pHeight)));
		next(state.mobileSize, replace(getValue(state.mobileSize), pId , WidthHeight(mobileWidth, mobileHeight)));
		next(state.dockedSize, replace(getValue(state.dockedSize), pId , WidthHeight(dockedWidth, dockedHeight)));
	}

	// Update UI layout
	iteri(panelsA, \i, p -> {

		cachedPanel = cachedPanelA[i];
		
		// Docking positions
		cAreaId = getJsonIntValue(getJsonFieldValue(cachedPanel, "areaId", JsonNull()), intMax);
		cColId = getJsonIntValue(getJsonFieldValue(cachedPanel, "colId", JsonNull()), intMax);
		cRowId = getJsonIntValue(getJsonFieldValue(cachedPanel, "rowId", JsonNull()), intMax);
		aId = if (cacheEnabled && cAreaId != intMax) cAreaId else p.panelDock.areaId;
		cId = if (cacheEnabled && cColId != intMax) cColId else p.panelDock.colId;
		rId = if (cacheEnabled && cRowId != intMax) cRowId else p.panelDock.rowId;
		next(dockA, replace(getValue(dockA), i , MDock(aId, cId, rId)));


		// Panels
		next(titleA, replace(getValue(titleA), i , p.title));
		next(panelIconA, replace(getValue(panelIconA), i , extractStruct(p.style, MDPanelIcon("web_asset")).icon));
		next(contentA, replace(getValue(contentA), i , p.content));
		next(groupA, replace(getValue(groupA), i ,  extractStruct(p.style, MDPanelAllowedGroups([])).allowedGroups));
		
		// End Alignment
		defPStyle = addMissingStruct(p.style, MDToolbarEndAlign(make(false)));
		nextDistinct(state.panelStyle, replace(getValue(panelsStyleA), i , defPStyle));
			
		switch (p.content) {
			// Toolbar
			MDToolbarContent(buttonsA) : {
				alignToolbar(i);
				minWidth = extractStruct(p.style, MDToolbarThickness(dockingItemHeight)).thickness;
				panelHeight = minWidth * i2d(length(buttonsA));
				// NOTE: Toolbar size is given in vertical orientation, as a panel
				size = WidthHeight(minWidth, panelHeight);
				next(state.size, replace(getValue(state.size), i , size));
				next(state.dockedSize, replace(getValue(state.dockedSize), i , size));
				next(state.mobileSize, replace(getValue(state.mobileSize), i , size));
			}
			// Content
			MDPanelContent(__): {
				getPanelSize(p, i);
			}
		};

		// Expanded
		expandedB = extractStruct(p.style, MDPanelExpanded(make(true))).expanded;
		cExpanded = getJsonBoolValue(getJsonFieldValue(cachedPanelA[i], "expanded", JsonNull()), getValue(expandedB));
		expB = if (cacheEnabled) make(cExpanded) else expandedB;
		next(state.expanded, replace(getValue(state.expanded), i , expB));

		// Side Expanded
		sideExpandedB = extractStruct(p.style, MDPanelSideExpanded(make(true))).sideExpanded;
		csideExpanded = getJsonBoolValue(getJsonFieldValue(cachedPanelA[i], "sideExpanded", JsonNull()), getValue(sideExpandedB));
		sExpB = if (cacheEnabled) make(csideExpanded) else sideExpandedB;
		next(state.sideExpanded, replace(getValue(state.sideExpanded), i , sExpB));

		// Positions
		zeroPosition = Point(
			offsetP.x + 36.0 * i2d(i),
			offsetP.y + dockingItemHeight * i2d(i)
		);
		positionB = extractStruct(p.style, MDPanelPosition(make(zeroPosition))).position;
		cPosX = getJsonDoubleValue(getJsonFieldValue(cachedPanelA[i], "x", JsonNull()), getValue(positionB).x);
		cPosY = getJsonDoubleValue(getJsonFieldValue(cachedPanelA[i], "y", JsonNull()), getValue(positionB).y);
		posB = if (cacheEnabled) {
			make(Point(cPosX , cPosY))
		} else {
			positionB;
		}
		next(mPositionsA, replace(getValue(mPositionsA), i , posB));

		// Enabled
		enabledB = extractStruct(p.style, MDPanelEnabled(make(true))).enabled;
		cEnabled = getJsonBoolValue(getJsonFieldValue(cachedPanelA[i], "enabled", JsonNull()), getValue(enabledB));
		enB = if (cacheEnabled) make(cEnabled) else enabledB;
		next(state.enabled, replace(getValue(state.enabled), i , enB));
		// Undock disabled panels by default
		// if (!getValue(enB)) undockPanel(state, i);
	});

	next(state.redraw, !getValue(state.redraw));
}