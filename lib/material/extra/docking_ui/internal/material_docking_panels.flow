import material/material2tropic;
import material/extra/docking_ui/internal/material_docking_utils;
import material/extra/docking_ui/internal/material_docking_drop;

export {
	// NOTE: example in flow9/lib/material/tests/test_docking_ui.flow

	// Each panel can be movable or docked, and it's defined by:
	MDockingPanel(
		// note: if content is [MDockingPanelToolbarItem] is not used
		title : string,
		// creates easily different standard panels as array of MDockingPanelItems
		content : MDockingPanelContent,
		// store the position where the panel is docked
		panelDock : MDock,
		// starting panel size
		size : WidthHeight,
		// panel's style
		style : [MDockingPanelStyle]
	);

	makeMovablePanel(state : MDockingState, panelId : int, cursorPositionA : DynamicBehaviour<[Point]>, active : bool, style : [MDockingStyle]) -> Material;
	makeDockedPanel(isColumn : bool, colA : [int], state : MDockingState, areaId : int, colId : int, panelId : int) -> Material;
}


makeDockingPanelContent(state : MDockingState, panelId : int, mobilePanel : bool, isVerticalToolbarMobile : bool, resizePressed : DynamicBehaviour<bool>, isHovering : DynamicBehaviour<bool>, style : [MDockingStyle]) -> Material {
	manager = state.manager;
	panelContent = getValue(state.content)[panelId];
	dontResize  = contains(style, MDPanelDontResize());
	pstyle = getValue(state.panelStyle)[panelId];
	isToolbar = isDockingToolbar(state, panelId);
	isEndAligned = getValue(extractStruct(pstyle, MDToolbarEndAlign(make(false))).endAlign);
	dRadius = getDockingRadius(state.style);

	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	theme = if (darkTheme) const(false) else if (lightTheme) const(true) else manager.white;

	resizeHandleMobile = MInteractive(
		[
			TMouseDown(resizePressed), 
			TMouseInside(isHovering, false)
		],
		makeMDockingResizeHandleMobile(state, resizePressed, style)
			|> MBorderEnd(dRadius / 2.0)
			|> MBorderBottom(dRadius / 2.0)
	);

	content = 
		switch (panelContent : MDockingPanelContent) {
			MDToolbarContent(buttonsA): {			
				buttonsA
				|> (\m -> {
					if (isPanelFullSize(state, panelId)) if (isVerticalToolbarMobile) MColsA(m) else MLinesA(m) else {
						if (isEndAligned) {
							if (isVerticalToolbarMobile) MAlignCenterEndFill(MColsA(m)) else MAlignBottomEndFill(MLines(m))
						} else {
							if (isVerticalToolbarMobile) MAlignCenterStartFill(MColsA(m)) else MAlignTopCenter(MLines(m))
						}
					}
				})
			}
			MDPanelContent(mat) : mat;
		};

	MGroup([
		content,
		if (mobilePanel) {
			if (dontResize || isToolbar) TEmpty() else resizeHandleMobile;
		} else TEmpty()
	])
	|> (\m -> if (isToolbar) m else MCropSize(TFillXY(), m))
	|> (\m -> (MExplicitTheme(m, theme, [])));
}


makePanelTopBar(docked : bool, panelId: int, areaId : int, colId : int, colA : [int], state : MDockingState, isColumn: bool, pTheme : bool) -> Material {
	manager = state.manager;
	pstyle = getValue(state.panelStyle)[panelId];
	areaStyle = lookupTreeDef(getValue(state.areaStyle), areaId, []);
	enabled = getValue(state.enabled)[panelId];
	expanded = getValue(state.expanded)[panelId];
	sideExpanded = getValue(state.sideExpanded)[panelId];
	title = getValue(state.title)[panelId];
	minTitleEllipsisWidth = extractStruct(pstyle, MDPanelTitleEllipsis(120.0)).width;
	dRadius = getDockingRadius(state.style);
	// Order
	isFirstPanel = panelId == firstElement(colA, intMax);
	isLastPanel = panelId == lastElement(colA, intMax);
	// Topbar
	isToolbar = isDockingToolbar(state, panelId);
	toolbarThickness = extractStruct(pstyle, MDToolbarThickness(dockingItemHeight)).thickness;
	isVerticalToolbarMobile = !contains(pstyle, MDToolbarVerticalWhenMobile());
	topBarH = if (isToolbar) dockingBarHeightToolbar else dockingBarHeightPanel;
	topBarSize = makeHSize(topBarH, isColumn || !isToolbar);
	hideTopbar = contains(pstyle, MDPanelHideTopbar());
	containsDockingBarHeightPanel = if (hideTopbar) 0.0 else topBarH;
	// Prop
	disableButton = contains(pstyle, MDPanelShowCloseBtn());
	disableExpander = contains(pstyle, MDPanelHideExpanderBtn());
	// Colors
	barColorFn : MColor = topBarColorFn(pTheme, state.style);
	tColorFn : MColor = titleColorFn(pTheme, pstyle);
	sepColorFn = separatorColorFn(pTheme, state.style);
	toolbarSepColorFn = toolbarHandleColorFn(pTheme, state.style);
	// Font
	pTitleFont = extractStruct(pstyle, MDPanelTitleFont(dockingTitleFont)).titleFont;
	// Accordion menu
	accordionEnable = fsubselect(state.areaAccordion, FLift(\aa -> make(lookupTreeDef(aa, areaId, false))));

	addTooltip = \label : string, m : Material -> MTooltip(m , MText(label, [MTooltipDesktop()]), []);
	getPanelIcon = \st : [MIconStyle] ->  {
		icon = extractStruct(pstyle, MDPanelIcon("web_asset")).icon;
		dontShowIcon = contains(pstyle, MDPanelDontShowIcon());
		if (dontShowIcon) MEmpty() else MIcon(icon, concat([tColorFn], st));
	}

	makeIconToggleButton = \boolB : DynamicBehaviour<bool>,
		iconOn : string, iconOff : string,
		tooltipOn : string, tooltipOff :string,
		iconSize: double, clickFn : () -> void,
		iconColor : MColor, fill : double -> {
			MSelect(boolB, \b -> 
				if (b) {
					MIconButton(iconOn, clickFn, [MIconSize(iconSize), iconColor, FillOpacity(fill), MIconButtonBorder(1.0)], []) 
						|> (\m -> addTooltip(tooltipOn, m))
				} else {
					MIconButton(iconOff, clickFn, [MIconSize(iconSize), iconColor, FillOpacity(fill), MIconButtonBorder(1.0)], []) 
						|> (\m -> addTooltip(tooltipOff, m))
				}
			) |> MCenterY
		}

	linedHandle = \enableHover : bool, rotate : bool ->  makeMDockingLinedHandle(rotate, docked, state, toolbarSepColorFn, panelId, colA, enableHover, false, pTheme);

	// Toolbar drag handle (on the top)
	mobileToolbarDragHandle = \rotate : bool -> {
		linedHandle(true, rotate) 
			|> makeDraggableToolbar(state, panelId)
			|> (\m -> if (rotate) MCenterX(m) else MCenterY(m))
			|> showIf(!docked);
	}

	// Panel's buttons
	topbarCustomEndButtonsA = map(extractStruct(pstyle, MDPanelTopbarCustomButtons([])).buttons, \btn -> btn |> MCenterY);
	topbarCustomStartButtonsA = map(extractStruct(pstyle, MDPanelTopbarCustomButtonsStart([])).buttons, \btn -> btn |> MCenterY);
	hasTopbarCustomButtonsStart = topbarCustomStartButtonsA != [];
	hasTopbarCustomButtons = topbarCustomEndButtonsA != [];

	topbarPanelButtonsA = extractStruct(pstyle, MDPanelTopbarButtons([])).buttons;
	hasTopbarPanelButtons = topbarPanelButtonsA != [];
	topbarToggleButtonsA = map(topbarPanelButtonsA, \btn -> {
		clickedB = make(false);
		switch (btn) {
			MDPanelCustomToggleButton(ic, tt, clkFn, st) : {
				btnColor = extractStruct(st, tColorFn);
				makeIconToggleButton(clickedB, ic, ic, tt, tt, dockingTopBarIconSize, clkFn, btnColor, 0.5) |> MBorderLeftRight(4.0);
			}
			default: MEmpty();
		} |> MCenterY

	});

	// TopBar Content
	panelTopBarContent = {
		MSelect(accordionEnable, \isAccEnabled -> {
			MBaselineColsA([
				getPanelIcon([MIconSize(dockingTopBarIconSize), FillOpacity(dockingTitleOpacity)])
					|> MBorderStart(26.0)
					|> MCenterY,
				MEllipsisText(title, [tColorFn, pTitleFont, MMinWidth(minTitleEllipsisWidth)]) 
					|> MBorderStart(8.0)
					|> MCenterY,
				MBaselineColsA(concat(topbarCustomStartButtonsA, []))
					|> MCenterY 
					|> showIf(hasTopbarCustomButtonsStart),
				TFillX(),
				MBaselineColsA(concat(topbarCustomEndButtonsA, [])) |> MCenterY |> showIf(hasTopbarCustomButtons),
				MBaselineColsA(topbarToggleButtonsA) |> MCenterY |> showIf(hasTopbarPanelButtons),
				MSeparator(false) |> MBorderTopBottom(8.0) |> MBorderLeftRight(4.0) |> MCenterY 
					|> showIf((hasTopbarCustomButtons || hasTopbarPanelButtons) && ((disableExpander && !docked) || disableButton)),
				if (disableButton)
					MIconButton("close", \ -> next(enabled, false), [MIconSize(dockingTopBarIconSize - 4.0), tColorFn, MIconButtonBorder(1.0), FillOpacity(0.5)], [])
						|> MBorderStart(6.0)
						|> (\m -> addTooltip("hide panel", m))
						|> MCenterY
				else TEmpty(),
				TFixed(4.0, 0.0),

				if (disableExpander && docked) {
					MEmpty()
				} else {
					makeIconToggleButton(
						expanded,
						if (isAccEnabled) "remove" else "keyboard_arrow_up",
						if (isAccEnabled) "add" else "keyboard_arrow_down",
						_("Collapse Panel"),
						_("Expand Panel"),
						if (isAccEnabled) (dockingTopBarIconSize - 4.0) else dockingTopBarIconSize,
						\ -> if (isAccEnabled) accordionExpanPanelFn(colA, state, panelId) else reverseBehaviour(expanded),
						tColorFn,
						0.5
					) |> showIf(isColumn || !docked)
				},
				TFixed(if (isAccEnabled) 14.0 else 10.0, 0.0)
			])
		})
	}

	dockedTopBar = \isSideExpanded : bool  -> {
		MAvailable(
			MGroup([
				// Topbar background
				if (isToolbar) MEmpty() else {
					if (isColumn) {
						if (isSideExpanded) {
							MIfLazy(expanded,\e -> {
								topBarOpen = TRounded(dRadius, dRadius, 0.0, 0.0, [MFill(barColorFn), FillOpacity(1.0)], TFillXH(topBarH));
								topBarClose = TRounded4(dRadius, [MFill(barColorFn)], TFillXH(topBarH));
								if (e) topBarOpen else {
									if (isLastPanel) topBarOpen else topBarClose
								}
							})
						} else {
							isHovering = make(false);
							MInteractive(
								[
									TMouseInside(isHovering, false)
								],
								MClickable(
									MSelect(fselect2(manager.manager.dragging, isHovering, FLift2(\dr, h -> if (dr > 0) false else h)), \b -> {
										TRounded4(dRadius, [MFill(if (b) dockingDropLineHoverColor else barColorFn), FillOpacity(if (b) 0.4 else 1.0)], TFillXH(topBarH))
									}), \ -> iter(colA, \c -> {
										nextDistinct(getValue(state.sideExpanded)[c], true);
									}))
							) |> (\m -> addTooltip(title + " panel", m))
						}

					} else {
						TRectangle([MFill(barColorFn), /*FillOpacity(0.0)*/], TFillXH(topBarH))
					}
				},
				// Topbar content
				if (isToolbar) linedHandle(true, isColumn) else {
					panelTopBarContent |> showIf(isSideExpanded)
				}
			]), topBarSize
		) |> showIf(!hideTopbar);
	};

	mobileTopBar =
		if (isToolbar) mobileToolbarDragHandle(isColumn) else {
			MAvailable(
				MGroup([
					MSelect(expanded,\e -> if (e) TRounded(dRadius, dRadius, 0.0, 0.0, [MFill(barColorFn)], TFillXY())
						else TRounded(dRadius, dRadius, dRadius, dRadius, [MFill(barColorFn)], TFillXY())	
					),
					if (isSameStructType(getValue(state.content)[panelId], MDToolbarContent([])))
						MCenterIn(linedHandle(false, isColumn), TFillXH(topBarH))
					else panelTopBarContent
				]),
				TFillXH(topBarH)
			)
		}

	MSelect(getValue(state.sideExpanded)[panelId], \se -> {
		if (se) {
			if (docked) dockedTopBar(se) else mobileTopBar;
		} else {
			dockedTopBar(se);
		}
	}) 
	|> (\r -> (MExplicitTheme(r, const(pTheme), [])))
}

makeMovablePanel(state : MDockingState, panelId : int, cursorPositionA : DynamicBehaviour<[Point]>, active: bool, style : [MDockingStyle]) -> Material {
	manager = state.manager;
	itemId = makeTropicTag(manager);
	dockA = state.dock;
	enabled = getValue(state.enabled)[panelId];
	expanded = getValue(state.expanded)[panelId];
	position = getValue(state.positions)[panelId];
	pstyle = concat(state.style, getValue(state.panelStyle)[panelId]);
	isVerticalToolbarMobile = !contains(pstyle, MDToolbarVerticalWhenMobile());
	dRadius = getDockingRadius(state.style);

	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	mobileTheme = if (darkTheme) const(false) else if (lightTheme) const(true) else manager.white;

	resizePressed = make(false);
	isResizing = make(false);
	isHovering = make(false);
	activeHover = make(false);
	resizableContent = makeDockingPanelContent(state, panelId, true, isVerticalToolbarMobile, resizePressed, isHovering, pstyle);

	isToolbar = isDockingToolbar(state, panelId);
	topBarH = if (isToolbar) dockingBarHeightToolbar else dockingBarHeightPanel;

	// Size
	size = getValue(state.size)[panelId];
	minPanelSize = extractStruct(pstyle, MDPanelMimimumSize(size)).size;
	minWidth = minPanelSize.width;
	minHeight = minPanelSize.height;

	msize = getValue(state.mobileSize)[panelId];
	mwidth = msize.width;
	mheight = msize.height;

	disableButton = contains(pstyle, MDPanelShowCloseBtn());

	mouseXyLocal = make(zeroPoint);
	dragPressed = make(false);
	activePressed = make(false);

	mobileWHT = fselectLift(state.mobileSize, \ms -> ms[panelId]);
	tempWH = make(zeroWH);
	tempXY = makePoint();

	// Resize mobile panel bars
	resizeBPressed = make(false);
	resizeTPressed = make(false);
	resizeRPressed = make(false);
	resizeLPressed = make(false);
	
	isResizingB = make(false);
	isResizingT = make(false);
	isResizingR = make(false);
	isResizingL = make(false);

	makeResizeHandle = \rszPressedB : DynamicBehaviour<bool>, resizeH : bool, ex : bool -> {
		MInteractive(
			[TMouseDown(rszPressedB)],
			MCursor(if (resizeH) NSResizeCursor() else EWResizeCursor(), 
				TRectangle([MFill(mobileResizeHelperColor), FillOpacity(mobileResizeHelperOpacity)], 
					TSelect(mobileWHT, \wh ->  
						if (resizeH) {
							TFixed(wh.width, mobileHelperResize)
						} else TFixed(mobileHelperResize, (if (ex) wh.height else dockingBarHeightPanel)  + (2.0 * mobileHelperResize))
					)
				)
			)
		)
		|> (\m -> if (resizeH && ex) m else if (resizeH && !ex) TFixed(mobileHelperResize, mobileHelperResize) else m)
	}

	resizeHandleB = \ex : bool -> makeResizeHandle(resizeBPressed, true, ex);
	resizeHandleT = \ex : bool -> makeResizeHandle(resizeTPressed, true, ex);
	resizeHandleR = \ex : bool -> makeResizeHandle(resizeRPressed, false, ex);
	resizeHandleL = \ex : bool -> makeResizeHandle(resizeLPressed, false, ex);

	resizablePanel = \c : Material, lg : bool -> {
		addShadow = \m : Material -> {
			MShadowShape(const(4.0), m, 
				TropicShape(
					TRounded4(dRadius, [MFill(panelBgColorFn(lg, pstyle))], TFillXY())
				)
			)
		}
		
		// Mobile Panel Topbar
		panelTopBar = {
			makePanelTopBar(false, panelId, intMin, intMax, [], state, !isVerticalToolbarMobile, lg)
			// NOTE: same drag handle as docked panel
			|> (\m -> 
				if (isToolbar) m else {
					MGroup2(
						m,
						// Drag handle for mobile panels
							MDraggable(panelId,
								makeMDockingDottedHandle(state, smallDragHandleColorFn(lg, state.style), !isToolbar)
									|> addMTooltip(_("Drag and Drop into a Dockable Area")),
								[
									MDragShape(makeMobileDraggingShape(state, panelId, dockingPrimaryColor, dockingMobileOpacity, true)),
									TCursorShape(GrabCursor())
								]
							)
							|> highlightActiveHoverLayer(activeHover)
					)
				}
			)
			// Overlay for z-order background (disabled)
			|> (\mp -> if (isToolbar) mp else
				MSelect(state.activePanel, \zordId -> {
				if (zordId == panelId) mp else 
					MGroup2(
						mp,
						makeMobileDraggingShape(state, panelId, topBarColorFn(lg, state.style), activeOverlayPanelOpacity, false)
					);
				})
			);
		}
		
		MInteractive([TMouseXY(mouseXyLocal)],
			if (isToolbar) {
				toolbarSize = TFixed(mwidth, size.height + topBarH) |> (\m -> if (!isVerticalToolbarMobile) m else TRotate90(m));
					[
						panelTopBar,
						c
					]
					|> (\m -> if (isVerticalToolbarMobile) MCols(m) else MLines(m))
					|> addShadow
					|> (\m -> MCopySize(m, \sz -> MAvailable(m,	sz), true)) // fix for zero-size fillers
					|> (\m -> MAvailable(m,	toolbarSize))
			} else {
				MSelect(expanded,\e -> {
					MAvailable(
						MLines2(
							panelTopBar,
							if (e) c else TEmpty() 
						)
						|> (\m -> MGroup2(m, MShowLazy(activeHover, \ -> activeHoverLayer(state))))
						|> addShadow,
						if (e) TSized(mobileWHT) else TSelect(mobileWHT, \wh ->  TFixed(wh.width, dockingBarHeightPanel)),
					)
					|> (\mp -> {
						MCols([
							resizeHandleL(e),
							MLines([resizeHandleT(e), mp, resizeHandleB(e)]),
							resizeHandleR(e)
						])
					})
				})
			}
		)
	}

	movablePanel = \lg -> {
		MConstruct([
			makeSubscribe2(activePressed, \p -> {
				if (p) nextDistinct(state.activePanel, panelId); // set z-order top panel
			}),
			makeSubscribe2(dragPressed, \p -> {
				if (p) next(cursorPositionA, replace(getValue(cursorPositionA), panelId, getValue(mouseXyLocal)))
			})],
			resizablePanel(resizableContent, lg)
			|> (\m-> MSelect(state.activePanel, \activePanel ->  
				MInteractive(if (activePanel == panelId) [TMouseDownGhost(dragPressed)] else [TMouseDown(activePressed)], m) // fix z-order interactivity problem
			))
			|> (\m-> {
				MMovable(
					m,
					TFillXY(),
					[
						MMovableCustomCursor(MoveCursor()),
						MMovableDontPassClicks(),
						MMovablePosition(position),
					]
				)
			})
		)
	}

	makeResizing = \rszPressedB : DynamicBehaviour<bool>, isRszingB :DynamicBehaviour<bool>, storeTempData : bool -> {
		makeSubscribe2(rszPressedB, \p -> {
			if (p) {
				addMInteractionItemId(manager, itemId);
				next(isRszingB, true);
				if (storeTempData) {
					nextDistinct(tempXY, getValue(position));
					nextDistinct(tempWH, fgetValue(mobileWHT));
				}
			} else {
				if (getValue(isRszingB)) next(isRszingB, false);
			}
		})
	}

	MConstruct([
		makeResizing(resizePressed, isResizing, false),
		makeResizing(resizeBPressed, isResizingB, false),
		makeResizing(resizeRPressed, isResizingR, false),
		makeResizing(resizeLPressed, isResizingL, true),
		makeResizing(resizeTPressed, isResizingT, true),
		// Translate while resizing
		makeSubscribe(state.mobileSize, \msA -> {
			ms = msA[panelId];
			if (getValue(isResizingT) && ms.height > (minHeight + mobileHelperResize)) {
				next(position, Point(getValue(position).x, getValue(state.mouseXy).y));
			}
			if (getValue(isResizingL) && ms.width > (minWidth - mobileHelperResize)) {
				next(position, Point(getValue(state.mouseXy).x, getValue(position).y));
			}
		}),
		makeSubscribe(mouseXyLocal, \mxy -> {
			// Update mobile size only (do not affect dockedSize)
			if (isCurrentMInteractionItemId(manager, itemId) && (getValue(isResizing) || getValue(isResizingB) || getValue(isResizingT) || getValue(isResizingR) || getValue(isResizingL))) {
				rtlCorrection = if (getDefaultRtl()) 1.0 else 1.0;
				blockOtherMInteractionItemIds(manager, itemId);

				safeLimit = getValue(tempXY).y - (mobileHelperResize);
				deltaH = getValue(tempXY).y - max(0.0, getValue(state.mouseXy).y);
				deltaW = getValue(tempXY).x - max(0.0, getValue(state.mouseXy).x);

				if (getValue(isResizingT)) {
					updateH(state.mobileSize, panelId,
						max(minHeight, getValue(tempWH).height + deltaH)
					);
				} else if (getValue(isResizingL)) {
					updateW(state.mobileSize, panelId,
						max(minWidth, getValue(tempWH).width + deltaW)
					);
				} else if (getValue(isResizingB)) {
					// Update while resizing, but do not exceed the editor size
					updateH(state.mobileSize, panelId,
						min(
							max(minHeight, mxy.y),
							getValue(state.editorWH).height - getValue(position).y - (mobileHelperResize * 2.0) // do not grow in vertical while resizing
						)
					);

				} else if (getValue(isResizingR)) {
					updateW(state.mobileSize, panelId,  
						min(
							max(minWidth, rtlCorrection * mxy.x),
							getValue(state.editorWH).width - mobileHelperResize
						)
					);
				} else {
					// Update while resizing, but do not exceed the editor size
					updateH(state.mobileSize, panelId,
						min(
							max(minHeight, mxy.y),
							getValue(state.editorWH).height - getValue(position).y - (mobileHelperResize * 2.0) // do not grow in vertical while resizing
						)
					);
					updateW(state.mobileSize, panelId,  
						min(
							max(minWidth, rtlCorrection * mxy.x),
							getValue(state.editorWH).width - mobileHelperResize
						)
					);
				}
			}
		})
	],	
	MSelect(mobileTheme, \lg -> {
		MShowLazy(enabled, \ -> movablePanel(lg));
	}))
}

makeDockedPanel(isColumn : bool, colA : [int], state : MDockingState, areaId : int, colId : int, panelId : int) -> Material {
	manager = state.manager;
	pstyle = concat(state.style, getValue(state.panelStyle)[panelId]);
	areaStyle = lookupTreeDef(getValue(state.areaStyle), areaId, []);
	dontMove  = contains(pstyle, MDPanelDontMove());
	isToolbar = isDockingToolbar(state, panelId);
	toolbarThickness = extractStruct(pstyle, MDToolbarThickness(dockingItemHeight)).thickness;
	isVerticalToolbarMobile = !contains(pstyle, MDToolbarVerticalWhenMobile());
	isEndAligned = getValue(extractStruct(pstyle, MDToolbarEndAlign(make(false))).endAlign);
	fillSpace = contains(pstyle, MDToolbarFullSizeFixed());
	dRadius = getDockingRadius(state.style);
	activeHover = make(false);

	topBarH = if (isToolbar) dockingBarHeightToolbar else dockingBarHeightPanel;
	hideTopBar = contains(pstyle, MDPanelHideTopbar());
	containsDockingBarHeightPanel = if (hideTopBar) 0.0 else topBarH;
	
	// Order
	isFirstPanel = panelId == firstElement(colA, intMax);
	isLastPanel = panelId == lastElement(colA, intMax);

	// Expanded
	expanded = getValue(state.expanded)[panelId];
	sideExpanded = getValue(state.sideExpanded)[panelId];
	enabled = getValue(state.enabled)[panelId];
	
	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	isAreaDark = contains(areaStyle, MDAreaForceDarkTheme());
	isAreaLight = contains(areaStyle, MDAreaForceLightTheme());
	dockedTheme = if (darkTheme || isAreaDark) const(false) else if (lightTheme || isAreaLight) const(true) else manager.white;
	toolbarSepColorFn = toolbarHandleColorFn(fgetValue(dockedTheme), state.style);

	// Accordion menu
	accordionEnable = fsubselect(state.areaAccordion, FLift(\aa -> make(lookupTreeDef(aa, areaId, false))));
	
	// Panel's content
	dContent = makeDockingPanelContent(state, panelId, false, !isColumn, make(false), make(false), pstyle);
	dPanel = \lg : bool -> {
		if (isToolbar) {
			dContent 
		} else {
			MSelect(expanded, \e -> {
				croppedContent = MCropSize(TFillXY(), dContent);

				if (isColumn) {
					if (e) croppedContent else TFillXH(dockingCollapsedSeparatorH);
				} else	croppedContent;

			}) 
			|> (\m -> if (!isColumn) m else MShowLazy(sideExpanded, \ -> m))
		} 
	}

	// Dragging panel utils
	makeDraggingShape = \dsA : [WidthHeight], e : bool -> 
		TRounded(dRadius, dRadius, dRadius, dRadius, [MFill(dockingPrimaryColor), FillOpacity(dockingMobileOpacity)],
			TFixed(
				dsA[panelId].width, 
				if (e) dsA[panelId].height + containsDockingBarHeightPanel else dockingBarHeightPanel
			)
		);

	rotateDS = \dgs : Tropic -> if (isToolbar && isVerticalToolbarMobile) TRotate90(dgs) else dgs;

	// Draggable handle
	makeDraggableHandle = \handleM : Material ->
		MSelect2(state.dockedSize, expanded, \ds, e -> {
			positionCorrection = 
			if (isEndAligned && isToolbar) {
				if (isColumn) Point(0.0, -(ds[panelId].height)) else Point(-(ds[panelId].height), 0.0)
			} else zeroPoint;
			MDraggable(panelId, handleM, [
				TDragShape(TTranslate(const(positionCorrection), rotateDS(makeDraggingShape(ds, e)))),
				TCursorShape(GrabCursor())
			])
		})

	MSelect2(dockedTheme, accordionEnable, \lg, accEnab -> {
		[
			// Docked Topbar
			makePanelTopBar(true, panelId, areaId, colId, colA, state, isColumn, lg)	
				|> (\tpbar -> 
					if (isToolbar) {
						if (dontMove || fillSpace) {
							tpbar
						} else {
							makeDraggableHandle(tpbar) |> highlightActiveHoverLayer(activeHover);
						}
					} else {
						MGroup2(
							MClickable(tpbar, \ -> {
								if (accEnab) accordionExpanPanelFn(colA, state, panelId) else reverseBehaviour(expanded)
							}),
							MShowLazy(sideExpanded, \ -> 
								makeMDockingDottedHandle(state, smallDragHandleColorFn(lg, state.style), !isToolbar && !dontMove)
									|> (\m -> 
										if (!isToolbar && !dontMove) {
											makeDraggableHandle(m) |> addMTooltip(_("Drag to Undock")) |> highlightActiveHoverLayer(activeHover)
										} else m
									)
							)
							|> showIf(!(dontMove && hideTopBar))
						)
					}
				),
			dPanel(lg),
			makeMDockingLinedHandle(isColumn, true, state, toolbarSepColorFn, panelId, colA, false, true, lg)
				|> showIf(containsDockingToolbarButtons(state, colA) && isPanelFullSize(state, panelId))
		]
		|> (\r -> {
			if (isToolbar) {
				if (isColumn) MLinesA(if (isEndAligned) reverseA(r) else r)
					else MColsA(if (isEndAligned) reverseA(r) else r)
			} else {
				if (hideTopBar) MGroup(r) else	MLines(r)
			}
		})
		// Panel's BG rounded
		|> (\m -> 
			MGroup2(
				TRounded4(dRadius, [MFill(panelBgColorFn(lg, pstyle))], TFillXY()),
				m
			)
		)
		|> (\m -> MGroup2(m, MShowLazy(activeHover, \ -> activeHoverLayer(state))))
	})
}