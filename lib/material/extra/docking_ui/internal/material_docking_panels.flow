import material/material2tropic;
import material/extra/docking_ui/internal/material_docking_utils;
import material/extra/docking_ui/internal/material_docking_drop;

export {
	// NOTE: example in flow9/lib/material/tests/test_docking_ui.flow

	// Each panel can be movable or docked, and it's defined by:
	MDockingPanel(
		// note: if content is [MDockingPanelToolbarItem] is not used
		title : string,
		// creates easily different standard panels as array of MDockingPanelItems
		content : MDockingPanelContent,
		// store the position where the panel is docked
		panelDock : MDock,
		// starting panel size
		size : WidthHeight,
		// panel's style
		style : [MDockingPanelStyle]
	);
						
	makeMovablePanel(state : MDockingState, panelId : int, cursorPositionA : DynamicBehaviour<[Point]>) -> Material;
	makeDockedPanel(isColumn : bool, colA : [int], state : MDockingState, panelId : int, style : [MDockingStyle]) -> Material;
}


makeDockingPanelContent(state : MDockingState, panelId : int, mobilePanel : bool, isVerticalToolbarMobile : bool, resizePressed : DynamicBehaviour<bool>, isHovering : DynamicBehaviour<bool>, style : [MDockingStyle]) -> Material {
	manager = state.manager;
	panelContent = getValue(state.content)[panelId];
	dontResize  = contains(style, MDPanelDontResize());
	pstyle = getValue(state.panelStyle)[panelId];
	isToolbar = isDockingToolbar(state, panelId);
	isEndAligned = getValue(extractStruct(pstyle, MDPanelEndAlign(make(false))).endAlign);

	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	theme = if (darkTheme) const(false) else if (lightTheme) const(true) else manager.white;

	resizeHandleMobile = MInteractive(
		[
			TMouseDown(resizePressed), 
			TMouseInside(isHovering, false)
		],
		makeMDockingResizeHandleMobile(state, resizePressed, style)
			|> MBorderEnd(dockingRadius / 2.0)
			|> MBorderBottom(dockingRadius / 2.0)
	);

	content = 
		switch (panelContent : MDockingPanelContent) {
			MDPanelToolbarButtons(buttonsA): {			
				buttonsA 
				|> (\m -> {
					if (isPanelFullSize(state, panelId)) if (isVerticalToolbarMobile) MColsA(m) else MLinesA(m) else {
						if (isEndAligned) {
							if (isVerticalToolbarMobile) MAlignCenterEnd(MColsA(m)) else MAlignBottomEnd(MLines(m))
						} else {
							if (isVerticalToolbarMobile) MAlignCenterStart(MColsA(m)) else MAlignTopCenter(MLines(m))
						}
					}
				})
			}
			MDPanelToolbarCustom(mat) : {
				mat |> (\m -> if (isPanelFullSize(state, panelId)) m else {if (isEndAligned) MAlignCenterEnd(m) else MAlignCenterStart(m)}) 
				// |> (\m -> MFrame(0.0, 6.0, [MFill(MPurple(400)), FillOpacity(0.2)], m)); //debug
			}
			MDPanelContent(mat) : mat;
		};

	MGroup([
		content,
		if (mobilePanel) {
			if (dontResize || isToolbar) TEmpty() else resizeHandleMobile;
		} else TEmpty()
	])
	|> (\m -> (MExplicitTheme(m, theme, [])));
}

makePanelTopBar(docked : bool, panelId: int, colA : [int], state : MDockingState, isColumn: bool, pTheme : bool) -> Material {
	manager = state.manager;
	pstyle = getValue(state.panelStyle)[panelId];
	enabled = getValue(state.enabled)[panelId];
	expanded = getValue(state.expanded)[panelId];
	sideExpanded = getValue(state.sideExpanded)[panelId];
	title = getValue(state.title)[panelId];
	minTitleEllipsisWidth = extractStruct(pstyle, MDPanelTitleEllipsis(120.0)).width;
	dontMove = contains(pstyle, MDPanelDontMove());
	// Order
	isFirstPanel = panelId == firstElement(colA, intMax);
	isLastPanel = panelId == lastElement(colA, intMax);
	// Topbar
	isToolbar = isDockingToolbar(state, panelId);
	isCustomToolbar = isSameStructType(getValue(state.content)[panelId], MDPanelToolbarCustom(MEmpty()));
	toolbarThickness = extractStruct(pstyle, MDPanelToolbarThickness(dockingItemHeight)).thickness;
	isVerticalToolbarMobile = !contains(pstyle, MDPanelVerticalToolbarWhenMobile());
	topBarH = if (isToolbar) dockingBarHeightToolbar else dockingBarHeightPanel;
	topBarSize = makeHSize(topBarH, isColumn || !isToolbar);
	hideTopbar = contains(pstyle, MDPanelHideTopbar());
	// Prop
	disableButton = contains(pstyle, MDPanelShowCloseBtn());
	disableExpander = contains(pstyle, MDPanelHideExpanderBtn());
	// Colorsm
	barColorFn : MColor = topBarColorFn(pTheme, pstyle);
	panelColorFn : MColor = panelBgColorFn(pTheme, pstyle);
	tColorFn : MColor = titleColorFn(pTheme, pstyle);
	sepColorFn = separatorColorFn(pTheme, state.style);
	toolbarSepColorFn = toolbarHandleColorFn(pTheme, state.style);

	addTooltip = \label : string, m : Material -> MTooltip(m , MText(label, [MTooltipDesktop()]), []);
	getPanelIcon = \st : [MIconStyle] ->  {
		icon = extractStruct(pstyle, MDPanelIcon("web_asset")).icon;
		MIcon(icon, concat([tColorFn], st));
	}

	makeIconToggleButton = \boolB : DynamicBehaviour<bool>,
		iconOn : string, iconOff : string,
		tooltipOn : string, tooltipOff :string,
		iconSize: double, clickFn : () -> void,
		iconColor : MColor, fill : double -> {
			MSelect(boolB,\b -> if (b) MIconButton(iconOn, clickFn, [MIconSize(iconSize), iconColor, FillOpacity(fill), MIconButtonBorder(1.0)], []) |> (\m -> addTooltip(tooltipOn, m))
				else MIconButton(iconOff, clickFn, [MIconSize(iconSize), iconColor, FillOpacity(fill), MIconButtonBorder(1.0)], []) |> (\m -> addTooltip(tooltipOff, m))
			) |> MCenterY
	}

	linedHandle = \enableHover : bool, rotate : bool ->  makeMDockingLinedHandle(rotate, docked, state, toolbarSepColorFn, panelId, colA, enableHover, false);

	// Drag handle
	r = dockingRadius;			
	mobileDragHandle = \rotate : bool -> {
		cursor = fif(fselect(manager.manager.dragging, FLift(\dr -> dr >= 0)), const(GrabbingCursor()), const(GrabCursor()));		
		linedHandle(true, rotate) 
		|> (\m -> 
			if (dontMove) m else {
				MSelect(state.mobileSize, \ms -> {
					draggingShape = 
						TRounded(r, r, r, r, [MFill(dockingPrimaryColor), FillOpacity(dockingMobileOpacity)],
						if (isCustomToolbar) TFixed(toolbarThickness, toolbarThickness * 3.0) else
							TFixed(
								ms[panelId].width, 
								if (getValue(getValue(state.expanded)[panelId])) ms[panelId].height else dockingBarHeightPanel
							)
						);						
					dsRotated = draggingShape |> (\ds -> if (isToolbar && isVerticalToolbarMobile) TRotate90(ds) else ds);	
					positionCorrection = if (isToolbar) zeroPoint else Point(-(ms[panelId].width / 2.0) + dockingTopBarHandleWidth, 0.0);
					// Draggable handle
					MDraggable(panelId, m, [
						TDragShape(TTranslate(const(positionCorrection), dsRotated)),
						TCursorShape(DynamicCursor(cursor))
					])
				})
			}
		)
		|> (\m -> if (rotate) MCenterX(m) else MCenterY(m))
	}

	// Panel's buttons
	customButtonsA = extractStruct(pstyle, MDPanelCustomButtons([])).buttons;
	customButtons = customButtonsA != [];
	panelButtonsA = map(customButtonsA, \btn -> {
		clickedB = make(false);
		switch (btn) {
			MDPanelCustomButton(ic, tt, clkFn, st) : {
				btnColor = extractStruct(st, tColorFn);
				makeIconToggleButton(clickedB, ic, ic, tt, tt, dockingTopBarIconSize, clkFn, btnColor, 0.5);
			}
			default: MEmpty();
		} |> MCenterY

	});

	// TopBar Content
	panelTopBarButtons = {
		MBaselineColsA([
			getPanelIcon([MIconSize(dockingTopBarIconSize), FillOpacity(dockingTitleOpacity)])
				|> MBorderStart(10.0)
				|> MCenterY,
			MEllipsisText(title, [tColorFn, dockingTitleFont, MMinWidth(minTitleEllipsisWidth)]) 
				|> MBorderStart(10.0)
				|> MCenterY,
			TFillX(),
			if (customButtons)
				MBaselineColsA(panelButtonsA) |> MCenterY
			else TEmpty(),
			if (disableButton)
				MIconButton("close", \ -> next(enabled, false), [MIconSize(dockingTopBarIconSize - 4.0), tColorFn, MIconButtonBorder(1.0), FillOpacity(0.5)], [])
					|> MBorderStart(6.0)
					|> (\m -> addTooltip("hide panel", m))
					|> MCenterY
			else TEmpty(),
			TFixed(4.0, 0.0),

			if (disableExpander)
				TEmpty()
			else
				makeIconToggleButton(
					expanded,
					"keyboard_arrow_up",
					"keyboard_arrow_down",
					"collapse panel",
					"expand panel",
					dockingTopBarIconSize,
					\ -> {
						reverseBehaviour(expanded);
						// next(state.dock, getValue(state.dock)); //force redraw (should be used for mobile)
					},
					tColorFn,
					0.5
				) |> showIf(isColumn || !docked),
			TFixed(8.0, 0.0)
		])
		|> (\m -> if (docked) m else MGroup2(m, mobileDragHandle(!isColumn)))
	}

	dockedTopBar = \isSideExpanded : bool  -> {
		MAvailable(
			MGroup([
				// Topbar background
				if (isToolbar) MEmpty() else {
					if (isColumn) {
						if (isSideExpanded) {
							MIfLazy(expanded,\e -> 
								if (e) TRounded(dockingRadius, dockingRadius, 0.0, 0.0, [MFill(barColorFn), FillOpacity(1.0)], TFillXH(topBarH))
									else TRounded(dockingRadius, dockingRadius, dockingRadius, dockingRadius, [MFill(barColorFn)], TFillXH(topBarH))
							)
						} else {
							TRounded(dockingRadius, dockingRadius, dockingRadius, dockingRadius, [MFill(barColorFn)], TFillXH(topBarH))
								|> (\m -> addTooltip(title + " panel", m))
						}

					} else {				
						TRectangle([MFill(barColorFn), /*FillOpacity(0.0)*/], TFillXH(topBarH))
					}
				},
				// Topbar content
				if (isToolbar) linedHandle(true, isColumn) else {
					if (isSideExpanded) panelTopBarButtons else {			
						getPanelIcon([MIconSize(sideExpanderButtonSize), FillOpacity(0.3)]) |> MCenter
					}
				}
			]), topBarSize
		) |> showIf(!hideTopbar);
	};

	mobileTopBar =
		if (isToolbar) mobileDragHandle(isColumn) else {
			MAvailable(
				MGroup([
					MSelect(expanded,\e -> if (e) TRounded(r, r, 0.0, 0.0, [MFill(barColorFn)], TFillXY())
						else TRounded(r, r, r, r, [MFill(barColorFn)], TFillXY())	
					),
					if (isSameStructType(getValue(state.content)[panelId], MDPanelToolbarButtons([])))
						MCenterIn(linedHandle(false, isColumn), TFillXH(topBarH))
					else panelTopBarButtons			
				]),
				TFillXH(topBarH)
			);
		}

	MSelect(getValue(state.sideExpanded)[panelId], \se -> {
		if (se) {
			if (docked) dockedTopBar(se) else mobileTopBar;
		} else {
			dockedTopBar(se);
		}
	});
}

makeMovablePanel(state : MDockingState, panelId : int, cursorPositionA : DynamicBehaviour<[Point]>) -> Material {
	manager = state.manager;
	itemId = makeTropicTag(manager);
	dockA = state.dock;
	groupA = state.group;
	enabled = getValue(state.enabled)[panelId];
	expanded = getValue(state.expanded)[panelId];
	position = getValue(state.positions)[panelId];
	pstyle = getValue(state.panelStyle)[panelId];
	isVerticalToolbarMobile = !contains(pstyle, MDPanelVerticalToolbarWhenMobile());
	cacheEnabled = extractStruct(state.style, MDockingEnableLoadAndStoreCache("", false)).cacheEnabled;
	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	mobileTheme = if (darkTheme) const(false) else if (lightTheme) const(true) else manager.white;

	resizePressed = make(false);
	isResizing = make(false);
	isHovering = make(false);
	dContent = makeDockingPanelContent(state, panelId, true, isVerticalToolbarMobile, resizePressed, isHovering, pstyle);

	isToolbar = isDockingToolbar(state, panelId);
	topBarH = if (isToolbar) dockingBarHeightToolbar else dockingBarHeightPanel;

	// Size
	size = getValue(state.size)[panelId];
	minWidth = size.width;
	minHeight = size.height + topBarH;

	msize = getValue(state.mobileSize)[panelId];
	width = msize.width;
	height = msize.height + topBarH;

	disableButton = contains(pstyle, MDPanelShowCloseBtn());

	mouseXyLocal = make(zeroPoint);
	dragPressed = make(false);
	tempWH = make(WidthHeight(width, height));

	resizablePanel = \c : Material, lg : bool -> {	
		addShadow = \m : Material -> {
			MShadowShape(const(4.0), m, 
				TropicShape(
					TRounded4(dockingRadius, [MFill(panelBgColorFn(lg, pstyle))], TFillXY())
				)
			)
		}
		panelTopBar = makePanelTopBar(false, panelId, [], state, !isVerticalToolbarMobile, lg);
		 
		MConstruct([
				makeSubscribe(isResizing, \rs -> {
					if (!rs) {
					// update mobile size
					next(state.mobileSize, replace(getValue(state.mobileSize), panelId , getValue(tempWH)));
				}
			})
		],
		MInteractive([TMouseXY(mouseXyLocal)],
			if (isToolbar) {
				toolbarSize = TFixed(width, size.height + topBarH) |> (\m -> if (!isVerticalToolbarMobile) m else TRotate90(m));
				println("toolbarSize: "+ toString(width) + " x " + toString(size.height + topBarH));
					[
						panelTopBar,
						c
					]
					|> (\m -> if (isVerticalToolbarMobile) MCols(m) else MLines(m))
					|> addShadow
					|> (\m -> MCopySize(m, \sz -> MAvailable(m,	sz), true)) // fix for zero-size fillers
					|> (\m -> MAvailable(m,	toolbarSize))
			} else {
				MSelect(expanded,\e -> {
					MAvailable(
						MLines2(
							panelTopBar,
							if (e) c else TEmpty() 
						)
						|> addShadow,
						if (e) TSized(tempWH) else TSelect(tempWH, \wh ->  TFixed(wh.width, dockingBarHeightPanel)),
					)
				})	
			}
		))
	}

	movablePanel = \lg -> {
		MConstruct([
			makeSubscribe(dragPressed, \p -> {
				if (p) next(cursorPositionA, replace(getValue(cursorPositionA), panelId, getValue(mouseXyLocal)));
			})],
			resizablePanel(dContent, lg)
		)
		|> (\m-> MInteractive([TMouseDownGhost(dragPressed)], m))
		|> (\m->
			MMovable(
				m,
				TFillXY(),
				[
					MMovableCustomCursor(MoveCursor()),
					MMovableDontPassClicks(),
					MMovablePosition(position)
				]
			)
		)
	}

	MConstruct([
		makeSubscribe2(isResizing, \rs -> { 
			if (!rs) {
				// Update mobile size
				updateH(state.mobileSize, panelId,  getValue(tempWH).height - topBarH);
				updateW(state.mobileSize, panelId,  getValue(tempWH).width);
				// Update docked size
				updateH(state.dockedSize, panelId,  getValue(tempWH).height - topBarH);
				updateW(state.dockedSize, panelId,  getValue(tempWH).width);

				// Update cache
				if (cacheEnabled) setMDockingStateKeyValue(state) |> ignore;
			}
		}),
		makeSubscribe(resizePressed, \p -> {
			if (p) {
				addMInteractionItemId(manager, itemId);
				next(isResizing, true);
			} else {
				if (getValue(isResizing)) next(isResizing, false);
			}
		}),
		makeSubscribe(mouseXyLocal, \mxy -> {
			if (isCurrentMInteractionItemId(manager, itemId) && getValue(isResizing)) {
				rtlCorrection = if (getDefaultRtl()) 1.0 else 1.0;
				blockOtherMInteractionItemIds(manager, itemId);
				nextDistinct(tempWH, WidthHeight(getValue(tempWH).width, max(minHeight, mxy.y)));
				nextDistinct(tempWH, WidthHeight(max(minWidth, rtlCorrection * mxy.x), getValue(tempWH).height));
			}
		})
	],	
	MSelect2(dockA, mobileTheme, \d, lg -> {
		if (d[panelId].areaId < 0) {
			next(getValue(state.sideExpanded)[panelId], true);
			MShowLazy(enabled, \ -> movablePanel(lg));
		} else MEmpty();	
	})) 
	
}

makeDockedPanel(isColumn : bool, colA : [int], state : MDockingState, panelId : int, style : [MDockingStyle]) -> Material {
	manager = state.manager;
	pstyle = getValue(state.panelStyle)[panelId];
	dontMove  = contains(pstyle, MDPanelDontMove());
	isToolbar = isDockingToolbar(state, panelId);
	isCustomToolbar = isSameStructType(getValue(state.content)[panelId], MDPanelToolbarCustom(MEmpty()));
	toolbarThickness = extractStruct(pstyle, MDPanelToolbarThickness(dockingItemHeight)).thickness;
	isVerticalToolbarMobile = !contains(pstyle, MDPanelVerticalToolbarWhenMobile());
	isEndAligned = getValue(extractStruct(pstyle, MDPanelEndAlign(make(false))).endAlign);
	fillSpace = contains(pstyle, MDPanelToolbarFillSpace());

	// Scroll
	dontScroll  = contains(pstyle, MDPanelDontScroll());
	hasScrollPos = \dockingstyle -> {
		!exists([MDPanelScrollPosition(make(zeroPoint))], \dsp -> isSameStructType(dsp, dockingstyle))
	};
	scrollPos = extractStruct(style, MDPanelScrollPosition(make(zeroPoint))).scrollPos;
	hasScrollInspectVisible = \dockingstyle -> {
		!exists([MDPanelScrollInspectVisible(make(zeroPoint), makeWH())], \dsiv -> isSameStructType(dsiv, dockingstyle))
	};
	scrollTopLeft = extractStruct(style, MDPanelScrollInspectVisible(make(zeroPoint), makeWH())).scrollTopLeft;
	scrollWidthHeight = extractStruct(style, MDPanelScrollInspectVisible(make(zeroPoint), makeWH())).scrollWidthHeight;
	
	// Order
	isFirstPanel = panelId == firstElement(colA, intMax);
	isLastPanel = panelId == lastElement(colA, intMax);

	// Expanded
	expanded = getValue(state.expanded)[panelId];
	sideExpanded = getValue(state.sideExpanded)[panelId];
	enabled = getValue(state.enabled)[panelId];
	
	// Theme
	darkTheme = contains(pstyle, MDPanelForceDarkTheme());
	lightTheme = contains(pstyle, MDPanelForceLightTheme());
	dockedTheme = if (darkTheme) const(false) else if (lightTheme) const(true) else manager.white;
	toolbarSepColorFn = toolbarHandleColorFn(fgetValue(dockedTheme), state.style);


	// Panel's content
	dContent = makeDockingPanelContent(state, panelId, false, !isColumn, make(false), make(false), pstyle);
	dPanel = \lg : bool -> {
		if (isToolbar) {
			dContent |> (\m -> MGroup2(TRectangle([MFill(panelBgColorFn(lg, pstyle))], TFillXY()), m))		
		} else {
			MSelect(expanded, \e -> {	
				pContent = if (dontScroll) {
					MCropSize(TFillXY(), dContent)
				} else {
					MScroll(
						dContent, TFillXY(),
						if (hasScrollPos(style) && hasScrollInspectVisible(style))
							[MScrollPosition(scrollPos), TScrollInspectVisible(scrollTopLeft, scrollWidthHeight)]
						else if (hasScrollPos(style))
							[MScrollPosition(scrollPos)]
						else []
					)
				}
				if (isColumn) {
					if (e) pContent else TFillXH(dockingCollapsedSeparatorH);
				} else	pContent;

			}) 
			|> (\m -> MGroup2(TRounded(0.0, 0.0, dockingRadius, dockingRadius, [MFill(panelBgColorFn(lg, pstyle))], TFillXY()), m))  
			|> (\m -> if (!isColumn) m else MShowLazy(sideExpanded, \ -> m))
		} 
	}

	MSelect(dockedTheme, \lg -> {
		[
			makePanelTopBar(true, panelId, colA, state, isColumn, lg)	
				|> (\m -> 
					if (dontMove || fillSpace) m else {
						MSelect(state.dockedSize, \ms -> {
							r = dockingRadius;
							cursor = fif(fselect(manager.manager.dragging, FLift(\dr -> dr >= 0)), const(GrabbingCursor()), const(GrabCursor()));
							draggingShape = 
								TRounded(r, r, r, r, [MFill(dockingPrimaryColor), FillOpacity(dockingMobileOpacity)],
								if (isCustomToolbar) TFixed(toolbarThickness, toolbarThickness * 3.0) else
									TFixed(
										ms[panelId].width, 
										if (getValue(getValue(state.expanded)[panelId])) ms[panelId].height else dockingBarHeightPanel
									)
								);
							dsRotated = draggingShape |> (\ds -> if (isToolbar && isVerticalToolbarMobile) TRotate90(ds) else ds);	

							// Draggable handle
							MDraggable(panelId, m, [
								TDragShape(TTranslate(const(zeroPoint), dsRotated)),
								TCursorShape(DynamicCursor(cursor))
							])
						})
					}
				),
			dPanel(lg),
			makeMDockingLinedHandle(isColumn, true, state, toolbarSepColorFn, panelId, colA, false, true)
				|> showIf(containsDockingToolbarButtons(state, colA) && isPanelFullSize(state, panelId))
		]
		|> (\r -> {
			if (isToolbar) {
				if (isColumn) MLinesA(if (isEndAligned) reverseA(r) else r)
					else MColsA(if (isEndAligned) reverseA(r) else r)
			} else MLines(r)
		});
	})
}