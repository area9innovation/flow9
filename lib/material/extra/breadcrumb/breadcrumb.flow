import material/material;
import pebbles/pebble_controller;

export {

	MBreadcrumb : (
		parts : Transform<[?]>,
		partCaption : (int, ?) -> string,
		goToPart : (int, ?) -> void,
		getChildren : (?) -> [?],
		style : [MBreadcrumbStyle]
	) -> Material;

	MBreadcrumbStyle ::=
		MBreadcrumbActiveStyle, MBreadcrumbInactiveStyle,
		MBreadcrumbIconStyle, MBreadcrumbIconBorder,
		MEnabled, TFlexibleGridContentSizeInspector, TFillersSupport, TFlexibleGridMaxContentWidthInspector, MBreadcrumbMakeLine;

		MBreadcrumbActiveStyle : (style : [MTextStyle]);
		MBreadcrumbInactiveStyle : (style : [MTextStyle]);
		MBreadcrumbIconStyle : (style : [MIconStyle]);
		MBreadcrumbIconBorder : (border : double);
		// MCols2A by default
		MBreadcrumbMakeLine(combine : ([Material]) -> Material);
}

MBreadcrumb(
	parts : Transform<[?]>,
	partCaption : (int, ?) -> string,
	goToPart : (int, ?) -> void,
	getChildren : (?) -> [?],
	style : [MBreadcrumbStyle]
) -> Material {
	MGetManager(\manager -> MGetFocusGroup(\parent ->
		MBreadcrumbInternal(manager, parent, parts, partCaption, goToPart, getChildren, style)
	));
}

MBreadcrumbInternal(
	manager : MaterialManager,
	parent : MFocusGroup,
	parts : Transform<[?]>,
	partCaption : (int, ?) -> string,
	goToPart : (int, ?) -> void,
	getChildren : (?) -> [?],
	style : [MBreadcrumbStyle]
) -> Material {

	activeStyle =
		extractStruct(style,
			MBreadcrumbActiveStyle([
				MSubheading(),
				MPrimaryColor()
			])
		).style;

	inactiveStyle =
		extractStruct(style,
			MBreadcrumbInactiveStyle([
				MSubheading(),
				MTextColor()
			])
		).style;

	iconStyle =
		extractStruct(style,
			MBreadcrumbIconStyle([
				MIconSize(20.0),
				MTextColor()
			])
		).style;

	iconBorder =
		extractStruct(style,
			MBreadcrumbIconBorder(2.0)
		).border;
	makeLine = extractStruct(style, MBreadcrumbMakeLine(\arr -> MColsA(arr))).combine;

	MFlexibleGrid(fselect(parts, FLift(\ps -> {
		lastIndex = length(ps) - 1;

		mapi(ps, \i, part : ? ->
			if (i == lastIndex)
				MText(partCaption(i, part), inactiveStyle)
			else {
				siblings = getChildren(part);
				breadcrumb =
					MFocusClickable(
						[MOnClick(\ -> goToPart(i, part)), extractStruct(style, MEnabled(const(true)))],
						\focus ->
							MText(partCaption(i, part), activeStyle)
					);
				arrow = MIcon("chevron_end", iconStyle);

				lastItem =
					if (siblings == [])
						arrow
					else {
						openMenu = make(false);

						MFocusClickable(
							[MOnClick(\ -> reverseBehaviour(openMenu))],
							\focus ->
								MMenu(
									arrow,
									mapi(siblings, \j, sibling ->
										MMenuSingleLine(
											partCaption(j, sibling),
											[MOnClick(\ -> goToPart(j, sibling))]
										)
									),
									[MOpenMenu(openMenu)]
								)
						)
					};

				makeLine([breadcrumb, lastItem |> MBorderLeftRight(iconBorder)])
			}
		);
	})),
		ifArrayPush([
			MOrdered(true),
			extractStruct(style, TFlexibleGridContentSizeInspector(makeWH())),
			extractStruct(style, TFlexibleGridMaxContentWidthInspector(make(0.)))
		], contains(style, TFillersSupport()), TFillersSupport())
	)
}
