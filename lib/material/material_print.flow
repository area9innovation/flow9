import material/material_dialog;

export {
    // JS Only

	// Content displayed in browser print dialog
	MPrintTarget(m : Material) -> Material;
	// Similar to MPrintTarget, only every element is printed on different page
	// By default TPrintPages uses web view size as available space for printing
	// TPrintPageSize variable or ShowMPrintDialog dialog let to redefine this size with any other
	MPrintPages(m : Transform<[Material]>) -> Material;
	// Print helper
	MPrintPage(
		page : Material,
		fn : DynamicBehaviour<Maybe<(cb : (png : string) -> void) -> void>>,
		mode : DynamicBehaviour<bool>
	);
	// Change content when printing
	MIfPrint(then : Material, elsem : Material) -> Material;

	// Show dialog to select page size for printing
	// Selected page size affects only content inside TPrintPages/MPrintPages and can be read from TPrintPageSize
	ShowMPrintDialog(manager : MaterialManager) -> () -> void;
	// Same as ShowMPrintDialog, only takes pages for printing as parameter
	ShowMPrintPagesDialog(manager : MaterialManager, pages : Transform<[MPrintPage]>, style : [MPrintStyle]) -> () -> void;

	// Styles for print customizations
	MPrintStyle ::= MPrintSelectedPageSize, MPrintSelectedOrientation, MPrintExtractPNG, MPrintOnOK, MPrintCustomContent, MPrintClose;
		MPrintSelectedPageSize(selPageSize : DynamicBehaviour<int>);
		MPrintSelectedOrientation(selOrientation : DynamicBehaviour<int>);
        // JS: Extract every page as png in base64 format
        // Ignores crossorigin images
        // setfn returns a function that recieves a page index and callback which gets base64 string when image is ready
	    // setfn returns None() if child is destroyed
        MPrintExtractPNG(setfn : (fn : Maybe<(page : int, cb : (png : string) -> void) -> void>) -> void);
		MPrintOnOK(onOK : () -> void);
		// Replace the default UI in print dialog
		MPrintCustomContent(content : Material);
		MPrintClose(closeB : DynamicBehaviour<bool>);

    // JS: Extract every material as png in base64 format
    // Ignores crossorigin images
    // setfn returns a function that recieves a material index and callback which gets base64 string when image is ready
    // setfn returns None() when destroyed
    MPrintPagesExtractPNG(m : [Material], size : Transform<WidthHeight>, setfn : (fn : Maybe<(page : int, cb : (png : string) -> void) -> void>) -> void) -> Material;

	MPrintPagesExtractPNG2(
		mp : [Pair<DynamicBehaviour<bool>, Material>],
		size : Transform<WidthHeight>,
		setfn : (fn : Maybe<(page : int, cb : (png : string) -> void) -> void>) -> void
	) -> Material;

	// Default chrome print dialog sizes
	MPrintPaperSizes : [Pair<string, WidthHeight>];

	// Workaround for Chrome resizing print page when DOM element with absolute positioning is scaled. Not for general use.
	// Looks like printing issues were fixed on browser level, so we are going to get rid of this workaround
	MPrintNeutral(m : Material) { if (isUrlParameterTrue("new")) m else MAccess([ClassName("printNeutral")], m) }

	// Printing units helpers

	cm2pixels(cm : double) -> double{
		cm * 96.0 / 2.54
	}

	pixels2cm(px : double) -> double{
		px * 2.54 / 96.0
	}

	mm2pixels(mm : double) -> double{
		mm * 96.0 / 25.4
	}

	pixels2mm(px : double) -> double{
		px * 25.4 / 96.0
	}

	mm2pixelsDPI(mm : double, dpi : int) -> double{
		mm * i2d(dpi) / 25.4
	}

	pixels2mmDPI(px : double, dpi : int) -> double{
		px * 25.4 / i2d(dpi)
	}

	whmm2pixels(wh : WidthHeight) -> WidthHeight {
		WidthHeight(
			mm2pixels(wh.width),
			mm2pixels(wh.height)
		)
	}

	whpixels2mm(wh : WidthHeight) -> WidthHeight {
		WidthHeight(
			pixels2mm(wh.width),
			pixels2mm(wh.height)
		)
	}
}


// International Paper Sizes

// A Series Paper Sizes
A4A0 = WidthHeight(1682., 2378.) |> whmm2pixels;
A2A0 = WidthHeight(1189., 1682.) |> whmm2pixels;
A0 = WidthHeight(841., 1189.) |> whmm2pixels;
A1 = WidthHeight(594., 841.) |> whmm2pixels;
A2 = WidthHeight(420., 594.) |> whmm2pixels;
A3 = WidthHeight(297., 420.) |> whmm2pixels;
A4 = WidthHeight(210., 297.) |> whmm2pixels;
A5 = WidthHeight(148., 210.) |> whmm2pixels;
A6 = WidthHeight(105., 148.) |> whmm2pixels;
A7 = WidthHeight(74., 105.) |> whmm2pixels;
A8 = WidthHeight(52., 74.) |> whmm2pixels;
A9 = WidthHeight(37., 52.) |> whmm2pixels;
A10 = WidthHeight(26., 37.) |> whmm2pixels;

// B Series Paper Sizes
B0 = WidthHeight(1000., 1414.) |> whmm2pixels;
B1 = WidthHeight(707., 1000.) |> whmm2pixels;
B2 = WidthHeight(500., 707.) |> whmm2pixels;
B3 = WidthHeight(353., 500.) |> whmm2pixels;
B4 = WidthHeight(250., 353.) |> whmm2pixels;
B5 = WidthHeight(176., 250.) |> whmm2pixels;
B6 = WidthHeight(125., 176.) |> whmm2pixels;
B7 = WidthHeight(88., 125.) |> whmm2pixels;
B8 = WidthHeight(62., 88.) |> whmm2pixels;
B9 = WidthHeight(44., 62.) |> whmm2pixels;
B10 = WidthHeight(31., 44.) |> whmm2pixels;

// C Series Envelope Sizes
C0 = WidthHeight(917., 1297.) |> whmm2pixels;
C1 = WidthHeight(648., 917.) |> whmm2pixels;
C2 = WidthHeight(458., 648.) |> whmm2pixels;
C3 = WidthHeight(324., 458.) |> whmm2pixels;
C4 = WidthHeight(229., 324.) |> whmm2pixels;
C5 = WidthHeight(162., 229.) |> whmm2pixels;
C6 = WidthHeight(114., 162.) |> whmm2pixels;
C7 = WidthHeight(81., 114.) |> whmm2pixels;
C8 = WidthHeight(57., 81.) |> whmm2pixels;
C9 = WidthHeight(40., 57.) |> whmm2pixels;
C10 = WidthHeight(28., 40.) |> whmm2pixels;

// US Paper Sizes
HalfLetter = WidthHeight(140., 216.) |> whmm2pixels;
Letter = WidthHeight(216., 279.) |> whmm2pixels;
Legal = WidthHeight(216., 356.) |> whmm2pixels;
JuniorLegal = WidthHeight(127., 203.) |> whmm2pixels;
LedgerTabloid = WidthHeight(279., 432.) |> whmm2pixels;

MPrintPaperSizes = [
    Pair("A0", A0),
    Pair("A1", A1),
    Pair("A2", A2),
    Pair("A3", A3),
    Pair("A4", A4),
    Pair("A5", A5),
    Pair("Legal", Legal),
    Pair("Letter", Letter),
    Pair("Tabloid", LedgerTabloid),
    Pair(_("Custom"), zeroWH)
];

// Material helpers

MPrintTarget(m : Material) -> Material {
	MGetFocusGroup(\p -> MGetMaterial2Tropic(\m2t -> TPrintTarget(m2t(m, p))))
}

MPrintPages(m : Transform<[Material]>) -> Material {
	MGetFocusGroup(\p -> MGetMaterial2Tropic(\m2t -> TPrintPages(fmap(m, \_m -> m2t(_m, p)))))
}

MIfPrint(then : Material, elsem : Material) -> Material {
    MGetFocusGroup(\p -> MGetMaterial2Tropic(\m2t -> TIfPrint(m2t(then, p), m2t(elsem, p))))
}

ShowMPrintDialog(manager : MaterialManager) -> () -> void {
	ShowMPrintPagesDialog(manager, const([]), []);
}

ShowMPrintPagesDialog(manager : MaterialManager, printPagesT : Transform<[MPrintPage]>, style : [MPrintStyle]) -> () -> void {
    close = extractStruct(style, MPrintClose(make(false))).closeB;
    selectedPageSize = extractStruct(style, MPrintSelectedPageSize(make(4))).selPageSize;
    selectedOrientation = extractStruct(style, MPrintSelectedOrientation(make(0))).selOrientation;
	hasPrintExtractPNGFn = containsStruct(style, MPrintExtractPNG(nop1));
    pngSetFn = extractStruct(style, MPrintExtractPNG(nop1)).setfn;
	onOK = extractStruct(style, MPrintOnOK(nop)).onOK;
    widthS = make("21.0");
    heightS = make("29.7");
    isReady = make(false);
	pagesB = make(MEmpty());

	ShowMDialog(
        manager,
        close,
        [
            MDialogTitle(_("Print Dialog")),
            MDialogActions([
                MTextButton(_("CLOSE"), \ -> reverseBehaviour(close), [], []),
                MTextButton(_("PRINT"), \ -> {
					onOK();
					// Print pages to update pagesB
					next(pagesB,
						MCropSize(
							TFixed(0.0, 0.0),
							MPrintPages(fmap(printPagesT, \p ->
									MExtractPNG(
										MIfPrint(p.page, MIf(p.mode, MAvailable(p.page, TSized(feither(TPrintPageSize, const(zeroWH)))), MEmpty())),
										\fn -> next(p.fn, fn)
									)
								)
							)
						)
					);
					// Show browser's print dialog
					showPrintDialog();
				}, [MButtonRaised()], []),
            ]),
            MDialogUseFrame(),
            MDialogClickOutToClose(),
            MIsReady(isReady)
        ],
        MConstruct(concatA([
			if (hasPrintExtractPNGFn) [
				make2Subscribe(fsubmap(printPagesT, \p -> p.fn), isReady, \pfn, r -> {
					if (r && !contains(pfn, None())) {
						pngSetFn(Some(\i, cb -> {
							printPage = elementAt(fgetValue(printPagesT), i, MPrintPage(MEmpty(), make(None()), make(false)));
							next(printPage.mode, true);
							maybeApply(pfn[i],
								\fn -> fn(\v -> {
									next(printPage.mode, false);
									cb(v);
								})
							);
						}))
					} else {
						pngSetFn(None());
					}
				}),
			] else [],
			[
				make2SubscribeUns(selectedPageSize, selectedOrientation, \s, o -> {
					if (s >= length(MPrintPaperSizes) - 1 || s < 0) {
						[
							makeSubscribe(fwh(fselect(widthS, FLift(\w -> cm2pixels(s2d(w)))), fselect(heightS, FLift(\h -> cm2pixels(s2d(h))))), \v -> {
								next(TPrintPageSize, Some(v))
							})()
						]
					} else {
						selPrintPageSize = secondOfPair(MPrintPaperSizes[s]);
						printPageSize = if (o == 0) {
							selPrintPageSize;
						} else {
							WidthHeight(selPrintPageSize.height, selPrintPageSize.width);
						};
						next(TPrintPageSize, Some(printPageSize));
						[];
					}
				}),
				\ -> mrender(manager, false, MMutable(pagesB)),
				\ -> \ -> next(TPrintPageSize, None())
			]]),
			MAvailableWidth(
				extractStruct(style,
					MPrintCustomContent(
						MLines([
							MDropDown(selectedPageSize, "", map(MPrintPaperSizes, firstOfPair), [MLabel(_("Page Size:")), MWidth(-1.0)]),
							MShowAnimationHeight(
								fselect(selectedPageSize, FLift(\s -> s >= length(MPrintPaperSizes) - 1 || s < 0)),
								MBaselineCols([
									MTextInput(widthS, [MWidth(64.0)], []),
									MText(_("cm"), []),
									MIcon("close", [])
									|> (\f -> MBorder4(12.0, f)),
									MTextInput(heightS, [MWidth(64.0)], []),
									MText(_("cm"), [])
								])
								|> MBorderTop(12.0)
								|> MCenterX,
								[MAutoStart()]
							),
							MShowAnimationHeight(
								fselect(selectedPageSize, FLift(\s -> s < length(MPrintPaperSizes) - 1 && s >= 0)),
								MDropDown(selectedOrientation, "", [_("Portrait"), _("Landscape")], [MLabel(_("Orientation:")), MWidth(-1.0)])
								|> MBorderTop(12.0),
								[MAutoStart()]
							),
						])
					)
				).content,
				const(260.0)
			)
         )
    );

    \ -> reverseBehaviour(close);
}

MPrintPagesExtractPNG(m : [Material], size : Transform<WidthHeight>, setfn : (fn : Maybe<(page : int, cb : (png : string) -> void) -> void>) -> void) -> Material {
    pngFn = make(generate(0, length(m), \__ -> None()));
    pngMode = generate(0, length(m), \__ -> make(false));

    MConstruct(
        [
            makeSubscribe(pngFn, \pfn -> {
                if (!contains(pfn, None())) {
                    setfn(Some(\i, cb -> {
                        next(pngMode[i], true);
                        maybeApply(pfn[i], \fn -> fn(\v -> { deferUntilNextFrameRendered(\ -> next(pngMode[i], false)); cb(v); }));
                    }))
                } else {
                    setfn(None())
                }
            })
        ],
        MLines(
            mapi(m, \i, mm ->
                MExtractPNG(
                    MIf(
                        pngMode[i],
                        MAccess(
                            [
                                ClassName("print-page")
                            ],
                            MAvailable(mm, TSized(size))
                        ),
                        TEmpty()
                    ),
                    \fn -> next(pngFn, replace(getValue(pngFn), i, fn))
                )
            )
        )
    )
}

MPrintPagesExtractPNG2(
	mp : [Pair<DynamicBehaviour<bool>, Material>],
	size : Transform<WidthHeight>,
	setfn : (fn : Maybe<(page : int, cb : (png : string) -> void) -> void>) -> void
) -> Material {

	pngFn = make(generate(0, length(mp), \__ -> None()));
	pngMode = generate(0, length(mp), \__ -> make(false));

	MConstruct(
		[
			makeSubscribe(pngFn, \pfn -> {
				if (!contains(pfn, None())) {
					setfn(Some(\i, cb -> {
						next(pngMode[i], true);
						maybeApply(pfn[i], \fn -> fn(\v -> {
							deferUntilNextFrameRendered(\ -> next(pngMode[i], false));
							cb(v);
						}));
					}))
				} else {
					setfn(None())
				}
			})
		],
		MLines(
			mapi(mp, \i, mm -> unpair(mm, \ready, m ->
				MExtractPNG2(
					MIf(
						pngMode[i],
						MAccess([ClassName("print-page")],
							MAvailable(m, TSized(size))
						),
						TEmpty()
					),
					ready,
					\fn -> next(pngFn, replace(getValue(pngFn), i, fn))
				)
			))
		)
	)
}
