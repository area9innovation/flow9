import material/internal/material_capture;
import material/iscript/material_iscript_config;

export {
	createRootMFocusGroup(style : [MRenderStyle]) -> MFocusGroup {createMaterialManagerMFocusGroup(style, false)};
	makeMaterialManager(style : [MRenderStyle]) -> MaterialManager;
}

managerId = ref 0;

createMaterialManagerMFocusGroup(style : [MRenderStyle], fromMakeManager : bool) -> MFocusGroup {
	theme = extractStruct(style, defaultMaterialTheme);

	currentManagerId = if (fromMakeManager) ^managerId else -1;
	if (fromMakeManager) managerId := currentManagerId + 1;

	MFocusGroup(
		make(false),
		currentManagerId,
		const(0),
		FEmpty(),
		const([]),
		const(-1),
		None(),
		"MaterialManagerFocus",
		None(),
		None(),
		None(),
		make(-1),
		make(-1),
		-1,
		make(makeTree()),
		0,
		make(makeTree()),
		const(true),
		const(true),
		const(true),
		extractStruct(style, MFocusEnabled(const(true))).enabled,
		const(false),
		const(true),
		const(false),
		const(true),
		make(false),
		make(false),
		const(zeroTransformMatrix),
		const(zeroWH),
		None(),
		MFocusGroupExtraFeatures(
			make(false),
			const(false)
		),
		extractStruct(style, MSetRTL(getDefaultRtl())).rtl,
		theme,
		makeTree()
	);
}

makeMaterialManager(style : [MRenderStyle]) -> MaterialManager {
	nextRenderId = ref 0;
	grabSnapshotTree = ref makeTree();
	closedDialogue = make(true);
	forceRender = make(false);

	// Regular application shouldn't have more than a single MaterialManager,
	// hence we shouldn't execute makeMaterialManager more than once.
	if (isDevMode() && ^managerId > 0) {
		println("WARNING: more than one material manager is created");
		printCallstack();
	}

	grabSnapshot = \grabFn -> {
		renderId = ^nextRenderId;
		nextRenderId := renderId + 1;
		grabSnapshotTree := setTree(^grabSnapshotTree, renderId, grabFn);
		\-> {
			grabSnapshotTree := removeFromTree(^grabSnapshotTree, renderId);
		}
	}
	tGrabSnapshot = TGrabSnapshot(grabSnapshot);
	feedbackOptions = extractStruct(style, MFeedbackOptions("", \__, __, __ -> {}, \__, __ -> {}));
	renderRoot = extractStructMany(style, RenderRoot(""));

	awh : TInspectStageWH = extractStruct(style, TInspectStageWH(make(WidthHeight(getStageWidth(), getStageHeight()))));
	stageWH : TStageWH = extractStruct(style, TStageWH(getStageWidth(), getStageHeight()));
	// Density-independent pixel (dp) scaling (https://material.io/guidelines/layout/units-measurements.html)
	// In js, 1dp = 1px
	dpiff : TDpif = extractStruct(style, TDpif(screenDensity));
	if (isUrlParameterTrue("debugMaterial")) {
		println(stageWH.width);
		println(dpiff);
	}
	sheet : Stylesheet = extractStruct(style, emptyStylesheet);
	dpiFactor : TInspectDpiFactor = extractStruct(style, TInspectDpiFactor(make(Factor(1.0, 1.0))));

	tStyle : [TRenderStyle] = concat([dpiff, stageWH, sheet, dpiFactor, tGrabSnapshot, awh], renderRoot);

	customHandlers = extractStruct(
		style,
		MCustomCaptureKeys(
			None(),
			None()
		)
	);

	captures = [
		MCaptureKeys(\manager ->
			captureKeysMaterial(
				manager,
				closedDialogue,
				feedbackOptions.saveFeedback,
				feedbackOptions.showFeedbacks,
				customHandlers.keyDownFn,
				customHandlers.keyUpFn

			)
		),
		MCaptureMouse(\manager -> captureMouseMaterial(manager)),
		MCaptureLongTouch(\manager -> captureLongTouch(manager, closedDialogue, feedbackOptions.saveFeedback)),
	];

	expanderIds = extractStruct(style, MExpanderIds(make(0)));
	hideDialogs = extractStruct(style, MHideDialogs(make(false)));

	mStyle = concatA([style, captures, [expanderIds, hideDialogs]]);
	foc = createMaterialManagerMFocusGroup(style, true);
	iscriptManager = makeIScriptManager();

	tm = makeTManager(-1, tStyle);
	tManager =
		TManager(tm with xy = \ ->
			Point(
				getMouseX(getStage()) / fgetValue(dpiFactor.factor).x,
				(getMouseY(getStage()) - if (getValue(iscriptManager.showPanel)) 48.0 else 0.0) / fgetValue(dpiFactor.factor).y
			)
		);

	MaterialManager(
		foc.managerId,
		make(foc.theme.palette.light),
		tManager.zorder,
		foc,
		make(None()),
		fstall(getCurrentFocusedItemBehaviour2(foc), 100),
		tManager,
		makeTree(),
		\ -> ^nextRenderId,
		mStyle,
		\ -> map(^grabSnapshotTree |> getTreeValues, apply0) |> TGroup,
		foc.theme,
		makeTree(),
		None(),
		make(false),
		make([]),
		iscriptManager,
		forceRender,
		make(true),
		fselect2(awh.wh, dpiFactor.factor, FLift2(\wh, dp -> WidthHeight(wh.width / dp.x, wh.height / dp.y))),
		make(false)
	);
}

makeIScriptManager() -> IScriptManager {
	state : DynamicBehaviour<IScriptState> = make(IScriptStopped());
	settings = updateIScriptSettingsFromConfig(defaultIScriptSettings());

	IScriptManager(
		make(makeEmptyIScript()),
		state,
		make(makeList()),
		make(makeList()),

		make([]),
		make([]),
		make(makeTree()),

		make(isIScriptUrlParamsSet()),
		make(false),
		make(false),
		make(zeroWH),

		settings,
		\ -> {
			CaptureOptions(
				getValue(settings.captureCallstack),
				getValue(settings.captureTextDetails),
				getValue(settings.captureHttpRequests),
				getValue(settings.captureMouseEvents),
				getValue(settings.captureKeyEvents),
				getValue(settings.captureInteractionEvents),
				getValue(settings.captureHover),
				getValue(settings.captureClickEvents),
				getValue(settings.captureAppearanceEvents),
				getValue(settings.captureValueEvents),
				getValue(settings.captureEnableEvents),
				getValue(settings.captureSelectionEvents),
				getValue(settings.captureInputValues),
				getValue(settings.captureWigiEvents),
				getValue(settings.capturePebbleEvents),
				getValue(settings.captureCustomTypes),
				getValue(settings.captureMiscEvents),
			)
		},

		make(false),
		make(""),

		make(None()),

		IScriptReplayCursor(make(Point(0.0, 0.0)), make(false), make(TEmpty()), make(Point(0.0, 0.0))),

		None(),

		None(),

		None(),

		IScriptScenario(make(""), make(""), make(false), make(-1), make([])),
	)
}
