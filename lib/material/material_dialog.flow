import material/material2tropic;

export {
	// ! "Content" field in next functions must be decorated with MBackground or similar.

	// Shows a modal dialog
	ShowMDialog(manager : MaterialManager, closeWhen : Transform<bool>, style : [MDialogStyle], content : Material) -> void;

	// Show a modal dialog with a title and content and actions
	ShowMPopup(manager : MaterialManager, closeWhen : Transform<bool>, title : string, content : Material, actions : [Material]) -> void;

	// Show a modal dialog with a single button
	ShowMConfirmation(manager : MaterialManager, title : string, button : string, shortcut : string, dialogContent : Material) -> void;

	// Shows a modal dialog with save and cancel callbacks
	ShowMDialogWithCallbacks(manager : MaterialManager, view : Material, saveEnabled : Transform<bool> , onSave : () -> void, onCancel : () -> void) -> void;

	// Shows a modal dialog for folder with cancel, create, "create and open" buttons
	ShowMDialogForFolderWithCallbacks(manager : MaterialManager, view : Material, createEnabled : Transform<bool>, onCreate : (bool) -> void, onCancel : () -> void) -> void;

	// styles for standard two-button dialog
	MDialogWithCallbacksStyle ::= MDialogWithCallbacksSaveTitle, MDialogWithCallbacksCancelTitle, MDialogWithCallbacksSaveEnabled, 
		MDialogWithCallbacksIgnoreEnter, MDialogWithCallbacksValidateBeforeSave, MDialogStyle, MDialogWithCallbacksBeforeSave;
		MDialogWithCallbacksSaveTitle(title : string); // title for save button
		MDialogWithCallbacksCancelTitle(title : string); // title for cancel button
		MDialogWithCallbacksBeforeSave(beforeSave : (onOK : () -> void) -> void);
		MDialogWithCallbacksSaveEnabled(enabled : Transform<bool>); // is save button clickable
		MDialogWithCallbacksIgnoreEnter();
		MDialogWithCallbacksValidateBeforeSave(isValid : () -> bool);
	// customizable version of standard two-button dialog
	ShowMDialogWithCallbacksCustom(manager : MaterialManager, view : Material, styles : [MDialogWithCallbacksStyle], onSave : () -> void, onCancel : () -> void) -> void;
	// Show floating button
	ShowMFloatingAction(manager : MaterialManager, m : Material) -> () -> void;
}

ShowMDialog(manager : MaterialManager, closeWhen : Transform<bool>, style : [MDialogStyle], content : Material) -> void {
	m2t = \m, p -> material2tropicWithParent(manager, p, m);

	renderMDialog2(
		manager,
		closeWhen,
		style,
		content,
		m2t,
		\t ->
			trender(
				TAvailable(
					TTranslate(fif(manager.iscript.showPanel, const(Point(0.0, 48.0)), const(zeroPoint)), t),
					TIf(manager.iscript.showPanel, TSized(manager.iscript.available), TFillXY())
				)
				|> (\f ->
					materialLogFn(
						\ ->
							TFixSize(
								TLines2(
									TFixed(36., 36.),
									f
								),
								TFillXY()
							),
						MaterialWarning(),
						\ -> f
					)
				),
				manager.manager.renderStyle
			)
	);
}

ShowMConfirmation(manager : MaterialManager, title : string, button : string, shortcut : string, dialogContent : Material) -> void {
	close = make(false);
	ShowMDialog(manager, close,
		[
			MDialogTitle(title),
			MDialogUseFrame(),
			MDialogDraggable(TEmpty(), TFillXY()),
			MDialogResizable(),
			MDialogActions([
				MTextButton(button, \ -> next(close, true), [], [MShortcut(shortcut)]),
			]),
			MDialogScroll()
		],
		dialogContent
	)
}

ShowMDialogWithCallbacks(manager : MaterialManager, view : Material, saveEnabled : Transform<bool> , onSave : () -> void, onCancel : () -> void) -> void {
	ShowMDialogWithCallbacksCustom(manager, view, [MDialogWithCallbacksSaveEnabled(saveEnabled)], onSave, onCancel);
}

ShowMDialogForFolderWithCallbacks(
	manager : MaterialManager, 
	view : Material, 
	createEnabled : Transform<bool>, 
	onCreate : (bool) -> void,
	onCancel : () -> void
) -> void {
	close = make(false);

	onCancelDialog = \-> {
		next(close, true);
		onCancel();
	};

	onCreateDialog = \ -> {
		next(close, true);
		onCreate(false);
	}

	onCreateAndOpenDialog = \-> {
		next(close, true);
		onCreate(true);
	};

	ShowMDialog(manager, close,
		concatA([
			[
				MDialogUseFrame(),
				MDialogScroll(),
				MDialogActions([
					MTextButton(_("CANCEL"), onCancelDialog, [], [MShortcut("esc")]),
					MTextButton(_("CREATE"), onCreateDialog, [], [MEnabled(createEnabled)]),
					MTextButton(_("CREATE AND OPEN"), onCreateAndOpenDialog, [], [MEnabled(createEnabled), MShortcut("enter")]),
				])
			],
			[]
			]),
		view
		|> MSetMinWidth(MFixedX(256.))
	)
}

ShowMDialogWithCallbacksCustom(manager : MaterialManager, view : Material, styles : [MDialogWithCallbacksStyle], onSave : () -> void, onCancel : () -> void) -> void {
	close = make(false);

	saveStr = extractStruct(styles, MDialogWithCallbacksSaveTitle(_("SAVE"))).title;
	cancelStr = extractStruct(styles, MDialogWithCallbacksCancelTitle(_("CANCEL"))).title;
	saveEnabled = extractStruct(styles, MDialogWithCallbacksSaveEnabled(const(true))).enabled;
	ignoreEnter = contains(styles, MDialogWithCallbacksIgnoreEnter());
	canSave = extractStruct(styles, MDialogWithCallbacksValidateBeforeSave(\ -> true)).isValid;
	beforeSave = extractStruct(styles, MDialogWithCallbacksBeforeSave(\onOK -> onOK())).beforeSave;

	onSaveDialog = \-> if (canSave()) {
		beforeSave(\ -> {
			next(close, true);
			onSave();
		});
	};

	onCancelDialog = \-> {
		next(close, true);
		onCancel();
	};

	dialogStyles = subtractA(styles, 
		[
			MDialogUseFrame(), MDialogScroll(), MDialogActions([]), 
			MDialogWithCallbacksSaveTitle(""), MDialogWithCallbacksCancelTitle(""), MDialogWithCallbacksSaveEnabled(const(true))
		]);
	dialogStyles2 = map(dialogStyles, \style -> cast(style : MDialogWithCallbacksStyle -> MDialogStyle));

	ShowMDialog(manager, close,
		concatA([
			[
				MDialogUseFrame(),
				MDialogScroll(),
				MDialogActions([
					MTextButton(cancelStr, onCancelDialog, [], [MShortcut("esc")]),
					MTextButton(saveStr, onSaveDialog, [MButtonRaised()],
						if (ignoreEnter) [MEnabled(saveEnabled)] else [MEnabled(saveEnabled), MShortcut("enter")]
					),
				])
			],
			dialogStyles2
			]),
		view
		|> MSetMinWidth(MFixedX(256.))
	)
}

ShowMFloatingAction(manager : MaterialManager, m : Material) -> () -> void {
	next(manager.floatingActions, arrayPush(getValue(manager.floatingActions), m));
	\ -> next(manager.floatingActions, removeFirst(getValue(manager.floatingActions), m))
}

ShowMPopup(manager : MaterialManager, closeWhen : Transform<bool>, title : string, content : Material, actions : [Material]) -> void {
	ShowMDialog(manager, closeWhen, [MDialogTitle(title), MDialogUseFrame(), MDialogScroll(), MDialogActions(actions)], content);
}
