import material/internal/material_popup;
import material/internal/material_typography;

export {
	MTooltip2T(manager : MaterialManager, parent : MFocusGroup, m : MTooltip, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MTooltip2T(manager : MaterialManager, parent : MFocusGroup, m : MTooltip, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	zorder = getValue(manager.manager.zorder);
	disableOnDragging = contains(m.style, MDisableOnDragging());

	hover = make(false);
	enabled =
		extractStruct(m.style, MEnabled(const(true))).enabled
		|> (\en -> if (disableOnDragging) fand(en, feq(manager.manager.dragging, -1)) else en);

	openMenu = MOpenMenu(make(false));

	positionScale = make(zeroPositionScale);

	buttonWH = makeWH();
	lightBackground = extractStruct(m.style, MLightBackground(getLightBackground(parent))).light;
	bgStyle = extractStruct(m.style, MBackgroundStyle([MFill(MGrey(if (lightBackground) 600 else 200))]));

	tooltipAlign =
		concatA([
			extractStructMany(m.style, MBottom()),
			extractStructMany(m.style, MTop()),
			extractStructMany(m.style, MLeft()),
			extractStructMany(m.style, MRight())
		])
		|> (\f -> if (length(f) > 0) f[0] else MBottom());
	translatePosition = extractStruct(m.style, MTranslatePosition(const(zeroPoint))).point;

	TConstruct(
		[
			makeSubscribe2Uns(fand(enabled, openMenu.opened), \op -> {
				if (op && !isEmptyMaterial(m.tooltip)) {
					[
						interruptibleDeferUntilNextFrameRendered(\ -> {
							MComponentGroup(
								MBorder(
									8.0,
									4.0,
									8.0,
									4.0,
									m.tooltip
								),
								[
									MFocusName("MTooltip"),
									UpdateMaterialTheme(\t ->
										MLightTheme(
											MaterialTheme(t with type = MaterialTypeManager(t.type with body1 = MTooltipDesktop(), body2 = MTooltipDesktop())),
											!lightBackground
										)
									)
								]
							)
							|> (\f -> {
								bwh = getValue(buttonWH);
								cwh = makeWH();

								RenderMPopup(
									manager,
									parent,
									MPopup(
										[[MAttachBox(f, cwh)]],
										fselect2(positionScale, translatePosition, FLift2(\ps : PositionScale, tr : Point ->
											PositionScale(Point(ps.pos.x + tr.x, ps.pos.y + tr.y), ps.scale))
										),
										[
											RMAlphaAnimation(),
											openMenu,
											MSameZorder(true),
											RMBorders(0.),
											bgStyle,
											RMDontHandle(),
											RMNoSnapSize(),
											MElevation(zero),
											RMOffset(
												fselect(
													cwh,
													FLift(
														\wh -> {
															WH = switch (tooltipAlign) {
																MBottom() :
																	WidthHeight((bwh.width - wh.width) / 2., bwh.height + 8.);
																MTop() :
																	WidthHeight((bwh.width - wh.width) / 2., -wh.height - 8.);
																MLeft() :
																	WidthHeight(-wh.width - 8., (bwh.height - wh.height) / 2.);
																default :
																	WidthHeight(bwh.width + 8., (bwh.height - wh.height) / 2.);
															};

															Pair(WH, 0.)
														}
													)
												)
											),
											MRippleShape("MTooltip")
										]
									),
									m2t
								)
							})
						})
					]
				} else {
					[]
				}
			}),
			\ -> fconnect(
				fand(enabled, fand(hover, fselect(manager.manager.zorder, FLift(\cz -> cz <= zorder)))),
				openMenu.opened
			),
			\ -> \ -> next(openMenu.opened, false)
		],
		(if (!mobile)
			TInteractive([TMouseInside(hover, false)], m2t(m.box, parent))
		else
			TCopySize(
				m2t(m.box, parent),
				\tr ->
					TInteractive([TMouseDownGhost(hover)], tr),
				true
			))
		|> (\f -> MPositionScaleAttachBox(positionScale, buttonWH, f, fand(enabled, openMenu.opened)))
	)
}

isEmptyMaterial(m : Material) -> bool {
	m == TEmpty() || m == TMutable(ConstBehaviour(TEmpty())) || m == MMutable(ConstBehaviour(TEmpty()))
}