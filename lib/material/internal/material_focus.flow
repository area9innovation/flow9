import material/internal/material_iscript_internal;
import sys/speechsynthesis;
import material/internal/material_util;

export {
	// when a modal dialog comes, use this to increase the z-order
	pushZorder(manager : MaterialManager) -> () -> void;
	MPushZorder(content : Material) -> Material;
	popZorder(manager : MaterialManager) -> () -> void;

	// If you need a focus id, call this.
	// Returns a focus id, and transforms defining whether this button is enabled or not,
	// and wheter it has focus
	addMaterialFocusId(
		manager : MaterialManager,
		parent : MFocusGroup,
		name : string,
		title : Transform<string>,
		onClick : Maybe<() -> void>,
		down : DynamicBehaviour<bool>,
		rightDown : DynamicBehaviour<bool>,
		middleDown : DynamicBehaviour<bool>,
		hover : DynamicBehaviour<bool>,
		focused : DynamicBehaviour<bool>,
		active : DynamicBehaviour<bool>,
		selected : DynamicBehaviour<bool>,
		enabled : Transform<bool>,
		focusEnabled : Transform<bool>,
		keepFocus : bool,
		importantFocus : bool,
		behaviours : [IScriptBehaviour],
		recordingEnabled : Transform<bool>,
		iScriptId : Maybe<string>,
		tabOrder : Maybe<Transform<int>>,
		fn : (MFocus) -> Tropic
	) -> Tropic;
	addMaterialFocusGroupId(manager : MaterialManager, parent : MFocusGroup, name : string, isInitialized : bool, vertical : Maybe<bool>,
		horizontal : Maybe<bool>, scrollInfo : Maybe<MScrollInfo>, ready : Transform<bool>, focused : DynamicBehaviour<bool>, active : DynamicBehaviour<bool>,
		enabled : Transform<bool>, focusEnabled : Transform<bool>, behaviours : [IScriptBehaviour], recordingEnabled : Transform<bool>, clickEnabled : Transform<bool>,
		keysEnabled : Transform<bool>, hintMarkerEnabled : Transform<bool>, iScriptId : Maybe<string>, tabOrder : Maybe<Transform<int>>, zorderB : Maybe<Transform<int>>,
		trapFocusEnabled : Transform<bool>, theme : MaterialTheme, rtl : bool, fn : (parent : MFocusGroup) -> Tropic) -> Tropic;

	// yield focus to the next/previous focus id. 'gotonext' gives the direction.
	yieldFocus(manager : MaterialManager, gotonext : bool) -> void;
	yieldFocusVH(manager : MaterialManager, gotonext : bool, vertical : bool) -> void;
	nextFocus(manager : MaterialManager, parent : MFocusGroup, inOrder : bool) -> Maybe<MFocus>;
	resetMFocus(manager : MaterialManager) -> void;
	previousMFocus(manager : MaterialManager) -> void;
	scrollIntoView(focus : MaterialFocus) -> void;
	scrollIntoView2(parent : MFocusGroup, transformMatrix : TransformMatrix, widthHeight : WidthHeight) -> void;

	getCurrentFocusedItem(manager : MaterialManager) -> Maybe<MFocus>;
	getCurrentActiveItem(manager : MaterialManager) -> Maybe<MaterialFocus>;
	getPreviousFocusedItem(manager : MaterialManager) -> Maybe<MFocus>;

	isMFocusChild(parent : MFocusGroup, child : MaterialFocus) -> bool;

	managerTimer(manager : MaterialManager) -> LazyDeltaTimer;
	renderTimer(rend : MaterialRender) -> LazyDeltaTimer;

	makeTropicTag(manager : MaterialManager) -> int;

	addMInteractionItemId(manager : MaterialManager, itemId : int) -> void;
	addMInteractionItemIdIfEmpty(manager : MaterialManager, itemId : int) -> void;
	removeMInteractionItemId(manager : MaterialManager, itemId : int) -> void;
	blockOtherMInteractionItemIds(manager : MaterialManager, itemId : int) -> void;
	isCurrentMInteractionItemId(manager : MaterialManager, itemId : int) -> bool;

	addMPopupItemId(manager : MaterialManager, itemId : int) -> void;
	removeMPopupItemId(manager : MaterialManager, itemId : int) -> void;
	blockOtherMPopupItemIds(manager : MaterialManager, itemId : int) -> void;
	isCurrentMPopupItemId(manager : MaterialManager, itemId : int) -> bool;

	filterIScriptIdentifierWithoutFormPosition(id : List<IScriptIdentifier>) -> List<IScriptIdentifier>;
	filterIScriptIdentifierOnlyFormPosition(id : List<IScriptIdentifier>) -> List<IScriptFormPosition>;

	MComponent2TAccessibility(manager : MaterialManager, content : Tropic, focus : MaterialFocus, properties : [FAccessProperty]) -> Tropic;

	mClickButton(manager : MaterialManager, name : string) -> void;

	mSpeechAccessiblityEnabled = ref isUrlParameterTrue("speech");
}

MPushZorder(content : Material) -> Material {
	MGetManager(\manager -> {
		zorder = make(getValue(manager.manager.zorder) + 1);

		MConstruct(
			[
				\ -> {
					v = getValue(manager.manager.zorder);
					next(manager.manager.zorder, v + 1);
					next(zorder, v + 1);
					setForceZorder(v + 1);

					\ -> {
						vPrev = getValue(manager.manager.zorder);
						if (vPrev > v) {
							nextDistinct(manager.manager.zorder, v);
							setForceZorder(v);
						}
					}
				}
			],
			MComponentGroup(
				content,
				[MZorder(zorder), MFocusName("MPushZorder")]
			)
		)
	});
}

pushZorder(manager : MaterialManager) -> () -> void {
	v = getValue(manager.manager.zorder);
	next(manager.manager.zorder, v + 1);
	setForceZorder(v + 1);
	\ -> {
		vPrev = getValue(manager.manager.zorder);
		if (vPrev > v) {
			nextDistinct(manager.manager.zorder, v);
			setForceZorder(v);
		}
	}
}

popZorder(manager : MaterialManager) -> () -> void {
	v = getValue(manager.manager.zorder);
	next(manager.manager.zorder, v - 1);
	setForceZorder(v - 1);
	\ -> {
		vPrev = getValue(manager.manager.zorder);
		if (vPrev < v) {
			nextDistinct(manager.manager.zorder, v);
			setForceZorder(v);
		}
	}
}

getNextFocusId(parent : MFocusGroup) -> int {
	parent.nextFocusId
}

getMFocusTabOrder(focus : MaterialFocus) -> [int] {
	switchMaterialFocus(
		focus,
		\f -> {
			arrayPush(getMFocusTabOrder(f.parent), fgetValue(f.id))
		},
		\f -> {
			eitherFn(
				f.parent,
				\fc ->
					arrayPush(getMFocusTabOrder(fc), fgetValue(f.id)),
				\ ->
					[fgetValue(f.id)]
			)
		}
	)
}

tryNext(t : Transform<?>, v : ?) -> void {
	switch (t) {
		DynamicBehaviour(__, __): nextDistinct(t, v);
		default: {}
	};
}

changeMaterialFocusId(manager : MaterialManager, parent : MFocusGroup, f : MaterialFocus, prevId : int, newId : int) -> void {
	tryNext(f.id, newId);

	if (prevId >= 0) {
		maybeApply(
			lookupTree(getValue(parent.focusIds), prevId),
			\fid -> if (fid == f) {
				next(parent.focusIds, removeFromTree(getValue(parent.focusIds), prevId));
				parent.nextFocusId ::= min(parent.nextFocusId, prevId);

				if (isIScriptEnabled(manager)) {
					maybeApply(f.iScriptId, \id -> {
						iScriptIdParent = getIScriptUniqueIdParent(f);
						next(iScriptIdParent.iScriptIds, removeFromTree(getValue(iScriptIdParent.iScriptIds), id));
						traverseInOrder(getValue(iScriptIdParent.iScriptIds), \__, v -> fixIScriptUniqueId(v));
					});
				}

				if (prevId >= 0 && getValue(parent.focusedId) == prevId) {
					next(f.focused, false);
					switchMaterialFocus(f, nop1, \foc -> next(foc.focusedId, -1));
					next(parent.focusedId, -1);
				}
			}
		)
	}

	if (newId >= 0) {
		setIdFn = \ -> {
		 	next(parent.focusIds, setTree(getValue(parent.focusIds), newId, f));
			if (parent.nextFocusId == newId) {
				if (isNone(lookupTree(getValue(parent.focusIds), parent.nextFocusId + 1))) {
					parent.nextFocusId ::= parent.nextFocusId + 1;
				} else {
					parent.nextFocusId ::= foldTree(getValue(parent.focusIds), 0, \k, __, acc -> if (k == acc) acc + 1 else acc);
				}
			}

		 	if (isIScriptEnabled(manager)) {
		 		fixIScriptUniqueId(f);
			}

			if (prevId >= 0 && getValue(parent.focusedId) == prevId)
				next(parent.focusedId, -1);
		};

		eitherFn(
			lookupTree(getValue(parent.focusIds), newId),
			\fid -> {
				if (fid != f) {
					setIdFn();
					changeMaterialFocusId(manager, parent, fid, newId, getNextFocusId(parent))
				}
			},
			setIdFn
		);
	}
}

setParentFocusValue(parent : MFocusGroup, id : int, value : bool) -> bool {
	pValue = getValue(parent.focusedId) == id;

	if (value && !pValue) {
		if (setMFocus(parent, value)) {
			next(parent.focusedId, id);

			true
		} else
			false
	} else if (!value && pValue) {
		setMFocus(parent, value);
		next(parent.focusedId, -1);

		true
	} else
		true
}

setMFocus(focus : MaterialFocus, value : bool) -> bool {
	fid = fgetValue(focus.id);
	fen = fgetValue(focus.focusEnabled);

	switchMaterialFocus(
		focus,
		\f -> {
			if (value && !fen) {
				false
			} else if (fgetValue(f.parent.id) == -2) {
				next(focus.focused, value);
				true
			} else {
				setParentFocusValue(f.parent, fid, value)
			}
		},
		\f -> {
			if (value && !fen) {
				false
			} else {
				eitherFn(
					f.parent,
					\p ->
						setParentFocusValue(p, fid, value),
					\ -> {
						nextDistinct(f.focused, value);
						true
					}
				)
			}
		}
	)
}

filterIScriptIdentifierWithoutFormPosition(id : List<IScriptIdentifier>) -> List<IScriptIdentifier> {
	filterList(id, \id0 ->
		switch (id0) {
			IScriptFocusTreePosition(__): {
				true
			}
			IScriptId(__): {
				true
			}
			default:
				false
		}
	)
}

filterIScriptIdentifierOnlyFormPosition(id : List<IScriptIdentifier>) -> List<IScriptFormPosition> {
	filterList(id, \id0 ->
		switch (id0) {
			IScriptFormPosition(__): {
				true
			}
			default:
				false
		}
	)
	|> (\f -> cast(f : List<IScriptIdentifier> -> List<IScriptFormPosition>))
}

updateNextId(parent : MFocusGroup, id : int) -> void {
	maybeApply(parent.parent, \p -> {
		nextDistinct(p.nextId, id);
		updateNextId(p, fgetValue(parent.id));
	});
}

addMaterialFocusId(
	manager : MaterialManager,
	parent : MFocusGroup,
	name : string,
	title : Transform<string>,
	onClick : Maybe<() -> void>,
	down : DynamicBehaviour<bool>,
	rightDown : DynamicBehaviour<bool>,
	middleDown : DynamicBehaviour<bool>,
	hover : DynamicBehaviour<bool>,
	focused : DynamicBehaviour<bool>,
	active : DynamicBehaviour<bool>,
	selected : DynamicBehaviour<bool>,
	enabled : Transform<bool>,
	focusEnabled : Transform<bool>,
	keepFocus : bool,
	importantFocus : bool,
	behaviours : [IScriptBehaviour],
	recordingEnabled : Transform<bool>,
	iScriptId : Maybe<string>,
	tabOrder : Maybe<Transform<int>>,
	fn : (MFocus) -> Tropic
) -> Tropic {
	next(parent.isInitialized, true);
	if (importantFocus) {
		nextDistinct(parent.extraFeatures.hasImportantChild, true)
	}

	defaultFocusId = make(-1);
	focusId = if (importantFocus) const(0) else either(tabOrder, defaultFocusId);

	isFocusWorkaround = !isUrlParameterFalse("material_focus_workaround");
	
	en = fand(parent.enabled, enabled);
	focusEn0 = fands([en, parent.zorderEnabled, focusEnabled], true);
	focusEn1 = if(isFocusWorkaround) {
		ffuse(focusEn0)
	} else {
		Pair(const(false), [])
	}
	focusEn = if(isFocusWorkaround) {
		focusEn1.first
	} else {
		fand(en, fand(parent.zorderEnabled, focusEnabled))
	}
	recEn = fand(parent.recordingEnabled, recordingEnabled);

	focusedB = fand(focusEn, focused);

	behavioursWithFocusedId = fold(behaviours, makeTree(), \acc, mb -> setTree(acc, strReplace(mb.name, " ", "_"), Pair(mb.beh, mb.type)));
	boxWidth = make(make(0.0));
	boxHeight = make(make(0.0));
	cs = if (isMaterialLogLevelEnabled(MaterialInfo()) && fgetValue(recEn)) captureStringCallstack() else "";

	TransformTAcc(\t2a, pi, s, mo -> {
		f =
			MFocus(
				focusId,
				FEmpty(),
				farrayPush(parent.tabOrder, focusId),
				iScriptId,
				strReplace(name, " ", "_"),
				title,
				parent,
				onClick,
				down,
				rightDown,
				middleDown,
				hover,
				en,
				focusEn,
				recEn,
				focused,
				active,
				selected,
				pi.transformMatrix,
				fsubselect2(boxWidth, boxHeight, \w, h -> fwidthheight(w, h)),
				make(true),
				behavioursWithFocusedId
			);

		updateParentFocusId = \foc -> {
			fid = fgetValue(f.id);
			pid = getValue(parent.focusedId);

			if (foc && pid != fid) {
				next(parent.focusedId, fid)
			} else if (!foc && pid == fid) {
				next(parent.focusedId, -1);
			}

			if (!foc) {
				deferred(\ -> nextDistinct(manager.previousFocus, Some(f)));
			}

			if (keepFocus) {
				if (foc) {
					setDownInsideFocus(manager, f);
				} else {
					manager.downInsideFocus ::= None();
				}
			}
		};

		content =
			TConstruct(
				concat(
					focusEn1.second,
					[
						\ -> {
							if (isNone(tabOrder))
								next(defaultFocusId, getNextFocusId(parent));

							disps =
								[
									makeSubscribe(fselectWithLast(f.id, FIdentity2()), \id ->
										changeMaterialFocusId(manager, parent, f, firstOfPair(id), secondOfPair(id)))(),
									makeSubscribe2(focusedB, \foc -> updateParentFocusId(foc))(),
									\ -> changeMaterialFocusId(manager, parent, f, fgetValue(f.id), -1)
								]
								|> (\disps ->
									if (isIScriptRecording(manager))
										arrayPush(disps, initIScriptRecording(manager, f, true))
									else
										disps
								);

							if (fgetValue(focusedB)) {
								updateParentFocusId(true);
							} else if (getValue(focused)) {
								next(focused, false);
							}

							\ -> applyall(disps)
						},

						\ -> \ -> next(f.updateEnabled, false)
					]
				),
				if (isMaterialLogLevelEnabled(MaterialInfo()) && fgetValue(f.parent.recordingEnabled))
					TCopySize(
						fn(f),
						\sz -> {
							color = randomMColor();
							md = make(false);

							TGroup2(
								TRounded(4.0, 4.0, 4.0, 4.0, [MStroke(color)], sz |> TBorderBottom(-1.0) |> TBorderRight(-1.0)),
								TInteractive(
									[TMouseDown(md)],
									TConstruct(
										[
											makeSubscribe2(md, \d -> if (d) iter(concat3([">>>"], strSplit(cs, "\n"), ["<<<"]), println))
										],
										TFrame(
											0.0,
											4.0,
											[MFill(color)],
											TBorder(
												2.0, 0.0, 2.0, 0.0,
												TText(f.name, [MFill(MTextMColor(color)), FontSize(10.0)])
											)
										)
									)
								)
							)
						},
						true
					)
				else
					fn(f)
			);

		b = t2a(content, pi, s, mo);
		f.form ::= b.form;

		next(boxWidth, b.metrics.width);
		next(boxHeight, b.metrics.height);

		b;
	});
}

addMaterialFocusGroupId(manager : MaterialManager, parent : MFocusGroup, name : string, isInitialized : bool, vertical : Maybe<bool>,
	horizontal : Maybe<bool>, scrollInfo : Maybe<MScrollInfo>, ready : Transform<bool>, focused : DynamicBehaviour<bool>, active : DynamicBehaviour<bool>,
	enabled : Transform<bool>, focusEnabled : Transform<bool>, behaviours : [IScriptBehaviour], recordingEnabled : Transform<bool>, clickEnabled : Transform<bool>,
	keysEnabled : Transform<bool>, hintMarkerEnabled : Transform<bool>, iScriptId : Maybe<string>, tabOrder : Maybe<Transform<int>>, zorderB : Maybe<Transform<int>>,
	trapFocusEnabled : Transform<bool>, theme : MaterialTheme, rtl : bool, fn : (parent : MFocusGroup) -> Tropic) -> Tropic {

	defaultFocusId = make(-1);
	hasImportantChild = make(false);
	focusId = fif(hasImportantChild, const(0), either(tabOrder, defaultFocusId));
	focusedId = make(-1);

	zorderKeeper = parent == manager.focus || isSome(zorderB);

	zorder =
		if (zorderKeeper)
			eitherMap(zorderB, idfn, const(getValue(manager.manager.zorder)))
		else
			parent.zorder;
	en = fand(parent.enabled, enabled);
	zorderEn =
		if (zorderKeeper)
			fand(fselect2(manager.manager.zorder, zorder, FLift2(\mz, z -> z >= mz)), clickEnabled)
		else
			fand(parent.zorderEnabled, clickEnabled);
	keysEn =
		if (zorderKeeper)
			fand(zorderEn, fand(clickEnabled, keysEnabled))
		else
			fand(parent.keysEnabled, fand(clickEnabled, keysEnabled));

	focusEn = fand(en, fand(parent.focusEnabled, focusEnabled));
	recEn = fand(parent.recordingEnabled, recordingEnabled);

	behavioursWithFocusedId = fold(behaviours, makeTree(), \acc, mb -> setTree(acc, strReplace(mb.name, " ", "_"), Pair(mb.beh, mb.type)));
	boxWidth = make(make(0.0));
	boxHeight = make(make(0.0));
	cs = if (isMaterialLogLevelEnabled(MaterialInfo()) && fgetValue(recEn)) captureStringCallstack() else "";

	TransformTAcc(\t2a, pi, s, mo -> {
		f =
			MFocusGroup(
				make(isInitialized),
				parent.managerId,
				focusId,
				FEmpty(),
				farrayPush(parent.tabOrder, focusId),
				zorder,
				eitherMap(iScriptId, \ui -> Some(strReplaces(ui, [" ", "_", "[", "_", "]", "_", ",", "_"])), None()),
				strReplace(name, " ", "_"),
				Some(parent),
				eitherMap(parent.vertical, \v -> Some(if (v == manager.focus.focusedId) parent.focusedId else v), None()),
				eitherMap(parent.horizontal, \h -> Some(if (h == manager.focus.focusedId) parent.focusedId else h), None()),
				focusedId,
				make(-1),
				getValue(focusedId),
				make(makeTree()),
				0,
				make(makeTree()),
				en,
				zorderEn,
				keysEn,
				focusEn,
				hintMarkerEnabled,
				recEn,
				const(false),
				fand(parent.ready, ready),
				focused,
				active,
				pi.transformMatrix,
				fsubwidthheight(boxWidth, boxHeight),
				scrollInfo,
				MFocusGroupExtraFeatures(
					hasImportantChild,
					trapFocusEnabled
				),
				rtl,
				theme,
				behavioursWithFocusedId,
			);

		maybeApply(vertical, \v -> f.vertical ::= if (v) Some(f.focusedId) else None());
		maybeApply(horizontal, \v -> f.horizontal ::= if (v) Some(f.focusedId) else None());

		content =
			TConstruct(
				[
					\ -> {
						if (isNone(tabOrder))
							next(defaultFocusId, getNextFocusId(parent));

						\ -> changeMaterialFocusId(manager, parent, f, fgetValue(f.id), -1);
					},
					makeSubscribe(fselectWithLast(f.id, FIdentity2()), \id ->
						changeMaterialFocusId(manager, parent, f, firstOfPair(id), secondOfPair(id))),
					makeSubscribe2(fmin3(focusEnabled, enabled, if (zorderKeeper) zorderEn else clickEnabled), \fen -> if (!fen) {
						nextDistinct(f.focusedId, -1);
						nextDistinct(f.focused, false);
					}),
					makeSubscribe2(f.nextId, \nid ->
						traverseInOrder(getValue(f.focusIds), \k, v -> if (k != nid) switchMaterialFocus(v, nop1, \fg -> nextDistinct(fg.nextId, -1)))
					),
					makeSubscribe(f.focusedId, \foc -> {
						if (foc != -1) {
							nextDistinct(f.nextId, foc);
						}

						fid = fgetValue(f.id);
						pid = getValue(parent.focusedId);

						if (foc == -1 && pid == fid)
							next(parent.focusedId, -1)
						else if (foc != -1 && pid != fid)
							next(parent.focusedId, fid);

						if (foc != f.previousFocusedId) {
							maybeApply(lookupTree(fgetValue(f.focusIds), f.previousFocusedId), \v -> switchMaterialFocus(
								v,
								\fc -> {
									nextDistinct(fc.focused, false);
								},
								\fc -> {
									nextDistinct(fc.focused, false);
									if (foc != -1) {
										next(fc.nextId, -1);
									}
									next(fc.focusedId, -1);
								}
							));

							maybeApply(lookupTree(fgetValue(f.focusIds), foc), \v -> switchMaterialFocus(
								v,
								\fc -> {
									if (fgetValue(fc.focusEnabled)) {
										nextDistinct(fc.focused, true);
										scrollIntoView(fc);
									} else {
										nextDistinct(fc.focused, false);
										deferred(\ -> next(f.focusedId, -1));
									}
								},
								\fc -> {
									nextDistinct(fc.focused, true);
								}
							));

							f.previousFocusedId ::= foc;
						}
					}),
					makeSubscribe(hasImportantChild, \ic -> if (ic) nextDistinct(parent.extraFeatures.hasImportantChild, ic))
				]
				|> (\disps ->
					if (isIScriptRecording(manager))
						arrayPush(disps, \ -> initIScriptRecording(manager, f, true))
					else
						disps
				),
				if (isMaterialLogLevelEnabled(MaterialInfo()) && fgetValue(f.recordingEnabled))
					TCopySize(
						fn(f),
						\sz -> {
							color = randomMColor();
							md = make(false);

							TGroup2(
								TRounded(4.0, 4.0, 4.0, 4.0, [MStroke(color)], sz |> TBorderBottom(-1.0) |> TBorderRight(-1.0)),
								TInteractive(
									[TMouseDown(md)],
									TConstruct(
										[
											makeSubscribe2(md, \d -> if (d) iter(concat3([">>>"], strSplit(cs, "\n"), ["<<<"]), println))
										],
										TFrame(
											0.0,
											4.0,
											[MFill(color)],
											TBorder(
												2.0, 0.0, 2.0, 0.0,
												TText(f.name, [MFill(MTextMColor(color)), FontSize(10.0)])
											)
										)
									)
								)
							)
						},
						true
					)
				else
					fn(f)
			);

		b = t2a(content, pi, s, mo);
		f.form ::= b.form;

		next(boxWidth, b.metrics.width);
		next(boxHeight, b.metrics.height);

		TAcc(b with disposers = arrayPush(b.disposers, \ -> f.form ::= FEmpty()))
	});
}

managerTimer(manager : MaterialManager) -> LazyDeltaTimer {
	manager.manager.deltaTimer
}

renderTimer(rend : MaterialRender) -> LazyDeltaTimer {
	managerTimer(rend.manager)
}

makeTropicTag(manager : MaterialManager) -> int {
	makeTagId(manager.manager);
}

yieldFocus(manager : MaterialManager, gotonext : bool) -> void {
	if (isNone(nextFocus(manager, manager.focus, gotonext))) {
		resetMFocus(manager);
	}
}

nextFocus(manager : MaterialManager, parent : MFocusGroup, inOrder : bool) -> Maybe<MFocus> {
	currentFocusedId = getValue(parent.focusedId);
	nextFocusedId = getValue(parent.nextId);

	checkFocusedId =
		if (currentFocusedId != -1) {
			if (inOrder)
				\fid -> fid >= currentFocusedId
			else
				\fid -> fid <= currentFocusedId;
		// } else if (nextFocusedId != -1) {
		// 	\fid -> nextFocusedId == fid;
		} else {
			\__ -> true;
		};

	filteredFocusIds = filterTree(
		getValue(parent.focusIds),
		\__, v -> fgetValue(v.focusEnabled)
	);	

	(\fid, focus ->
		if (checkFocusedId(fid))
			switchMaterialFocus(
				focus,
				\f : MFocus -> {
					if (fgetValue(f.focusEnabled) && fid != currentFocusedId && ((eitherMap(f.parent.vertical, \v -> getValue(v) == -1, true) &&
						eitherMap(f.parent.horizontal, \h -> getValue(h) == -1, true)) ||
						eitherMap(getCurrentFocusedItem(manager), \foc -> foc.name == "MSmallEditDialog" || foc.name == "MAutoComplete", false))) {
						if (setMFocus(f, true))
							Some(f)
						else
							None()
					} else
						None()
				},
				\f ->
					if (fgetValue(f.focusEnabled))
						nextFocus(manager, f, isSome(f.vertical) || isSome(f.horizontal) || inOrder)
					else
						None()
			)
		else
			None()
	)
	|> (\f -> if (inOrder) findInOrder(filteredFocusIds, f) else findRInOrder(filteredFocusIds, f));
}

yieldFocusVH(manager : MaterialManager, gotonext : bool, vertical : bool) -> void {
	curFoc = getCurrentFocusedItem(manager);
	firstAttempt = nextFocusVH(manager, manager.focus, curFoc, curFoc, gotonext, vertical);
	if (isNone(firstAttempt)) {
		curFoc2 = getCurrentFocusedItem(manager);
		scopeParent = either(maybeBind(curFoc, \cf -> getTrapFocusScope(cf.parent)), manager.focus);
		resetMFocus(manager);
		nextFocusVH(manager, scopeParent, None(), None(), gotonext, vertical) |> ignore;
	}
}

getTrapFocusScope(focusGroup : MFocusGroup) -> Maybe<MFocusGroup> {
	if (fgetValue(focusGroup.extraFeatures.trapFocusEnabled)) Some(focusGroup)
	else maybeBind(focusGroup.parent, \p -> getTrapFocusScope(p));
}

calculateFocusWeight(currentFocus : MFocus, newFocus : MFocus, inOrder : bool, vertical : bool) -> double {
	if (currentFocus == newFocus) {
		-doubleMax
	} else {
		currentFocusPosition = applyTransformMatrix(fgetValue(currentFocus.transformMatrix), zeroPoint);
		currentFocusWidthHeight = fgetValue(currentFocus.widthHeight);
		currentFocusPoint =
			Point(
				currentFocusPosition.x + currentFocusWidthHeight.width / 2.,
				currentFocusPosition.y + currentFocusWidthHeight.height / 2.
			);

		newFocusPosition = applyTransformMatrix(fgetValue(newFocus.transformMatrix), zeroPoint);
		newFocusWidthHeight = fgetValue(newFocus.widthHeight);
		newFocusPoint =
			Point(
				newFocusPosition.x + newFocusWidthHeight.width / 2.,
				newFocusPosition.y + newFocusWidthHeight.height / 2.
			);

		dx = newFocusPoint.x - currentFocusPoint.x;
		dy = newFocusPoint.y - currentFocusPoint.y;

		if (vertical) {
			if (newFocusPosition.x + newFocusWidthHeight.width <= currentFocusPosition.x ||
				newFocusPosition.x >= currentFocusPosition.x + currentFocusWidthHeight.width)
				-doubleMax
			else if (inOrder && dy >= 0.)
				dy + abs(dx) * 10.
			else if (!inOrder && dy <= 0.)
				-dy + abs(dx) * 10.
			else
				-doubleMax
		} else {
			if (newFocusPosition.y + newFocusWidthHeight.height <= currentFocusPosition.y ||
				newFocusPosition.y >= currentFocusPosition.y + currentFocusWidthHeight.height)
				-doubleMax
			else if (inOrder && dx >= 0.)
				dx + abs(dy) * 10.
			else if (!inOrder && dx <= 0.)
				-dx + abs(dy) * 10.
			else
				-doubleMax
		}
	}
}

compareFocusWeights(initialFocus : Maybe<MFocus>, prevFocus : Maybe<MFocus>, newFocus : MFocus, inOrder : bool, vertical : bool) -> bool {
	eitherMap(
		initialFocus,
		\cf -> {
			prevFocusWeight = eitherMap(prevFocus, \pf -> calculateFocusWeight(cf, pf, inOrder, vertical), 0.);
			newFocusWeight = calculateFocusWeight(cf, newFocus, inOrder, vertical);

			newFocusWeight > 0. && (prevFocusWeight < 0. || prevFocusWeight > newFocusWeight)
		},
		isNone(prevFocus)
	)
}

nextFocusVH(manager : MaterialManager, parent : MFocusGroup, initialFocus : Maybe<MFocus>, previousFocus : Maybe<MFocus>, inOrder : bool, vertical : bool) -> Maybe<MFocus> {
	initialFocusedId = getValue(parent.focusedId);

	checkFocusedId =
		if (initialFocusedId != -1) {
			if (inOrder)
				\fid -> fid >= initialFocusedId
			else
				\fid -> fid <= initialFocusedId;
		} else {
			\__ -> true;
		};

	compareFocus = \fid, focus, prevFocus -> {
		fgetValue(focus.focusEnabled) &&
			fid != initialFocusedId &&
			((vertical && isSome(focus.parent.vertical)) || isSome(focus.parent.horizontal)) &&
			compareFocusWeights(initialFocus, prevFocus, focus, inOrder, vertical) &&
			setMFocus(focus, true)
	};

	(\fid, currentFocus, prevFocus -> {
		if (checkFocusedId(fid)) {
			switchMaterialFocus(
				currentFocus,
				\focus -> {
					if (compareFocus(fid, focus, prevFocus)) {
						Some(focus);
					} else {
						prevFocus;
					}
				},
				\focus -> {
					if (fgetValue(focus.focusEnabled)) {
						eitherMap(nextFocusVH(manager, focus, initialFocus, prevFocus, inOrder, vertical), \f -> Some(f), prevFocus)
					} else {
						prevFocus;
					}
				}
			)
		} else {
			prevFocus;
		}
	})
	|> (\f ->
		if (inOrder)
			foldTree(getValue(parent.focusIds), previousFocus, f)
		else
			foldRTree(getValue(parent.focusIds), previousFocus, f)
	)
	|> (\f ->
		if (f == previousFocus)
			None()
		else
			f
	)
}

resetMFocus(manager : MaterialManager) -> void {
	hasFocusedId = getValue(manager.focus.focusedId) != -1;
	if (hasFocusedId) {
		nextDistinct(manager.focus.focusedId, -1);
		nextDistinct(manager.focus.focused, false);
	} else {
		deferred(\ -> nextDistinct(manager.previousFocus, None()));
		nextDistinct(manager.focus.nextId, -1);
	}

	traverseInOrder(getValue(manager.focus.focusIds), \k, v ->
		switchMaterialFocus(
			v,
			\f -> {
				nextDistinct(f.focused, false);
			},
			\f -> {
				if (hasFocusedId) {
					nextDistinct(f.focused, false);
					nextDistinct(f.focusedId, -1);
				} else {
					nextDistinct(f.nextId, -1);
				}
			}
		)
	)
}

previousMFocus(manager : MaterialManager) -> void {
	maybeApply(getPreviousFocusedItem(manager), \f -> nextDistinct(f.focused, true));
}

scrollIntoView(focus : MaterialFocus) -> void {
	switchMaterialFocus(
		focus,
		\f -> {
			scrollIntoView2(f.parent, fgetValue(f.transformMatrix), fgetValue(f.widthHeight))
		},
		\f -> {
			maybeApply(f.parent, \p -> scrollIntoView2(p, fgetValue(f.transformMatrix), fgetValue(f.widthHeight)));
		}
	)
}

scrollIntoView2(parent : MFocusGroup, transformMatrix : TransformMatrix, widthHeight : WidthHeight) -> void {
	eitherFn(
		parent.scrollInfo,
		\si -> {
			if (fgetValue(si.scrollToFocusEnabled)) {
				if (fgetValue(si.enabled)) {
					ptm = fgetValue(parent.transformMatrix);
					tm = prependInvertedTransformMatrix(transformMatrix, ptm);
					bwh = fgetValue(parent.widthHeight);
					sp = getValue(si.position);

					t = applyTransformMatrix(tm, Point(widthHeight.width / 2.0, widthHeight.height / 2.0));
					p1 = Point(sp.x + t.x - widthHeight.width / 2.0, sp.y + t.y - widthHeight.height / 2.0);
					p2 = Point(sp.x + t.x - bwh.width + widthHeight.width / 2.0, sp.y + t.y - bwh.height + widthHeight.height / 2.0);

					p = Point(max(min(sp.x, max(p1.x, p2.x)), min(p1.x, p2.x)), max(min(sp.y, max(p1.y, p2.y)), min(p1.y, p2.y)));
					d = Point(getValue(si.position).x - p.x, getValue(si.position).y - p.y);

					nextDistinct(si.position, p);

					maybeApply(parent.parent, \par -> scrollIntoView2(par, TransformMatrix(transformMatrix.a, transformMatrix.b, transformMatrix.c, transformMatrix.d,
						transformMatrix.tx + d.x * ptm.a + d.y * ptm.b, transformMatrix.ty + d.x * ptm.c + d.y * ptm.d), widthHeight));
				} else {
					maybeApply(parent.parent, \par -> scrollIntoView2(par, transformMatrix, widthHeight));
				}
			}
		},
		\ -> {
			maybeApply(parent.parent, \par -> scrollIntoView2(par, transformMatrix, widthHeight));
		}
	);
}

getCurrentFocusedItem(manager : MaterialManager) -> Maybe<MFocus> {
	fgetValue(getCurrentFocusedItemBehaviour(manager));
}

getCurrentActiveItem(manager : MaterialManager) -> Maybe<MaterialFocus> {
	fgetValue(getCurrentActiveItemBehaviour(manager));
}

isMFocusChild(parent : MFocusGroup, child : MaterialFocus) -> bool {
	foldTree(getValue(parent.focusIds), false, \k, v, acc -> switchMaterialFocus(v, \f -> acc || f == child, \f -> acc || isMFocusChild(f, child)))
}

getPreviousFocusedItem(manager : MaterialManager) -> Maybe<MFocus> {
	getValue(manager.previousFocus);
}

addMInteractionItemId(manager : MaterialManager, itemId : int) -> void {
	addInteractionItemId(manager.manager, itemId);
}

addMInteractionItemIdIfEmpty(manager : MaterialManager, itemId : int) -> void {
	addInteractionItemIdIfEmpty(manager.manager, itemId);
}

removeMInteractionItemId(manager : MaterialManager, itemId : int) -> void {
	removeInteractionItemId(manager.manager, itemId);
}

blockOtherMInteractionItemIds(manager : MaterialManager, itemId : int) -> void {
	blockOtherInteractionItemIds(manager.manager, itemId);
}

isCurrentMInteractionItemId(manager : MaterialManager, itemId : int) -> bool {
	isCurrentInteractionItemId(manager.manager, itemId);
}

addMPopupItemId(manager : MaterialManager, itemId : int) -> void {
	addPopupItemId(manager.manager, itemId);
}

removeMPopupItemId(manager : MaterialManager, itemId : int) -> void {
	removePopupItemId(manager.manager, itemId);
}

blockOtherMPopupItemIds(manager : MaterialManager, itemId : int) -> void {
	blockOtherPopupItemIds(manager.manager, itemId);
}

isCurrentMPopupItemId(manager : MaterialManager, itemId : int) -> bool {
	isCurrentPopupItemId(manager.manager, itemId);
}

getFFormById(f : FForm, id : List<IScriptFormPosition>) -> Transform<Maybe<FForm>> {
	switch (f : FForm) {
		FMask2(main1, mask, callstack): {
			fid = headList(id, IScriptFormPosition(-1)).id;

			if (fid == 0)
				getFFormById(main1, tailList(id))
			else if (fid == 1)
				getFFormById(mask, tailList(id))
			else
				const(None())
		}
		FGroup(layers, zorder): {
			fid = headList(id, IScriptFormPosition(-1)).id;

			if (fid >= 0 && fid < length(layers))
				getFFormById(layers[fid], tailList(id))
			else
				const(None())
		}
		FGroup2(layer1, layer2, zorder): {
			fid = headList(id, IScriptFormPosition(-1)).id;

			if (fid == 0)
				getFFormById(layer1, tailList(id))
			else if (fid == 1)
				getFFormById(layer2, tailList(id))
			else
				const(None())
		}
		FBorder(left, top, right, bottom, form): getFFormById(form, id);
		FConstructor(form, fn): getFFormById(form, id);
		FTranslate(x, y, form): getFFormById(form, id);
		FScale(x, y, form): getFFormById(form, id);
		FRotate(degree, form): getFFormById(form, id);
		FAlpha(alpha, form): getFFormById(form, id);
		FVisible(v, form): getFFormById(form, id);
		FAvailable2(widthHeight, form): getFFormById(form, id);
		FSize2(widthHeight, form): getFFormById(form, id);
		FSetPending(pending, form): getFFormById(form, id);
		FBaseline(baseline, form): getFFormById(form, id);
		FInteractive(listeners, form): getFFormById(form, id);
		FFilter2(filters, form, stack): getFFormById(form, id);
		FCursor(kind, form): getFFormById(form, id);
		FInspect(inspectors, form): getFFormById(form, id);
		FMutable2(form, stack): fsubselect(form, FLift(\f2 -> getFFormById(f2, id)));
		FCrop2(left, top, width, height, enabled, form, stack): getFFormById(form, id);
		FAccess(props, form): getFFormById(form, id);
		FControlFocus(focus, form): getFFormById(form, id);
		FFullWindow(fullscreen, av, form): getFFormById(form, id);
		default:
			if (countList(id) == 0)
				const(Some(f))
			else
				const(None())
	}
}

MComponent2TAccessibility(manager : MaterialManager, content : Tropic, focus : MaterialFocus, properties : [FAccessProperty]) -> Tropic {
	if ((!^fAccessibilityEnabled && !cpp) || (mobile && !js)) {
		content
	} else {
		accessRole = extractStruct(properties, AccessRole("")).role;

		if (accessRole == "hidden") {
			content;
		} else {
			externalAccessHiddenEnabled = extractStruct(properties, FAccessHiddenT(const(false))).enabled;
			switchMaterialFocus(
				focus,
				\f ->
					TAccess(
						[
							FAccessZorder(f.parent.zorder),
							FAccessHiddenT(
								fOr(
									externalAccessHiddenEnabled,
									fgreateri(manager.currentZorder, fmax(f.parent.zorder, const(0)))
								)
							),
							FAccessTabOrder(f.tabOrder),
							FAccessAttribute("aria-disabled", fif(f.enabled, const(""), const("true")))
						]
						|> (\props ->
							concat(props, if (accessRole != "") [
								AccessFocused(f.focused),
								FAccessEnabled(f.focusEnabled),
							] else [])
						)
						|> (\props -> concat(props, addAccessorderProperties(f.name, f.parent.managerId)))
						|> (\props -> MComponent2TAccessibilityFocusable(manager, f, props))
						|> (\props -> concat(props, removeAllStructs(properties, FAccessHiddenT(const(false))))),
						content
					)
					|> addSpeechSynthesis(f, accessRole),
				\f ->
					TAccess(
						[
							FAccessZorder(f.zorder),
							FAccessHiddenT(
								fOr(
									externalAccessHiddenEnabled,
									fgreateri(manager.currentZorder, f.zorder)
								)
							),
							FAccessVisible(f.isInitialized),
							FAccessTabOrder(f.tabOrder),
							FAccessAttribute("aria-disabled", fif(f.enabled, const(""), const("true")))
						]
						|> (\props -> if (accessRole != "") concat(props, [FAccessEnabled(f.focusEnabled)]) else props)
						|> (\props -> concat(props, addAccessorderProperties(f.name, manager.id)))
						|> (\props -> concat(props, removeAllStructs(properties, FAccessHiddenT(const(false))))),
						content
					)
			);
		}
	}
}

addAccessorderProperties(name : string, managerId : int) -> [FAccessProperty] {
	if (isUrlParameterTrue("accessorder")) {
		[
			FAccessAttribute("aria-roledescription", const(name)),
			FAccessAttribute("manager-id", const(i2s(managerId)))
		]
	} else {
		[]
	}
}

addSpeechSynthesis(f : MFocus, accessRole : string) -> (Tropic) -> Tropic {
	\accessForm ->
		if (^mSpeechAccessiblityEnabled) {
			getSpeechName = \ -> {
				controlName =
					if (f.name == "MSwitchControl")
						"switch"
					else if (f.name == "MCheckBox")
						"checkbox"
					else if (f.name == "MRadio")
						"radio"
					else if (f.name == "MTextInput" || f.name == "MAutoComplete" || f.name == "MSmallEditDialog")
						"input"
					else
						accessRole;
				strReplace(fgetValue(f.title), "_", " ") + " " + controlName;
			}

			TConstruct(
				[
					makeSubscribeUns(f.focused, \foc ->
						if (foc) {
							[interruptibleTimer(100, \ -> speechSynthesis(getSpeechName(), [])), clearSpeechSynthesisQueue]
						} else {
							[]
						}
					)
				],
				accessForm
			)
		}
		else accessForm
}

MComponent2TAccessibilityFocusable(manager : MaterialManager, f : MFocus, accessProperties : [FAccessProperty]) -> [FAccessProperty] {
	if (isSome(f.parent.vertical) || isSome(f.parent.horizontal))
		arrayPush(
			accessProperties,
			FAccessAttribute(
				"focusable",
				fselectLift(
					fmax(
						fmax(
							f.focused,
							fselect(getCurrentFocusedItemBehaviour(manager), FLift(\foc ->
								eitherMap(foc, \fc -> fc.name == "MSmallEditDialog" || fc.name == "MAutoComplete", false)))
						),
						fmin(
							eitherMap(
								f.parent.horizontal,
								\h ->
									feq(h, -1),
								const(true)
							),
							eitherMap(
								f.parent.vertical,
								\v ->
									feq(v, -1),
								const(true)
							)
						)
					),
					b2s
				)
			)
		)
	else
		accessProperties
}

mClickButton(manager : MaterialManager, name : string) -> void {
	buttons = ref [];
	mClickButton2(manager.focus, name, buttons);
	if (length(^buttons) > 0) {
		maybeApply(^buttons[0].onClick, apply0);
	}
}

mClickButton2(focus : MaterialFocus, name : string, buttons : ref [MFocus]) -> void {
	switchMaterialFocus(
		focus,
		\f -> {
			if (fgetValue(f.enabled) && fgetValue(f.parent.zorderEnabled) &&
				(strContains(toLowerCase(f.name), toLowerCase(name)) || strContains(toLowerCase(fgetValue(f.title)), toLowerCase(name))))
				buttons := arrayPush(^buttons, f);
		},
		\fg -> {
			traverseInOrder(getValue(fg.focusIds), \k, v -> mClickButton2(v, name, buttons));
		}
	);
}