import material/internal/selectmarray;
import material/internal/material_dragdrop;
import material/internal/material_icons;
import ui/animate/teasing;

export {
	MTree2T(manager : MaterialManager, parent : MFocusGroup, m : MTree<?>, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;

	MTree2s(tree : MTree<?>) -> string;
	MTreeOrder2s(nodes : [MTreeOrderNode<?>]) -> string;
}

MTree2s(tree : MTree<?>) -> string {
	children = fgetValue(tree.children);

	if (length(children) > 0) {
		"MTree([" + "\n" +
		strGlue(map(fgetValue(tree.children), \c -> MTreeNode2s(c, "\t")), "\n") + "\n"
		"])"
	} else {
		"MTree()"
	}
}

MTreeNode2s(tree : MTreeNode<?>, prefix : string) -> string {
	children = fgetValue(tree.children);

	if (length(children) > 0) {
		prefix + "MTreeNode(" + toString(tree.content) + ", [" + "\n" +
		strGlue(map(fgetValue(tree.children), \c -> MTreeNode2s(c, prefix + "\t")), "\n") + "\n" +
		prefix + "])"
	} else {
		prefix + "MTreeNode(" + toString(tree.content) + ")"
	}
}

MTreeOrder2s(nodes : [MTreeOrderNode<?>]) -> string {
	if (length(nodes) > 0) {
		"MTreeOrder([" + "\n" +
		strGlue(map(nodes, \c -> MTreeOrderNode2s(c, "\t")), "\n") + "\n"
		"])"
	} else {
		"MTreeOrder()"
	}
}

MTreeOrderNode2s(tree : MTreeOrderNode<?>, prefix : string) -> string {
	children = fgetValue(tree.children);

	if (length(children) > 0) {
		prefix + "MTreeOrderNode(" + toString(tree.content) + ", [" + "\n" +
		strGlue(map(fgetValue(tree.children), \c -> MTreeOrderNode2s(c, prefix + "\t")), "\n") + "\n" +
		prefix + "])"
	} else {
		prefix + "MTreeOrderNode(" + toString(tree.content) + ")"
	}
}

MTreeState(
	items : Behaviour<[[int]]>,
	selected : DynamicBehaviour<Set<[int]>>,
	expanded : DynamicBehaviour<Set<[int]>>,
	color : MColor,
	multiSelection : bool,
	showSeparators : bool,
	dragDropState : Maybe<MTreeDragDropState<?>>,
	lazyScrollMode : Maybe<MTreeLazyScrollModeState>,
	loadLazyButtonMode : Maybe<MTreeLazyLoad>
);

MTreeDragDropState(
	order : DynamicBehaviour<[MTreeOrderNode<?>]>,
	onChange : Maybe<(newOrder : [MTreeOrderNode<?>]) -> void>,
	content2id : (content : ?) -> int,
	id2content : (id : int) -> Maybe<?>,
	current : DynamicBehaviour<Maybe<(MFocusGroup) -> Tropic>>
);

MTreeLazyScrollModeState(
	enabled : Transform<bool>,
	scrollWH : DynamicBehaviour<WidthHeight>,
	scrollPositionScale : DynamicBehaviour<PositionScale>
);

fSelectionIndexes(children : Transform<[MTreeNode<?>]>) -> Transform<[[int]]> {
	fsubmapi(children, \i, c -> fSelectionIndexes2(farray(i), c.children)) |> fconcatA
}

fSelectionIndexes2(index : Transform<[int]>, children : Transform<[MTreeNode<?>]>) -> Transform<[[int]]> {
	fconcat(
		farray(index),
		fsubmapi(children, \i, c ->
			fSelectionIndexes2(farrayPush(index, i), c.children)
		)
		|> fconcatA
	)
}

fItemsLength(children : Transform<[MTreeNode<?>]>) -> Transform<int> {
	fselect(fsubmap(children, \c -> fItemsLength2(c.children)), sum |> FLift)
}

fItemsLength2(children : Transform<[MTreeNode<?>]>) -> Transform<int> {
	fadditioni(fselect(fsubmap(children, \c -> fItemsLength2(c.children)), sum |> FLift), const(1))
}

MTreeNodes2MTreeOrderNodes(nodes : Transform<[MTreeNode<?>]>) -> Pair<Behaviour<[MTreeOrderNode<?>]>, () -> () -> void> {
	children = make([]);

	Pair(
		children,
		\ -> fconnect(fmapu(nodes, MTreeNode2MTreeOrderNode), children)
	)
}

MTreeNode2MTreeOrderNode(node : MTreeNode<?>) -> Pair<MTreeOrderNode<?>, [() -> void]> {
	children = make([]);

	Pair(
		MTreeOrderNode(node.content, children),
		[
			fconnect(fmapu(node.children, MTreeNode2MTreeOrderNode), children)
		]
	)
}

MTreeOrderOnChangeListener(node : MTreeOrderNode<?>) -> Transform<[MTreeOrderNode<?>]>  {
	fconcat(
		farray(const(node)),
		fsubmap(node.children, MTreeOrderOnChangeListener)
		|> fconcatA
	)
}

moveMTreeOrderNode(nodes : DynamicBehaviour<[MTreeOrderNode<?>]>, id1 : ?, id2 : ?, dropOperation : DropOperation) -> void {
	maybeApply(
		findMTreeOrderNode(nodes, id1),
		\n1 -> {
			maybeApply(
				findMTreeOrderNode(nodes, id2),
				\n2 -> {
					moveMTreeOrderNode2(nodes, n1, n2, dropOperation);
				}
			);
		}
	);
}

moveMTreeOrderNode2(
	nodes : DynamicBehaviour<[MTreeOrderNode<?>]>,
	node1 : MTreeOrderNode<?>,
	node2 : MTreeOrderNode<?>,
	dropOperation : DropOperation
) -> void {
	nextDistinct(
		nodes,
		map(getValue(nodes), \n -> {
			if (n.content == node1.content) {
				moveMTreeOrderNode2(n.children, node1, node2, dropOperation);

				if (dropOperation == DropAbove()) {
					[
						node2,
						n
					]
				} else if (dropOperation == DropBelow()) {
					[
						n,
						node2
					]
				} else {
					next(n.children, concat([node2], getValue(n.children)));

					[
						n
					]
				}
			} else if (n.content == node2.content) {
				[]
			} else {
				moveMTreeOrderNode2(n.children, node1, node2, dropOperation);

				[n];
			}
		})
		|> concatA
	);
}

findMTreeOrderNode(nodes : DynamicBehaviour<[MTreeOrderNode<?>]>, id : ?) -> Maybe<MTreeOrderNode<?>> {
	fold(getValue(nodes), None(), \acc, n -> {
		if (isSome(acc)) {
			acc;
		} else if (n.content == id) {
			Some(n);
		} else {
			findMTreeOrderNode(n.children, id);
		}
	});
}

concatTrees(t1 : Tree<?, [??]>, t2 : Tree<?, [??]>) -> Tree<?, [??]> {
	foldTree(t2, t1, \k, v, t -> setTree(t, k, concat(lookupTreeDef(t, k, []), v)))
}

removeFirstFromTree(t : Tree<?, [??]>, k : ?) -> Tree<?, [??]> {
	eitherMap(
		lookupTree(t, k),
		\v ->
			if (length(v) > 1)
				setTree(t, k, tail(v))
			else
				removeFromTree(t, k),
		t
	)
}

fremoveFirstFromTree(t : Transform<Tree<?, [??]>>, k : ?) -> Transform<Tree<?, [??]>> {
	fselect(t, FLift(\tr ->
		eitherMap(
			lookupTree(tr, k),
			\v ->
				if (length(v) > 1)
					setTree(tr, k, tail(v))
				else
					removeFromTree(tr, k),
			tr
		)
	))
}

fnodes2tree(nodes : Transform<[MTreeNode<?>]>) -> Transform<Tree<?, [MTreeNode<?>]>> {
	fsubselect(
		nodes,
		FLift(\n : [MTreeNode<?>] ->
			fold(
				n,
				const(makeTree()),
				\acc : Transform<Tree<?, [MTreeNode<?>]>>, nn : MTreeNode<?> ->
					fselect2(
						acc,
						fnodes2tree(nn.children),
						FLift2(\a : Tree<?, [MTreeNode<?>]>, c : Tree<?, [MTreeNode<?>]> ->
							concatTrees(
								a,
								concatTrees(
									c,
									makeTree1(nn.content, [nn])
								)
							)
						)
					)
			)
		)
	)
}

MTreeOrder2MTreeNodes(nodes : Transform<[MTreeNode<?>]>, order : Transform<[MTreeOrderNode<?>]>) -> Transform<[MTreeNode<?>]> {
	fsubfiltermap(order, \c -> MTreeOrderNode2MTreeNode(c, fnodes2tree(nodes)));
}

MTreeOrderNode2MTreeNode(node : MTreeOrderNode<?>, tree : Transform<Tree<?, [MTreeNode<?>]>>) -> Transform<Maybe<MTreeNode<?>>> {
	treeNode = flookupTree(tree, const(node.content));

	feitherMap(
		treeNode,
		\n ->
			Some(
				MTreeNode(
					n[0].content,
					n[0].content2material,
					fsubfiltermap(node.children, \c -> MTreeOrderNode2MTreeNode(c, fremoveFirstFromTree(tree, node.content))),
					n[0].style
				)
			),
		const(None())
	);
}

applyMTreeLazyScrollMode(state : MTreeState<?>) -> (Tropic) -> Tropic {
	\node ->
		eitherMap(state.lazyScrollMode, \lsm -> {
			nodePosScale = make(zeroPositionScale);
			showItem = make(false);
			if (fgetValue(lsm.enabled))
				node
				|> (\t -> TShow(showItem, t))
				|> (\t -> TPositionScale(nodePosScale, t))
				|> (\t -> TConstruct([
						make3SubscribeTrigger(fnot(showItem), lsm.scrollWH, fstall(nodePosScale, 0), lsm.scrollPositionScale, \swh, ps, sps -> {
							y = ps.pos.y - sps.pos.y;
							if (y >= 0. && y < swh.height) nextDistinct(showItem, true);
						})
					], t))
			else
				node
			},
			node
		)
} 

makeMTreeDragDropState(
	manager : MaterialManager,
	order : DynamicBehaviour<[MTreeOrderNode<?>]>,
	onChange : Maybe<([MTreeOrderNode<?>]) -> void>
) -> MTreeDragDropState<?> {
	idContents = ref makeTree();
	contentIds = ref makeTree();

	content2id = \c -> {
		eitherFn(
			lookupTree(^idContents, c),
			\i -> i,
			\ -> {
				i = makeTropicTag(manager);

				idContents := setTree(^idContents, c, i);
				contentIds := setTree(^contentIds, i, c);

				i;
			}
		);
	};

	id2content = \i -> {
		lookupTree(^contentIds, i);
	};

	MTreeDragDropState(
		order,
		onChange,
		content2id,
		id2content,
		make(None())
	);
}

MTree2T(manager : MaterialManager, parent : MFocusGroup, m : MTree<?>, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	loadLazyButtonMode = tryExtractStruct(m.style, MTreeLazyLoad(0, None()));
	lazyLoadState = maybeMap(loadLazyButtonMode, makeMTreeLazyLoadState);

	lazyScrollMode =
		maybeMap(
			tryExtractStruct(m.style, MTreeLazyScrollMode(const(true))),
			\lsm -> MTreeLazyScrollModeState(
				lsm.enabled,
				makeWH(),
				make(zeroPositionScale)
			)
		);

	dragDropStateM =
		eitherFn(
			maybeMap(
				tryExtractStruct(m.style, MTreeOrder(make([]))),
				\o ->
					makeMTreeDragDropState(manager, o.order, maybeMap(tryExtractStruct(m.style, MTreeOnOrderChange(nop1)), \oc -> oc.onChange))
			),
			\dragDropState ->
				Some(dragDropState),
			\ ->
				maybeMap(
					tryExtractStruct(m.style, MTreeOnOrderChange(nop1)),
					\oc ->
						makeMTreeDragDropState(manager, make([]), Some(oc.onChange))
				)
		);

	children =
		eitherMap(lazyLoadState, \st -> fselect2(m.children, st.loadQuota, FLift2(\arr, lq -> take(arr, lq))), m.children)
		|> (\children ->
			ffuseinit(
				eitherMap(dragDropStateM, \dragDropState -> MTreeOrder2MTreeNodes(children, dragDropState.order), children),
				make([])
			)
		);
	selIndexes = ffuseinit(fSelectionIndexes(children.first), make([[]]));

	singleSelection = tryExtractStruct(m.style, MSingleSelection(make(-1)));
	multiSelection = tryExtractStruct(m.style, MMultiSelection(make(makeSet())));
	itemsLength = tryExtractStruct(m.style, MItemsLength(make(0)));
	showSeparators = !contains(m.style, MTreeHideSeparators());

	state =
		MTreeState(
			selIndexes.first,
			extractStruct(m.style, MTreeSelection(make(makeSet()))).selected,
			extractStruct(m.style, MTreeExpanded(make(makeSet()))).expanded,
			extractStruct(m.style, getPrimaryColor(parent)),
			isSome(multiSelection),
			showSeparators,
			dragDropStateM,
			lazyScrollMode,
			loadLazyButtonMode
		);

	TIf(
		flessi(flength(children.first), const(ARRAY_DIFF_TRESHOLD)),
		SelectMLines2T(
			manager,
			parent,
			fmap(children.first, \c : MTreeNode<?> -> c.content),
			\index, __, __, p ->
				TSelect(fthrottle2(felementAt(children.first, index, const(getValue(children.first)[fgetValue(index)])), 33), \c ->
					MTreeNode2T(
						manager,
						p,
						state,
						c,
						farray(index),
						m2t,
						const(showSeparators)
					)
				)
				|> applyMTreeLazyScrollMode(state)
		),
		TSelect(
			fmap(children.first, \c : MTreeNode<?> -> c.content),
			\contents : [?] -> mapi(contents, \i, __ ->
				TSelect(
					fselect(children.first, FLift(\nodes -> nodes[i])), \node ->
					MTreeNode2T(
						manager,
						parent,
						state,
						node,
						farray(const(i)),
						m2t,
						const(showSeparators)
					)
				)
				|> applyMTreeLazyScrollMode(state)
			)
			|> TLines
		)
	)
	|> addUnfoldPanel(parent, m2t, m.children, lazyLoadState)
	|> TBorderBottom(1.)
	|> (\mtree ->
		TConstruct(
			[
				makeSubscribe(state.selected, \selSet ->
					iterSet(selSet, \sel ->
						loop(
							subrange(sel, 0, length(sel) - 1),
							\s -> subrange(s, 0, length(s) - 1),
							\s -> length(s) > 0,
							\s -> nextDistinct(state.expanded, insertSet(getValue(state.expanded), s))
						)
					)
				)
			],
			mtree
		)
	)
	|> (\mtree ->
		eitherMap(
			singleSelection,
			\ss ->
				TConstruct(
					[
						\ -> fBidirectionalLink(
							fselect2(state.items, state.selected, FLift2(\ids, s ->
								if (sizeSet(s) > 0) findiDef(ids, \i -> containsSet(s, i), -1) else -1)),
							fselect2(state.items, ss.selected, FLift2(\ids, s ->
								if (s >= 0 && length(ids) > s) makeSet1(ids[s]) else makeSet())),
							\s -> nextDistinct(ss.selected, s),
							\s -> nextDistinct(state.selected, s)
						)
					],
					mtree
				),
			mtree
		)
	)
	|> (\mtree ->
		eitherMap(
			multiSelection,
			\ms ->
				TConstruct(
					[
						\ -> fBidirectionalLink(
							fselect2(state.items, state.selected, FLift2(\ids : [[int]], s : Set<[int]> ->
								foldSet(s, makeSet(), \acc : Set<int>, sel : [int] -> {
									id = findiDef(ids, \i -> i == sel, -1);

									if (id >= 0) {
										insertSet(acc, id);
									} else {
										acc;
									}
								})
							)),
							fselect2(state.items, ms.selected, FLift2(\ids : [[int]], s : Set<int> ->
								foldSet(s, makeSet(), \acc : Set<[int]>, sel : int -> {
									if (sel >= 0 && sel < length(ids)) {
										insertSet(acc, ids[sel]);
									} else {
										acc;
									}
								})
							)),
							\s : Set<int> -> nextDistinct(ms.selected, s),
							\s : Set<[int]> -> nextDistinct(state.selected, s)
						)
					],
					mtree
				),
			mtree
		)
	)
	|> (\mtree ->
		TConstruct(
			eitherMap(
				state.dragDropState,
				\dragDropState -> {
					order = MTreeNodes2MTreeOrderNodes(m.children);

					concat(
						[
							children.second,
							selIndexes.second,
							order.second,
							makeSubscribe(fmap(fthrottle2(fsubmap(order.first, MTreeOrderOnChangeListener) |> fconcatA, 33), \it -> it.content), \__ -> {
								nextDistinct(dragDropState.order, getValue(order.first));
							})
						],
						eitherMap(
							dragDropState.onChange,
							\oc ->
								[
									makeSubscribe2(fmap(fthrottle2(fsubmap(dragDropState.order, MTreeOrderOnChangeListener) |> fconcatA, 33), \it -> it.content), \__ -> {
										oc(fgetValue(dragDropState.order));
									})
								],
							[]
						)
					)
				},
				[
					children.second,
					selIndexes.second,
				]
			),
			mtree
		)
	)
	|> (\mtree ->
		eitherMap(
			itemsLength,
			\il ->
				TConstruct(
					[ \ -> fconnect(fItemsLength(children.first), il.length) ],
					mtree
				),
			mtree
		)
	)
	|> (\mtree : Tropic ->
		eitherMap(state.lazyScrollMode, \lsm ->
			TPositionScale(lsm.scrollPositionScale,
				MScroll2T(manager, parent, MScroll(mtree, TFillXY(), [MScrollWidthHeight(makeWH(), lsm.scrollWH)]), m2t)
			),
			mtree
		)
	)
}

hightlightFnDef(short : bool) -> (Transform<WidthHeight>) -> Material {
	\wh ->
		if (short)
			MPadA(16.0, 0.0,
				TRectangle([MFill(MGrey(200))], TSized(fwidthsubtract(wh, const(32.))))
			)
		else
			TRectangle([MFill(MGrey(200))], TSized(fheightaddition(wh, const(1.5))))
}

buildIconFnDef(isExpandedB : DynamicBehaviour<bool>, color : MColor) -> Material {
	MIconToggle(
		"remove_circle",
		[MIconSize(16.), FillOpacity(1.), color, MToggleFalseIcon("add_circle", [MIconSize(16.), FillOpacity(1.), color])],
		[MToggleValue(isExpandedB)]
	)
}

buildLineFnDef(contentBoxB : DynamicBehaviour<WidthHeight>, color : MColor) -> Material {
	TRectangle([MFill(color)], TFillWY(2.))
	|> (\t -> MBorderA(15., 0., 0., 6., t))
	|> MAlignStart
	|> (\m -> MTranslate(fselect(contentBoxB, FLift(\ch -> Point(0., 6.0 - 0.5 * ch.height))), m))
}

buildUnfoldPanelDef(onClick : () -> void) -> Material {
	MIcon("unfold_more", [])
	|> (\t -> MCenterIn(t, TFillXH(56.)))
	|> (\t -> MCopySize(t, \tr -> TRectangle([MFill(MGrey(200)), FillOpacity(0.5)], tr), false))
	|> (\t -> MComponent([MOnClick(onClick)], \__ -> t))
}

addUnfoldPanel(parent : MFocusGroup, m2t : (Material, MFocusGroup) -> Tropic, children : Transform<[MTreeNode]>, state : Maybe<MTreeLazyLoadState>) -> (Tropic) -> Tropic {
	\t ->
		eitherMap(state, \st ->
			TLines2(t, TShow(flessi(st.loadQuota, flength(children)), m2t(st.panel, parent))),
			t
		)
}

MTreeLazyLoadState(
	loadQuota : DynamicBehaviour<int>,
	panel : Material,
	resetFn : () -> void
);

makeMTreeLazyLoadState(loadLazy : MTreeLazyLoad) -> MTreeLazyLoadState {
	loadQuota = make(loadLazy.loadCount);
	resetFn = \ -> nextDistinct(loadQuota, loadLazy.loadCount);
	updateFn = \ -> nextDistinct(loadQuota, getValue(loadQuota) + loadLazy.loadCount);
	buildPanelFn = either(loadLazy.buildPanelFn, buildUnfoldPanelDef);
	MTreeLazyLoadState(loadQuota, buildPanelFn(updateFn), resetFn)
}

Corner ::= TopLeft, TopRight, BottomLeft, BottomRight;
	TopLeft();
	TopRight();
	BottomLeft();
	BottomRight();

DropOperation ::= DropAbove, DropBelow, DropInside;
	DropAbove();
	DropBelow();
	DropInside();

MTreeNode2T(manager : MaterialManager, parent : MFocusGroup, state : MTreeState<?>, m : MTreeNode<?>, treeIndex : Transform<[int]>,
	m2t : (Material, MFocusGroup) -> Tropic, addVerticalSeparator : Transform<bool>) -> Tropic {

	lazyLoadState = maybeMap(state.loadLazyButtonMode, makeMTreeLazyLoadState);

	children = ffuseinit(eitherMap(lazyLoadState, \st -> fselect2(m.children, st.loadQuota, FLift2(\arr, lq -> take(arr, lq))), m.children), make([]));
	childrenLength = flength(children.first);
	expanded = extractStruct(m.style, MExpanded(make(false))).expanded;
	selected0 = extractStruct(m.style, MSelected(make(false))).selected;
	selected = eitherMap(state.lazyScrollMode, \__ -> make(false), selected0);
	focused = extractStruct(m.style, MFocused(make(false))).focused;
	childSelected = tryExtractStruct(m.style, MChildSelected(make(false)));
	highlightOnSelect = extractStruct(m.style, MHighlightOnSelect(const(true))).enabled;
	shortHighlighter = contains(m.style, MShortHighlighter());

	highlightFn = extractStruct(m.style, MTreeNodeCustomHighlighter(hightlightFnDef(shortHighlighter))).fn;
	buildIconFn = extractStruct(m.style, MTreeNodeCustomIcon(buildIconFnDef)).fn;
	buildLineFn = extractStruct(m.style, MTreeNodeCustomLine(buildLineFnDef)).fn;

	focusState =
		replaceStructMany(
			m.style,
			[
				MOnClick(\ -> {
					nextDistinct(selected, if (state.multiSelection) !getValue(selected) else true);
					maybeApply(tryExtractStruct(m.style, MOnClick(nop)), \oc -> oc.click())
				}),
				MFocused(focused),
				MDefaultShortcutEnabled(const(false))
			]
		);

	contentBoxB = makeWH();

	// Drag and Drop
	hoveringIdB = make(-1);
	draggingB = make(false);
	hoveringCorner = make(None());
	dropOperation = fselect2(feither(hoveringCorner, const(TopLeft())), flength(children.first), FLift2(\c, l -> {
		switch (c : Corner) {
			TopLeft(): {
				DropAbove();
			}
			TopRight(): {
				DropInside();
			}
			BottomLeft(): {
				if (l > 0)
					DropInside()
				else
					DropBelow();
			}
			BottomRight(): {
				DropInside();
			}
		}
	}));

	TLinesDir([
		(\p -> MComponent2T(
			manager,
			p,
			"MTreeNode",
			focusState,
			\focus ->
				TColsYCenterA([
					TVisible(
						fneq(childrenLength, 0),
						m2t(buildIconFn(expanded, state.color), focus.parent)
					),
					TAttachBox(TGroup2(TFillX(), m2t(m.content2material(m.content), focus.parent)), contentBoxB)
				])
				|> (\t -> TAvailableHeight(t, const(0.)))
				|> (\t ->
					TDecorator(
						t,
						concat(
							eitherMap(
								tryExtractStruct(m.style, MTreeNodeCustomHighlighterOnHover(\__ -> TEmpty())),
								\highlightOnHover -> [Pair(focus.hover, \wh -> m2t(highlightOnHover.fn(wh), focus.parent))],
								[]
							),
							[Pair(fand(highlightOnSelect, selected), \wh -> m2t(highlightFn(wh), focus.parent))]
						),
						false
					)
				),
				m2t
		)
		|> (\f ->
			eitherMap(
				state.dragDropState,
				\dragDropState ->
					TLines2(
						TShow(
							fand(fneq(hoveringIdB, -1), feq(dropOperation, DropAbove())),
							TMutable(feitherMap(dragDropState.current, \fn -> fn(p), const(TEmpty())))
						),
						TCopySize(
							f,
							\sz -> {
								wh = makeWH();

								TShow(
									fneq(hoveringIdB, -1),
									TInteractive(
										[
											MouseMove2(\h, mi0 -> {
												mi = mi0();

												if (mi.inside) {
													if (mi.x < getValue(wh).width / 2.0) {
														if (mi.y < getValue(wh).height / 2.0) {
															nextDistinct(hoveringCorner, Some(TopLeft()));
														} else {
															nextDistinct(hoveringCorner, Some(BottomLeft()));
														}
													} else {
														if (mi.y < getValue(wh).height / 2.0) {
															nextDistinct(hoveringCorner, Some(TopRight()));
														} else {
															nextDistinct(hoveringCorner, Some(BottomRight()));
														}
													}
												}

												h;
											})
										],
										TRectangle(interactiveRectangleStyle, TInspect([ISize(wh)], sz))
									)
								)
							},
							true
						)
					),
				f
			)
		))
		|> (\f : (MFocusGroup) -> Tropic ->
			eitherFn(
				state.dragDropState,
				\dragDropState ->
					MDragDrop2T(
						manager,
						parent,
						MDragDrop(
							const(dragDropState.content2id(m.content)),
							MGetFocusGroup(f),
							[
								MOnDrop(\i ->
									maybeApply(
										dragDropState.id2content(i),
										\c ->
											moveMTreeOrderNode(dragDropState.order, m.content, c, fgetValue(dropOperation))
									)
								),
								MHoveringId(hoveringIdB),
								MDragging(draggingB),
								MOnDrag(\d -> {
									if (d) {
										next(
											dragDropState.current,
											Some(\pp ->
												TFrame(0.0, 4.0, [MFill(getPrimaryColor(manager)), FillOpacity(0.2)], TAlpha(const(0.5), f(pp)))
											)
										);
									}
								}),
								MDraggingContent(
									MShadow(
										const(8.0),
										MFrame(0.0, 4.0, [MFill(MBackgroundColor(getLightBackground(manager)))], MGetFocusGroup(f))
									)
								)
							]
						),
						m2t
					),
				\ -> f(parent)
			)
		),
		TShowLazy(
			fand(
				fOr(
					fneq(childrenLength, 0),
					if (isSome(state.dragDropState))
						fand(fneq(hoveringIdB, -1), feq(dropOperation, DropInside()))
					else
						const(false)
				),
				expanded
			),
			\ -> TIf(
				flessi(flength(children.first), const(ARRAY_DIFF_TRESHOLD)),
				SelectMLines2T(
					manager,
					parent,
					fmap(children.first, \c : MTreeNode<?> -> c.content),
					\index, __, __, p -> {
						nextDistinct(expanded, true);

						TSelect(fthrottle2(felementAt(children.first, index, const(getValue(children.first)[fgetValue(index)])), 33), \c -> {
							MTreeNode2T(
								manager,
								p,
								state,
								c,
								farrayPush(treeIndex, index),
								m2t,
								if (state.showSeparators)
									flessi(index, fadditioni(childrenLength, const(-1)))
								else
									const(false)
							)
						})
						|> applyMTreeLazyScrollMode(state)
					}
				),
				TSelect(
					fmap(children.first, \c : MTreeNode<?> -> c.content),
					\contents -> mapi(contents, \i, __ ->
						TSelect(
							fselect(children.first, FLift(\nodes -> nodes[i])), \node ->
							MTreeNode2T(
								manager,
								parent,
								state,
								node,
								farrayPush(treeIndex, const(i)),
								m2t,
								if (state.showSeparators)
									flessi(const(i), fadditioni(childrenLength, const(-1)))
								else
									const(false)
							)
						)
						|> applyMTreeLazyScrollMode(state)
					)
					|> TLines
				)
			)
				|> (\t ->
					TLines2(
						eitherMap(
							state.dragDropState,
							\dragDropState ->
								TShow(
									fand(fneq(hoveringIdB, -1), feq(dropOperation, DropInside())),
									TCols2(
										TFixed(24.0, 0.0),
										TMutable(feitherMap(dragDropState.current, \fn -> fn(parent), const(TEmpty())))
									)
								),
							TEmpty()
						),
						t
					)
				)
				|> (\t ->
					TCopySize(
						t,
						\tr ->
							TAvailable(m2t(buildLineFn(contentBoxB, state.color), parent), tr),
						true
					)
				)
				|> (\t -> TAvailableHeight(t, const(0.)))
				|> addUnfoldPanel(parent, m2t, m.children, lazyLoadState)
		),

		MSeparator2T(manager, parent, true)
		|> TBorderLeftRight(16.)
		|> (\t -> TShow(addVerticalSeparator, t))
		|> (\t -> TAlignStartDir(t, parent.rtl))
	], parent.rtl)
	|> (\treeNode -> if (length(fgetValue(treeIndex)) > 1) TBorderDir(24., 0., 0., 0., treeNode, parent.rtl) else treeNode)
	|> (\treeNode ->
		eitherMap(
			state.dragDropState,
			\dragDropState ->
				TEasingMetrics(
					manager.manager.deltaTimer,
					\__ -> easingTransitionDuration,
					easingStandartCurve,
					draggingB,
					TLines2(
						TScale(fif(draggingB, const(Factor(1.0, 0.0)), const(Factor(1.0, 1.0))), treeNode),
						TShow(
							fand(fneq(hoveringIdB, -1), feq(dropOperation, DropBelow())),
							TCols2(
								TFixed(24.0, 0.0),
								TMutable(feitherMap(dragDropState.current, \fn -> fn(parent), const(TEmpty())))
							)
						)
					),
				),
			treeNode
		)
	)
	|> (\treeNode ->
		TConstruct(
			[
				children.second,
				makeSubscribeUns(selected, \s -> if (s) [interruptibleDeferUntilNextFrameRendered(\ -> nextDistinct(focused, true))] else []),
				\ -> fBidirectionalLink(
					fselectWithLast(fif(selected, treeIndex, const([])), FLift2(\v1, v2 -> Pair(v1, v2))),
					fstall(fselect2(state.selected, treeIndex, containsSet |> FLift2), 0),
					\s ->
						if (length(s.first) > 0 || length(s.second) > 0)
							nextDistinct(
								state.selected,
								getValue(state.selected)
								|> (\sel ->
									if (length(s.first) > 0 && (!contains(getValue(state.items), s.first) || length(s.second) == 0))
										removeSet(sel, s.first)
									else
										sel
								)
								|> (\sel ->
									if (length(s.second) > 0) {
										if (state.multiSelection) {
											insertSet(sel, s.second)
										} else {
											makeSet1(s.second)
										}
									} else {
										sel
									}
								)
							),
					\s ->
						nextDistinct(selected, s)
				),
				\ -> fBidirectionalLink(
					fselectWithLast(fif(expanded, treeIndex, const([])), FLift2(\v1, v2 -> Pair(v1, v2))),
					fstall(fselect2(state.expanded, treeIndex, containsSet |> FLift2), 0),
					\e ->
						if (length(e.first) > 0 || length(e.second) > 0)
							nextDistinct(
								state.expanded,
								getValue(state.expanded)
								|> (\ex ->
									if (length(e.first) > 0)
										removeSet(ex, e.first)
									else
										ex
								)
								|> (\ex ->
									if (length(e.second) > 0)
										insertSet(ex, e.second)
									else
										ex
								)
							),
					\e ->
						nextDistinct(expanded, e)
				),
				\ -> {
					if (containsSet(getValue(state.selected), fgetValue(treeIndex)))
						nextDistinct(selected, true);

					if (containsSet(getValue(state.expanded), fgetValue(treeIndex)))
						nextDistinct(expanded, true);

					\ -> if (!contains(getValue(state.items), fgetValue(treeIndex))) {
							nextDistinct(state.expanded, removeSet(getValue(state.expanded), fgetValue(treeIndex)));
							nextDistinct(state.selected, removeSet(getValue(state.selected), fgetValue(treeIndex)));
						}
				}
			]
			|> (\arr -> eitherMap(state.lazyScrollMode, \__ ->
					arrayPush(arr, \ -> fconnect(selected0, selected)),
					arr
			))
			|> (\arr ->
				arrayPushMaybe(
					arr,
					maybeMap(
						childSelected,
						\cs ->
							\ -> fconnect(
								fselect2(state.selected, treeIndex, FLift2(\s, i ->
									foldSet(s, false, \acc, si -> acc || (length(si) > length(i) && subrange(si, 0, length(i)) == i)))
								),
								cs.selected
							)
					)
				)
			)
			|> (\arr -> arrayPushMaybe(arr, maybeMap(lazyLoadState, \st ->
				makeSubscribe2(expanded, \e -> if (!e) st.resetFn())
			))),
			treeNode
		)
	)
}