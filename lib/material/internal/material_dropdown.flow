import material/internal/material_menu;

// Internal to material

export {
	MDropDown2T(manager : MaterialManager, parent : MFocusGroup, m : MDropDown, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MDropDown2T(manager : MaterialManager, parent : MFocusGroup, m : MDropDown, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	leftDecorations = extractStruct(m.style, MLeftDecorations([])).extraItems;
	rightDecorations = extractStruct(m.style, MRightDecorations([])).extraItems;
	groups = extractStruct(m.style, MGroups([])).groups;
	nonSpec = MMenuSingleLine(m.nonSpecified, []);

	lLen = length(leftDecorations);
	rLen = length(rightDecorations);
	emptyDecorations = lLen + rLen == 0;

	MDropDownMenu2T(
		manager,
		parent,
		MDropDownMenu(
			mapi(m.items, \i, it ->
				MMenuSingleLine(it,
					if (emptyDecorations) []
					else
						concat(
							if (i < lLen) [MPrimaryAction(leftDecorations[i])] else	[],
							if (i < rLen) [MSecondaryAction(rightDecorations[i])] else []
						)
				)
			)
			|> (\f -> fold(groups, f, \acc, gr -> insertArray(acc, gr.first, MMenuGroupLine(gr.second, [])))),
			m.current,
			MDropDownStyle2MDropDownMenuStyle(addStyleIfAbsent(m.style, MCustomLineHeight(32.)))
			|> (\arr -> if (m.nonSpecified == "") arr else concat(arr, [MSelectedAlwaysOnTop(nonSpec), MNonSpecified(nonSpec)]))
		),
		m2t
	)
}

MDropDownStyle2MDropDownMenuStyle(style : [MDropDownStyle]) -> [MDropDownMenuStyle] {
	filtermap(style, \st -> switch (st) {
		MDropDownMenuStyle() : {a : Maybe<MDropDownMenuStyle> = Some(st); a};
		default : None()
	})
}