import material/internal/material_scroll;
import material/internal/material_shadow;

export {
	MPopup(items : [[Material]], positionScale : Transform<PositionScale>, style : [MPopupStyle]);

	MPopupStyle ::= RMWidth, RMWidthLimits, RMOffset, MElevation, RMSelected, RMBorders, RMOutterBorder, MOpenMenu,
		RMLeftAnimation, RMHeight, RMClickOutToClose, RMTranslateAnimation, RMScaleAnimation, RMExpandingAnimation,
		RMAlphaAnimation, MSameZorder, RMSameFocus, RMScrollArea, MFixPosition,
		MBackgroundStyle, // Transparency only works in combination with RMNoShadow
		RMNoScrollbars, TScrollPersistent, TScrollPosition, RMCurrentHeight, RMCurrentWidth, RMCurrentMaxSize, RMDontHandle, RMNoSnapSize,
		RMNoShadow, TScrollWidthHeight, MCustomScale, MComponentGroupState, MFocusOnPrevious, MBlockClicks, MRippleShape, MActive, MVirtualScreen,
		RMOnClick;

		RMWidth(width : Transform<double>);
		RMWidthLimits(min : Transform<double>, max : Transform<double>);
		RMOffset(offset : Transform<Pair<WidthHeight, double>>);
		RMSelected(selected : int);
		RMBorders(borders : double);
		// Doesn't let a popup closer than border to the edge of screen
		RMOutterBorder(border : double);
		RMLeftAnimation(leftAnimation : bool);
		RMClickOutToClose();
		RMTranslateAnimation();
		RMScaleAnimation();
		RMAlphaAnimation();
		RMExpandingAnimation();
		RMSameFocus(focus : MaterialFocus);
		RMScrollArea(from : int, to : int);
		RMNoScrollbars();
		RMCurrentHeight(height : DynamicBehaviour<double>);
		RMCurrentWidth(width : DynamicBehaviour<double>);
		RMCurrentMaxSize(wh : DynamicBehaviour<WidthHeight>);
		RMDontHandle();
		RMNoSnapSize();
		RMHeight(min : double, max : double);
		RMOnClick(onClick : () -> void);

		// Shadow always applies background to your popup, color of
		// background depends on color scheme of your manager. If you need to
		// remove background completely or not satisfied with the shadow,
		// use this one. You can also apply your custom shadow of kind you want
		// to the content itself.
		RMNoShadow();

	RenderMPopup(manager : MaterialManager, parent : MFocusGroup, m : MPopup, m2t : (Material, MFocusGroup) -> Tropic) -> void;
}

popupDownFnUns = initUnsM();

RenderMPopup(manager : MaterialManager, parent : MFocusGroup, m : MPopup, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	items = if (parent.rtl) map(m.items, reverseA) else m.items;
	style = m.style;
	positionScale0 = m.positionScale;
	stageWH = makeWH();

	dpiScale = extractStruct(manager.manager.renderStyle, TInspectDpiFactor(make(Factor(1.0, 1.0)))).factor;
	elevation = extractStruct(style, MElevation(const(8.))).elevation;
	virtualScreen = tryExtractStruct(style, MVirtualScreen(makeWH(), make(zeroPositionScale)));
	customScale = tryExtractStruct(style, MCustomScale(const(Factor(1., 1.))));
	positionScale1 = eitherMap(virtualScreen, \vs ->
		fselect2(vs.position, positionScale0, FLift2(\vsp, ps ->
			PositionScale(Point(ps.pos.x - vsp.pos.x, ps.pos.y - vsp.pos.y), ps.scale)
		)),
		positionScale0
	);
	positionScale = eitherMap(customScale, \cus -> fselect2(positionScale1, cus.factor, FLift2(\ps, cs ->
		PositionScale(ps.pos, Point(cs.x, cs.y)))), positionScale1);

	offset = extractStruct(style, RMOffset(const(Pair(WidthHeight(0., 0.), 0.)))).offset;
	mWidth = extractStruct(style, RMWidth(const(-1.))).width;
	mWidthLimits = extractStruct(style, RMWidthLimits(const(-1.), const(-1.)));
	fixPosition = tryExtractStruct(style, MFixPosition(true));
	scrollArea = extractStruct(style, RMScrollArea(0, 0));
	borders = extractStruct(style, RMBorders(16.)).borders;
	additionalBorder = extractStruct(style, RMOutterBorder(8.0)).border;

	mHeight = extractStruct(style, RMHeight(-1., -1.)) |> (\f -> RMHeight(f.min - borders * 2., f.max - borders * 2.));
	maxHeight = \wh : WidthHeight, offs : Point, ps : PositionScale -> {
		hgt0 = wh.height / ps.scale.y - borders * 2.;

		hgt =
			eitherMap(fixPosition,
				\fp ->
					if (fp.top) {
						if (ps.pos.y > 0.) {
							hgt0 - additionalBorder - (ps.pos.y / ps.scale.y + offs.y)
						} else hgt0
					} else max(ps.pos.y, 0.),
				hgt0
			);

		if (mHeight.max > 0. && scrollArea.from < scrollArea.to)
			min(mHeight.max, max(max(mHeight.min, 0.), hgt))
		else if (mHeight.min > 0.)
			max(mHeight.min, hgt)
		else
			hgt
	};

	selected = extractStruct(style, RMSelected(-1)).selected;
	menuOpened = extractStruct(style, MOpenMenu(make(false))).opened;
	leftAnimation = extractStruct(style, RMLeftAnimation(true)).leftAnimation;
	sameZorder = extractStruct(style, MSameZorder(false)).same;
	sameFocus = tryExtractStruct(style, RMSameFocus(manager.focus));
	currentMaxSize = extractStruct(style, RMCurrentMaxSize(makeWH())).wh;
	currentHeight = extractStruct(style, RMCurrentHeight(make(0.))).height;
	currentWidth = extractStruct(style, RMCurrentWidth(make(0.))).width;
	dontHandle = contains(style, RMDontHandle());
	scrollPosition : TScrollPosition = extractStruct(style, MScrollPosition(make(zeroPoint)));
	scrollWH = extractStruct(style, MScrollWidthHeight(makeWH(), makeWH()));
	scrollPersistent = tryExtractStruct(style, TScrollPersistent(const(false)));
	blockClicks = contains(style, MBlockClicks());
	shape = extractStruct(style, MRippleShape("")).component;
	expandingAnimation = contains(style, RMExpandingAnimation());

	zorder = getValue(manager.manager.zorder);
	newZorder = zorder + 1;
	previousFocus =
		if (extractStruct(style, MFocusOnPrevious(true)).enabled) {
			eitherFn(
				getCurrentFocusedItem(manager),
				\fi -> Some(fi),
				\ -> getPreviousFocusedItem(manager)
			)
		} else {
			None();
		};

	if (!sameZorder) nextDistinct(manager.manager.zorder, newZorder);

	uns = initUnsM();
	beginAnimation = make(false);
	animated = make(false);
	nextDistinct(menuOpened, true);

	sizes : [[DynamicBehaviour<WidthHeight>]] = generate(0, length(items), \i -> generate(0, length(items[i]), \__ -> makeWH()));

	sizesB = fmerge(map(sizes, \s -> fold(s, const(WidthHeight(0., 0.)), \acc : Transform<WidthHeight>, s2 : Transform<WidthHeight> ->
		fselect2(acc, s2, FLift2(\wh1, wh2 -> WidthHeight(wh1.width + wh2.width, max(wh1.height, wh2.height))))
	)));
	positionDpiScale = fselect2(positionScale, dpiScale, FLift2(\psc : PositionScale, dsc : Factor ->
		PositionScale(Point(psc.pos.x / dsc.x, psc.pos.y / dsc.y), Point(psc.scale.x / dsc.x, psc.scale.y / dsc.y))));

	startingOffset = make(zeroPoint);
	startingPosition =
		eitherMap(
			fixPosition,
			\fp ->
				if (fp.top)
					fselect2(startingOffset, positionDpiScale, FLift2(\so : Point, ps : PositionScale ->
						Point(ps.pos.x + so.x, ps.pos.y + so.y)
					))
				else
					fselect3(startingOffset, positionDpiScale, currentHeight, \so : Point, ps : PositionScale, curH ->
						Point(ps.pos.x + so.x, ps.pos.y + so.y - (curH + borders) * ps.scale.y)
					),
			fselect5(
				startingOffset,
				positionDpiScale,
				stageWH,
				currentWidth,
				currentHeight,
				\so : Point, ps : PositionScale, sWH : WidthHeight, curW, curH -> {
					wd = additionalBorder + curW * ps.scale.x;
					hgt = additionalBorder + (curH + borders * 2.) * ps.scale.y;

					Point(
						min(
							if (ps.pos.x + so.x < additionalBorder)
								min(ps.pos.x, additionalBorder)
							else
								ps.pos.x + so.x,
							max(
								sWH.width - wd,
								ps.pos.x - wd
							)
						),
						min(
							if (ps.pos.y + so.y < additionalBorder)
								min(ps.pos.y, additionalBorder)
							else
								ps.pos.y + so.y,
							max(
								sWH.height - hgt,
								ps.pos.y - hgt
							)
						)
					)
			})
		)
		|> (\sp -> eitherMap(virtualScreen, \vs -> fpointaddition(sp, fselect(vs.position, FLift(\vsp -> vsp.pos))), sp));

	popupDownFn = \h, mi : MouseDownInfo -> {
			if (mi.inside()) {
				setDownInsideFocusTimer(manager, either(sameFocus, RMSameFocus(manager.focus)).focus, popupDownFnUns)
			}

			h
		};

	popupClickOutDownFn = \h, mi : MouseDownInfo -> {
			if (!h && mi.inside()) {
				next(menuOpened, false);
			}

			h || blockClicks
		};

	onPopupClickM = tryExtractStruct(style, RMOnClick(nop));

	contentWH : Transform<WidthHeight> =
		fselect4(sizesB, mWidth, mWidthLimits.min, mWidthLimits.max, \szs, w, minW, maxW ->
			fold(szs, WidthHeight(0., 0.), \acc, sz ->
				WidthHeight(if (w > 0.) w else max(sz.width, acc.width), sz.height + acc.height)
			)
			|> (\wh : WidthHeight -> WidthHeight(max(minW, wh.width), wh.height))
			|> (\wh : WidthHeight -> if (maxW > 0.) WidthHeight(min(maxW, wh.width), wh.height) else wh)
			|> (\wh : WidthHeight ->
				if (contains(style, RMNoSnapSize()) || w > 0. || minW > 0. || maxW > 0.) wh
				else WidthHeight(
					(if (mobile) 56. else 64.)
					|> (\snappingSize -> dceil(wh.width / snappingSize) * snappingSize),
					wh.height
				)
			)
		);

	popupPart = \par, start, end ->
		TEvenGrid(
			generate(start, end, \i ->
				mapi(items[i], \j, it -> TAttachBox(m2t(it, par), sizes[i][j]))
			),
			false
		)
		|> (\f -> TGroup2(f, TFillX()));

	scrollStyle =
		[scrollPosition, scrollWH]
		|> (\arr -> maybeArrayPush(arr, scrollPersistent))
		|> (\st ->
			if (contains(style, RMNoScrollbars()))
				concat(st, [TScrollEnabled(const(false)), TScrollbars(invisibleScrollBar, invisibleScrollBar)])
			else
				st
		);

	(\p ->
		TLines(
			[
				if (scrollArea.from > 0)
					[popupPart(p, 0, min(scrollArea.from, length(items)))]
				else
					[],
				if (scrollArea.from < scrollArea.to) {
					if (scrollArea.to - scrollArea.from > 100) {
						gridContentSize = makeWH();
						[
							generate(max(scrollArea.from, 0), min(scrollArea.to, length(items)), \i ->
								mapi(items[i], \j, it -> MGetFocusGroup(\p2 -> TAttachBox(m2t(it, p2), sizes[i][j])))
							)
							|> (\f ->
								MRecyclerGrid2T(
									manager,
									p,
									MRecyclerGrid(
										const(f),
										arrayPush(scrollStyle, MRecyclerGridInspectContentSize(gridContentSize))
									),
									m2t
								)
							)
							// Initialize sizes of non-rendered items by average values, evaluated in MRecyclerGrid
							|> (\f -> TConstruct([
									makeSubscribe2(gridContentSize, \gsz -> {
										if (length(sizes) > 0) {
											hgt = gsz.height / i2d(length(sizes));
											iter(sizes, \part -> if (length(part) > 0) {
												wd = gsz.width / i2d(length(part));
												iter(part, \sz -> if (getValue(sz).width == 0. && getValue(sz).height == 0.) {
													nextDistinct(sz, WidthHeight(wd, hgt))
												})
											})
										}
									})
								],
								f
							))
						]
					} else
						[
							MGetFocusGroup(\p2 ->
								popupPart(p2, max(scrollArea.from, 0), min(scrollArea.to, length(items)))
							)
							|> (\f ->
								MScroll2T(
									manager,
									p,
									MScroll(
										f,
										TFillXY(),
										scrollStyle
									),
									m2t
								)
							)
						]
				} else
					[],
				if (scrollArea.to < length(items))
					[popupPart(p, max(scrollArea.to, 0), length(items))]
				else
					[]
			]
			|> concatA
		)
		|> (\t -> TAvailableWidthHeight(t, currentWidth, currentHeight))
		|> (\f ->
			TConstruct(
				[
					make4Subscribe(
						contentWH,
						stageWH,
						offset,
						positionDpiScale,
						\realSize : WidthHeight, wh : WidthHeight, offs : Pair<WidthHeight, double>, ps : PositionScale -> {
							maxSize =
								WidthHeight(
									max(wh.width / ps.scale.x, 0.),
									max(
										maxHeight(
											wh,
											Point(offs.first.width, offs.second + if (selected < 0) offs.first.height else 0.),
											ps
										),
										0.
									)
								);

							wd = min(realSize.width, maxSize.width);
							hgt = min(realSize.height, maxSize.height);

							nextDistinct(currentMaxSize, maxSize);
							nextDistinct(currentWidth, wd);
							nextDistinct(currentHeight, hgt);

							offsetX =
								if (!leftAnimation)
									offs.first.width - wd
								else
									offs.first.width;

							szs = fgetValue(sizesB);
							offsY = dsum(map(subrange(szs, 0, selected), \sz -> sz.height));

							if (selected >= 0 && scrollArea.from < scrollArea.to)
								nextDistinct(
									scrollPosition.position,
									Point(
										0.,
										if (realSize.height > maxSize.height && maxSize.height > 0.)
											max(
												0.,
												min(
													offsY - (hgt - szs[selected].height) / 2.,
													realSize.height - maxSize.height
												)
											)
										else
											0.
									)
								);

							offsetY =
								if (selected >= 0)
									offs.second - offsY - borders + getValue(scrollPosition.position).y
								else
									offs.second + offs.first.height;

							nextDistinct(startingOffset, Point(offsetX * ps.scale.x, offsetY * ps.scale.y))
						}
					)
				],
				f
			)
		)
		|> TBorderTopBottom(borders)
		|> (\f -> MPopupAlphaAnimation(manager, p, f, style, beginAnimation, alphaContentDelay, alphaContentDuration, animated))
		|> (\t -> if (expandingAnimation) TAnimatedExpander(manager, parent, t, beginAnimation, []) else t)
		|> (\f -> MShapeFrame(p, extractStruct(style, MBackgroundStyle([MFill(MDialogMColor(getLightBackground(p)))])).style, f, shape))
		|> (\f -> MPopupScaleAnimation(manager, p, f, style, beginAnimation, elevation, animated))
		|> (\f -> MPopupAlphaAnimation(manager, p, f, style, if (expandingAnimation) make(true) else beginAnimation, alphaDelay, alphaDuration, animated))
		|> (\f -> if (dontHandle) f else TInteractive([TMouseDown(make(false))], f))
		|> (\f ->
			if (isSome(sameFocus))
				TCopySize(
					f,
					\tr ->
						TInteractive(
							[
								MouseDown2(popupDownFn),
								MouseRightDown2(popupDownFn),
								MouseMiddleDown2(popupDownFn)
							],
							tr
						),
					true
				)
			else
				f
		)
		|> (\f -> eitherMap(onPopupClickM, \onPopupClick ->
			TCopySize(
				f,
				\tr -> TInteractive(
					[MouseDown2(\h, mi -> {if (mi.inside()) onPopupClick.onClick(); h})],
					tr
				),
				true
			),
			f
		))
		|> (\f -> TScale(fselect(positionDpiScale, FLift(\psc -> Factor(psc.scale.x, psc.scale.y))), f))
		|> (\f ->
			TInteractive(
				[
					KeyDown2(\h, ke -> {
						if (fgetValue(p.keysEnabled) && ke.keycode == KEY_ESCAPE) {
							nextDistinct(menuOpened, false);

							true;
						} else {
							h;
						}
					}),
				],
				f
			)
		)
	)
	|> (\f ->
		MComponentGroup2T(
			manager,
			parent,
			"MPopup",
			concat(
				extractMComponentGroupState(style)
				|> (\f2 -> replaceStruct(f2, MFocusOnPrevious(false)))
				|> (\f2 -> if (sameZorder) f2 else concatStyles(f2, [MZorder(const(newZorder))])),
				[
					IScriptBehaviour("opened", menuOpened, ISInputValue([], "bool", false)),
					IScriptBehaviour("animated", animated, ISAppearanceEvent())
				]
			),
			f
		)
	)
	|> (\f -> if (dontHandle) f else disableInteractivesBelow(f))
	|> (\f -> MPopupTranslateAnimation(manager, parent, f, style, beginAnimation, startingPosition, animated))
	|> (\f ->
		if (contains(style, RMClickOutToClose()))
			TGroup2(
				TInteractive(
					[
						MouseDown2(popupClickOutDownFn),
						MouseRightDown2(popupClickOutDownFn),
						MouseMiddleDown2(popupClickOutDownFn)
					],
					TRectangle(interactiveRectangleStyle, TFillXY())
				),
				f
			)
		else
			f
	)
	|> (\f ->
		TConstruct(
			[
				makeSubscribe(menuOpened, \op -> if (op) deferUntilRender(\ -> next(beginAnimation, true)) else dispUnsM(uns))
			],
			f
		)
	)
	|> (\t -> TAttachAvailable(t, stageWH))
	|> (\t -> eitherMap(virtualScreen, \vs ->
		TAvailable(t, TSized(fselect2(vs.size, vs.position, FLift2(\vss, vsp ->
			WidthHeight(vss.width * vsp.scale.x, vss.height * vsp.scale.y)))
		)),
		t
	))
	|> (\t -> TShow(parent.ready, t))
	|> (\f -> {
		renderFn = \ -> trender(f, manager.manager.renderStyle);
		tUns = ref Some(renderFn());

		fwUns = onFullWindow(\fw -> if (fw) {
			dispUnsM(tUns);
			setUnsM(tUns, renderFn());
		});

		setUnsM(
			uns,
			\ -> {
				fwUns();
				dispUnsM(tUns);
				if (!sameZorder) {
					nextDistinct(manager.manager.zorder, zorder);
				};

				maybeApply(previousFocus, \pf -> {
					deferred(\ -> {
						if (isNone(getCurrentFocusedItem(manager)) && fgetValue(pf.focusEnabled)) {
							nextDistinct(pf.focused, true);
						}
					});

					if (isNone(manager.downInsideFocus)) {
						setDownInsideFocus(manager, pf);
					}
				});
			}
		);
	})
}

alphaDuration = mStepOf5TransitionDuration(2);
alphaDelay = 0.;
alphaContentDuration = mStepOf5TransitionDuration(3) - mStepOf5TransitionDuration(1);
alphaContentDelay = mStepOf5TransitionDuration(1);
alphaStart = 0.0;
alphaFinish = 1.0;

MPopupAlphaAnimation(
	manager : MaterialManager,
	parent : MFocusGroup,
	content : Tropic,
	style : [MPopupStyle],
	beginAnimation : DynamicBehaviour<bool>,
	delay : double,
	duration : double,
	animated : DynamicBehaviour<bool>
) -> Tropic {
	if (parent.theme.enableAnimations && contains(style, RMAlphaAnimation())) {
		TAnimation(
			TShow(beginAnimation, content),
			fif(
				beginAnimation,
				const([
					FAnimationKeyframe([FAlphaValue(alphaStart)]),
					FAnimationKeyframe([FAlphaValue(alphaFinish)])
				]),
				const([])
			),
			[
				FAnimationDelay(const(delay)),
				FAnimationDuration(const(duration)),
				FAnimationEasing(const(easingStandartCurve)),
				FAnimationOnFinish(\ -> {
					if (fgetValue(beginAnimation)) nextDistinct(animated, true);
				})
			]
		)
	} else
		content
}

heightDuration = mStepOf5TransitionDuration(3);
heightStart = 0.25;
heightFinish = 1.0;

widthDuration = mStepOf5TransitionDuration(2);
widthStart = 0.5;
widthFinish = 1.0;

MPopupScaleAnimation(
	manager : MaterialManager,
	parent : MFocusGroup,
	content : Tropic,
	style : [MPopupStyle],
	beginAnimation : DynamicBehaviour<bool>,
	elevation : Transform<double>,
	animated : DynamicBehaviour<bool>
) -> Tropic {
	leftAnimation = extractStruct(style, RMLeftAnimation(true)).leftAnimation;
	maybeShadow =
		\f -> if ( contains(style, RMNoShadow()) ) {
			f;
		} else {
			TShadowShape(parent, elevation, f, RectangleShape("MDialog"));
		};

	if (parent.theme.enableAnimations && contains(style, RMScaleAnimation())) {
		(\tr, mi ->
			TCropSize(
				TAnimation(
					TShow(beginAnimation, tr),
					fif(
						beginAnimation,
						const([
							FAnimationKeyframe([FScaleValue(Factor(widthStart, 1.0))]),
							FAnimationKeyframe([FScaleValue(Factor(widthFinish, 1.0))])
						]),
						const([])
					),
					[
						FAnimationDuration(const(widthDuration)),
						FAnimationEasing(const(easingStandartCurve)),
						FAnimationOnFinish(\ -> {
							if (fgetValue(beginAnimation)) nextDistinct(animated, true);
						})
					]
				)
				|> (\f2 ->
					TAnimation(
						f2,
						fif(
							beginAnimation,
							const([
								FAnimationKeyframe([FScaleValue(Factor(1.0, heightStart))]),
								FAnimationKeyframe([FScaleValue(Factor(1.0, heightFinish))])
							]),
							const([])
						),
						[
							FAnimationDuration(const(heightDuration)),
							FAnimationEasing(const(easingStandartCurve)),
							FAnimationOnFinish(\ -> {
								if (fgetValue(beginAnimation)) nextDistinct(animated, true);
							})
						]
					)
				),
				mi
			)
			|> maybeShadow
			|> (\f2 -> TFixSize(f2, tr))
		)
		|> (\f -> TCopySize2(content, f))
	} else {
		maybeShadow(content);
	}
}

translateDuration = easingTransitionDuration;
translateStart = -48.0;
translateFinish = 0.;

MPopupTranslateAnimation(
	manager : MaterialManager,
	parent : MFocusGroup,
	content : Tropic,
	style : [MPopupStyle],
	beginAnimation : DynamicBehaviour<bool>,
	startingB : Transform<Point>,
	animated : DynamicBehaviour<bool>
) -> Tropic {
	if (parent.theme.enableAnimations && contains(style, RMTranslateAnimation())) {
		TTranslate(
			startingB,
			TAnimation(
				TCopySize(
					TShow(beginAnimation, content),
					\tr ->
						TShow(
							fnot(animated),
							disableInteractivesBelow(tr)
						),
					true
				),
				fif(
					beginAnimation,
					const([
						FAnimationKeyframe([FTranslateValue(Point(0.0, translateStart))]),
						FAnimationKeyframe([FTranslateValue(Point(0.0, translateFinish))])
					]),
					const([])
				),
				[
					FAnimationDuration(const(translateDuration)),
					FAnimationEasing(const(easingStandartCurve)),
					FAnimationOnFinish(\ -> {
						if (fgetValue(beginAnimation)) nextDistinct(animated, true);
					})
				]
			)
		);
	} else {
		TTranslate(startingB, content);
	}
}
