import material/internal/material_list_util;

export {
	MList2T(manager : MaterialManager, parent : MFocusGroup, m : MList, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
	MDynamicList2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicList, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MList2T(manager : MaterialManager, parent : MFocusGroup, m : MList, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	style : [MListStyle] = m.style;
	mstate : [flow] = m.state;
	stateList : [flow] = concat(m.style, mstate);
	state = makeMListInnerState(defaultListState(manager, parent, m2t), stateList);

	down = make(false);
	MComponentGroup2T(
		manager,
		parent,
		"MList",
		[
			IScriptBehaviour("selected", state.selection.selected, ISInputValue([], "[int]", true)),
			extractStruct(m.state, IScriptRecordingEnabled(const(true))),
			AccessRole(if (isSome(state.reorder)) "listbox" else "list")
		],
		\p ->
			MGetFocusGroup(\p2 -> eitherFn(
				state.reorder,
				\rdr -> {
					reorderState = concatA([
						[
							MEnabled(fminA([state.enabled, rdr.enabled, fnot(down)], false)),
							MWrapNewColumn(true),
							MDragLowestPriority()
						],
						extractStructMany(m.state, MGrabDelay(0)),
						extractStructMany(m.state, MReorderDropSpot(MEmpty())),
						extractStructMany(m.state, MReorderCustomDraggingOrder()),
						extractStructMany(m.state, MReorderOnDragEnd())
					]);

					MReorderGrid2T(
						manager,
						p2,
						mapi(m.lines, \i, line -> MReorderItem(MGetFocusGroup(\par -> MListLine2T(par, const(i), const(line), state, None(), down, true)), [])),
						rdr.order,
						reorderState,
						m2t
					)
				},
				\ -> {
					listHeight = make(0.);

					TLines(mapi(m.lines, \i, line -> MListLine2T(p2, const(i), const(line), state, None(), down, false)))
					|> (\t -> TAttachHeight(t, listHeight)) 
					|> TSetMinHeight2(listHeight)
				}
			))
			|> (\f : Material ->
				eitherMap(
					tryExtractStruct(style, MAddScroll(None())),
					\addScroll -> {
						scrl = either(addScroll.scrollInpect, TScrollInspectVisible(make(zeroPoint), make(WidthHeight(-1., -1.))));
						wh = getValue(scrl.widthHeight);
						scrollBox =	wh2Tropic(wh);
						MScroll2T(manager, p, MScroll(f, scrollBox, [scrl]), m2t)
					},
					m2t(f, p)
				)
			)
			|> (\f -> TConstruct(state.subs, f))
			|> (\t -> if (m.lines == []) eitherMap(state.emptyMessage, \em -> TCenter(m2t(em, p)), t) else t)
	)
	|> MListAddAvailable(state)
}

MDynamicListData(
	lineHeights : MDynamicListLineHeights,
	items : [MListLine],
	sameHeight : bool
);

MDynamicListLineHeights(
	lineHeights : [Maybe<double>],
	itemsLength : int
);

MDynamicListItem(
	id : int,
	translation : double
);

MDynamicList2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicList, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	mstate : [flow] = m.state;
	stateList : [flow] = concat(m.style, mstate);
	state = makeMListInnerState(defaultListState(manager, parent, m2t), stateList);
	scrollPosition : TScrollPosition = extractStruct(m.state, MScrollPosition(make(zeroPoint)));
	addScroll = !contains(m.state, MNoScroll());
	reorderOnEnd = contains(m.state, MReorderOnDragEnd());
	reorderArr = eitherMap(state.reorder, \rdr -> if (reorderOnEnd) cloneBehaviour(rdr.order) else rdr.order, make([]));

	boxH = make(0.);
	availableW = make(0.);
	hgt = make(0.);
	listMinHeight = make(0.);

	down = make(false);
	draggingItem = make(-1);
	draggingXY = make(zeroPoint);

	changingSize = ref false;
	metricsLock = ref false;
	prevBottomItem : ref MDynamicListItem = ref MDynamicListItem(0, 0.);
	prevItemsCount : ref Pair<int, double> = ref Pair(0, 0.);
	prevLineHeights : ref MDynamicListLineHeights = ref MDynamicListLineHeights([], 0);

	itemsLoadQuota = 1000;
	loadQuota = make(itemsLoadQuota);

	itemsHeights0 = tryExtractStruct(m.state, MItemsHeights(\__ -> zero));
	heightsTree : ref Tree<int, DynamicBehaviour<Maybe<double>>> = ref makeTree();
	itemsHeights : (int) -> DynamicBehaviour<Maybe<double>> = \i ->
		lookupTreeSet(heightsTree, i, \__ ->
			eitherFn(
				itemsHeights0,
				\ih -> fgetValue(ih.height(i)),
				\-> {
					items = fgetValue(m.items);
					if (existsIndex(items, i)) either(MListLine2Height(items[i], state), 1.0) else 0.0
				}
			)
			|> (\hg -> make(if (hg > 0.0) Some(hg) else None()))
		);

	dynListData : Transform<MDynamicListData> =
		fsubselect(m.items, FLift(\itms : [MListLine] -> {
			sh = isNone(itemsHeights0) && !exists(itms, \it -> MListLine2Height(it, state) |> isNone);

			if (sh) {
				il = length(itms);
				ih : Maybe<double> =
					if (il > 0)
						MListLine2Height(itms[0], state)
					else
						None();

				nextDistinct(hgt, i2d(il) * either(ih, 0.));
				const(
					MDynamicListData(
						MDynamicListLineHeights([ih], il),
						itms,
						sh
					)
				)
			} else {
				fsubselect(loadQuota, FLift(\lq -> {
					il = min(length(itms), lq);
					eitherMap(
						state.reorder,
						\rdr ->
							fselect2(
								fmerge(generate(0, il, itemsHeights)),
								rdr.order,
								FLift2(\lh : [Maybe<double>], ord : [int] -> {
									nextDistinct(hgt, dsum(map(lh, \v -> either(v, 0.))));
									lhgts =	generate(0, il, \i -> elementAt(lh, elementAt(ord, i, i), None()));

									MDynamicListData(
										MDynamicListLineHeights(lhgts, il),
										itms,
										sh
									)
								})
							),
						fselect(
							fmerge(generate(0, il, itemsHeights)),
							FLift(\lh : [Maybe<double>] -> {
								nextDistinct(hgt, dsum(map(lh, \v -> either(v, 0.))));
								MDynamicListData(
									MDynamicListLineHeights(lh, il),
									itms,
									sh
								)
							})
						)
					)
					|> fthrottleNextFrame
				}));
			}
		}));

	items = fselect(dynListData, FLift(\data : MDynamicListData -> data.items));
	sameHeight = fselect(dynListData, FLift(\data -> data.sameHeight));
	itemsLength = fselect(dynListData, FLift(\data -> data.lineHeights.itemsLength));
	hasItems = fselect(itemsLength, FLift(\il -> il > 0));

	translates : DynamicBehaviour<[DynamicBehaviour<MDynamicListItem>]> = make([]);
	mainHoverTrigger = make(false);

	(\par ->
		MGetFocusGroup(\p ->
			SelectTGroup(
				translates,
				\__, __, tr : DynamicBehaviour<MDynamicListItem> -> {
					itemId = fselect(tr, FLift(\t -> t.id));
					materialLog("Rerender container " + i2s(fgetValue(itemId)), MaterialInfo());
					lineHeight = make(0.);
					hgtUns = initUnsM();

					lineId =
						eitherMap(
							state.reorder,
							\rdr ->
								fselect2(itemId, rdr.order, FLift2(\id, ord ->
									elementAt(ord, id, id)
								)),
							itemId
						);

					lineContent : Transform<MListLine> =
						if (!isUrlParameterFalse("dynamic_list_sub_fix")) {
							fselect2(dynListData, lineId, FLift2(\dlData, i -> {
								elementAt(dlData.items, i, dummySingleLine())
							}))
						} else {
							fselect2(items, lineId, FLift2(\itms, i ->
								elementAt(itms, i, dummySingleLine())
							))
						};

					MListLine2T(p, lineId, lineContent, state, None(), down, true)
					|> (\line -> TAttachHeightTrigger(line, lineHeight, fnot(sameHeight)))
					|> (\t -> TConstruct(
							[
								make2SubscribeTrigger(fnot(sameHeight), lineHeight, lineId, \hg, i -> {
									dispUnsM(hgtUns);
									itemHgt : DynamicBehaviour<Maybe<double>> = itemsHeights(i);
									hgFn = \ -> nextDistinct(itemHgt, Some(hg + state.gap));

									if (^metricsLock)
										setUnsM(
											hgtUns,
											interruptibleTimer(0, hgFn)
										)
									else
										hgFn();
								}),
								\ -> \ -> dispUnsM(hgtUns)
							],
							t
					))
					|> (\listLine ->
						eitherMap(
							state.reorder,
							\rdr ->
								if (isFConst(rdr.enabled) && !fgetValue(rdr.enabled)) {
									listLine
								} else {
									rdrdown = make(false);
									point = make(Point(-1., -1.));
									hover = make(false);
									grabDelay = extractStruct(m.state, MGrabDelay(300)).delay;

									TVisible(fselect2(draggingItem, lineId, FLift2(\dr, ln -> dr != ln)), listLine)
									|> (\listLineVisible -> {
										above = fselectLift(lineContent, \lc -> 
											switch (lc : MListLine) {
												MSingleLine(__, state0): contains(state0, MInteractionsOnTop());
												MDoubleLine(__, __, state0): contains(state0, MInteractionsOnTop());
												MTripleLine(__, __, state0): contains(state0, MInteractionsOnTop());
												MCustomLine(__, state0): contains(state0, MInteractionsOnTop());
												MSubmenuLine(__, __, __, __): false;
												MSeparatorLine(): false;
											}
										);

										TCopySize2(
											listLineVisible,
											\sz, trop -> {
												dragDropHandleLayer =
													MComponent2T(
														manager,
														p,
														"MDynamicListDragDropHandle",
														[
															MFocusEnabled(const(false)),
															MClickEnabled(rdr.enabled),
															THovering(hover),
															TPressed(rdrdown),
															MMousePosition(point),
															MOnTouch(grabDelay, \ -> {
																if (!getValue(down)) {
																	nextDistinct(draggingXY, getValue(point));
																	nextDistinct(draggingItem, fgetValue(lineId));
																}
															}),
															MCursorShape(const(if (mobile) FingerCursor() else DefaultCursor())),
															MPassClicks(),
															MMobileForceHover(),
															AccessRole("")
														],
														\__ -> sz,
														m2t
													);
												TGroup([
													TShow(fnot(above), dragDropHandleLayer),
													trop,
													TShow(above, dragDropHandleLayer)
												])
											}
										)
									})
									|> (\listLineVisible ->
										TConstruct(
											concat([
												make2Subscribe(hover, fif(rdr.enabled, draggingItem, const(-1)), \h, hId -> if (h && hId >= 0) {
													id = fgetValue(lineId);
													ordr = getValue(reorderArr);

													nextDistinct(
														reorderArr,
														insertArray(removeIndex(ordr, findiDef(ordr, eq(hId), -1)), findiDef(ordr, eq(id), -1), hId)
													);
												}),
												makeSubscribe2(fand(rdr.enabled, rdrdown), \d ->
													if (!d) {
														nextDistinct(draggingItem, -1);
														nextDistinct(mainHoverTrigger, false);
														if (reorderOnEnd) {
															nextDistinct(rdr.order, getValue(reorderArr));
														}
													}
												)
											], if (mobile) [
												makeSubscribe2(hover, \h -> if (h) nextDistinct(mainHoverTrigger, true)),
												makeSubscribe2(mainHoverTrigger, \mh -> if (!mh) nextDistinct(hover, false))
											] else []),
											listLineVisible
										)
									)
								},
							listLine
						)
					)
					|> makeTropicConstantHeight(Some(0.))
					|> (\listLine -> TTranslate(fselect(tr, FLift(\t -> Point(0., t.translation))), listLine))
				}
			)
			|> (\f ->
				TransformTAcc2(\t2a -> {
					bf = f |> t2a;

					TAcc(
						bf.form,
						TFormMetrics(
							bf.metrics.width,
							hgt,
							hgt
						),
						bf.minWidth,
						hgt,
						bf.maxWidth,
						hgt,
						zero,
						bf.yFillers,
						bf.addFillers,
						bf.xConstant,
						bf.yConstant,
						bf.disposers
					)
				})
			)
			|> MListAddAvailable(state)
		)
		|> (\f ->
			if (addScroll)
				MScroll2T(
					manager,
					par,
					MScroll(
						f,
						TFillXY(),
						replaceStructMany(extractMScrollStyle(m.state), [scrollPosition, TScrollResizeBeforePrint(false)])
					),
					m2t
				)
			else
				m2t(f, par)
		)
		|> (\f ->
			TConstruct(
				[
					make3Subscribe(dynListData, scrollPosition.position, boxH, \data, sPosition, boxHeight -> {
						itms = data.items;
						lhgts = data.lineHeights;
						sh = data.sameHeight;

						if (!contains(lhgts.lineHeights, None())) {
							metricsLock := true;

							lineHeightsChanged = ^prevLineHeights != lhgts;

							lineHeights = map(lhgts.lineHeights, \v -> either(v, 0.));
							il = lhgts.itemsLength;
							getLineHeights = \idx -> lineHeights[if (sh) 0 else idx];

							lineHeight2translate = \idStart, idEnd ->
								if (sh)
									lineHeights[0] * i2d(idEnd - idStart)
								else
									dsum(subrange(lineHeights, idStart, max(0, idEnd - idStart)));

							bottomItem : MDynamicListItem =
								updateWhile(
									if (lineHeightsChanged)
										MDynamicListItem(
											^prevBottomItem.id,
											lineHeight2translate(0, ^prevBottomItem.id)
										)
									else
										^prevBottomItem,
									\item -> {
										id = item.id;

										if (id < 0 || id >= il)
											Some(MDynamicListItem(0, 0.))
										else
											if (sPosition.y <= item.translation + getLineHeights(id)) {
												if (id == 0 || sPosition.y >= item.translation)
													None()
												else
													Some(MDynamicListItem(id - 1, item.translation - getLineHeights(id - 1)))
											} else {
												if (id == il - 1)
													None()
												else
													Some(MDynamicListItem(id + 1, item.translation + getLineHeights(id)))
											}
									}
								);

							bottomItemChanged = ^prevBottomItem != bottomItem;

							itemsCount : Pair<int, double> =
								updateWhile(
									if (sh || (!bottomItemChanged && !lineHeightsChanged))
										^prevItemsCount
									else
										Pair(
											^prevItemsCount.first,
											lineHeight2translate(bottomItem.id, bottomItem.id + ^prevItemsCount.first)
										),
									\item : Pair<int, double> -> {
										id = bottomItem.id + item.first;
										heightWithoutBottomItem = item.second - getLineHeights(bottomItem.id);

										if (id < 0 || id > il)
											Some(Pair(0, 0.))
										else
											if (boxHeight <= heightWithoutBottomItem && addScroll) {
												if (id == 0 || boxHeight >= heightWithoutBottomItem - getLineHeights(id - 1))
													None()
												else
													Some(Pair(item.first - 1, item.second - getLineHeights(id - 1)))
											} else {
												if (id == il)
													None()
												else
													Some(Pair(item.first + 1, item.second + getLineHeights(id)))
											}
									}
								);

							itemsCountChanged = ^prevItemsCount != itemsCount;

							if (itemsCount.first > 0) {
								topItem =
									MDynamicListItem(
										bottomItem.id + itemsCount.first,
										bottomItem.translation + itemsCount.second
									);

								prevTranslates = getValue(translates);

								newTranslates =
									if (lineHeightsChanged)
										foldi(enumFromTo(bottomItem.id, topItem.id - 1), Triple([], 0., 0), \i, acc, id -> {
											itemTranslation = secondOfTriple(acc) + lineHeight2translate(thirdOfTriple(acc), id);
											item = MDynamicListItem(id, itemTranslation);

											tr =
												if (i < length(prevTranslates)) {
													nextDistinct(prevTranslates[i], item);
													prevTranslates[i]
												} else
													make(item);

											Triple(arrayPush(firstOfTriple(acc), tr), itemTranslation, id)
										})
										|> firstOfTriple
										|> Some
									else if (bottomItemChanged || itemsCountChanged)
										prevTranslates
										|> (\translatesPrev : [DynamicBehaviour<MDynamicListItem >] ->
											if (itemsCount.first < length(translatesPrev)) {
												filter(translatesPrev, \tr : DynamicBehaviour<MDynamicListItem> ->
													getTrId(tr) < topItem.id && getTrId(tr) >= bottomItem.id
												)
												|> uniq;
											} else
												translatesPrev
										)
										|> (\translatesPrev : [DynamicBehaviour<MDynamicListItem>] ->
											if (itemsCount.first > length(translatesPrev)) {
												maxId = either(maxA(map(translatesPrev, getTrId)), -1);

												concat(translatesPrev, generate(0, itemsCount.first - length(translatesPrev), \i -> {
													id0 = maxId + i + 1;
													id = if (id0 > length(itms) - 1) id0 - itemsCount.first else id0;

													translation = bottomItem.translation + lineHeight2translate(bottomItem.id, id);

													make(MDynamicListItem(id, translation))
												}));
											} else
												translatesPrev
										)
										|> (\translatesPrev : [DynamicBehaviour<MDynamicListItem >] -> {
											bottomLine = bottomItem.id - bottomItem.id % itemsCount.first + itemsCount.first;

											iter(translatesPrev, \tr -> {
												id = getTrId(tr);

												if (id < bottomItem.id || id >= topItem.id) {
													newId =
														(bottomLine + id % itemsCount.first)
														|> (\f2 -> if (f2 >= topItem.id) f2 - itemsCount.first else f2);

													translation = bottomItem.translation + lineHeight2translate(bottomItem.id, newId);

													nextDistinct(tr, MDynamicListItem(newId, translation))
												}
											});

											Some(translatesPrev)
										})
									else // if nothing changed
										None();

								maybeApply(newTranslates, \tr -> nextDistinct(translates, tr));

								materialLogFn(
									\ ->
										map(getValue(translates), \tr -> getValue(tr).id)
										|> (\f2 ->
											if (length(f2) != length(uniq(f2))
												|| either(minA(f2), bottomItem.id) < bottomItem.id
												|| either(maxA(f2), topItem.id - 1) >= topItem.id) {

												println("MDynamicList error");
												println(bottomItem);
												println(topItem);
												println(itemsCount);
												println(f2);
											}
										),
									MaterialInfo(),
									nop
								);

								if (addScroll && !^changingSize && !sh && il != length(itms)
									&& sPosition.y > (fgetValue(hgt) - boxHeight) * i2d(il - itemsLoadQuota / 5) / i2d(il)) {

									changingSize := true;
									newItemsLength = il + itemsLoadQuota;

									deferred(\ -> {
										nextDistinct(loadQuota, newItemsLength);
										changingSize := false;
									})
								}
							} else
								nextDistinct(translates, []);

							prevBottomItem := bottomItem;
							prevItemsCount := itemsCount;
							prevLineHeights := lhgts;

							metricsLock := false;
						}
					})
				]
				|> (\subs ->
					eitherMap(
						state.reorder,
						\rdr ->
							arrayPush(
								subs,
								makeSubscribe(itemsLength, \il -> {
									sl = getValue(rdr.order);
									if (length(sl) < il) {
										nextDistinct(rdr.order, concat(sl, enumFromTo(length(sl), il - 1)));
										nextDistinct(reorderArr, getValue(rdr.order));
									} else if (length(sl) > il) {
										nextDistinct(rdr.order, filter(sl, \s -> s < il));
										nextDistinct(reorderArr, getValue(rdr.order));
									}
								})
							),
						subs
					)
				),
				f
			)
		)
		|> (\f2 -> {
			if (isUrlParameterTrue("learner_dashboard_optimization")) {
				TGroup2(
					TVisible(fnot(hasItems), eitherMap(state.emptyMessage, \em -> TCenter(m2t(em, par)), TFillX())),
					TVisible(hasItems, f2)
				)
			} else {
				TIf(hasItems, f2, eitherMap(state.emptyMessage, \em -> TCenter(m2t(em, par)), TFillX()))
			}
		})
	)
	|> (\f ->
		MComponentGroup2T(
			manager,
			parent,
			"MDynamicList",
			concatA([
				[
					IScriptBehaviour("selected", state.selection.selected, ISInputValue([], "[int]", true)),
					AccessRole(if (isSome(state.reorder)) "listbox" else "list"),
				],
				eitherMap(
					tryExtractStruct(m.state, MButtonTitle(const(""))),
					\bt -> [FAccessAttribute("aria-label", bt.title)],
					[]
				),
				extractStructMany(m.state, IScriptRecordingEnabled(const(true)))
			]),
			f
		)
	)
	|> (\f ->
		eitherMap(
			state.reorder,
			\rdr -> {
				posScaleB = make(zeroPositionScale);
				TConstruct(
					[
						makeSubscribe2Uns(fif(rdr.enabled, draggingItem, const(-1)), \dr ->
							if (dr >= 0) {
								mouseXY = make(zeroPoint);

								[
									(\par ->
										MListLine2T(par, const(dr), const(fgetValue(items)[dr]), state, None(), down, true)
										|> (\t -> TScale(fselect(posScaleB, FLift(\ps -> Factor(ps.scale.x, ps.scale.y))), t)) 
										|> (\t -> TAvailableWidth(t, availableW))
										|> (\t -> MGrabAnimation(state.manager, par, t, Some(getValue(draggingXY))))
									)
									|> (\fn -> MComponentGroup2T(manager, parent, "MDynamicListDraggable", [], fn))
									|> (\t -> TTranslate(
											fselect(mouseXY, FLift(\xy0 -> Point(xy0.x - getValue(draggingXY).x, xy0.y - getValue(draggingXY).y))),
											t
										))
									|> (\t -> MMonitorMousePosition(manager, mouseXY, t)())
								]
							} else {
								[]
							}
						)
					],
					f
				)
				|> (\t -> TPositionScale(posScaleB, t))
			},
			f
		)
	)
	|> (\f -> TAttachWidth(f, availableW))
	|> (\f -> TInspect([IMinHeight(listMinHeight)], f))
	|> (\f -> TAttachAvailableHeight(f, boxH))
	|> (\f -> TOverride([OMinHeight(listMinHeight)], f))
	|> MListAddAvailable(state)
}

dummySingleLine() -> MSingleLine {
	MSingleLine("", [])
}

getTrId(tr : DynamicBehaviour<MDynamicListItem>) -> int {
	getValue(tr).id
}
