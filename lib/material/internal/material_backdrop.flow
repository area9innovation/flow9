import material/internal/material_button;
import material/internal/tropic_size_util;

export {
	MBackdrop2T(manager : MaterialManager, parent : MFocusGroup, m : MBackdrop, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MBackdrop2T(manager : MaterialManager, parent : MFocusGroup, m : MBackdrop, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	opened = extractStruct(m.style, MBackdropOpened(make(false))).opened;
	titleText = extractStruct(m.style, MBackdropTitle(const("Title"))).title;
	subtitleText = tryExtractStruct(m.style, MBackdropSubtitle(const("")));
	leftIcon = extractStruct(m.style, MBackdropLeftIcon("close", "menu"));
	right = extractStruct(m.style, MBackdropItems([])).items;

	duration = const(easingTransitionDuration);
	headerHeight = make(0.);
	avHeight = make(0.);
	minContentHeight = 48.;

	title =
		MBaselineCols(
			concat([
				MIconToggle2T(manager, parent, MIconToggle(leftIcon.opened, [MToggleFalseIcon(leftIcon.closed, []), MAnimatedIcon()], [MToggleValue(opened)]), m2t)
				|> TBorderLeftRight(4.),
				TSelect(titleText, \txt -> MText2T(parent, txt, [MTitle()]))
				|> TBorderLeft(16.),
				TFillX()
			],
			right
		));

	headerContent =
		MScroll(MGroup2(m.header, TFillX()), TFillXY(), [MScrollCropByContent()])
		|> MBorderLeftRight(16.);

	header =
		MLines2(
			title,
			MAnimatedHide(opened, headerContent, Some(duration))
		)
		|> (\t -> MAttachHeight(t, headerHeight))
		|> MBorderBottom(minContentHeight);

	arrow =
		MAnimatedHide(opened, MIconButton("keyboard_arrow_up", \ -> nextDistinct(opened, false), [MIconButtonBorder(0.)], []), Some(duration));

	subtitle =
		eitherMap(subtitleText, \txt ->
			MLines2(
				MDynamicParagraph(txt.subtitle, [MSubtitle()])
				|> (\sbt -> MBaselineCols([sbt, TFillX(), arrow])),
				MSeparator(true) |> MBorderTop(8.)
			),
			TEmpty()
		);

	content =
		TLines2(
			m2t(MAnimatedResizer(const(-1.), headerHeight, [MDuration(duration)]), parent)
			|> TSetMinWidth2(const(0.)),

			MScroll(m.content, TFillXY(), [])
			|> MBorderTop(8.)
			|> (\t -> MLines2(subtitle, t))
			|> (\cont -> m2t(cont, parent))
			|> (\t -> TBorder4(16., t))
			|> (\t -> MShapeFrame(parent, [MThemeFill(parent, MBackgroundColor())], t, "backdrop"))
		);

	MGroup2(header, content)
	|> (\cont -> m2t(cont, parent))
	|> addTBackground(MThemeColor2MColor(parent, MAccentColor()))
	|> (\t -> TAttachAvailableHeight(t, avHeight))
}