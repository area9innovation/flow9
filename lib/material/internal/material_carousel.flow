import material/internal/material_button;

export {
	MCarousel2T(manager : MaterialManager, parent : MFocusGroup, m : MCarousel, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

goldenRatio = (1. + sqrt(5.)) / 2.;
contentZoomDef = MCarouselContentZoom(1., 1., 0.);

MCarousel2T(manager : MaterialManager, parent : MFocusGroup, m : MCarousel, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	state = makeMCarouselState(manager, m);

	if (state.contentsLength > 0)
		(\p -> {
			mapi(m.contents, \i, c ->
				MCarouselContent2T(manager, p, c, i, m.style, state, m2t)
			)
			|> TGroup
			|> (\f -> TTranslate(fpoint(state.tx, const(0.)), f))
			|> (\f ->
				if (state.heightByContent) {
					TConstruct(
						[
							makeSubscribe(state.width, \w -> updateTxForWidth(w, state))
						],
						TAttachAvailableWidth(TCropWidth2(f, state.width), state.width)
					);
				} else {
					awh = makeWH();
					whWithRatio =
						fselect(awh, FLift(\wh -> {
							w = wh.width;
							h = wh.height;
							WidthHeight(
								if (w < 120.) {if (h < 120.) 120. else h * goldenRatio} else w,
								if (h < 120.) {if (w < 120.) 120. else w / goldenRatio} else h
							)
						}));

					TConstruct(
						[
							makeSubscribe(whWithRatio, \wh -> {
								nextDistinct(state.width, wh.width);
								nextDistinct(state.height, wh.height);
								updateTxForWidth(wh.width, state);
							})
						],
						TAttachAvailable(
							TCrop(const(zeroPoint), whWithRatio, f),
							awh
						)
					);
				}
			)
			|> (\f ->
				// We need to make sure that native clip is container, so that
				// swipe events on it is not biased while content is translated
				// (useful if no MInteractionsOnTop style is set)
				TDecorator(f, [], false)
			)
			|> (\f : Tropic ->
				if (state.contentsLength > state.itemsSpan)
					MSwipe2T(
						manager,
						p,
						MSwipe(
							f,
							[
								MOnSwipeLeft(state.swipeFn),
								MOnSwipeRight(state.swipeFn),
								TPressed(state.down),
								MCurrentInteractive(state.swipeInteractive),
								MEnabled(
									eitherMap(
										state.maximize,
										\mx ->
											if (contains(mx.style, MDisableSwipe()))
												fand(state.enabled, fnot(mx.maximized))
											else
												state.enabled,
										state.enabled
									)
								)
							]
						),
						m2t
					)
				else
					f
			)
			|> (\f ->
				if (state.enableChevrons)
					MCarouselChevrons(manager, p, f, state, m2t)
					|> (\f2 ->
						eitherMap(
							state.maximize,
							\mx ->
								if (contains(mx.style, MNoChevrons()))
									TVisible(fnot(mx.maximized), f2)
								else
									f2,
							f2
						)
					)
				else
					f
			)
			|> (\f ->
				if (isSome(state.progress))
					MCarouselProgress(manager, p, f, state)
					|> (\f2 ->
						eitherMap(
							state.maximize,
							\mx ->
								if (contains(mx.style, MNoProgress()))
									TVisible(fnot(mx.maximized), f2)
								else
									f2,
							f2
						)
					)
				else
					f
			)
			|> (\f ->
				[
					(\cz : MCarouselContentZoom, vis0 : bool -> {
						vis =
							eitherMap(
								state.maximize,
								\mx ->
									eitherFn(
										tryExtractStruct(mx.style, cz),
										\mxCz -> {
											fselect(mx.maximized, FLift(\mxd -> {
												zoom = if (mxd) mxCz else cz;
												nextDistinct(state.zoomScale, max(min(1., zoom.max), zoom.min));
												vis0 || mxd
											}))
										},
										\ ->
											if (vis0)
												fselect(mx.maximized, FLift(\mxd -> {
													if (mxd)
														nextDistinct(state.zoomScale, 1.);
													!mxd
												}))
											else
												const(false)
									),
								const(vis0)
							);

						if (vis == const(false)) []
						else {
							czStep : () -> double =
								eitherMap(
									state.maximize,
									\mx ->
										eitherMap(
											tryExtractStruct(mx.style, cz),
											\mxCz -> \ ->
												if (getValue(mx.maximized))
													mxCz.step
												else
													cz.step,
											\ -> cz.step
										),
									\ -> cz.step
								);

							[
								MCarouselZoomButton(manager, parent, m2t, state, "zoom_in", vis, \ -> state.addToContentZoom(czStep())),
								MCarouselZoomButton(manager, parent, m2t, state, "zoom_out", vis, \ -> state.addToContentZoom(-czStep())),
							]
						}
					})
					|> (\makeZoomButtons ->
						eitherMap(
							state.contentZoom,
							\cz -> makeZoomButtons(cz, true),
							makeZoomButtons(contentZoomDef, false)
						)
					),

					eitherMap(
						state.maximize,
						\mx ->
							[
								MIconToggle2T(
									manager,
									p,
									MIconToggle(
										"fullscreen_exit",
										[
											state.buttonsColor, MIconButtonBorder(8.), MElevation(const(2.)),
											MToggleFalseIcon("fullscreen", [state.buttonsColor, MElevation(const(2.))])
										],
										[MEnabled(state.enabled), MToggleValue(mx.maximized)]
									),
									m2t
								)
								|> (\f2 -> TBorder(0., 8., 8., 8., f2))
							],
						[]
					)
				]
				|> concatA
				|> (\buttons ->
					if (length(buttons) > 0)
						TCopySize(
							f,
							\tr -> {
								defTranslation = fselect(state.bottomPanelHeight, FLift(\bph -> Point(0., -bph)));

								TBaselineColsDir(buttons, p.rtl)
								|> TBorderLeftRight(8.)
								|> (\f3 ->
									TTranslate(
										eitherMap(
											state.maximize,
											\mx ->
												if (contains(mx.style, MShowTextOverlay()))
													defTranslation
												else
													fif(fnot(mx.maximized), defTranslation, const(zeroPoint)),
											defTranslation
										),
										f3
									)
								)
								|> (\f3 -> TTweak([TAlign(const(b2d(!p.rtl)), const(1.))], f3, tr))
							},
							true
						)
					else
						f
				)
			)
			|> (\f ->
				eitherMap(
					state.maximize,
					\mx ->
						if (contains(mx.style, MMaximizeFullscreen()))
							TFullScreen(mx.maximized, f)
						else
							TFullWindow(mx.maximized, f),
					f
				)
			)
			|> (\f -> TConstruct(state.subs, f))
		})
		|> (\f -> MComponentGroup2T(manager, parent, "MCarousel", [
					// IScriptBehaviour("active", m.activeIdx, MInputValue(generate(0, length(m.contents), idfn))),
					IScriptBehaviour("tx", state.tx, ISAppearanceEvent()),
					IScriptBehaviour("scale", state.zoomScale, ISAppearanceEvent())
					// , IScriptBehaviour("swipe_down", state.down)
				], f))
	else
		TEmpty()
}

updateTxForWidth(width : double, state : MCarouselState) {
	nextDistinct(state.tx, -width * i2d(getValue(state.activeIdx)));
	next(state.activeIdx, getValue(state.activeIdx)); // in order to trigger the animation
}

MCarouselState(
	contentsLength : int,
	width : DynamicBehaviour<double>,
	height : DynamicBehaviour<double>,
	itemWidth : Transform<double>,
	tx : DynamicBehaviour<double>,
	down : DynamicBehaviour<bool>,
	enabled : Transform<bool>,
	activeIdx : DynamicBehaviour<int>,
	activeI : DynamicBehaviour<int>,
	goToPrev : () -> void,
	goToNext : () -> void,
	hasPrev : Transform<bool>,
	hasNext : Transform<bool>,
	swipeInteractive : DynamicBehaviour<Maybe<bool>>,
	swipeFn : (Point, Point) -> bool,
	maximize : Maybe<MCarouselMaximize>,
	contentZoom : Maybe<MCarouselContentZoom>,
	addToContentZoom : (double) -> void,
	zoomScale : DynamicBehaviour<double>,
	itemsSpan : int,
	endlessScroll : bool,
	enableArrows : Maybe<MCarouselEnableArrows>,
	enableChevrons : bool,
	disableSideChevrons : bool,
	tuneChevrons : Maybe<MCarouselChevronTunes>,
	chevronsStyle : Maybe<MCarouselChevronStyle>,
	progress : Maybe<MCarouselProgressState>,
	wrapBottomTitle : (Tropic) -> Tropic,
	bottomPanelHeight : Transform<double>,
	buttonsColor : MThemeColor,
	iconSize : double,
	heightByContent : bool,
	subs : [() -> () -> void]
);

MCarouselProgressState(
	bottom : bool,
	activeColor : MThemeColor,
	inactiveColor : MThemeColor,
	spacing : double
);

makeMCarouselState(manager : MaterialManager, m : MCarousel) -> MCarouselState {
	customPanning = extractStruct(m.style, MCarouselCustomPanning(make(0.), make(false)));
	duration = extractStruct(m.style, MDuration(const(0.5))).duration;
	endlessScroll = contains(m.style, MLoopScroll());
	contentSideOffset = extractStruct(m.style, MCarouselSidesOffset(0.)).sideOffset;
	tuneChevrons = tryExtractStruct(m.style, MCarouselChevronTunes(true, 0.0));
	chevronsStyle = tryExtractStruct(m.style, MCarouselChevronStyle([]));

	// Group carousel items by N elements
	itemsSpan = max(extractStruct(m.style, MCarouselItemSpan(1)).span, 1);
	contentsLength =
		if (!endlessScroll)
			floor(i2d(length(m.contents)) / i2d(itemsSpan)) * itemsSpan
		else
			length(m.contents);

	enableArrows = tryExtractStruct(m.style, MCarouselEnableArrows(false));
	enableChevrons = !contains(m.style, MNoChevrons()) && contentsLength > itemsSpan;
	enableProgress = !contains(m.style, MNoProgress()) && contentsLength > itemsSpan;
	disableSideChevrons = contains(m.style, MCarouselDisableSideChevrons());

	width = make(0.0);
	height = make(0.0);
	itemWidth = fsubtract(fdivide(width, const(i2d(itemsSpan))), const(2. * contentSideOffset));
	uns = initUnsM();
	targetTx = make(0.);
	tx = customPanning.shiftX;
	down = customPanning.mousedown;
	enabled = extractStruct(m.style, MEnabled(const(true))).enabled;
	activeI = m.activeIdx;
	activeIdx = cloneBehaviour(activeI);
	fs = make(false);

	hasPrev = make(false);
	hasNext = make(false);
	goToPrev = \ -> if (getValue(hasPrev)) next(activeIdx, getValue(activeIdx) - 1) else next(activeIdx, contentsLength - 1);
	goToNext = \ -> if (getValue(hasNext)) next(activeIdx, getValue(activeIdx) + 1) else next(activeIdx, 0);

	swipeInteractive = extractStruct(m.style, MCurrentInteractive(make(None()))).current;
	carouselMaximize = tryExtractStruct(m.style, MCarouselMaximize(make(false), []));
	contentZoom = tryExtractStruct(m.style, contentZoomDef);
	zoomScale = make(1.);
	addToContentZoom : (double) -> void =
		(\cz : (double) -> void ->
			eitherMap(
				carouselMaximize,
				\mx ->
					eitherMap(
						tryExtractStruct(mx.style, contentZoomDef),
						\mxCz ->
							\ad -> {
								if (getValue(mx.maximized))
									nextDistinct(zoomScale, min(max(mxCz.min, getValue(zoomScale) + ad), mxCz.max))
								else
									cz(ad)
							},
						cz
					),
				cz
			)
		)
		|> (\f2 ->
			eitherMap(
				contentZoom,
				\cz ->
					f2(\ad -> nextDistinct(zoomScale, min(max(cz.min, getValue(zoomScale) + ad), cz.max))),
				f2(nop1)
			)
		);
	bottomTitleHeights = make([]);
	wrapBottomTitle = \m2 -> {
		hgt = make(0.);
		dynArrayPush(bottomTitleHeights, hgt);
		TAttachHeight(m2, hgt)
	}
	bottomPanelHeight = fsubselect(bottomTitleHeights, FLift(\bth -> fmaxA(bth, 0.)));

	buttonsColor = extractStruct(m.style, MCarouselButtonsColor(MWhite())).color;

	progress =
		if (enableProgress) {
			progressColors = extractStruct(m.style, MProgressColors(buttonsColor, MGrey(500)));
			Some(
				MCarouselProgressState(
					contains(m.style, MProgressAtBottom()),
					progressColors.active,
					progressColors.inactive,
					extractStruct(m.style, MCarouselProgressSpacing(2.0)).spacing
				)
			)
		} else None();

	iconSize = extractStruct(m.style, MIconSize(36.)).size;

	updatePanning = \delta -> {
		// Accumulate the displacement
		newTx = getValue(tx) + delta.x;

		if (endlessScroll) {
			next(tx, newTx);
		} else {
			// Allow panning only if there is a next/previous page
			// TODO: Should allow spring beyond the first/last contents
			sTx = newTx + i2d(getValue(activeIdx)) * fgetValue(itemWidth);

			if ((sTx > 0.0 && getValue(hasPrev)) || (sTx < 0.0 && getValue(hasNext)))
				next(tx, max(min(newTx, 0.), -fgetValue(itemWidth) * i2d(contentsLength - 1)));
		}
	}

	endPanning = \ -> {
		// If panning horizontally for more than half of the screen, switch page
		sTx = getValue(tx) + i2d(getValue(activeIdx)) * fgetValue(itemWidth);

		if (abs(sTx) > fgetValue(itemWidth) * 0.1) {
			if (sTx > 0.0) {
				goToPrev();
			} else {
				goToNext();
			}
		} else {
			// in order to trigger the animation
			next(activeIdx, getValue(activeIdx));
		}
	}

	swipeFn = \p : Point, delta : Point -> {
		updatePanning(delta);
		true
	}

	heightByContent = contains(m.style, MCarouselHeightByContent());

	subs =
		[
			\ -> subscribe2(targetTx, \finalTx -> {
				startTx = getValue(tx);
				dispUnsM(uns);

				easing = makeEasing(fgetValue(duration), easingStandartCurve, manager.manager.deltaTimer, nop);

				setUnsM(
					uns,
					subscribe2(easing.percent, \t -> {
						if (getValue(down)) {
							dispUnsM(uns);
						} else {
							next(tx, lerp(startTx, finalTx, t));
						}
					})
				);

				easing.start();
			}),
			makeSubscribe(down, \d -> if (!d) endPanning()),
			// Animates the transition snap
			\ -> subscribe(activeIdx, \idx -> {
				nextDistinct(hasPrev, endlessScroll || idx > 0);
				nextDistinct(hasNext, endlessScroll || idx < contentsLength - 1);

				aI =
					if (idx >= 0)
						idx % contentsLength
					else
						(idx + 1) % contentsLength + contentsLength - 1;

				nextDistinct(activeI, aI);
				next(targetTx, -fgetValue(itemWidth) * i2d(idx));
			}),
			\ -> subscribe2(activeI, \i -> {
				nextDistinct(zoomScale, 1.);
				idx = fgetValue(activeIdx);
				nextDistinct(activeIdx, floor(i2d(idx) / i2d(contentsLength)) * contentsLength + i);
			}),
			\ -> \ -> dispUnsM(uns)
		];

	MCarouselState(
		contentsLength,
		width,
		height,
		itemWidth,
		tx,
		down,
		enabled,
		activeIdx,
		activeI,
		goToPrev,
		goToNext,
		hasPrev,
		hasNext,
		swipeInteractive,
		swipeFn,
		carouselMaximize,
		contentZoom,
		addToContentZoom,
		zoomScale,
		itemsSpan,
		endlessScroll,
		enableArrows,
		enableChevrons,
		disableSideChevrons,
		tuneChevrons,
		chevronsStyle,
		progress,
		wrapBottomTitle,
		bottomPanelHeight,
		buttonsColor,
		iconSize,
		heightByContent,
		subs
	)
}

MCarouselProgress(manager : MaterialManager, parent : MFocusGroup, content : Tropic, state : MCarouselState) -> Tropic {
	eitherMap(
		state.progress, \pr ->
		TCopySize(
			content,
			\tr ->
				generate(0, state.contentsLength, \i ->
					TIf(
						feq(state.activeI, i),
						TShadowShape(parent, const(4.), TCircle(4., [MThemeFill(parent, pr.activeColor)]), RoundShape())
						|> (\f -> TAlpha(fif(state.enabled, const(1.), const(0.5)), f)),

						TShadowShape(parent, const(2.), TCircle(4., [MThemeFill(parent, pr.inactiveColor)]), RoundShape())
						|> (\f ->
							TIf(state.enabled,
								mouseDownAround(manager, None(), make(false), make(false), \ -> next(state.activeI, i), f)
								|> (\f2 -> TCursor(FingerCursor(), f2)),
								TAlpha(const(0.5), f)
							)
						)
					)
					|> (\f -> TBorder(pr.spacing, 16., pr.spacing, 16., f))
				)
				|> TCols
				|> (\f -> if (pr.bottom) TLines2(TFillY(), f) else f)
				|> (\f -> TCenterXIn(f, tr)),
			true
		),
		content
	)
}

MCarouselChevrons(manager : MaterialManager, parent : MFocusGroup, content : Tropic, state : MCarouselState, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	leftFocused = make(false);
	rightFocused = make(false);

	makeChevron = \isLeft -> {
		focused = make(false);
		onClick = if (isLeft) state.goToPrev else state.goToNext;
		selfFocused = if (isLeft) leftFocused else rightFocused;
		otherFocused = if (isLeft) rightFocused else leftFocused;

		buttonEnabled = if (state.disableSideChevrons) {if (isLeft) state.hasPrev else state.hasNext} else const(true);

		MIconButton2T(
			manager,
			parent,
			MIconButton(
				if (isLeft) "chevron_start" else "chevron_end",
				onClick,
				concatA([
					[state.buttonsColor, MIconSize(state.iconSize), MIconButtonBorder(8.)],
					eitherMap(state.chevronsStyle, \cs -> cs.style, []),
					eitherMap(state.tuneChevrons, \ts -> if (ts.shadows) [MElevation(const(1.))] else [], [])
				]),
				[MEnabled(fand(state.enabled, buttonEnabled))]
				|> (\f ->
					eitherMap(state.enableArrows, \ea ->
						concat(f, [
							MFocused(selfFocused),
							MShortcutFilter(\foc, k -> {
								filtered = k.keycode == (if (isLeft) 37 else 39) && (ea.ignoreFocus || foc || fgetValue(otherFocused));
								if (filtered) onClick();
								filtered
							})
						]),
						f
					)
				)
			),
			m2t
		)
		|> (\f -> TBorder4(8., f))
		|> (\t -> TCenterYIn(t, eitherMap(state.tuneChevrons, \ts -> TFixed(0.0, ts.positionY * 2.), TFillY())))
	}

	TCols([
		makeChevron(true),
		TFillX(),
		makeChevron(false)
	])
	|> (\f ->
		TCopySize(
			content,
			\tr ->
				TAvailable(f, tr),
			true
		)
	)
}

MCarouselContent2T(manager : MaterialManager, parent : MFocusGroup, c : MCarouselContent, i : int, style : [MCarouselStyle], state : MCarouselState,
	m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	textColor = extractStruct(style, MCarouselTextColor(MWhite())).color;
	contentScaling = !contains(style, MCarouselNoContentScaling());
	bgStyle = extractStruct(style, MBackgroundStyle([MFill(MBackgroundMColor(!getLightBackground(parent)))])).style;
	contentSideOffset = extractStruct(style, MCarouselSidesOffset(0.)).sideOffset;

	makeTitle = \text, st : [MTextStyle] -> {
		if (text != "") {
			MEllipsisText2T(manager, parent, MEllipsisText(text, arrayPush(st, MShowTooltip(const(true)))), m2t)
		} else {
			TEmpty();
		}
	};

	(\p ->
		(\p2 ->
			m2t(c.content, p2)
			|> (\t ->
				if (contentScaling) // Crop content to the size of the box
					TTweak([TFillZoom(true)], t, TSizedWidthHeight(state.itemWidth, state.height))
				else
					TCropWidth2(t, state.itemWidth)
			)
		)
		|> (\f : (MFocusGroup) -> Tropic ->
			if (isSome(state.contentZoom) ||
				eitherMap(state.maximize, \mx -> containsStruct(mx.style, contentZoomDef), false)) {

				currentInteractive = make(None());
				xOfBounds = make(0.);
				disableOutOfBounds = make(false);
				zoomScale = make(1.);
				uns = initUnsM();

				TConstruct(
					[
						make2Subscribe2(currentInteractive, state.swipeInteractive, \ci, si -> {
							if (isSome(ci) && isSome(si)) {
								next(disableOutOfBounds, false);
								nextDistinct(state.swipeInteractive, None());
								nextDistinct(currentInteractive, Some(false));
							} else if (isNone(ci) && isNone(si)) {
								dispUnsM(uns);
							}
						}),
						make2Subscribe(currentInteractive, xOfBounds, \ci, xob -> {
							if (xob != 0. && isSome(ci)) {
								next(disableOutOfBounds, true);
								nextDistinct(currentInteractive, None());
								nextDistinct(state.swipeInteractive, Some(false));

								prevTx = getValue(state.tx);
								dispUnsM(uns);
								setUnsM(
									uns,
									makeSubscribe2(state.tx, \tx -> {
										if (if (xob > 0.) tx > prevTx + 1. else tx < prevTx - 1.) {
											next(disableOutOfBounds, false);
											nextDistinct(state.swipeInteractive, None());
											nextDistinct(currentInteractive, Some(false));
										}
									})()
								);
							}
						}),
						make2Subscribe(state.activeI, state.zoomScale, \activeI, zs ->
							nextDistinct(zoomScale, if (activeI == i) zs else 1.)
						),
						\ -> \ -> dispUnsM(uns)
					],
					m2t(
						MZoomMaterial(
							MGetFocusGroup(f),
							zoomScale,
							[
								MCurrentInteractive(currentInteractive),
								MScrollOutOfBounds(xOfBounds, make(0.)),
								MScrollDisableOutOfBounds(disableOutOfBounds),
								MEnabled(state.enabled)
							]
						),
						p
					)
				)
			} else
				f(p)
		)
		|> (\f -> {
				txt = extractStruct(c.style, MCarouselText("", ""));
				textStyle = extractStruct(c.style, MCarouselTextStyle([MSubheading(), textColor], [MCaption(), textColor]));
				footerColor = extractStruct(style, MCarouselFooterColor(MGrey(900))).color;
				footerOpacity = extractStruct(style, MCarouselFooterOpacity(0.8)).opacity;
				TCopySize(
					f,
					\tr -> {
						TLines2(
							makeTitle(txt.title, textStyle.title),
							makeTitle(txt.subtitle, textStyle.subtitle)
						)
						|> state.wrapBottomTitle
						|> (\f2 -> TCenterYIn(f2, TSizedHeight(0., state.bottomPanelHeight)))
						|> (\f2 -> TBorder4(16., f2))
						|> (\f2 -> TGroup2(TFillX(), f2))
						|> (\f2 -> TFrame(0., 0., [MThemeFill(parent, footerColor), FillOpacity(footerOpacity)], f2))
						|> (\f2 -> TAvailableWidth(f2, state.itemWidth))
						|> (\f2 ->
							eitherMap(
								state.maximize,
								\mx ->
									if (contains(mx.style, MShowTextOverlay()))
										f2
									else
										TShow(fnot(mx.maximized), f2),
								f2
							)
						)
						|> (\f2 -> TVisible(fgreater(state.bottomPanelHeight, zero), f2))
						|> (\f2 -> TTweak([TAlign(const(0.), const(1.))], f2, tr))
					},
					true
				)
			}
		)
	)
	|> (\f -> { // Add OnClick
			onClickM = tryExtractStruct(c.style, MOnClick(nop));
			selectionM = tryExtractStruct(style, MSingleSelection(make(-1)));
			if (isSome(onClickM) || isSome(selectionM)) {
				clickFn = \ -> {
					maybeMap(selectionM, \selection ->
						nextDistinct(selection.selected, if (getValue(selection.selected) == i) -1 else i)
					);
					maybeApply(onClickM, \oc -> oc.click())
				}
				TIf(
					state.enabled,
					MComponent2T(
						manager,
						parent,
						"MCarouselClickable",
						[MOnClick(clickFn), MFocusEnabled(const(false)), MAddFocusGroup(true), MPassClicks()],
						\focus -> f(focus.parent),
						m2t
					),
					f(parent)
				)
			} else {
				f(parent)
			}
		}
	)
	|> (\f -> TFrame(0., 0., bgStyle, f)) // Add background
	|> (\f ->
		if (state.heightByContent) {
			TAvailableWidth(f, state.itemWidth);
		} else {
			TAvailableWidthHeight(f, state.itemWidth, state.height);
		}
	)
	// |> (\f ->
	// 	TInteractive(
	// 		[
	// 			FineGrainMouseWheel(\mi -> addToContentZoom(mi.dy))
	// 		],
	// 		f
	// 	)
	// )
	|> (\f ->
			TTranslate(
				if (state.endlessScroll)
					fselect2(state.tx, state.itemWidth, FLift2(\tx0, wd ->
						if (wd > 0.) {
							len = i2d(state.contentsLength);
							fullWidth = wd * len;
							currentIndex = dceil((-tx0 - contentSideOffset) / wd);
							currentWidth = currentIndex * wd;
							currentPage = (currentIndex + len / 2. - i2d(i)) / len;
							currentTranslate =
								dfloor(if (currentPage < 0.) currentPage - 1. else currentPage) * fullWidth 
								+ i2d(i) * wd
								+ contentSideOffset;

							Point(currentTranslate, 0.)
						} else
							zeroPoint
					))
				else
					fselect(state.itemWidth, FLift(\wd -> Point(i2d(i) * wd + contentSideOffset, 0.))),
				f
			)
	)
}

MCarouselZoomButton(
	manager : MaterialManager,
	parent : MFocusGroup,
	m2t : (Material, MFocusGroup) -> Tropic,
	state : MCarouselState,
	name : string,
	visible : Transform<bool>,
	onClick : () -> void) -> Tropic {

	MIconButton2T(
		manager,
		parent,
		MIconButton(
			name,
			onClick,
			[state.buttonsColor, MIconButtonBorder(8.), MElevation(const(2.))],
			[MEnabled(state.enabled)]
		),
		m2t
	)
	|> (\f2 -> TShow(visible, f2))
	|> (\f2 -> TBorder(0., 8., 8., 8., f2))
}