import material/measing;
import tropic/tropic_constant;
import material/internal/material_theme;
import material/internal/material_debug;
import text/serialize;

export {
	// Increases the size of this tropic to be a 8x8 multiple
	MGridSize(t : Tropic) -> Tropic;

	// Increases the size of this tropic to be a 8x8 multiple, and centers it
	MGridCenterX(t : Tropic) -> Tropic;

	TKeyListener(keys : [string], fnPressed : () -> void, fnReleased : () -> void, box : Tropic) -> Tropic;

	enterOrSpace = [" ", "enter"];

	// Temporary replacement of MGrid. Builds table with fixed size cells. Aligns data to the left and centers vertically
	FixedTableAlignLeft(lines : [[Material]], widths : [double], height : double) -> Material;
	FixedTableAlignBaselineLeft(lines : [[Material]], widths : [double], height : double) -> Material;
	FixedTableAlignBaselineA(lines : [[Material]], widths : [double], height : double) -> Material;
	FixedTableCenterAlignBaselineA(lines : [[Material]], widths : [double], height : double) -> Material;

	getMaterialScrollBar() -> TScrollbars;

	getMaterialScrollBarWithShade(shade : int) -> TScrollbars;

	concatStyles(a : [?], b : [?]) -> [?];
	addStyleIfAbsent(a : [?], st : ?) -> [?];

	mDisabledStyle(lightBackground : bool) -> [GraphicsStyle] {
		[Fill(if (lightBackground) 0x000000 else 0xffffff), FillOpacity(if (lightBackground) 0.26 else 0.3)]
	};

	distributeSizesEvenlyInt(minWidths : [int], maxWidths : [int], columnWidths : [DynamicBehaviour<int>], availableWidth : int) -> void;
	distributeSizesEvenly(minWidths : [double], maxWidths : [double], availableWidth : double) -> [double];

	selectableCells(cells : Transform<[[?]]>, currentCell : DynamicBehaviour<?>, isEmptyCellFn : (cell : ?) -> bool) -> Pair<(MDirection) -> void, () -> void>;
		MDirection ::= MRight, MLeft, MUp, MDown;
			MUp();
			MDown();

	makeTropicWidthHeightStatic(t : Tropic) -> Tropic;
	makeTropicWidthStatic(t : Tropic) -> Tropic;
	makeTropicHeightStatic(t : Tropic) -> Tropic;

	MSeparator2T(manager : MaterialManager, parent : MFocusGroup, horizontal : bool) -> Tropic;
	MZoomMaterial(mat : Material, scale : Transform<double>, scrollStyle : [MScrollStyle]) -> Material;

	fn2IScriptBehaviourDisposable(fn : () -> void, name : string, type : IScriptRecordType) -> IScriptBehaviourDisposable;
	fn2IScriptBehaviourDisposableAsync(fn : (() -> void) -> void, name : string, type : IScriptRecordType) -> IScriptBehaviourDisposableAsync;
	fn2IScriptBehaviourDisposable1(fn : (flow) -> void, name : string, type : IScriptRecordType) -> IScriptBehaviourDisposable1;

		IScriptBehaviourDisposable(fn : () -> void, monitorBehaviour : IScriptBehaviour, subscribers : () -> () -> void);
		IScriptBehaviourDisposableAsync(fn : (() -> void) -> void, monitorBehaviour : IScriptBehaviour, subscribers : () -> () -> void);
		IScriptBehaviourDisposable1(fn : (flow) -> void, monitorBehaviour : IScriptBehaviour, subscribers : () -> () -> void);

	interruptibleTimerChain(chain : List<Pair<double, ((bool) -> void) -> void>>, onFinished : (bool) -> void) -> () -> void;

	setDownInsideFocus(manager : MaterialManager, focus : MaterialFocus) -> void;
	setDownInsideFocusTimer(manager : MaterialManager, focus : MaterialFocus, uns : ref Maybe<() -> void>) -> void;

	getTypeString(value : flow) -> string;

	MMonitorMousePosition(manager : MaterialManager, position : DynamicBehaviour<Point>, content : Tropic) -> () -> () -> void;

	applyIfDynamic(beh : Transform<?>, fn : () -> ?) -> void;

	updateMaterialTheme(manager : MaterialManager, theme : MaterialTheme) -> void;
	setManagerWhite(manager : MaterialManager, white : bool) -> void;
	switchManagerWhite(manager : MaterialManager) -> void;

	// Use it if TSelect doesn`t affect metrics
	TSelectConstantMetrics(value : Transform<?>, fn : (?) -> Tropic) -> Tropic;

	// Helper for background drawing
	addTBackground(color : MColor) -> (Tropic) -> Tropic;
	addMBackground(color : MThemeColor) -> (Material) -> Material;

	addMTooltip(tooltip : string) -> (Material) -> Material;
	addMTooltipT(tooltip : Transform<string>) -> (Material) -> Material;

	getStrokeGraphicsStyle(stroke : MColor, opacity : double, width : double) -> [TGraphicsStyle];

	TAnimatedExpander(
		manager : MaterialManager,
		parent : MFocusGroup,
		content : Tropic,
		expanded : Transform<bool>,
		style : [MAnimatedExpanderStyle]) -> Tropic;

	TCollapsingBox(
		manager : MaterialManager,
		parent : MFocusGroup,
		content : Tropic,
		yTrans : Transform<double>,
		expand : Transform<bool>,
		trigger : Maybe<Transform<bool>>,
		heightInspect : Maybe<DynamicBehaviour<double>>,
		absoluteOffsetMode : bool,
		fade : bool) -> Tropic;

	TAnimator(
		manager : MaterialManager,
		parent : MFocusGroup,
		t : Tropic,
		trigger : Transform<bool>,
		style : [MAnimatorStyle]) -> Tropic;

	MLetDisplay(name : string, value : Material, scope : Tropic, above : bool) -> Material;

	flerpPoint(point: Transform<Point>, progress : Transform<double>) -> Transform<Point>;
	colorLerp(previous : int, current : int, percent : double) -> int;
	mColorLerp(previous : MColor, current : MColor, percent : double) -> MColor;

	// Helper to navigate list like in radio-group
	MArrowNavigationArray(
		itemsMaterialFunctions : [([MButtonState]) -> Material]
	) -> [Material];

	MArrowNavigationItem(
		i : int,
		listLen : int,
		makeListItem : ([MButtonState]) -> Material,
		focusedIdx : DynamicBehaviour<int>,
		focusEnabledIdx : DynamicBehaviour<int>,
		getDefFocusableItem : () -> int,
		style : [MArrowNavigationStyle]
	) -> Material;

	MArrowNavigationExt(
		i : int,
		listLen : int,
		makeListItem : ([MButtonState]) -> Material,
		focusedIdx : DynamicBehaviour<int>,
		focusEnabledIdx : DynamicBehaviour<int>,
		getDefFocusableItem : () -> int,
		style : [MArrowNavigationStyle]
	) -> Triple<Material, DynamicBehaviour<bool>, Transform<bool>>;

	MArrowNavigationStyle ::= MArrowNavigationDirection, MArrowNavigationSupportHomeEnd, MArrowNavigationCheckFocusability,
							MArrowNavigationSelectItem, MArrowNavigationResetSelection, MArrowNavigationCustomOnArrow;
		MArrowNavigationDirection(vertical : bool); // true by default
		MArrowNavigationSupportHomeEnd(support : bool); // true by default
		MArrowNavigationCheckFocusability(fn : (int) -> bool);
		MArrowNavigationSelectItem(fn : () -> void, auto : bool);
		MArrowNavigationResetSelection(fn : () -> bool);
		MArrowNavigationCustomOnArrow(fn : (forw : bool) -> void, enabled : () -> bool);

	isMDomRender(style : [flow]) -> bool;
	setMDomRenderEnabled(enabled : bool) -> void;
}

MGridSize(t : Tropic) -> Tropic {
	TTweak([TSnapSize(8.0, 8.0, true), TAlign(const(0.5), const(0.5))], t, TEmpty())
}

MGridCenterX(t : Tropic) -> Tropic {
	TTweak([TSnapSize(8.0, 0.0, true), TAlign(const(0.5), zero)], t, TEmpty())
}

TKeyListener(keys : [string], fnPressed : () -> void, fnReleased : () -> void, box : Tropic) -> Tropic{
	tKeys = makeTKey(\k -> contains(keys, k.utf));

	TConstruct([makeSubscribe2(tKeys.pressed, \k -> if (k != noKey) fnPressed() else fnReleased())],
		TInteractive([tKeys], box)
	);
}

FixedTableAlignLeft(lines : [[Material]], widths : [double], height : double) -> Material {
	MLines(
		map(lines, \columns -> {
			MCols(
				mapi(columns, \i, cell -> {
					MAvailable(
						MGroup2(
							TFillX(),
							MCenterYIn(cell, TFixed(0., height))
						),
						TFixed(widths[i], height)
					)
				})
			)
		})
	)
}

FixedTableAlignBaselineLeft(lines : [[Material]], widths : [double], height : double) -> Material {
	MLines(
		map(lines, \columns -> {
			MBaselineCols(
				mapi(columns, \i, cell -> {
					MAvailable(
						MCols2(
							cell,
							TFillX()
						),
						TFixed(widths[i], height)
					)
				})
			)
		})
	)
}

FixedTableAlignBaselineA(lines : [[Material]], widths : [double], height : double) -> Material {
	MLinesA(
		map(lines, \columns -> {
			MBaselineColsA(
				mapi(columns, \i, cell -> {
					MAvailable(
						MAlignStartFill(cell),
						TFixed(widths[i], height)
					)
				})
			)
		})
	)
}

FixedTableCenterAlignBaselineA(lines : [[Material]], widths : [double], height : double) -> Material {
	MLinesA(
		map(lines, \columns -> {
			MBaselineColsA(
				mapi(columns, \i, cell -> {
					MAvailable(
						MCenter(cell),
						TFixed(widths[i], height)
					)
				})
			)
		})
	)
}

getMaterialScrollBarWithShade(shade : int) -> TScrollbars {
	TScrollbarStyle(materialScrollBar with bar =
		\ins, dwn ->
			TBorder4(
				if (mobile) 2. else 4.,
				TAlpha(fif(dwn, const(1.0), fif(ins, const(0.9), const(0.8))), TRoundedFull([MFill(MGrey(shade)), Stroke(white), StrokeOpacity(0.3)], TFillXY()))
			)
	)
	|> (\f -> TScrollbars(f, f))
}

getMaterialScrollBar() -> TScrollbars {
	getMaterialScrollBarWithShade(500);
}

concatStyles(a : [?], b : [?]) -> [?] {
	map(b, \v -> extractStruct(a, v))
	|> (\f -> replaceStructMany(a, f))
}

addStyleIfAbsent(a : [?], st : ?) -> [?] {
	eitherMap(
		tryExtractStruct(a, st),
		\__ -> a,
		arrayPush(a, st)
	)
}

distributeSizesEvenlyInt(minWidths : [int], maxWidths : [int], columnWidths0 : [DynamicBehaviour<int>], availableWidth : int) -> void {
	columnsCount = either(minA([length(minWidths), length(maxWidths), length(columnWidths0)]), 0);

	additionThreshold = 0;

	if (columnsCount > 0) {
		columnWidths = generate(0, columnsCount, \i -> max(columnWidths0[i] |> getValue, minWidths[i]));
		sumWidth = sum(columnWidths);

		if (sumWidth < availableWidth - additionThreshold || sumWidth > availableWidth + additionThreshold) {
			available = availableWidth - sumWidth;
			addition = available / columnsCount;

			availableForAddition =
				mapi(subrange(maxWidths, 0, columnsCount), \i, mw -> ref
					(if (sumWidth < availableWidth)
						(mw > minWidths[i] && columnWidths[i] < mw) || (mw < minWidths[i])
					else
						columnWidths[i] > minWidths[i])
				);

			newWidths =
				mapi(subrange(columnWidths, 0, columnsCount), \i, cw -> ref
					if (^(availableForAddition[i])) {
						nw = cw + addition;

						if (nw < minWidths[i]) {
							availableForAddition[i] := false;
							minWidths[i];
						} else if (maxWidths[i] >= minWidths[i] && nw > maxWidths[i]) {
							availableForAddition[i] := false;
							maxWidths[i];
						} else {
							nw
						}
					} else {
						if (minWidths[i] > 0) {
							if (minWidths[i] <= maxWidths[i])
								min(max(cw, minWidths[i]), maxWidths[i])
							else
								max(cw, minWidths[i])
						} else
							cw
					}
				);

			newAddition =
				updateWhile(
					countA(availableForAddition, \afa -> ^afa)
					|> (\afa ->
						if (afa > 0)
							floor(i2d(availableWidth - columnsCount - fold(newWidths, 0, \acc, nw -> acc + ^nw)) / i2d(afa))
						else
							0
					),
					\newAddition ->
						if ((newAddition > additionThreshold || newAddition < additionThreshold) &&
							foldi(availableForAddition, false, \i, acc, afa -> {
								if (^afa) {
									nw =
										max(minWidths[i], ^(newWidths[i]) + newAddition)
										|> (\f -> if (minWidths[i] < maxWidths[i]) min(f, maxWidths[i]) else f);
									b = ^(newWidths[i]) != nw;
									newWidths[i] := nw;
									acc || b
								} else {
									acc
								}
							}))
							Some((availableWidth - fold(newWidths, 0, \acc, nw -> acc + ^nw)) / countA(availableForAddition, \afa -> ^afa))
						else
							None()
				);

			iteri(newWidths, \i, nw -> nextDistinct(columnWidths0[i], ^nw))
		} else {
			fori(0, columnsCount - 1, \i -> nextDistinct(columnWidths0[i], columnWidths[i]))
		}
	}
}

distributeSizesEvenly(
	minWidths : [double],
	maxWidths : [double], // -1. for no limits
	availableWidth : double) -> [double] {

	currentSum = dsum(minWidths);

	newWidths =
		mapi(minWidths, \i, w -> {
			newWidth = w / currentSum * availableWidth;
			if (maxWidths[i] != -1. && newWidth > maxWidths[i])
				Pair(maxWidths[i], true)
			else if (newWidth < minWidths[i])
				Pair(minWidths[i], false)
			else
				Pair(newWidth, false)
		});

	exceedLimits = map(newWidths, secondOfPair);

	if (any(exceedLimits) && !(all(exceedLimits))) {
		filterFixed = \arr -> mapi(arr, \i, item -> if (newWidths[i].second) 0. else item);
		newAvailableWidth = availableWidth - fold(newWidths, 0., \acc, item -> acc + if (item.second) item.first else 0.);

		innerNewSizes =
			distributeSizesEvenly(
				filterFixed(minWidths),
				filterFixed(maxWidths),
				newAvailableWidth
			);

		mapi(newWidths, \i, item -> if (item.second) item.first else innerNewSizes[i])
	} else
		map(newWidths, firstOfPair);
}

nextCellPosition(
	cells : [[?]],
	initialPosition : Pair<int, int>,
	isEmptyCellFn : (cell : ?) -> bool,
	positionMoveFn : (Pair<int, int>) -> Pair<int, int>
) -> Pair<int, int> {
	movedPosition = positionMoveFn(initialPosition);

	eitherFn(
		elementAtM(cells[movedPosition.first], movedPosition.second),
		\cell -> {
			if (isEmptyCellFn(cell)) {
				nextCellPosition(cells, movedPosition, isEmptyCellFn, positionMoveFn)
			} else {
				movedPosition
			}
		},
		\ -> nextCellPosition(cells, movedPosition, isEmptyCellFn, positionMoveFn)
	)
}

selectableCells(
	cells : Transform<[[?]]>,
	currentCell : DynamicBehaviour<?>,
	isEmptyCellFn : (cell : ?) -> bool
) -> Pair<(MDirection) -> void, () -> void> {
	x = ref -1;
	y = ref -1;

	fn0 = \dir : MDirection, cls : [[?]] -> {
		initialPosition = Pair(^x, ^y);
		calculateNextPosition =\moveFn -> nextCellPosition(cls, initialPosition, isEmptyCellFn, moveFn);

		newPosition = switch (dir) {
			MLeft(): calculateNextPosition(\p -> {
				cx = p.first - 1;
				Pair(if (cx < 0) {length(cls) - 1} else {cx}, p.second)
			});
			MRight(): calculateNextPosition(\p -> {
				cx = p.first + 1;
				Pair(if (cx > length(cls) - 1) {0} else {cx}, p.second)
			});
			MUp(): calculateNextPosition(\p -> {
				cx = p.first;
				cy = p.second - 1;
				Pair(cx, if (cy < 0) {length(cls[cx]) - 1} else {cy})
			});
			MDown(): calculateNextPosition(\p -> {
				cx = p.first;
				cy = p.second + 1;
				Pair(cx, if (cy > length(cls[cx]) - 1) {0} else {cy})
			});
		}

		x := newPosition.first;
		y := newPosition.second;
	};

	fn = \dir : MDirection -> {
		cls = fgetValue(cells);

		if (length(cls) > 0 && length(cls[0]) > 0) {
			fn0(dir, cls);

			while(\ -> !existsIndex(cls, ^x) || !existsIndex(cls[^x], ^y), \ -> fn0(dir, cls));

			nextDistinct(currentCell, cls[^x][^y]);
		}
	}

	uns =
		makeSubscribe(fpair(cells, currentCell), \cls : Pair<[[?]], ?>-> {
			x := either(findi(cls.first, \c -> contains(c, cls.second)), -1);
			y := if (^x >= 0) either(findi(cls.first[^x], \c -> c == cls.second), 0) else -1;
		})();

	Pair(fn, uns)
}

makeTropicWidthHeightStatic(t : Tropic) -> Tropic {
	TransformTAcc2(\t2a -> {
		bf = t |> t2a;

		TAcc(
			bf.form,
			TFormMetrics(
				bf.metrics.width,
				bf.metrics.height,
				bf.metrics.height
			),
			bf.metrics.width,
			bf.metrics.height,
			bf.metrics.width,
			bf.metrics.height,
			zero,
			zero,
			const(true),
			bf.xConstant,
			bf.yConstant,
			bf.disposers
		)
	})
}

makeTropicWidthStatic(t : Tropic) -> Tropic {
	TransformTAcc2(\t2a -> {
		bf = t |> t2a;

		TAcc(
			bf.form,
			TFormMetrics(
				bf.metrics.width,
				bf.metrics.height,
				bf.metrics.height
			),
			bf.metrics.width,
			bf.minHeight,
			bf.metrics.width,
			bf.maxHeight,
			zero,
			bf.yFillers,
			const(true),
			bf.xConstant,
			bf.yConstant,
			bf.disposers
		)
	})
}

makeTropicHeightStatic(t : Tropic) -> Tropic {
	TransformTAcc2(\t2a -> {
		bf = t |> t2a;

		TAcc(
			bf.form,
			TFormMetrics(
				bf.metrics.width,
				bf.metrics.height,
				bf.metrics.height
			),
			bf.minWidth,
			bf.metrics.height,
			bf.maxWidth,
			bf.metrics.height,
			bf.xFillers,
			zero,
			const(true),
			bf.xConstant,
			bf.yConstant,
			bf.disposers
		)
	})
}

MSeparator2T(manager : MaterialManager, parent : MFocusGroup, horizontal : bool) -> Tropic {
	(if (horizontal) TFillXH(1.) else TFillWY(1.))
	|> (\f -> TRectangle([MFill(getSeparatorColor(parent)), FillOpacity(0.12)], f))
}

MZoomMaterial(mat : Material, scale : Transform<double>, scrollStyle : [MScrollStyle]) -> Material {
	box : TScrollWidthHeight = extractStruct(scrollStyle, TScrollWidthHeight(makeWH(), makeWH()));
	scrollPosition : TScrollPosition = extractStruct(scrollStyle, TScrollPosition(make(zeroPoint)));
	prevSc = ref fgetValue(scale);

	MScroll(
		MZoom(mat, TScale(ffactor2(scale), TSized(box.box)), true)
		|> MCenter,
		TFillXY(),
		replaceStructMany(scrollStyle, [box, scrollPosition])
	)
	|> (\f ->
		MConstruct(
			[
				make2Subscribe(scale, box.box, \sc, bx -> {
					if (sc > 1.) {
						sp = getValue(scrollPosition.position);

						nextDistinct(
							scrollPosition.position,
							Point(
								max((sp.x + bx.width / 2.) * sc / ^prevSc - bx.width / 2., 0.),
								max((sp.y + bx.height / 2.) * sc / ^prevSc - bx.height / 2., 0.)
							)
						);
					}

					prevSc := sc;
				})
			],
			f
		)
	)
}

fn2IScriptBehaviourDisposable(fn : () -> void, name : string, type : IScriptRecordType) -> IScriptBehaviourDisposable {
	beh = make(false);
	runFn = ref true;

	IScriptBehaviourDisposable(
		\ -> {
			runFn := false;
			next(beh, true);
			fn();
			next(beh, false);
			runFn := true;
		},
		IScriptBehaviour(name, beh, type),
		\ -> subscribe2(beh, \b -> if (b && ^runFn) fn())
	)
}

fn2IScriptBehaviourDisposableAsync(fn : (() -> void) -> void, name : string, type : IScriptRecordType) -> IScriptBehaviourDisposableAsync {
	beh = make(false);
	runFn = ref true;

	IScriptBehaviourDisposableAsync(
		\asyncFn -> {
			runFn := false;
			next(beh, true);
			fn(asyncFn);
			next(beh, false);
			runFn := true;
		},
		IScriptBehaviour(name, beh, type),
		\ -> subscribe2(beh, \b -> if (b && ^runFn) fn(nop))
	)
}

fn2IScriptBehaviourDisposable1(fn : (flow) -> void, name : string, type : IScriptRecordType) -> IScriptBehaviourDisposable1 {
	beh : DynamicBehaviour<Maybe<flow>> = make(None());
	runFn = ref true;

	IScriptBehaviourDisposable1(
		\v -> {
			runFn := false;
			next(beh, Some(v));
			fn(v);
			runFn := true;
		},
		IScriptBehaviour(name, beh, type),
		\ -> subscribe2(beh, \b -> if (^runFn) maybeApply(b, fn))
	)
}

interruptibleTimerChain(chain : List<Pair<double, ((bool) -> void) -> void>>, onFinished : (bool) -> void) -> () -> void {
	interruptibleTimerChain2(0., chain, onFinished);
}

interruptibleTimerChain2(prevDelay : double, chain : List<Pair<double, ((bool) -> void) -> void>>, onFinished : (bool) -> void) -> () -> void {
	uns = initUnsM();

	if (countList(chain) > 0) {
		c = headList(chain, Pair(0., nop1));

		setUnsM(
			uns,
			interruptibleTimer(ceil(c.first - prevDelay), \ -> c.second(\b -> {
				if (b) {
					setUnsM(
						uns,
						interruptibleTimerChain2(c.first, tailList(chain), onFinished)
					);
				} else {
					onFinished(false);
				}
			}))
		);
	} else {
		onFinished(true);
	}

	\ -> dispUnsM(uns);
}

setDownInsideFocus(manager : MaterialManager, focus : MaterialFocus) -> void {
	manager.downInsideFocus ::= Some(focus);
	deferred(\ -> manager.downInsideFocus ::= None());
}

setDownInsideFocusTimer(manager : MaterialManager, focus : MaterialFocus, uns : ref Maybe<() -> void>) -> void {
	dispUnsM(uns);

	manager.downInsideFocus ::= Some(focus);
	// Timeout to keep focus inside autocomplete popup
	setUnsM(
		uns,
		interruptibleTimer(120, \ -> {
			manager.downInsideFocus ::= None();
			dispUnsM(uns)
		})
	);
}

getTypeString(value : flow) -> string {
	sv : string = serialize(value);
	ts : string = toStringNoExtraQuotes(value);

	if (sv != ts)
		"string"
	else if ((startsWith(sv, "-") && isDigits(substring(sv, 1, strlen(sv)))) || isDigits(sv))
		"int"
	else if (isDouble(sv) || sv == "-nan(ind).0" || sv == "nan.0" ||
		sv == "-inf.0" || sv == "inf.0" || value != value)
		"double"
	else if (sv == "false" || sv == "true")
		"bool"
	else if (isArray(value))
		"array"
	else
		value.structname
}

MMonitorMousePosition(manager : MaterialManager, position : DynamicBehaviour<Point>, content : Tropic) -> () -> () -> void {
	\ -> {
		density = extractStruct(manager.manager.renderStyle, TDpif(screenDensity)).dpif;
		stage = getStage();
		aZoom = if (js) getAccessibilityZoom() * getValue(accessibilityZoom) else getValue(accessibilityZoom);
		nextDistinct(position, Point(getMouseX(stage) / density / aZoom, getMouseY(stage) / density / aZoom));

		trender(
			TInteractive(
				[
					MouseMove2(\h, mi0 -> {
						mi = mi0();
						nextDistinct(position, Point(mi.x, mi.y));
						h;
					})
				],
				TRectangle(interactiveRectangleStyle, TFillXY())
			)
			|> (\tr ->
				if (content == TEmpty())
					tr
				else
					TGroup2WithoutMetrics(tr, content)
			),
			manager.manager.renderStyle
		)
	}
}

applyIfDynamic(beh : Transform<?>, fn : () -> ?) -> void {
	switch (beh : Transform<?>) {
		DynamicBehaviour(val, __): next(beh, fn());
		default: {};
	}
}

updateMaterialTheme(manager : MaterialManager, theme : MaterialTheme) -> void {
	if (manager.theme != theme) {
		manager.theme ::= theme;
		(manager.focus).theme ::= theme;
		switch (manager.white) {
			DynamicBehaviour(__, __): {
				nextDistinct(cast(manager.white : Transform<bool> -> DynamicBehaviour<bool>), theme.palette.light);
			}
			default : {}
		}
		prevPebblesEnabled = getValue(manager.pebblesEnabled);
		nextDistinct(manager.pebblesEnabled, false);
		reverseBehaviour(manager.forceRender);
		nextDistinct(manager.pebblesEnabled, prevPebblesEnabled);
	}
}

setManagerWhite(manager : MaterialManager, white : bool) -> void {
	updateMaterialTheme(
		manager,
		MaterialTheme(manager.theme with palette = MaterialPalette(manager.theme.palette with light = white))
	);
}

switchManagerWhite(manager : MaterialManager) -> void {
	setManagerWhite(manager, !manager.theme.palette.light);
}

TSelectConstantMetrics(value : Transform<?>, fn : (?) -> Tropic) -> Tropic {
	TFSelect(value, FLift(fn))
	|> makeTropicConstant(None(), None())
}

addTBackground(color : MColor) -> (Tropic) -> Tropic {
	\t -> TFrame(0.0, 0.0, [MFill(color)], t)
}

addMBackground(color : MThemeColor) -> (Material) -> Material {
	\m -> MFrame(0.0, 0.0, [MThemedFill(color)], m)
}

addMTooltip(tooltip : string) -> (Material) -> Material {
	\m ->
		if (tooltip == "") m
		else 
			switch (m) {
				MIconButton(icon, onClick, style, state) :
					MIconButton(icon, onClick, style, concat(state, [MTooltipText(const(tooltip)), MButtonTitle(const(tooltip))]));
				MTextButton(title, onClick, style, state) :
					MTextButton(title, onClick, style, concat(state, [MTooltipText(const(tooltip)), MButtonTitle(const(tooltip))]));
				default : MTooltip(m, MText(tooltip, []), []);
			}
}

addMTooltipT(tooltip : Transform<string>) -> (Material) -> Material {
	\m ->
		if (tooltip == const("")) m
		else
			switch (m) {
				MIconButton(icon, onClick, style, state) :
					MIconButton(icon, onClick, style, concat(state, [MTooltipText(tooltip), MButtonTitle(tooltip)]));
				MTextButton(title, onClick, style, state) :
					MTextButton(title, onClick, style, concat(state, [MTooltipText(tooltip), MButtonTitle(tooltip)]));
				default : MTooltip(m, MSelect(tooltip, \tl -> MText(tl, [])), []);
			}
}

getStrokeGraphicsStyle(stroke : MColor, opacity : double, width : double) -> [TGraphicsStyle] {
	if (!equalDoubles(width, 0.0)) [MStroke(stroke), StrokeOpacity(opacity), StrokeWidth(width)] else [];
};

TAnimatedExpander(
	manager : MaterialManager,
	parent : MFocusGroup,
	content : Tropic,
	expanded : Transform<bool>,
	style : [MAnimatedExpanderStyle]) -> Tropic {

	if (isUrlParameterTrue("new")) {
		vertical = !contains(style, MExpanderHorizontal());
		expandFromStart = contains(style, MExpandFromStart());
		duration = tryExtractStruct(style, MDuration(const(-1.)));
		progress = tryExtractStruct(style, MExpanderProgress(make(0.)));

		animatorStyle =	concatA([
			[
				MTranslateAnimation(if (vertical) MTop() else MLeft(), if (expandFromStart) [MExpandFromStart()] else []),
				MFadeAnimation(),
				MAnimatorCropByContent(),
				MAnimatorLazy()
			],
			eitherMap(duration, \d -> [d], []),
			eitherMap(progress, \p -> [p], []),
			extractStructMany(style, MNoCrop()),
		]);

		TAnimator(manager, parent, content, expanded, animatorStyle);
	} else {

	vertical = !contains(style, MExpanderHorizontal());
	expandFromStart = contains(style, MExpandFromStart());
	progress = tryExtractStruct(style, MExpanderProgress(make(0.)));
	duration = extractStruct(style, MDuration(const(0.3))).duration;
	crop = !contains(style, MNoCrop());

	size = TGhost("TAnimatedExpanderContent");
	startFactor = b2d(fgetValue(expanded));
	makeFactor = \f -> if (vertical) Factor(1.0, f) else Factor(f, 1.0);
	alpha = make(startFactor);
	factor = make(makeFactor(startFactor));
	box = TScale(factor, size);
	offset = TSubtractGroup2(size, box);

	TDisplay("TAnimatedExpanderContent")
	|> (\t -> TAlpha(alpha, t))
	|> (\t -> if (expandFromStart) t else TShift(t, offset))
	|> (\t -> if (crop) TCropSize(box, t) else TFixSize(t, box))
	|> (\t ->
			MEasingAnimation(
				manager,
				parent,
				duration,
				const(easingFastOutSlowIn),
				expanded,
				\e -> Some(e),
				\e, prc -> {
					f = if (e) prc else 1.0 - prc;
					next(alpha, f);
					next(factor, makeFactor(f));
					maybeApply(progress, \pr -> nextDistinct(pr.progress, f))
				},
				[],
				t
			))
	|> (\t -> TLet("TAnimatedExpanderContent", content, t))
	}
}

TCollapsingBox(
	manager : MaterialManager,
	parent : MFocusGroup,
	content : Tropic,
	yTrans : Transform<double>, // in upper direction only
	expand : Transform<bool>,
	trigger : Maybe<Transform<bool>>, // trigger to open or close
	heightInspect : Maybe<DynamicBehaviour<double>>,
	absoluteOffsetMode : bool, // if true then y counts from top, otherwise it depends on expand state
	fade : bool
) -> Tropic {

	height = either(heightInspect, make(0.));
	trans = make(0.); // px to translate up

	block = ref false; // to block outer translations while animation is active
	startFactor = ref 1.;
	factor = make(0.); // 0 .. 1

	box = TSizedHeight(0., fsubtract(height, trans));

	content
	|> (\t -> if (fade) TAlpha(factor, t) else t)
	|> (\t -> TAttachHeight(t, height))
	|> (\t -> TTranslate(fselect(trans, FLift(\y -> Point(0., -y))), t))
	|> (\t -> TFixSize(t, box))
	|> (\t -> TConstruct([
			make2Subscribe(yTrans, height, \y, hgt -> if (!^block)
				nextDistinct(factor, forceRange(if (absoluteOffsetMode) y / hgt else b2d(fgetValue(expand)) - y / hgt, 0., 1.))
			),
			\ -> fconnect2Select(factor, height, trans, \f, hgt -> (1. - f) * hgt)
		], t
	))
	|> (\t ->
		MEasingAnimation(
			manager,
			parent,
			const(0.3),
			const(easingFastOutSlowIn),
			either(trigger, expand),
			\tr -> {
				if (isNone(trigger) || !tr) {
					startFactor := fgetValue(factor);
					block := true;
					Some(fgetValue(expand))
				} else
					None()
			},
			\e, prc -> {
				f = lerp(^startFactor, b2d(e), prc);
				nextDistinct(factor, f);
				if (prc == 1.) block := false;
			},
			[],
			t
		)
	)
}

TAnimator(
	manager : MaterialManager,
	parent : MFocusGroup,
	content : Tropic,
	trigger : Transform<bool>,
	style : [MAnimatorStyle]) -> Tropic {

	contentWH = makeWH();

	duration = extractStruct(style, MDuration(const(0.3))).duration;
	bezier = extractStruct(style, MBezier(const(easingStandartCurve))).bezier;
	progress = extractStruct(style, MExpanderProgress(make(b2d(fgetValue(trigger))))).progress;

	noCrop = contains(style, MNoCrop());
	cropByContent = contains(style, MAnimatorCropByContent()) || exists(style, \st -> switch (st) {
			MTranslateAnimation(__, st2) : contains(st2, MExpandFromStart());
			default : false;
		}
	);
	cropW = make(false);
	cropH = make(false);

	lazy = contains(style, MAnimatorLazy());

	fade = contains(style, MFadeAnimation());
	wh2point = \fn -> fselect(contentWH, FLift(fn));

	centerModeHandler = \fn -> {
		centerPoint = wh2point(\wh -> Point(wh.width / 2., wh.height / 2.));

		\t, center ->
			if (!center) fn(t)
			else
				(
					if (cropByContent) TBorderT(fselect(centerPoint, FLift(\p -> -p.x)), fselect(centerPoint, FLift(\p -> -p.y)), zero, zero, t)
					else TTranslate(fselect(centerPoint, FLift(\p -> Point(-p.x, -p.y))), t)
				)
				|> fn
				|> (\t2 ->
					if (cropByContent) TBorderT(fpointX(centerPoint), fpointY(centerPoint), zero, zero, t2)
					else TTranslate(centerPoint, t2)
				)
	}

	scaleHandler = \sc, t -> {
		scaleFactor = extractStruct(sc.style, MScaleAnimationCustomFactor(const(Factor(0., 0.)))).factor;
		center = contains(sc.style, MCenterMode());
		centerModeHandler(\t2 -> TScale(flerpFactor(scaleFactor, progress), t2))(t, center)
	}

	rotateHandler = \rot, t -> {
		angle = extractStruct(rot.style, MRotateAnimationCustomAngle(const(360.))).angle;
		center = contains(rot.style, MCenterMode());
		centerModeHandler(\t2 -> TRotate(fsubselect(angle, FLift(\ang -> flerp(ang, 0., progress))), t2))(t, center)
	}

	translateHandler = \trans : MTranslateAnimation, t -> {
		position = switch (trans.type) {
			MTop() : wh2point(\wh -> Point(0., -wh.height));
			MBottom() : wh2point(\wh -> Point(0., wh.height));
			MLeft() : wh2point(\wh -> Point(-wh.width, 0.));
			MRight() : wh2point(\wh -> Point(wh.width, 0.));
			MTranslatePosition(pos) : pos;
		}
		translatePoint = flerpPoint(position, progress);

		if (cropByContent) {
			t2 =
				if (contains(trans.style, MExpandFromStart())) t
				else TTranslate(translatePoint, t);

			TransformTAcc2(\t2a -> {
				b = t2a(t2);
				xTranslation = fneq(fpointX(position), 0.);
				yTranslation = fneq(fpointY(position), 0.);
				TAcc(
					b with metrics = TFormMetrics(
						fif(
							xTranslation,
							fselect2(contentWH, translatePoint, FLift2(\wh, p -> wh.width - abs(p.x))),
							b.metrics.width,
						),
						fif(
							yTranslation,
							fselect2(contentWH, translatePoint, FLift2(\wh, p -> wh.height - abs(p.y))),
							b.metrics.height
						),
						b.metrics.baseline
					),
					disposers = concat(
						b.disposers, [
							makeSubscribe(xTranslation, \x -> if (x) nextDistinct(cropW, true))(),
							makeSubscribe(yTranslation, \y -> if (y) nextDistinct(cropH, true))()
						]
					)
				)
			})
		} else
			TTranslate(translatePoint, t)
	}

	content
	|> (\t -> if (lazy) TShowLazy(fgreater(progress, const(0.)), \ -> t) else t)
	|> (\t -> TAttachBox(t, contentWH))
	|> (\t -> if (fade) TAlpha(progress, t) else t)
	|> (\t ->
		fold(style, t, \acc, st ->
			switch (st) {
				MScaleAnimation(__) : scaleHandler(st, acc);
				MRotateAnimation(__) : rotateHandler(st, acc);
				MTranslateAnimation(__, __) : translateHandler(st, acc);
				default : acc
			}
		)
	)
	|> (\t ->
			if (noCrop) t
			else if (cropByContent) {
				transformedContentWH = makeWH();
				TVisible(
					fselect(transformedContentWH, FLift(\wh -> wh.width > 0.5 && wh.height > 0.5)),
					TAttachBox(t, transformedContentWH)
				)
				|> (\t2 -> TIf(cropH, TCropHeight2(t2, fheight(transformedContentWH)), t2))
				|> (\t2 -> TIf(cropW, TCropWidth2(t2, fwidth(transformedContentWH)), t2))
			} else {
				TVisible(
					fselect(contentWH, FLift(\wh -> wh.width > 0.5 && wh.height > 0.5)),
					TCrop(const(zeroPoint), contentWH, t)
				)
			}
		)
	|> (\t ->
		MEasingAnimation(
			manager,
			parent,
			duration,
			bezier,
			trigger,
			\tr -> {
				Some(tr)
			},
			\e, prc -> {
				nextDistinct(progress, if (e) prc else 1. - prc);
			},
			[],
			t
		)
	)
}

flerpFactor(factor: Transform<Factor>, progress : Transform<double>) -> Transform<Factor> {
	fselect2(factor, progress, FLift2(\f, pr ->
		Factor(
			lerp(f.x, 1., pr),
			lerp(f.y, 1., pr)
		)
	))
}

flerpPoint(point: Transform<Point>, progress : Transform<double>) -> Transform<Point> {
	fselect2(point, progress, FLift2(\p, pr ->
		Point(
			lerp(p.x, 0., pr),
			lerp(p.y, 0., pr)
		)
	))
}

MLetDisplay(name : string, value : Material, scope : Tropic, above : bool) -> Material {
	MLet(
		name,
		value,
		TransformTAcc2(\t2a -> {
			ba1 = TDisplay(name) |> t2a;
			ba2 = scope |> t2a;

			TAcc(
				FGroup([
					ba1.form,
					ba2.form
				] |> (\f : [FForm] -> if (above) f else reverseA(f)), true),
				TFormMetrics(
					fmax(ba1.metrics.width, ba2.metrics.width),
					fmax(ba1.metrics.height, ba2.metrics.height),
					fmax(ba1.metrics.baseline, ba2.metrics.baseline)
				),
				fmax(ba1.minWidth, ba2.minWidth),
				fmax(ba1.minHeight, ba2.minHeight),
				fmax(ba1.maxWidth, ba2.maxWidth),
				fmax(ba1.maxHeight, ba2.maxHeight),
				fmax(ba1.xFillers, ba2.xFillers),
				fmax(ba1.yFillers, ba2.yFillers),
				if (isUrlParameterTrue("group2filler")) fmax(ba1.addFillers, ba2.addFillers) else const(false),
				ba1.xConstant && ba2.xConstant,
				ba1.yConstant && ba2.yConstant,
				concat(ba1.disposers, ba2.disposers)
			)
		})
	)
}

colorLerp(previous : int, current : int, percent : double) -> int {
	prevRgb = int2rgb(previous);
	currentRgb = int2rgb(current);

	rgb2int(
		RGB(
			round((1.0 - percent) * i2d(prevRgb.r) + percent * i2d(currentRgb.r)),
			round((1.0 - percent) * i2d(prevRgb.g) + percent * i2d(currentRgb.g)),
			round((1.0 - percent) * i2d(prevRgb.b) + percent * i2d(currentRgb.b))
		)
	);
}

mColorLerp(previous : MColor, current : MColor, percent : double) -> MColor {
	MCustomColor(
		colorLerp(
			MColor2int(previous),
			MColor2int(current),
			percent
		)
	);
}

// Helper for wraping items into basic form of MArrowNavigationItem.
MArrowNavigationArray(
	itemsMaterialFunctions : [([MButtonState]) -> Material],
) -> [Material] {
	itemsLen = length(itemsMaterialFunctions);
	itemsFocusedIdx = make(-1);
	itemsFocusEnabledIdx = make(0);
	mapi(
		itemsMaterialFunctions,
		\i, fn -> MArrowNavigationItem(
			i,
			itemsLen,
			fn,
			itemsFocusedIdx,
			itemsFocusEnabledIdx,
			\ -> 0,
			[]
		)
	)
}

// Navigate through list by arrows, not by tab. Wrap each item of list into this.
MArrowNavigationItem(
	i : int,
	listLen : int,
	makeListItem : ([MButtonState]) -> Material,
	focusedIdx : DynamicBehaviour<int>,
	focusEnabledIdx : DynamicBehaviour<int>,
	getDefFocusableItem : () -> int,
	style : [MArrowNavigationStyle]
) -> Material {
	MArrowNavigationExt(i, listLen, makeListItem, focusedIdx, focusEnabledIdx, getDefFocusableItem, style).first;
}

MArrowNavigationExt(
	i : int,
	listLen : int,
	makeListItem : ([MButtonState]) -> Material,
	focusedIdx : DynamicBehaviour<int>,
	focusEnabledIdx : DynamicBehaviour<int>,
	getDefFocusableItem : () -> int,
	style : [MArrowNavigationStyle]
) -> Triple<Material, DynamicBehaviour<bool>, Transform<bool>> {

	vertical = extractStruct(style, MArrowNavigationDirection(true)).vertical;
	supportHomeEnd = extractStruct(style, MArrowNavigationSupportHomeEnd(true)).support;
	checkFocusability = extractStruct(style, MArrowNavigationCheckFocusability(\__ -> true)).fn;
	customOnArrow = tryExtractStruct(style, MArrowNavigationCustomOnArrow(nop1, \ -> false));
	selectItem = tryExtractStruct(style, MArrowNavigationSelectItem(nop, false));
	resetSelection = extractStruct(style, MArrowNavigationResetSelection(\ -> false)).fn;
	mainKeys = if (vertical) Pair(KEY_UP, KEY_DOWN) else Pair(KEY_LEFT, KEY_RIGHT);

	focused = make(false);
	focusEnabled = feq(focusEnabledIdx, i);

	makeListItem([MFocused(focused), MFocusEnabled(focusEnabled)])
	|> (\m -> MInteractive([
		KeyUp2(\handled, ke -> {
			if (!handled && fgetValue(focused)) {
				idx = getValue(focusedIdx);
				updateFocusedAnswer = \newIdx -> if (checkFocusability(newIdx)) nextDistinct(focusedIdx, newIdx);
				onArrow = \forw, newIdx ->
					eitherFn(customOnArrow,
						\coa ->
							if (coa.enabled()) coa.fn(forw)
							else updateFocusedAnswer(newIdx),
						\ -> updateFocusedAnswer(newIdx)
					);
				if (ke.keycode == mainKeys.first) { // backwards
					onArrow(false, if (idx == 0) listLen - 1 else idx - 1);
					true
				} else if (ke.keycode == mainKeys.second) { // forward
					onArrow(true, if (idx == listLen - 1) 0 else idx + 1);
					true
				} else if (supportHomeEnd && ke.keycode == KEY_HOME) {
					onArrow(false, 0);
					true
				} else if (supportHomeEnd && ke.keycode == KEY_END) {
					onArrow(true, listLen - 1);
					true
				} else if (ke.keycode == KEY_ESCAPE) {
					resetSelection()
				} else if (eitherMap(selectItem, \si -> !si.auto, false) && ke.keycode == KEY_SPACE) {
					maybeApply(selectItem, \si -> si.fn());
					true
				} else {
					false
				}
			} else handled
		})
	], m))
	|> (\m -> MConstruct([
			makeSubscribe(focusedIdx, \focIdx ->
				if (focIdx == i) deferUntilNextFrameRendered(\ -> {
					nextDistinct(focusEnabledIdx, i);
					nextDistinct(focused, true);
					maybeApply(selectItem, \si -> if (si.auto) si.fn())
				}) else {
					nextDistinct(focused, false);
				}
			),
			makeSubscribe(focused, \foc -> if (foc) {
				deferUntilNextFrameRendered(\ -> nextDistinct(focusedIdx, i))
			} else {
				nextDistinct(focusEnabledIdx, getDefFocusableItem());
			})
		], m))
	|> (\m -> Triple(m, focused, focusEnabled))
}

mDomRenderEndabled = ref isUrlParameterTrue("dom_render");

isMDomRender(style : [flow]) -> bool {
	js && (contains(style, MDomRender()) || ^mDomRenderEndabled);
}

setMDomRenderEnabled(enabled : bool) -> void {
	mDomRenderEndabled := enabled;
}