import material/internal/types;

export {
	ManagerOrFocusGroup ::= MaterialManager, MFocusGroup;

	getShapeByComponent(parent : MFocusGroup, component : string) -> MaterialShapeRange;

	getLightBackground(parent : ManagerOrFocusGroup) -> bool { parent.theme.palette.light };
	getPrimaryColor(parent : ManagerOrFocusGroup) -> MColor { parent.theme.palette.primary };
	getAccentColor(parent : ManagerOrFocusGroup) -> MColor { parent.theme.palette.accent };
	getTextColor(parent : ManagerOrFocusGroup) -> MColor { parent.theme.palette.text };
	getIconColor(parent : ManagerOrFocusGroup) -> MColor { parent.theme.palette.icon };
	getToolbarItemsColor(parent : ManagerOrFocusGroup) -> MColor { parent.theme.palette.toolbar };
	getSeparatorColor(parent : ManagerOrFocusGroup) -> MColor { parent.theme.palette.separator };
	getFontStyle(parent : ManagerOrFocusGroup) -> MFontStyle { parent.theme.type.body2 };

	contrastingColor(parent : ManagerOrFocusGroup) -> int { if (getLightBackground(parent)) black else white };
	contrastingMColor(parent : ManagerOrFocusGroup) -> MColor { MDefaultTextColor(getLightBackground(parent)) };
	sameLightMColor(parent : ManagerOrFocusGroup) -> MColor { MDefaultTextColor(!getLightBackground(parent)) };

	FMaterialShape(parent : MFocusGroup, style : [GraphicsStyle], component : string) -> (WidthHeight) -> FForm;
	MShape(manager : MFocusGroup, style : [TGraphicsStyle], size : Tropic, component : string) -> Tropic;
	MShapeFrame(manager : MFocusGroup, style : [TGraphicsStyle], content : Tropic, component : string) -> Tropic;

	makeMaterialTheme(lightBackground : bool, primaryColor : MColor, accentColor : MColor) -> MaterialTheme;
	MLightTheme(theme : MaterialTheme, lightBackground : bool) -> MaterialTheme;

	defaultMaterialPalette =
		MaterialPalette(true, MBlue(500), MOrangeA(200), MBlack(), MBlack(), MWhite(), MBlack());

	defaultMaterialSharpCorner =
		CutCorner(0.0);
	defaultMaterialRoundedCorner =
		RoundedCorner(4.0);
	defaultMaterialRoundCorner =
		RoundedCornerPercent(0.5);

	defaultMaterialSharpShape =
		MaterialShape(defaultMaterialSharpCorner, defaultMaterialSharpCorner, defaultMaterialSharpCorner, defaultMaterialSharpCorner);
	defaultMaterialRoundedShape =
		MaterialShape(defaultMaterialRoundedCorner, defaultMaterialRoundedCorner, defaultMaterialRoundedCorner, defaultMaterialRoundedCorner);
	defaultMaterialRoundShape =
		MaterialShape(defaultMaterialRoundCorner, defaultMaterialRoundCorner, defaultMaterialRoundCorner, defaultMaterialRoundCorner);

	defaultMaterialShapeApplicable =
		MaterialShapeApplicable(true, true, true, true);
	defaultMaterialShapeLimit =
		MaterialShapeLimit(0.0, 48.0, 0.0, 0.5);

	defaultMaterialShapeRange =
		MaterialShapeRange(defaultMaterialSharpShape, defaultMaterialShapeApplicable, defaultMaterialShapeLimit, defaultMaterialShapeLimit);

	defaultMaterialShapeManager =
		MaterialShapeManager(
			pairs2tree([
				Pair(
					"default",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 28.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 28.0, 0.0, 0.5)
					)
				),
				Pair(
					"icon",
					MaterialShapeRange(
						defaultMaterialRoundShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 28.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 28.0, 0.0, 0.5)
					)
				),
				Pair(
					"button",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 20.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 6.0, 0.0, 0.5)
					)
				),
				Pair(
					"chip",
					MaterialShapeRange(
						defaultMaterialRoundShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 16.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 0.0, 0.0, 0.0)
					)
				),
				Pair(
					"floating action button",
					MaterialShapeRange(
						defaultMaterialRoundShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 28.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 28.0, 0.0, 0.5)
					)
				),
				Pair(
					"filled text field",
					MaterialShapeRange(
						MaterialShape(
							defaultMaterialRoundedCorner,
							defaultMaterialRoundedCorner,
							defaultMaterialSharpCorner,
							defaultMaterialSharpCorner
						),
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 20.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 12.0, 0.0, 0.5)
					)
				),
				Pair(
					"filled text field",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 20.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 12.0, 0.0, 0.5)
					)
				),
				Pair(
					"snackbar",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 24.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 12.0, 0.0, 0.5)
					)
				),
				Pair(
					"tooltip",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 16.0, 0.0, 0.5),
						MaterialShapeLimit(0.0, 0.0, 0.0, 0.0)
					)
				)
			]),
			pairs2tree([
				Pair(
					"default",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 36.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 16.0, 0.0, 0.0)
					)
				),
				Pair(
					"card",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 24.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 16.0, 0.0, 0.0)
					)
				),
				Pair(
					"dialog",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 36.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 16.0, 0.0, 0.0)
					)
				),
				Pair(
					"dialog fullscreen",
					MaterialShapeRange(
						defaultMaterialSharpShape,
						MaterialShapeApplicable(false, false, false, false),
						MaterialShapeLimit(0.0, 0.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 0.0, 0.0, 0.0)
					)
				),
				Pair(
					"image list",
					MaterialShapeRange(
						defaultMaterialSharpShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 24.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 16.0, 0.0, 0.0)
					)
				),
				Pair(
					"menu",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 20.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 12.0, 0.0, 0.0)
					)
				)
			]),
			pairs2tree([
				Pair(
					"default",
					MaterialShapeRange(
						defaultMaterialSharpShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 36.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 24.0, 0.0, 0.0)
					)
				),
				Pair(
					"backdrop",
					MaterialShapeRange(
						MaterialShape(RoundedCorner(24.0), RoundedCorner(24.0), RoundedCorner(24.0), RoundedCorner(24.0)),
						MaterialShapeApplicable(true, true, false, false),
						MaterialShapeLimit(0.0, 36.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 24.0, 0.0, 0.0)
					)
				),
				Pair(
					"datatable",
					MaterialShapeRange(
						defaultMaterialRoundedShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 20.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 12.0, 0.0, 0.0)
					)
				),
				Pair(
					"expanded bottom sheet",
					MaterialShapeRange(
						defaultMaterialSharpShape,
						MaterialShapeApplicable(false, false, false, false),
						MaterialShapeLimit(0.0, 0.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 0.0, 0.0, 0.0)
					)
				),
				Pair(
					"collapsed bottom sheet",
					MaterialShapeRange(
						defaultMaterialSharpShape,
						MaterialShapeApplicable(true, true, false, false),
						MaterialShapeLimit(0.0, 24.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 12.0, 0.0, 0.0)
					)
				),
				Pair(
					"navigation drawer",
					MaterialShapeRange(
						defaultMaterialSharpShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 36.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 18.0, 0.0, 0.0)
					)
				),
				Pair(
					"side sheet",
					MaterialShapeRange(
						defaultMaterialSharpShape,
						defaultMaterialShapeApplicable,
						MaterialShapeLimit(0.0, 36.0, 0.0, 0.0),
						MaterialShapeLimit(0.0, 18.0, 0.0, 0.0)
					)
				)
			])
		);

	MaterialShapeGroup ::= MaterialSmallShape, MaterialMediumShape, MaterialLargeShape;
		MaterialSmallShape(name : string);
		MaterialMediumShape(name : string);
		MaterialLargeShape(name : string);

	defaultMaterialShapeMapping : Tree<string, MaterialShapeGroup> =
		pairs2tree([
			// Small
			Pair("MIcon", cast(MaterialSmallShape("icon") : MaterialSmallShape -> MaterialShapeGroup)),
			Pair("MLetterIcon", MaterialSmallShape("icon")),
			Pair("MAvatar", MaterialSmallShape("icon")),
			Pair("MIconButton", MaterialSmallShape("icon")),
			Pair("MIconToggle", MaterialSmallShape("icon")),
			Pair("MCheckBox", MaterialSmallShape("icon")),
			Pair("MRadio", MaterialSmallShape("icon")),
			Pair("MSwitchControl", MaterialSmallShape("icon")),
			Pair("MSlider", MaterialSmallShape("icon")),

			Pair("MTextButton", MaterialSmallShape("button")),
			Pair("MTextClickable", MaterialSmallShape("button")),
			Pair("MClickable", MaterialSmallShape("button")),
			Pair("MComponent", MaterialSmallShape("button")),

			Pair("MChip", MaterialSmallShape("chip")),

			Pair("MFloatingButton", MaterialSmallShape("floating action button")),

			Pair("MSmallEditDialog", MaterialSmallShape("filled text field")),
			Pair("MAutoComplete", MaterialSmallShape("filled text field")),

			Pair("MTextInput", MaterialSmallShape("outlined text field")),

			Pair("MSnackbar", MaterialSmallShape("snackbar")),

			Pair("MTooltip", MaterialSmallShape("tooltip")),

			// Medium
			Pair("MCard", MaterialMediumShape("card")),
			Pair("MTextCard", MaterialMediumShape("card")),

			Pair("MTimePicker", MaterialMediumShape("dialog")),
			Pair("MDatePicker", MaterialMediumShape("dialog")),
			Pair("MColorPicker", MaterialMediumShape("dialog")),
			Pair("MColorPickerMultiSelect", MaterialMediumShape("dialog")),
			Pair("MDialog", MaterialMediumShape("dialog")),

			Pair("MFullscreenDialog", MaterialMediumShape("dialog fullscreen")),

			Pair("MGridList", MaterialMediumShape("image list")),
			Pair("MImageMap", MaterialMediumShape("image list")),

			Pair("MMenu", MaterialMediumShape("menu")),
			Pair("MDynamicMenu", MaterialMediumShape("menu")),
			Pair("MDropDownMenu", MaterialMediumShape("menu")),
			Pair("MMenuPanel", MaterialMediumShape("menu")),
			Pair("MDropDown", MaterialMediumShape("menu")),
			Pair("MMultiSelectDropDown", MaterialMediumShape("menu")),

			// Large
			Pair("MDynamicDataTable", MaterialLargeShape("datatable")),
			Pair("MDataTable", MaterialLargeShape("datatable")),
			Pair("MChart", MaterialLargeShape("datatable")),

			Pair("MNavigation", MaterialLargeShape("navigation drawer")),

			Pair("MSideNav", MaterialLargeShape("side sheet")),

			Pair("MBottomNav", MaterialLargeShape("collapsed bottom sheet")),

			Pair("MDynamicTabs", MaterialLargeShape("tabs")),
		]);

	// defaultMaterialTypeManager =
	// 	MaterialTypeManager(
	// 		MaterialTypeScale("RobotoLight", 96., PrimaryOpacity(), -1.5),
	// 		MaterialTypeScale("RobotoLight", 60., PrimaryOpacity(), -0.5),
	// 		MaterialTypeScale("Roboto", 48., PrimaryOpacity(), 0.0),
	// 		MaterialTypeScale("Roboto", 34., PrimaryOpacity(), 0.25),
	// 		MaterialTypeScale("Roboto", 24., PrimaryOpacity(), 0.0),
	// 		MaterialTypeScale("RobotoMedium", 20., PrimaryOpacity(), 0.15),
	// 		MaterialTypeScale("Roboto", 16., PrimaryOpacity(), 0.15),
	// 		MaterialTypeScale("RobotoMedium", 14., PrimaryOpacity(), 0.1),
	// 		MaterialTypeScale("Roboto", 16., PrimaryOpacity(), 0.5),
	// 		MaterialTypeScale("Roboto", 14., PrimaryOpacity(), 0.25),
	// 		MaterialTypeScale("RobotoMedium", 14., PrimaryOpacity(), 1.25),
	// 		MaterialTypeScale("Roboto", 12., PrimaryOpacity(), 0.4),
	// 		MaterialTypeScale("Roboto", 10., PrimaryOpacity(), 1.5)
	// 	);

	defaultMaterialTypeManager =
		MaterialTypeManager(
			MDisplay5(),
			MDisplay4(),
			MDisplay3(),
			MDisplay1(),
			MHeadline(),
			MTitle(),
			MSubheading(),
			MSubtitle(),
			MBodyPrimary(),
			MBody(),
			MButtonStyle(),
			MCaption(),
			MOverline()
		);

	MaterialType2MFontStyle(parent : MFocusGroup, type : MaterialType) -> MFontStyle;

	defaultMaterialTheme =
		MaterialTheme(
			defaultMaterialPalette,
			defaultMaterialShapeManager,
			defaultMaterialTypeManager,
			true,
			true,
		);
}

parseMaterialShapeCorner(corner : MaterialShapeCorner) -> (WidthHeight) -> Pair<double, bool> {
	switch (corner : MaterialShapeCorner) {
		RoundedCorner(radius) : \__ -> Pair(radius, true);
		CutCorner(radius) : \__ -> Pair(radius, false);
		RoundedCornerPercent(percent) : \wh -> Pair(min(wh.width, wh.height) * percent, true);
		CutCornerPercent(percent) : \wh -> Pair(min(wh.width, wh.height) * percent, false);
	}
}

getShapeByComponent(parent : MFocusGroup, component : string) -> MaterialShapeRange {
	componentMapping = lookupTree(defaultMaterialShapeMapping, component);

	either(
		fold(
			eitherMap(
				componentMapping,
				\group -> {
					switch (group) {
						MaterialSmallShape(name) : {
							[
								\ -> lookupTree(parent.theme.shape.small, component),
								\ -> lookupTree(parent.theme.shape.small, name),
								\ -> lookupTree(parent.theme.shape.small, "default")
							]
						}
						MaterialMediumShape(name) : {
							[
								\ -> lookupTree(parent.theme.shape.medium, component),
								\ -> lookupTree(parent.theme.shape.medium, name),
								\ -> lookupTree(parent.theme.shape.medium, "default"),
								\ -> lookupTree(parent.theme.shape.small, "default")
							]
						}
						MaterialLargeShape(name) : {
							[
								\ -> lookupTree(parent.theme.shape.large, component),
								\ -> lookupTree(parent.theme.shape.large, name),
								\ -> lookupTree(parent.theme.shape.large, "default"),
								\ -> lookupTree(parent.theme.shape.medium, "default"),
								\ -> lookupTree(parent.theme.shape.small, "default")
							]
						}
					}
				},
				[
					\ -> lookupTree(parent.theme.shape.large, component),
					\ -> lookupTree(parent.theme.shape.medium, component),
					\ -> lookupTree(parent.theme.shape.small, component),
					\ -> Some(defaultMaterialShapeRange)
				]
			),
			None(),
			\acc, sh -> {
				if (isNone(acc)) {
					sh();
				} else {
					acc;
				}
			}
		),
		defaultMaterialShapeRange
	);
}

FMaterialShape(parent : MFocusGroup, style : [GraphicsStyle], component : string) -> (WidthHeight) -> FForm {
	shape = getShapeByComponent(parent, component);

	topLeft = parseMaterialShapeCorner(shape.defaultShape.tl);
	topRight = parseMaterialShapeCorner(shape.defaultShape.tr);
	bottomRight = parseMaterialShapeCorner(shape.defaultShape.br);
	bottomLeft = parseMaterialShapeCorner(shape.defaultShape.bl);

	\wh -> {
		width = wh.width;
		height = wh.height;

		if (width == 0.0 || height == 0.0) { // special case (without this check we get two small dots)
			FEmpty() // Fixed(width, height)
		} else {
			topLeftR = topLeft(wh);
			topRightR = topRight(wh);
			bottomRightR = bottomRight(wh);
			bottomLeftR = bottomLeft(wh);

			w = width;
			h = height;

			r1 = topLeftR.first;
			r2 = topRightR.first;
			r3 = bottomRightR.first;
			r4 = bottomLeftR.first;

			sqrt2 = sqrt(2.0);
			n1 = 1.0 / sqrt2;
			n2 = sqrt2 - 1.0;

			FGraphics(
				concatA([
					[
						MoveTo(r1, 0.0),
						LineTo(w - r2, 0.0)
					],
					if (topRightR.second && r2 > 0.0)
						[
							CubicBezierTo(
								(n1 * r2) + w - r2,
								(-n1 * r2) + r2,
								(n2 * r2) + w - r2,
								(-r2)  + r2
							),
							CubicBezierTo(
								w,
								r2,
								w,
								-n2 * r2 + r2
							)

						]
					else
						[
							LineTo(w, r2)
						],
					[
						LineTo(w, h - r3)
					],
					if (bottomRightR.second && r3 > 0.0)
						[
							CubicBezierTo(
								(n1 * r3) + w - r3,
								(n1 * r3) + h - r3,
								w,
								(n2 * r3) + h - r3,
							),
							CubicBezierTo(
								w - r3,
								h,
								(n2 * r3) + w - r3,
								h
							)
						]
					else
						[
							LineTo(w - r3, h)
						],
					[
						LineTo(r4, h)
					],
					if (bottomLeftR.second && r4 > 0.0)
						[
							CubicBezierTo(
								-n1 * r4 + r4,
								n1 * r4 + h - r4,
								(-n2 * r4) + r4,
								h
							),
							CubicBezierTo(
								0.0,
								h - r4,
								0.0,
								(n2 * r4) + h - r4,
							)
						]
					else
						[
							LineTo(0.0, h - r4)
						],
					[
						LineTo(0.0, r1)
					],
					if (topLeftR.second && r1 > 0.0)
						[
							CubicBezierTo(
								(-n1 * r1) + r1,
								(-n1 * r1) + r1,
								0.0,
								(-n2 * r1) + r1
							),
							CubicBezierTo(
								r1,
								0.0,
								(-n2 * r1) + r1,
								0.0
							)
						]
					else
						[
							LineTo(r1, 0.0)
						]
				]),
				style
			)
		}
	}
}

MShape(parent : MFocusGroup, style : [TGraphicsStyle], size : Tropic, component : string) -> Tropic {
	TransformTAcc(\t2a, pi, ss, mo -> {
		b = t2a(size, pi, ss, true);
		container = FMaterialShape(parent, style |> tgraphicsStyle2graphicsStyle, component);

		TAcc(
			FMutable(fselect(fwidthheight(b.metrics.width, b.metrics.height), container |> FLift)),
			b.metrics,
			b.minWidth,
			b.minHeight,
			b.maxWidth,
			b.maxHeight,
			b.xFillers,
			b.yFillers,
			b.addFillers,
			b.xConstant,
			b.yConstant,
			b.disposers
		)
	})
}

MShapeFrame(parent : MFocusGroup, style : [TGraphicsStyle], content : Tropic, component : string) -> Tropic {
	TCopySize(
		content,
		\tr -> MShape(parent, style, tr, component),
		false
	)
}

makeMaterialTheme(lightBackground : bool, primaryColor : MColor, accentColor : MColor) -> MaterialTheme {
	toolbarItemsColor = MTextMColor(primaryColor);
	itemsColor = if (lightBackground) MBlack() else MWhite();

	MaterialTheme(
		MaterialPalette(
			lightBackground,
			primaryColor,
			accentColor,
			itemsColor,
			itemsColor,
			toolbarItemsColor,
			itemsColor
		),
		defaultMaterialTheme.shape,
		defaultMaterialTheme.type,
		defaultMaterialTheme.enableAnimations,
		defaultMaterialTheme.enableRipple
	)
}

MLightTheme(theme : MaterialTheme, lightBackground : bool) -> MaterialTheme {
	if (lightBackground != theme.palette.light)
		MaterialTheme(
			MaterialPalette(
				lightBackground,
				theme.palette.primary,
				theme.palette.accent,
				if (MColor2int(theme.palette.text) == white && lightBackground)
					MBlack()
				else if (MColor2int(theme.palette.text) == black && !lightBackground)
					MWhite()
				else
					theme.palette.text,
				if (MColor2int(theme.palette.icon) == white && lightBackground)
					MBlack()
				else if (MColor2int(theme.palette.icon) == black && !lightBackground)
					MWhite()
				else
					theme.palette.icon,
				theme.palette.toolbar,
				if (MColor2int(theme.palette.separator) == white && lightBackground)
					MBlack()
				else if (MColor2int(theme.palette.separator) == black && !lightBackground)
					MWhite()
				else
					theme.palette.separator,
			),
			theme.shape,
			theme.type,
			theme.enableAnimations,
			theme.enableRipple,
		)
	else
		theme
}

MaterialType2MFontStyle(parent : MFocusGroup, type : MaterialType) -> MFontStyle {
	switch (type : MaterialType) {
		MH1(): parent.theme.type.h1;
		MH2(): parent.theme.type.h2;
		MH3(): parent.theme.type.h3;
		MH4(): parent.theme.type.h4;
		MH5(): parent.theme.type.h5;
		MH6(): parent.theme.type.h6;
		MSubtitle1(): parent.theme.type.subtitle1;
		MSubtitle2(): parent.theme.type.subtitle2;
		MBody1(): parent.theme.type.body1;
		MBody2(): parent.theme.type.body2;
		MButton1(): parent.theme.type.button;
		MCaption1(): parent.theme.type.caption;
		MOverline1(): parent.theme.type.overline;
	}
}