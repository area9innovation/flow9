import material/internal/material_mutable;

export {
	MMovable2T(manager : MaterialManager, parent : MFocusGroup, m : MMovable, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
	MResizable2T(manager : MaterialManager, parent : MFocusGroup, m : MResizable, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MMovable2T(manager : MaterialManager, parent : MFocusGroup, m : MMovable, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	movableId = makeTropicTag(manager);
	position = extractStruct(m.style, MMovablePosition(make(zeroPoint))).position;
	expand = contains(m.style, MMovableExpandArea());
	enabled = extractStruct(m.style, MEnabled(const(true))).enabled;
	interactionsOnTop = contains(m.style, MInteractionsOnTop());
	dontPassClicks = contains(m.style, MMovableDontPassClicks());
	keepCenter = contains(m.style, MMovableKeepCenter());

	isDragging = make(false);
	mouseXY = make(zeroPoint);
	mouseXYLocal = make(zeroPoint);
	cursorPosition = ref zeroPoint;

	movableAreaWH = makeWH();
	paddingWH = makeWH();
	targetWH = makeWH();
	targetWHBuffer = cloneBehaviour(targetWH);
	safetySpace = 36.0;

	grabArea = extractStruct(m.style, MMovableGrabArea(TEmpty(), TSized(targetWH)));

	updatePosition = \movWH, prevTarWH, tarWH, mxy, dxy -> {
		expandGapWidth = if (expand) tarWH.width - safetySpace else 0.;
		expandGapHeight = if (expand) tarWH.height - safetySpace else 0.;

		newPosition =
			Point(
				mxy.x - dxy.x + (if (keepCenter) (prevTarWH.width - tarWH.width) / 2. else 0.),
				mxy.y - dxy.y + (if (keepCenter) (prevTarWH.height - tarWH.height) / 2. else 0.)
			);

		nextDistinct(
			position,
			Point(
				max(-expandGapWidth, min(newPosition.x, movWH.width - tarWH.width + expandGapWidth)),
				max(-expandGapHeight, min(newPosition.y, movWH.height - tarWH.height + expandGapHeight))
			)
		)
	};

	cursor = extractStruct(m.style, MMovableCustomCursor(DynamicCursor(fif(isDragging, const(GrabbingCursor()), const(GrabCursor()))))).cursor;

	target = \p ->
		m2t(m.target, p)
		|> (\t -> TAttachBox(t, targetWH));

	grabAreaM = \p ->
		m2t(grabArea.area, p)
			|> TSizeOf
			|> (\t -> TFrame(0., 0., interactiveRectangleStyle, t))
			|> (\t ->
				TInteractive(
					[
						if (dontPassClicks) TMouseDown(isDragging) else TMouseDownGhost(isDragging),
						TMouseXY(mouseXYLocal)
					], t)
				)
			|> (\t -> TCursor(cursor, t))
			|> (\t -> TBorderT(fwidth(paddingWH), fheight(paddingWH), zero, zero, t))
			|> (\t -> TAttachBox2(m2t(grabArea.padding, p), paddingWH, t))
			|> (\t -> TAvailable(t, TSized(targetWH)));

	(\p ->
		TAttachBox2(m2t(m.movableArea, p), movableAreaWH, TEmpty())
		|> (\t -> TFrame(0., 0., interactiveRectangleStyle, t))
		|> (\t -> TInteractive([TMouseXY(mouseXY)], t))
		|> (\t ->
			(
				if (interactionsOnTop) TGroup2(target(p), grabAreaM(p))
				else TGroup2(grabAreaM(p), target(p))
			)
			|> (\t2 -> TTranslate(position, t2))
			|> (\t2 -> TFixSize(t2, TEmpty()))
			|> (\t2 -> TGroup2(t, t2))
		)
		|> (\t ->
			TConstruct(
				[
					\ -> {nextDistinct(targetWHBuffer, fgetValue(targetWH)); nop},
					\ -> fconnect(fstall(targetWH, 0), targetWHBuffer),
					make2Subscribe(
						movableAreaWH,
						fselectWithLast(targetWHBuffer, FLift2(\old, new -> Pair(old, new))),
						\movWH, tarWH -> updatePosition(movWH, tarWH.first, tarWH.second, getValue(position), zeroPoint)
					),
					makeSubscribe2(isDragging, \dr -> if (dr) {
						addMInteractionItemId(manager, movableId);
						local = getValue(mouseXYLocal);
						curPad = getValue(paddingWH);
						cursorPosition :=
							Point(local.x + curPad.width, local.y + curPad.height);
					}),
					makeSubscribe2(mouseXY, \mxy ->
						if (isCurrentMInteractionItemId(manager, movableId) && getValue(isDragging) && fgetValue(enabled)) {
							blockOtherMInteractionItemIds(manager, movableId);
							updatePosition(getValue(movableAreaWH), getValue(targetWH), getValue(targetWH), mxy, ^cursorPosition)
						}
					)
				],
				t
			)
		)
	)
	|> (\t -> MComponentGroup2T(manager, parent, "MMovable",
		[IScriptBehaviour("position", position, ISInputValue([], serialize(getValue(position)), true))], t))
}

MResizable2T(manager : MaterialManager, parent : MFocusGroup, m : MResizable, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	resizableId = makeTropicTag(manager);
	style = m.style;
	minMax : MResizableMinMax = extractStruct(style, MResizableMinMax(TFixed(20., 20.), TFillXY()));
	onCreate = extractStruct(style, MResizableOnCreate(minMax.max)).size;
	controlOnStart = contains(style, MResizableControlOnStart());
	availableWH : DynamicBehaviour<WidthHeight> = make(WidthHeight(100., 100.));

	mouseDown = make(false);
	mouseXY = make(zeroPoint);
	startPoint = ref zeroPoint;
	minWH = makeWH();
	maxWH = makeWH();
	createWH = makeWH();

	updateWidthHeight = \mWH, maWH, aWH ->
		nextDistinct(
			availableWH,
			WidthHeight(
				max(min(mWH.width, maWH.width), min(aWH.width, max(mWH.width, maWH.width))),
				max(min(mWH.height, maWH.height), min(aWH.height, max(mWH.height, maWH.height)))
			)
		);


	(\p -> {
		reversed = p.rtl != controlOnStart;
		TDisplay("MResizableContent")
		|> (\f ->
			TGroup2(
				f,
				TRectangle(interactiveRectangleStyle, TFixed(10., 10.))
				|> (\f2 ->
					TGroup2(
						f2,
						TGraphics(
							if (reversed)
								[
									LineTo(9., 9.),
									MoveTo(0., 4.),
									LineTo(5., 9.)
								]
							else
								[
									MoveTo(0., 9.),
									LineTo(9., 0.),
									MoveTo(4., 9.),
									LineTo(9., 4.)
								],
							[
								StrokeOpacity(1.),
								Stroke(black),
								FillOpacity(0.)
							]
						)
					)
				)
				|> (\f2 ->
					TInteractive(
						[
							TMouseDown(mouseDown)
						],
						f2
					)
				)
				// |> (\f2 -> TCursor(MoveCursor, f2))
				|> (\f2 -> TCols2Dir(TFillX(), f2, reversed))
				|> (\f2 -> TLines2(TFillY(), f2))
				|> (\f2 -> TAvailable(f2, TGhost("MResizableContent")))
			)
		)
		|> (\f -> TAvailable(f, TSized(availableWH)))
		|> (\f -> TInteractive([TMouseXY(mouseXY)], f))
		|> (\f -> TGroup2(TAttachBox2(m2t(minMax.min, p), minWH, TEmpty()), f))
		|> (\f -> TGroup2(TAttachBox2(m2t(minMax.max, p), maxWH, TEmpty()), f))
		|> (\f -> TGroup2(TAttachBox2(m2t(onCreate, p), createWH, TEmpty()), f))
		|> (\f -> TFixSize(f, TGhost("MResizableContent")))
		|> (\f -> TLet("MResizableContent", m2t(m.content, p), f))
		|> (\f ->
			TConstruct(
				[
					makeSubscribe(mouseDown, \md -> if (md) addMInteractionItemId(manager, resizableId)),
					makeSubscribe2(mouseXY, \xy ->
						if (isCurrentMInteractionItemId(manager, resizableId) && getValue(mouseDown)) {
							blockOtherMInteractionItemIds(manager, resizableId);
							avX = if (reversed) getValue(availableWH).width - xy.x else xy.x;
							updateWidthHeight(getValue(minWH), getValue(maxWH), WidthHeight(avX, xy.y))
						}
					),
					\ -> {updateWidthHeight(getValue(minWH), getValue(maxWH), getValue(createWH)); nop},
					make3Subscribe(minWH, maxWH, availableWH, updateWidthHeight)
				],
				f
			)
		)
	})
	|> (\f -> MComponentGroup2T(manager, parent, "MMovable", [IScriptBehaviour("available", availableWH, ISInputValue([], serialize(getValue(availableWH)), true))], f))
}
