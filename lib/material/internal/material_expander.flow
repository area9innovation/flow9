import material/internal/material_button;

export {
	MExpander2T(manager : MaterialManager, parent : MFocusGroup, m : MExpander, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MExpander2T(manager : MaterialManager, parent : MFocusGroup, m : MExpander, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	header = m.header;
	expander = m.expander;
	details = m.details;

	expanderIds = extractStruct(manager.style, MExpanderIds(make(0))).ids;
	nextDistinct(expanderIds, getValue(expanderIds) + 1);
	expanderId = "expander_content_" + i2s(getValue(expanderIds));

	hideSeparator = contains(m.style, MExpanderHideSeparator());
	noDispose = contains(m.style, MNoDispose());
	bgStyle = extractStruct(m.style, MExpanderBackgroundStyle([], []));
	animated = contains(m.style, MDetailsAnimated());
	mWidth = extractStruct(m.style, MWidth(-1.0)).width;

	align = fold(m.style, StartAlign(), \acc, st -> {
		switch (st : MExpanderStyle) {
			LeftAlign() : st;
			CenterAlign() : st;
			RightAlign() : st;
			EndAlign() : st;
			default : acc;
		};
	});

	TCopySize2(
		MExpanderHeader(manager, parent, m2t(header, parent), expander, m.style, align, mWidth, expanderId, m2t),
		\tr, hd ->
			TLines2A(
				hd
				|> (\f -> if (bgStyle.header != []) TGroup2(TRounded(2., 2., 0., 0., bgStyle.header, tr), f) else f),
				(\p ->
					m2t(details(), p)
					|> TAlignHorizontalDir(align, p.rtl)
					|> (\f -> if (bgStyle.details != []) TCopySize(f, \tr2 -> TRounded(0., 0., 2., 2., bgStyle.details, tr2), false) else f)
					|> (\f ->
						if (hideSeparator)
							f
						else
							TLines2A(
								MSeparator2T(manager, parent, true)
								|> (\f2 -> TAvailable(f2, TGroup2(TWidth(tr), TFixed(0., 1.)))),
								f
							)
					)
				)
				|> (\f ->
					if (animated)
						TAnimatedExpander(manager, parent, f(parent), expander, [MExpandFromStart()])
					else if (noDispose)
						MRenderOnce2T(manager, parent, expander, f)
					else
						MSelect2T(
							manager,
							parent,
							"MExpanderContent",
							[],
							expander,
							\ex, p ->
								if (ex)
									f(p)
								else
									TEmpty()
						)
				)
				|> (\t -> TAccess([FAccessAttribute("id", const(expanderId))], t))
			)
	)
		|> (\t -> if (mWidth > 0.) TAvailableWidth(t, const(mWidth)) else t)
}

MExpanderHeader(
	manager : MaterialManager,
	parent : MFocusGroup,
	c : Tropic,
	expander : DynamicBehaviour<bool>,
	style : [MExpanderStyle],
	align : CommonAlignment,
	mWidth : double,
	expanderId : string,
	m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {

	expandByArrowOnly = contains(style, MExpandByArrowOnly());
	arrowColor = extractMColor(parent, style, MThemedColor(MGrey(600), MWhite()));
	arrowOpacity = extractStruct(style, FillOpacity(0.54));
	buttonTitle = extractStruct(style, MButtonTitle(fif(expander, const("Hide"), const("Expand"))));
	enabledState : MEnabled = extractStruct(style, MEnabled(const(true)));
	narrowHeader = contains(style, MExpanderNarrowHeader());
	front = contains(style, MExpanderFront());
	icons = extractStruct(style, MExpanderIcon("keyboard_arrow_down", "keyboard_arrow_up", const(true)));

	iconStyle : [MIconStyle] = [extractStruct(style, MIconSize(24.0)), arrowColor, arrowOpacity];
	isRTL = parent.rtl;

	icon =
		TTranslate(extractStruct(style, MIconTranslate(const(Point(0.0, 1.0)))).point, MIconToggle2T(
			manager,
			parent,
			MIconToggle(
				icons.closeIcon,
				concat(
					[MToggleFalseIcon(icons.openIcon, iconStyle)],
					ifArrayPush(iconStyle, narrowHeader, MIconButtonBorder(0.))
				),
				[
					MToggleValue(expander),
					enabledState,
					FAccessAttribute("aria-expanded", fselect(expander, FLift(b2s))),
					FAccessAttribute("aria-controls", const(expanderId)),
					FAccessAttribute("aria-pressed", const("")),
					buttonTitle
				]
			),
			m2t
		))
		|> (\f ->
			if (narrowHeader) f
			else if (front) TBorderDir(0.0, 0.0, 16.0, 0.0, f, isRTL)
			else TBorderDir(16.0, 0.0, 0.0, 0.0, f, isRTL)
		)
		|> (\f -> TShow(icons.show, f));


	combinerL = if (isRTL) TAlignRight else TAlignLeft;
	combinerR = if (isRTL) TAlignLeft else TAlignRight;

	colsFn = extractStruct(style, MExpanderHeaderCustomColsFn(\header, arrow, __ -> TBaselineCols2Dir(header, arrow, isRTL))).fn;

	contents =
		if (front) {
			if (mWidth != 0.) {
				switch (align : CommonAlignment) {
					StartAlign() : colsFn(icon, c, isRTL) |> combinerL;
					EndAlign() : colsFn(icon |> combinerL, c, isRTL);
					LeftAlign() : colsFn(icon, c |> TAlignLeft, isRTL);
					RightAlign() : colsFn(icon, c |> TAlignRight, isRTL);
					CenterAlign() : TCenterX(colsFn(icon, c, isRTL));
				}
			} else {
				switch (align : CommonAlignment) {
					StartAlign() : colsFn(icon, c, isRTL) |> (\t -> TAlignStartDir(t, isRTL));
					EndAlign() : colsFn(icon |> combinerL, c, isRTL);
					LeftAlign() : colsFn(icon, c, isRTL);
					RightAlign() : colsFn(icon, c |> TAlignRight, isRTL);
					CenterAlign() : TCenterX(colsFn(icon, c, isRTL));
				}
			}
		} else {
			if (mWidth != 0.) {
				switch (align : CommonAlignment) {
					StartAlign() : colsFn(c |> combinerL, icon, isRTL);
					EndAlign() : colsFn(c, icon, isRTL) |> combinerR;
					LeftAlign() : colsFn(c |> TAlignLeft, icon, isRTL);
					RightAlign() : colsFn(c |> TAlignRight, icon, isRTL);
					CenterAlign() : TCenterX(colsFn(c, icon, isRTL));
				}
			} else {
				switch (align : CommonAlignment) {
					StartAlign() : colsFn(c, icon, isRTL) |> (\t -> TAlignStartDir(t, isRTL));
					EndAlign() : colsFn(c, icon, isRTL) |> combinerR;
					LeftAlign() : colsFn(c, icon, isRTL);
					RightAlign() : colsFn(c, icon, isRTL) |> TAlignRight;
					CenterAlign() : TCenterX(colsFn(c, icon, isRTL));
				}
			}
		};

	contents
	|> (\f ->
		if (expandByArrowOnly)
			f
		else
			TCopySize(
				f,
				\t ->
					mouseDownAround(
						manager,
						None(),
						make(false),
						make(false),
						\ -> if (fgetValue(enabledState.enabled)) reverseBehaviour(expander),
						t
					),
				false
			)
	)
}