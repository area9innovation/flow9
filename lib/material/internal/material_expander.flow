import material/internal/material_button;

export {
	MExpander2T(manager : MaterialManager, parent : MFocusGroup, m : MExpander, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MExpander2T(manager : MaterialManager, parent : MFocusGroup, m : MExpander, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	header = m.header;
	expander = m.expander;
	details = m.details;

	hideSeparator = contains(m.style, MExpanderHideSeparator());
	noDispose = contains(m.style, MNoDispose());
	bgStyle = extractStruct(m.style, MExpanderBackgroundStyle([], []));
	animated = contains(m.style, MDetailsAnimated());
	mWidth = extractStruct(m.style, MWidth(-1.0)).width;

	align = fold(m.style, StartAlign(), \acc, st -> {
		switch (st : MExpanderStyle) {
			LeftAlign() : st;
			CenterAlign() : st;
			RightAlign() : st;
			EndAlign() : st;
			default : acc;
		};
	});

	TCopySize2(
		MExpanderHeader(manager, parent, m2t(header, parent), expander, m.style, align, mWidth, m2t),
		\tr, hd ->
			TLines2(
				hd
				|> (\f -> if (bgStyle.header != []) TGroup2(TRounded(2., 2., 0., 0., bgStyle.header, tr), f) else f),
				(\p ->
					m2t(details(), p)
					|> TAlignHorizontalRTL(align, p.theme.rtl)
					|> (\f -> if (bgStyle.details != []) TCopySize(f, \tr2 -> TRounded(0., 0., 2., 2., bgStyle.details, tr2), false) else f)
					|> (\f ->
						if (hideSeparator)
							f
						else
							TLines2(
								MSeparator2T(manager, parent, true)
								|> (\f2 -> TAvailable(f2, TGroup2(TWidth(tr), TFixed(0., 1.)))),
								f
							)
					)
				)
				|> (\f ->
					if (animated)
						TAnimatedExpander(manager, parent, f(parent), expander, [MExpandFromStart()])
					else if (noDispose)
						MRenderOnce2T(manager, parent, expander, f)
					else
						MSelect2T(
							manager,
							parent,
							"MExpanderContent",
							[],
							expander,
							\ex, p ->
								if (ex)
									f(p)
								else
									TEmpty()
						)
				)
			)
	)
		|> (\t -> if (mWidth > 0.) TAvailableWidth(t, const(mWidth)) else t)
}

MExpanderHeader(
	manager : MaterialManager,
	parent : MFocusGroup,
	c : Tropic,
	expander : DynamicBehaviour<bool>,
	style : [MExpanderStyle],
	align : CommonAlignment,
	mWidth : double,
	m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {

	expandByArrowOnly = contains(style, MExpandByArrowOnly());
	arrowColor = extractStruct(style, if (getLightBackground(parent)) MGrey(600) else MWhite());
	arrowOpacity = extractStruct(style, FillOpacity(0.54));
	enabledState : MEnabled = extractStruct(style, MEnabled(const(true)));
	narrowHeader = contains(style, MExpanderNarrowHeader());
	front = contains(style, MExpanderFront());
	icons = extractStruct(style, MExpanderIcon("keyboard_arrow_down", "keyboard_arrow_up"));

	iconStyle : [MIconStyle] = [MIconSize(24.0), arrowColor, arrowOpacity];
	isRTL = parent.theme.rtl;

	icon =
		MIconToggle2T(
			manager,
			parent,
			MIconToggle(
				icons.closeIcon,
				concat(
					[MToggleFalseIcon(icons.openIcon, iconStyle)],
					ifArrayPush(iconStyle, narrowHeader, MIconButtonBorder(0.))
				),
				[MToggleValue(expander), enabledState, FAccessAttribute("aria-expanded", fif(expander, const("true"), const("false")))]
			),
			m2t
		)
		|> (\f ->
			if (narrowHeader) f
			else if (front) TBorderRTL(0.0, 0.0, 16.0, 0.0, f, isRTL)
			else TBorderRTL(16.0, 0.0, 0.0, 0.0, f, isRTL)
		);

	
	combinerL = if (isRTL) TAlignRight else TAlignLeft;
	combinerR = if (isRTL) TAlignLeft else TAlignRight;

	contents =
		if (front) {
			if (mWidth != 0.) {
				switch (align : CommonAlignment) {
					StartAlign() : TBaselineCols2RTL(icon, c, isRTL) |> combinerL;
					EndAlign() : TBaselineCols2RTL(icon |> combinerL, c, isRTL);
					LeftAlign() : TBaselineCols2RTL(icon, c |> TAlignLeft, isRTL);
					RightAlign() : TBaselineCols2RTL(icon, c |> TAlignRight, isRTL);
					CenterAlign() : TCenterX(TBaselineCols2RTL(icon, c, isRTL));
				}
			} else {
				switch (align : CommonAlignment) {
					StartAlign() : TBaselineCols2RTL(icon, c, isRTL) |> (\t -> TAlignStartRTL(t, isRTL));
					EndAlign() : TBaselineCols2RTL(icon |> combinerL, c, isRTL);
					LeftAlign() : TBaselineCols2RTL(icon, c, isRTL);
					RightAlign() : TBaselineCols2RTL(icon, c |> TAlignRight, isRTL);
					CenterAlign() : TCenterX(TBaselineCols2RTL(icon, c, isRTL));
				}
			}
		} else {
			if (mWidth != 0.) {
				switch (align : CommonAlignment) {
					StartAlign() : TBaselineCols2RTL(c |> combinerL, icon, isRTL);
					EndAlign() : TBaselineCols2RTL(c, icon, isRTL) |> combinerR;
					LeftAlign() : TBaselineCols2RTL(c |> TAlignLeft, icon, isRTL);
					RightAlign() : TBaselineCols2RTL(c |> TAlignRight, icon, isRTL);
					CenterAlign() : TCenterX(TBaselineCols2RTL(c, icon, isRTL));
				}
			} else {
				switch (align : CommonAlignment) {
					StartAlign() : TBaselineCols2RTL(c, icon, isRTL) |> (\t -> TAlignStartRTL(t, isRTL));
					EndAlign() : TBaselineCols2RTL(c, icon, isRTL) |> combinerR;
					LeftAlign() : TBaselineCols2RTL(c, icon, isRTL);
					RightAlign() : TBaselineCols2RTL(c, icon, isRTL) |> TAlignRight;
					CenterAlign() : TCenterX(TBaselineCols2RTL(c, icon, isRTL));
				}
			}
		};

	contents
	|> (\f ->
		if (expandByArrowOnly)
			f
		else
			TCopySize(
				f,
				\t ->
					mouseDownAround(
						manager,
						None(),
						make(false),
						make(false),
						\ -> if (fgetValue(enabledState.enabled)) reverseBehaviour(expander),
						t
					),
				false
			)
	)
}