import material/internal/material_ellipsis;
import material/internal/material_ripple;

export {
	MGraphicsState : (
		strokeWidth : double,
		color : int,
		opacity : double,
		ripple : bool
	);

	graphicsStateLerp(
		prev : MGraphicsState,
		current : MGraphicsState,
		percent : double) -> MGraphicsState;

	MOutline(
		manager : MaterialManager,
		parent : MFocusGroup,
		size : Transform<WidthHeight>,
		labelWidth : Transform<double>,
		progress : Transform<double>,
		graphicsState : Transform<MGraphicsState>,
		dynamicColor : DynamicBehaviour<MThemeColor>) -> Tropic;

	addMOutline(
		manager : MaterialManager,
		parent : MFocusGroup,
		labelWidth : Transform<double>,
		progress : Transform<double>,
		graphicsState : Transform<MGraphicsState>,
		dynamicColor : DynamicBehaviour<MThemeColor>) -> (Tropic) -> Tropic;

	addMFilledUnderline(
		manager : MaterialManager,
		parent : MFocusGroup,
		graphicsState : Transform<MGraphicsState>,
		dynamicColor : DynamicBehaviour<MThemeColor>) -> (Tropic) -> Tropic;

	MFloatingLabelState(
		progress : DynamicBehaviour<double>,
		showFloatingLabel : Transform<bool>,
		labelSizeFocused : double,
		labelSizeUnfocused : double,
		xMinOffset : double,
		xOffsetDelta : double,
		yMinOffset : double,
		yOffsetDelta : double
	);

	makeMFloatingLabel(
		manager : MaterialManager,
		parent : MFocusGroup,
		label : string,
		state : MFloatingLabelState,
		style : Transform<[CharacterStyle]>,
		dynColor : Maybe<MDynamicColor>,
		widthInspect : DynamicBehaviour<double>,
		m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

skinAnimationDuration = easingTransitionDuration;

addMOutline(
	manager : MaterialManager,
	parent : MFocusGroup,
	labelWidth : Transform<double>,
	progress : Transform<double>,
	graphicsState : Transform<MGraphicsState>,
	dynamicColor : DynamicBehaviour<MThemeColor>) -> (Tropic) -> Tropic {

	\tr -> {
		size = makeWH();
		baseline = make(0.);
		
		TGroup2SameMetrics(
			TInspect([ISize(size), IBaseline(baseline)], tr),
			MOutline(
				manager,
				parent,
				size,
				labelWidth,
				progress,
				graphicsState,
				dynamicColor
			)
		)
		|> (\t -> TBaseline(baseline, t))
	}
}

MOutline(
	manager : MaterialManager,
	parent : MFocusGroup,
	size : Transform<WidthHeight>,
	labelWidth : Transform<double>,
	progress : Transform<double>,
	graphicsState : Transform<MGraphicsState>,
	dynamicColor : DynamicBehaviour<MThemeColor>) -> Tropic {

	outlineState = make(fgetValue(graphicsState));

	stateWithLast =
		fselectWithLast(
			graphicsState,
			FLift2(\prev, curr -> Pair(prev, curr))
		);

	shape = getShapeByComponent(parent, "outlined text field");
	topLeft = parseMaterialShapeCorner(shape.tl);
	topRight = parseMaterialShapeCorner(shape.tr);
	bottomRight = parseMaterialShapeCorner(shape.br);
	bottomLeft = parseMaterialShapeCorner(shape.bl);

	MEasingAnimation(
		manager,
		parent,
		const(skinAnimationDuration),
		const(easingStandartCurve),
		stateWithLast,
		\st -> Some(st),
		\st, percent -> {
			prev = st.first;
			curr = st.second;
			grState = graphicsStateLerp(prev, curr, percent);
			next(outlineState, grState);
			nextDistinct(dynamicColor, MCustomColor(grState.color));
		},
		[],
		TSelect3(outlineState, fpair(labelWidth, progress), size, \st, label, wh -> {
			a = if (label.first == 0.) 0. else label.first + 8.0;
			b = label.second / 2.0;
			c = 8.0;

			gapStart = a * b + c;
			gapEnd = a * (1. - b) + c;

			makeStaticOutline(
				st,
				if (parent.rtl) Pair(wh.width - gapEnd, wh.width - gapStart) else Pair(gapStart, gapEnd),
				wh,
				MOutlineCorners(topLeft, topRight, bottomRight, bottomLeft)
			)
		})
	);
}

makeStaticOutline(
	st : MGraphicsState,
	gap : Pair<double, double>,
	size : WidthHeight,
	corners : MOutlineCorners) -> Tropic {

	w = size.width;
	h = size.height;
	
	style = [Stroke(st.color), StrokeWidth(st.strokeWidth), StrokeOpacity(st.opacity)];

	if (w == 0.0 || h == 0.0) {
		TEmpty();
	} else {
		topLeftR = corners.getTopLeft(size);
		topRightR = corners.getTopRight(size);
		bottomRightR = corners.getBottomRight(size);
		bottomLeftR = corners.getBottomLeft(size);

		r1 = topLeftR.first;
		r2 = topRightR.first;
		r3 = bottomRightR.first;
		r4 = bottomLeftR.first;

		sqrt2 = sqrt(2.0);
		n1 = 1.0 / sqrt2;
		n2 = sqrt2 - 1.0;

		TGraphics(
			concatA([
				[
					MoveTo(r1, 0.0)
				],
				if (gap.first == gap.second) {
					[];
				} else {
					[
						LineTo(gap.first, 0.0),
						MoveTo(gap.second, 0.0)
					];
				},
				[
					LineTo(w - r2, 0.0)
				],
				if (topRightR.second && r2 > 0.0)
					[
						CubicBezierTo(
							(n1 * r2) + w - r2,
							(-n1 * r2) + r2,
							(n2 * r2) + w - r2,
							(-r2)  + r2
						),
						CubicBezierTo(
							w,
							r2,
							w,
							-n2 * r2 + r2
						)

					]
				else
					[
						LineTo(w, r2)
					],
				[
					LineTo(w, h - r3)
				],
				if (bottomRightR.second && r3 > 0.0)
					[
						CubicBezierTo(
							(n1 * r3) + w - r3,
							(n1 * r3) + h - r3,
							w,
							(n2 * r3) + h - r3,
						),
						CubicBezierTo(
							w - r3,
							h,
							(n2 * r3) + w - r3,
							h
						)
					]
				else
					[
						LineTo(w - r3, h)
					],
				[
					LineTo(r4, h)
				],
				if (bottomLeftR.second && r4 > 0.0)
					[
						CubicBezierTo(
							-n1 * r4 + r4,
							n1 * r4 + h - r4,
							(-n2 * r4) + r4,
							h
						),
						CubicBezierTo(
							0.0,
							h - r4,
							0.0,
							(n2 * r4) + h - r4,
						)
					]
				else
					[
						LineTo(0.0, h - r4)
					],
				[
					LineTo(0.0, r1)
				],
				if (topLeftR.second && r1 > 0.0)
					[
						CubicBezierTo(
							(-n1 * r1) + r1,
							(-n1 * r1) + r1,
							0.0,
							(-n2 * r1) + r1
						),
						CubicBezierTo(
							r1,
							0.0,
							(-n2 * r1) + r1,
							0.0
						)
					]
				else
					[
						LineTo(r1, 0.0)
					]
			]),
			style
		)
	}
}

graphicsStateLerp(
	prev : MGraphicsState,
	current : MGraphicsState,
	percent : double) -> MGraphicsState {

	MGraphicsState(
		lerp(prev.strokeWidth, current.strokeWidth, percent),
		colorLerp(prev.color, current.color, percent),
		lerp(prev.opacity, current.opacity, percent),
		current.ripple
	);
}

addMFilledUnderline(
	manager : MaterialManager,
	parent : MFocusGroup,
	graphicsState : Transform<MGraphicsState>,
	dynamicColor : DynamicBehaviour<MThemeColor>) -> (Tropic) -> Tropic {

	width = make(0.);
	\t ->
		TBaselineLines2(
			TAttachWidth(t, width),
			MFilledUnderline(manager, parent, width, graphicsState, dynamicColor)
		)
}

MFilledUnderline(
	manager : MaterialManager,
	parent : MFocusGroup,
	width : DynamicBehaviour<double>,
	graphicsState : Transform<MGraphicsState>,
	dynamicColor : DynamicBehaviour<MThemeColor>) -> Tropic {

	initState       = fgetValue(graphicsState);
	backgroundState = make(initState);
	rippleState     = make(None());
	rippleTrigger   = make(false);

	stateWithLast =
		fselectWithLast(
			graphicsState,
			FLift2(\prev, curr -> Pair(prev, curr))
		);

	MEasingAnimation(
		manager,
		parent,
		const(skinAnimationDuration),
		const(easingStandartCurve),
		stateWithLast,
		\st -> {
			// Swap buffers
			maybeApply(getValue(rippleState), \s -> {
				next(backgroundState, s);
				next(rippleState, None());
				next(rippleTrigger, false);
			});

			if (st.second.ripple) {
				next(rippleState, Some(st.second));
				deferred(\ -> next(rippleTrigger, true));
				None();
			} else {
				Some(st);
			}
		},
		\st, percent -> {
			prev = st.first;
			curr = st.second;
			next(backgroundState, graphicsStateLerp(prev, curr, percent));
		},
		[],
		TGroup2(
			TSelect(backgroundState, \st -> if (st.opacity == 0.0) {
				TEmpty();
			} else if (st.strokeWidth == 0.0) {
				TDynamicGraphics(
					fselect(width, FLift(\w -> 
						generate(0, floor(w / 4.0), \i ->
							[MoveTo(i2d(i) * 4.0, 0.0), LineTo(i2d(i) * 4.0 + 1.0, 0.0)]
						) |> concatA
					)),
					const([Stroke(st.color), StrokeOpacity(st.opacity)])
				)
			} else {
				TRectangle(
					[Fill(st.color), FillOpacity(st.opacity)],
					TFillXH(st.strokeWidth)
				);
			})
				|> (\f -> TTweak([TAlign(const(0.0), const(1.0))], f, TFixed(0.0, 2.0))),
			TSelect(rippleState, \st ->
				eitherMap(
					st,
					\s ->
						MRipple2T(
							manager,
							parent,
							TFillXH(s.strokeWidth),
							rippleTrigger,
							[
								MRippleColor(const(MCustomColor(s.color))),
								MRippleOpacity(const(1.0)),
								MRippleType(const(MRippleFill())),
								MRipplePosition(fselect(width, FLift(\w -> Point(w / 2.0, s.strokeWidth / 2.0))))
							]
						),
					TEmpty()
				)
			)
		)
		|> (\t -> TConstruct([
			makeSubscribe2(backgroundState, \st -> nextDistinct(dynamicColor, MCustomColor(st.color))),
			makeSubscribe2(rippleState, \rSt -> maybeApply(rSt, \st -> nextDistinct(dynamicColor, MCustomColor(st.color))))
		], t))
	);
}

makeMFloatingLabel(
	manager : MaterialManager,
	parent : MFocusGroup,
	label : string,
	state : MFloatingLabelState,
	style : Transform<[CharacterStyle]>,
	dynColor : Maybe<MDynamicColor>,
	widthInspect : DynamicBehaviour<double>,
	m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {

	progress = state.progress;
	xOffset = fselect(progress, FLift(\pr -> (state.xMinOffset + pr * state.xOffsetDelta) * (if (parent.rtl) -1. else 1.)));
	yOffset = fselect(progress, FLift(\pr -> state.yMinOffset + pr * state.yOffsetDelta));
	labelSize = flerp(state.labelSizeFocused, state.labelSizeUnfocused, progress);

	labelStyle : Transform<[MTextStyle]> =
		fselect2(labelSize, style, FLift2(\sz, st ->
			maybeArrayPush(
				MCharacterStyle2MTextStyle(replaceStruct(st, FontSize(sz))),
				dynColor
			)
		));

	MEasingAnimation(
		manager,
		parent,
		const(skinAnimationDuration),
		const(easingStandartCurve),
		state.showFloatingLabel,
		\fl -> {
			pr = getValue(progress);

			if ((fl && pr == 0.) || (!fl && pr == 1.))
				None()
			else
				Some(fl)
		},
		\fl, prc -> next(progress, if (fl) 1. - prc else prc),
		[MAutoStart()],
		TSelect(labelStyle, \st ->
			MEllipsisText2T(
				manager,
				parent,
				MEllipsisText(label, st),
				m2t
			)
		)
		|> (\t -> TAttachWidth(t, widthInspect))
		|> (\t -> TTranslate(fpoint(xOffset, yOffset), t))
		|> (\t -> TAlignStartDir(t, parent.rtl))
	)
}

MOutlineCorners(
	getTopLeft : (WidthHeight) -> Pair<double, bool>,
	getTopRight : (WidthHeight) -> Pair<double, bool>,
	getBottomRight : (WidthHeight) -> Pair<double, bool>,
	getBottomLeft : (WidthHeight) -> Pair<double, bool>
);