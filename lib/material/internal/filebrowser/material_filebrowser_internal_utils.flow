import ds/tuples;
import string;
import formats/json/json;

export {
	FBContentPath(
		item : ?,
		propertiesR : ref Tree<string, Json>,
		isFolder : bool,
		dirPath : string, 	// path without the item name and ends with `/`
		filename : string 	// name with extension
	);

	addSlash2pathEnd(path : string) -> string;
	split2Name_Path(fullPath : string) -> Pair<string /*name*/, string /*path*/>;
	getItemFullname(item : FBContentPath<?>) -> string;
}

addSlash2pathEnd(path : string) -> string {
	if (path == "") ""
	else if (endsWith(path, "/")) path
	else path + "/"
}

split2Name_Path(fullPath : string) -> Pair<string /*name*/, string /*path*/> {
	if (fullPath == "") Pair("", "")
	else {
		fullPath2 = if (endsWith(fullPath, "/")) rtrim2(fullPath, "/") else fullPath;

		lastSlashIdx = strLastIndexOf(fullPath2, "/");

		if (lastSlashIdx == -1) {
			if (endsWith(fullPath2, ":"))
				Pair("", fullPath2 + "/")
			else
				Pair("", "")
		} else {
			Pair(
				strRight(fullPath2, lastSlashIdx + 1),
				strLeft(fullPath2, lastSlashIdx) + "/"
			)
		}
	}
}

getItemFullname(item : FBContentPath<?>) -> string {
	item.dirPath + item.filename + (if (item.isFolder) "/" else "");
}
