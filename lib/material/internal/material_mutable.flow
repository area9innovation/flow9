import material/internal/material_focus;
import material/internal/material_theme;
import material/measing;
import tropic/tropic_constant;

export {
	MSelect2T(manager : MaterialManager, parent : MFocusGroup, name : string, state : [MComponentGroupState], value : Transform<?>,
		fn : (?, MFocusGroup) -> Tropic) -> Tropic;
	MIfLazy2T(manager : MaterialManager, parent : MFocusGroup, value : Transform<bool>, fn : (bool, MFocusGroup) -> Tropic, sameSize : bool) -> Tropic;
	TIfLazy2(value : Transform<bool>, fn : (bool) -> Tropic, sameSize : bool) -> Tropic;

	MExplicitTheme2T(manager : MaterialManager, parent : MFocusGroup, state : [MComponentGroupState], light : Transform<bool>, fn : (MFocusGroup) -> Tropic) -> Tropic;

	MComponentGroup2T(manager : MaterialManager, parent : MFocusGroup, name : string, state : [MComponentGroupState], content : (MFocusGroup) -> Tropic) -> Tropic;
	extractDynamicBehaviours(v : Transform<?>, name : string, type : IScriptRecordType) -> [IScriptBehaviour];
	extractDynamicBehavioursEx(v : Transform<?>, name : string, type : IScriptRecordType) -> [IScriptBehaviour];

	MRenderOnce2T(manager : MaterialManager, parent : MFocusGroup, condition : Transform<bool>, content : (MFocusGroup) -> Tropic) -> Tropic;
	MIfPreRender2T(manager : MaterialManager, parent : MFocusGroup, condition : Transform<bool>, thenContent : (MFocusGroup) -> Tropic,
		elseContent : (MFocusGroup) -> Tropic, sameSize : bool) -> Tropic;
	MShowPreRender2T(manager : MaterialManager, parent : MFocusGroup, condition : Transform<bool>, fn : (MFocusGroup) -> Tropic) -> Tropic;

	TSubscribe2(v : Transform<?>, fn : (?) -> Tropic) -> Tropic;

	// TShow which creates tropic only once and only when show is true
	MShow2T(manager : MaterialManager, parent : MFocusGroup, show : Transform<bool>, tr : (MFocusGroup) -> Tropic) -> Tropic;

	MDynamicGroup2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicGroup2, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;

	// Shows TPositionScale only when enabled is true
	TPositionScaleEnabled(positionScale : DynamicBehaviour<PositionScale>, content : Tropic, enabled : Transform<bool>) -> Tropic;
	// Shows TPositionScale and attaches boxWH only when enabled is true
	MPositionScaleAttachBox(positionScale : DynamicBehaviour<PositionScale>, boxWH : DynamicBehaviour<WidthHeight>,
		content : Tropic, enabled : Transform<bool>) -> Tropic;

	extractMComponentGroupState(state : [flow]) -> [MComponentGroupState];

	// radius = None() is for radius by content
	MHintMarker(manager : MaterialManager, focus : MFocus, radius : Maybe<double>) -> (Tropic) -> Tropic;
}

MIfLazy2T(manager : MaterialManager, parent : MFocusGroup, value : Transform<bool>, fn : (bool, MFocusGroup) -> Tropic, sameSize : bool) -> Tropic {
	if (isFConst(value)) {
		fn(fgetValue(value), parent)
	} else {
		tTrue : ref Maybe<TAcc> = ref None();
		tFalse : ref Maybe<TAcc> = ref None();
		uns = ref [];

		(\p -> TransformTAcc2(\t2a -> {
			ba =
				fselect(value, FLift(\v ->
					onlyOnce(
						if (v)
							tTrue
						else
							tFalse,
						\ -> {
							ba = t2a(fn(v, p));
							uns := concat(^uns, ba.disposers);
							ba;
						}
					)
				));

			TAcc(
				FMutable(fselect(ba, extractFormFromTAccFLift())),
				TFormMetrics(
					fsubselect(ba, extractMetrictWidthFLift()),
					fsubselect(ba, extractMetrictHeightFLift()),
					fsubselect(ba, extractMetrictBaselineFLift())
				),
				fsubselect(ba, extractMetrictMinWidthFLift()),
				fsubselect(ba, extractMetrictMinHeightFLift()),
				fsubselect(ba, extractMetrictMaxWidthFLift()),
				fsubselect(ba, extractMetrictMaxHeightFLift()),
				fsubselect(ba, extractMetrictXFillersFLift()),
				fsubselect(ba, extractMetrictYFillersFLift()),
				fsubselect(ba, extractMetrictAddFillersFLift()),
				false,
				false,
				[\ -> {
					tTrue := None();
					tFalse := None();
					applyall(^uns);
					uns := [];
				}]
			)
		}))
		|> (\f ->
			MComponentGroup2T(
				manager,
				parent,
				"MIfLazy",
				[],
				f
			)
		)
		|> if (sameSize) makeTropicConstant(None(), None()) else idfn
	}
}

TIfLazy2(value : Transform<bool>, fn : (bool) -> Tropic, sameSize : bool) -> Tropic {
	TIfLazy(value, fn)
	|> if (sameSize) makeTropicConstant(None(), None()) else idfn
}

MSelect2T(manager : MaterialManager, parent : MFocusGroup, name : string, state : [MComponentGroupState], value : Transform<?>,
	fn : (?, MFocusGroup) -> Tropic) -> Tropic {
	if (isFConst(value))
		MComponentGroup2T(manager, parent, name, state, \p -> fn(fgetValue(value), p))
	else
		MComponentGroup2T(
			manager,
			parent,
			name,
			/*concat(*/state/*, extractDynamicBehaviours(value, "value", MLowLevelChanges()))*/,
			\p -> TSelect(value, \v -> fn(v, p))
		)
}

MExplicitTheme2T(manager : MaterialManager, parent : MFocusGroup, state : [MComponentGroupState], light : Transform<bool>,
	fn : (MFocusGroup) -> Tropic) -> Tropic {
	name = extractStruct(state, MFocusName("Theme")).name;

	TSelect(light, \lght -> {
		MComponentGroup2T(manager, parent, (if (lght) "Light" else "Dark") + name, replaceStruct(state, MLightBackground(lght)), fn)
	})
}

childActiveFixDisabled = isUrlParameterTrue("child_active_fix_disabled");

MComponentGroup2T(manager : MaterialManager, parent : MFocusGroup, name : string, state : [MComponentGroupState], content : (MFocusGroup) -> Tropic) -> Tropic {
	if (fgetValue(parent.id) != -2) {
		enabled = extractStruct(state, MEnabled(const(true))).enabled;
		focusEnabled = extractStruct(state, MFocusEnabled(const(true))).enabled;
		clickEnabled = extractStruct(state, MClickEnabled(const(true))).enabled;
		keysEnabled = extractStruct(state, MKeysEnabled(const(true))).enabled;
		hintMarkerEnabled = extractStruct(state, MHintMarkerEnabled(parent.hintMarkerEnabled)).enabled;
		monitorBehaviours = extractStructMany(state, IScriptBehaviour("", make(flow(false)), ISMiscEvent()));
		monitorEnabled = extractStruct(state, IScriptRecordingEnabled(const(true))).enabled;
		iScriptId = eitherMap(tryExtractStruct(state, IScriptId("")), \uid -> Some(uid.id), None());
		tabOrder = eitherMap(tryExtractStruct(state, MFocusId(const(-1))), \fid -> Some(fid.id), None());
		vertical = eitherMap(tryExtractStruct(state, MFocusVertical(false)), \v -> Some(v.vertical), None());
		horizontal = eitherMap(tryExtractStruct(state, MFocusHorizontal(false)), \v -> Some(v.horizontal), None());
		scrollInfo = tryExtractStruct(state, MScrollInfo(makePoint(), const(zeroWH), const(false), const(false)));
		focused = extractStruct(state, MFocused(make(false))).focused;
		accessRole = extractStruct(state, AccessRole("")).role;
		accessAttributes = extractStructMany(state, FAccessAttribute("", const("")));
		isInitialized = accessRole != "" || exists(accessAttributes, \attr -> attr.name == "role" && fgetValue(attr.value) != "");
		zorder = eitherMap(tryExtractStruct(state, MZorder(const(0))), \zo -> Some(zo.zorder), None());
		parentTheme = extractStruct(state, parent.theme);
		lightBackground = extractStruct(state, MLightBackground(parentTheme.palette.light)).light;
		lightTheme = MLightTheme(parentTheme, lightBackground);
		theme = extractStruct(state, UpdateMaterialTheme(idfn)).fn(lightTheme);
		active = extractStruct(state, MActive(make(false))).active;
		childActive = tryExtractStruct(state, MChildActive(make(false)));
		setParent = tryExtractStruct(state, MSetParent(const(None())));
		getParent = tryExtractStruct(state, MGetParent(make(None())));
		hovering = tryExtractStruct(state, THovering(make(false)));
		rtl = extractStruct(state, MSetRTL(parent.rtl)).rtl;
		focusName = extractStruct(state, MFocusName(name)).name;
		ready = extractStruct(state, MSetReady(const(true))).ready;
		isReady = tryExtractStruct(state, MIsReady(make(true)));
		focusOnPrevious = extractStruct(state, MFocusOnPrevious(false)).enabled;

		(\p -> addMaterialFocusGroupId(
			manager, p, focusName, isInitialized, vertical, horizontal, scrollInfo, ready, focused, active, enabled, focusEnabled,
			monitorBehaviours, monitorEnabled, clickEnabled, keysEnabled, hintMarkerEnabled, iScriptId, tabOrder, zorder, theme, rtl,
			\f -> {
				maybeApply(getParent, \p2 -> next(p2.parent, Some(f)));

				MComponent2TAccessibility(
					manager,
					content(f),
					f,
					extractFAccessProperties(state)
					|> (\f2 ->
						eitherMap(
							f.iScriptId,
							\id -> arrayPush(f2, FAccessAttribute("id", const(id))),
							f2
						)
					)
				)
				|> (\f2 ->
					eitherMap(
						childActive,
						\ac ->
							TConstruct(
								[
									\ -> fconnect(
										fselectdelay(fselect2(
											getCurrentFocusedItemBehaviour2(f),
											getCurrentActiveItemBehaviour2(f),
											FLift2(\foc, act -> {
												isSome(foc) ||
												isSome(act)
											})
										), \v -> if (v || (isSome(getCurrentFocusedItem(manager)) && (mobile || childActiveFixDisabled))) 0 else 33),
										ac.active
									)
								],
								f2
							),
						f2
					)
				)
				|> (\f2 ->
					eitherMap(
						hovering,
						\hv ->
							TConstruct(
								[
									\ -> fconnect(
										fselectdelay(
											fIsSome(getCurrentHoveredItemBehaviour2(f)),
											\v -> if (v || isSome(getCurrentFocusedItem(manager))) 0 else 33
										),
										hv.hovering
									)
								],
								f2
							),
						f2
					)
				)
				|> (\f2 ->
					eitherMap(
						isReady,
						\ir ->
							TConstruct(
								[
									\ -> fconnect(
										f.ready,
										ir.ready
									)
								],
								f2
							),
						f2
					)
				)
				|> (\f2 : Tropic ->
					if (focusOnPrevious)
						TConstruct(
							[
								makeSubscribe2(focused, \foc ->
									if (!foc) {
										maybeApply(getPreviousFocusedItem(manager), \pf ->
											deferred(\ -> if (fgetValue(pf.focusEnabled) && isNone(getCurrentFocusedItem(manager))) nextDistinct(pf.focused, true))
										);
									}
								)
							],
							f2
						)
					else
						f2
				);
			}
		))
		|> (\f -> eitherFn(setParent, \p -> TSelect(p.parent, \p2 -> eitherFn(p2, f, \ -> f(parent))), \ -> f(parent)))
	} else {
		content(parent)
	}
}

extractDynamicBehaviours(v : Transform<?>, name : string, type : IScriptRecordType) -> [IScriptBehaviour] {
	mapi(fgetDynamicBehaviours(v), \i, v0 -> IScriptBehaviour(name + if (i > 0) i2s(i) else "", v0, type))
}

// We have a problem with the extraction of DynamicBehaviour from Transform.
// If Transform contains FSelect2 it split into an array of independent DynamicBehaviours - as a result we get a wrong IScriptBehavior.
extractDynamicBehavioursEx(v : Transform<?>, name : string, type : IScriptRecordType) -> [IScriptBehaviour] {
	fgetDynamicBehavioursEx = \v0 : Transform<?> -> {
		switch (v0 : Transform<?>) {
			DynamicBehaviour(b, s): [v0];
			default : []
		}
	};

	mapi(fgetDynamicBehavioursEx(v), \i, v0 -> IScriptBehaviour(name + if (i > 0) i2s(i) else "", v0, type))
}

MRenderOnce2T(manager : MaterialManager, parent : MFocusGroup, condition : Transform<bool>, content : (MFocusGroup) -> Tropic) -> Tropic {
	MComponentGroup2T(manager, parent, "MRenderOnce", [MEnabled(condition)], \p -> TRenderOnce(condition, \ -> content(p)));
}

MIfPreRender2T(manager : MaterialManager, parent : MFocusGroup, condition : Transform<bool>, thenContent : (MFocusGroup) -> Tropic,
	elseContent : (MFocusGroup) -> Tropic, sameSize : bool) -> Tropic {
	if (isFConst(condition)) {
		if (fgetValue(condition))
			thenContent(parent)
		else
			elseContent(parent)
	} else {
		TGroup2(
			TVisible(
				condition,
				TCopySize2(
					MComponentGroup2T(manager, parent, "MIfPreRender true", [MEnabled(condition)], thenContent),
					\sz, tr -> TFixSize(tr, TShow(condition, sz))
				)
			),
			TVisible(
				fnot(condition),
				TCopySize2(
					MComponentGroup2T(manager, parent, "MIfPreRender false", [MEnabled(fnot(condition))], elseContent),
					\sz, tr -> TFixSize(tr, TShow(fnot(condition), sz))
				)
			)
		)
		|> if (sameSize) makeTropicConstant(None(), None()) else idfn
	}
}

MShowPreRender2T(manager : MaterialManager, parent : MFocusGroup, condition : Transform<bool>, fn : (MFocusGroup) -> Tropic) -> Tropic {
	if (isFConst(condition)) {
		if (fgetValue(condition))
			fn(parent)
		else
			TEmpty()
	} else {
		TVisible(
			condition,
			TCopySize2(
				MComponentGroup2T(manager, parent, "MShowPreRender", [MEnabled(condition)], fn),
				\sz, tr -> TFixSize(tr, TShow(condition, sz))
			)
		)
	}
}

TSubscribe2(v : Transform<?>, fn : (?) -> Tropic) -> Tropic {
	if (isFConst(v)) {
		// If we are constant, let's avoid the mutable altogether
		TEmpty()
	} else {
		tMutable = make(TEmpty());

		TConstruct(
			[
				\ -> {
					nextDistinct(tMutable, TEmpty());
					makeSubscribe2(v, \v0 -> next(tMutable, fn(v0)))();
				}
			],
			TMutable(tMutable)
		);
	}
}

MShow2T(manager : MaterialManager, parent : MFocusGroup, show : Transform<bool>, tr : (MFocusGroup) -> Tropic) -> Tropic {
	t : ref Maybe<Tropic> = ref None();

	MSelect2T(manager, parent, "MShow2T", [], show, \s, p -> {
		if (s) onlyOnce(t, \ -> tr(p)) else TEmpty()
	})
}

TPositionScaleEnabled(positionScale : DynamicBehaviour<PositionScale>, content : Tropic, enabled : Transform<bool>) -> Tropic {
	TCopySize(content, \tr -> TShowLazy(enabled, \ -> TPositionScale(positionScale, tr)), true)
}

MPositionScaleAttachBox(positionScale : DynamicBehaviour<PositionScale>, boxWH : DynamicBehaviour<WidthHeight>, content : Tropic,
	enabled : Transform<bool>) -> Tropic {
	TCopySize(content, \tr -> TShowLazy(enabled, \ -> TPositionScale(positionScale, TAttachBox2(tr, boxWH, TEmpty()))), true)
}

MDynamicGroup2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicGroup2, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	tStackChanges = make([]);

	TConstruct(
		[
			makeSubscribe(m.stackChanges, \grc : [MGroupChange] ->
				nextDistinct(
					tStackChanges,
					concat(
						getValue(tStackChanges),
						map(grc, \g ->
							switch (g : MGroupChange) {
								MGroupAdd(mat, z) : TGroupAdd(m2t(mat, parent), z);
								MGroupDelete(z) : TGroupDelete(z);
								MGroupMove(f, t) : TGroupMove(f, t);
								MGroupReplace(mat, z) : TGroupReplace(m2t(mat, parent), z);
							}
						)
					)
				)
			)
		],
		TDynamicGroup2(tStackChanges, ref map(^(m.currentStack), \mat -> m2t(mat, parent)), m.combiner)
	)
}

extractMComponentGroupState(state : [flow]) -> [MComponentGroupState] {
	filtermap(state, \st ->
		switch (st : flow) {
			MComponentGroupState(): {a : Maybe<MComponentGroupState> = Some(st); a};
			default: None();
		}
	)
}

MHintMarker(manager : MaterialManager, focus : MFocus, radius : Maybe<double>) -> (Tropic) -> Tropic {
	size = makeWH();
	innerRadius = eitherFn(radius, \r -> const(r), \ -> fdivide(fmax(fwidth(size), fheight(size)), const(2.)));

	percent = make(0.);

	wasClicked = make(false);
	show0 = make(true);
	show = fands([show0, focus.parent.hintMarkerEnabled, focus.enabled, fnot(wasClicked)], false);

	marker =
		TDynamicGraphics(
			fselect2(percent, innerRadius, FLift2(\prc, ir -> {
				or = lerp(ir, ir * 2., prc);
				DonutSectorPath(or, or, ir, or, 0., 360., false, false, true)
			})),
			fselect(percent, FLift(\prc -> [
				MThemeFill(focus, MAccentColor()),
				FillOpacity(1. - prc)
			]))
		)
		|> (\t -> MEasingAnimation(
			manager,
			focus.parent,
			const(1.),
			const(easingFastOutSlowIn),
			make(false),
			\v -> Some(v),
			\__, prc -> nextDistinct(percent, prc),
			[MAutoStart(), MLoop(\v -> Some(v)), MDelay(0.2)],
			t
		))
		|> (\t -> TShow(show, t))
		|> (\t -> TConstruct([
				makeSubscribe2(fOr(focus.focused, focus.hover), \f -> {
					nextDistinct(show0, !f)
				}),
				makeSubscribeTrigger(fnot(wasClicked), fOr(fOr(focus.down, focus.rightDown), focus.middleDown), \d -> {
					if (d) nextDistinct(wasClicked, true)
				})
			], t));

	\t -> TCopySize2(t, \sz, tr -> TGroup2(
		TTweak([TAlign(const(0.5), const(0.5)), TTweakNoLeftTopLimit()], marker, sz)
		|> (\t2 -> TFixSize(t2, TEmpty())),
		if (isNone(radius)) TAttachBox(tr, size) else tr
	))
}