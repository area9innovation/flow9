import material/internal/material_dragdrop;
import material/internal/selectmarray;

export {
	MFlexibleGrid2T(manager : MaterialManager, parent : MFocusGroup, m : MFlexibleGrid, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
	MReorderGrids2T(manager : MaterialManager, parent : MFocusGroup, m : MReorderGrids, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
	MReorderGrid2T(manager : MaterialManager, parent : MFocusGroup, items0 : [MReorderItem],
		order : DynamicBehaviour<[int]>, style : [MReorderGridStyle], m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
	MDynamicGrid2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicGrid, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MFlexibleGrid2T(manager : MaterialManager, parent : MFocusGroup, m : MFlexibleGrid, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	en = extractStruct(m.style, MEnabled(const(true))).enabled;
	enableAnimation = extractStruct(m.style, MAnimationEnabled(false)).enabled;
	ordered = extractStruct(m.style, MOrdered(true)).ordered;
	verticalGrid = contains(m.style, MVertical());
	wrapNewRow = extractStruct(m.style, MWrapNewRow(!verticalGrid)).wrap;
	wrapNewColumn = extractStruct(m.style, MWrapNewColumn(verticalGrid)).wrap;
	maxRows = extractStruct(m.style, MMaxRows(-1)).rows;
	lockWidth = extractStruct(m.style, MLockWidth(!verticalGrid)).lock;
	isSameSize = contains(m.style, TSameSize());
	alignBaselines = contains(m.style, TBaselineAlign());

	(\par -> TFlexibleGrid(
		fmap(m.items, \it -> m2t(it, par)),
		concatA([
			extractStructMany(m.style, CenterAlign()),
			extractStructMany(m.style, EndAlign()),
			extractStructMany(m.style, TFillersSupport()),
			extractStructMany(m.style, TightWidth()),
			extractStructMany(m.style, TFlexibleGridContentSizeInspector(makeWH())),
			extractStructMany(m.style, TFlexibleGridMaxContentWidthInspector(make(0.))),
			extractStructMany(m.style, TAvailableMinMax(zero, zero)),
			extractStructMany(m.style, TArrayThreshold(const(0))),
			extractStructMany(m.style, TFlexibleGridLazyRenderingMode(makeWH(), makePoint())),
			if (enableAnimation) [TAnimationTimer(manager.manager.deltaTimer)] else [],
			if (ordered) [TOrder(fmapi(m.items, \i, __ -> i))] else [],
			if (verticalGrid) [TVertical()] else [],
			if (maxRows > 0) [TMaxRows(maxRows)] else [],
			if (isSameSize) [TSameSize()] else [],
			if (alignBaselines) [TBaselineAlign()] else [],
			[
				TEnabled(en),
				TLockWidth(lockWidth),
				extractStruct(m.style, TSetRTL(par.rtl)),
				TWrapNewRow(wrapNewRow),
				TWrapNewColumn(wrapNewColumn)
			]
		])
	))
	|> (\f ->
		MComponentGroup2T(
			manager,
			parent,
			"MFlexibleGrid",
			[],
			f
		)
	)
}

MReorderGrid2T(manager : MaterialManager, parent : MFocusGroup, items : [MReorderItem],
	order : DynamicBehaviour<[int]>, style : [MReorderGridStyle], m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	en = extractStruct(style, MEnabled(const(true))).enabled;
	enableAnimation = extractStruct(style, MAnimationEnabled(true)).enabled;
	verticalGrid = contains(style, MVertical());
	wrapNewRow = extractStruct(style, MWrapNewRow(!verticalGrid)).wrap;
	wrapNewColumn = extractStruct(style, MWrapNewColumn(verticalGrid)).wrap;
	maxRows = extractStruct(style, MMaxRows(-1)).rows;
	lockWidth = extractStruct(style, MLockWidth(!verticalGrid)).lock;
	isSameSize = contains(style, TSameSize());
	alignBaselines = contains(style, TBaselineAlign());
	borders = extractStruct(style, MBorders(0.)).borders;
	grabDelay : [MDragDropStyle] = extractStructMany(style, MGrabDelay(0));
	customDraggingOrder = contains(style, MReorderCustomDraggingOrder());
	itemsLength = length(items) + if (customDraggingOrder) 2 else 1;
	draggingPlaceholder = map(extractStructMany(style, MReorderDropSpot(TEmpty())), \c -> MDraggingPlaceholder(c.spot));
	dragLowestPrioriry = contains(style, MDragLowestPriority());
	reorderOnDragEnd = contains(style, MReorderOnDragEnd());

	uniqueIds = ref makeTree();
	getUniqueIdByIndex = \index -> lookupTreeSet(uniqueIds, index, \__ -> makeTropicTag(manager));
	getIndexByUniqueId = \uniqueId -> either(findInOrder(^uniqueIds, \k, v -> if (v == uniqueId) Some(k) else None()), -1);
	onHover = generate(0, itemsLength, \__ -> make(-1));

	draggingItem = extractStruct(style, MDraggingItem(make(-1))).item;
	hoveringItem = extractStruct(style, MHoveringItem(make(-1))).item;

	contentSize = makeWH();
	orderIn = make(concat([itemsLength - 1], generate(0, itemsLength - 1, \i -> if (length(getValue(order)) > i) getValue(order)[i] else i)));
	edgeVisible = if (length(items) > 1) const(false) else if (length(items) > 0) fselect(onHover[0], FLift(\oh -> oh != -1)) else const(true);

	(\par -> TFlexibleGrid(
		mapi(
			items,
			\i, it -> {
				extractStruct(it.style, MReorderItemId(ref -1)).id := getUniqueIdByIndex(i);
				itEn = extractStruct(it.style, MEnabled(const(true))).enabled;
				isDragging : MDragging = extractStruct(it.style, MDragging(make(false)));

				MDragDrop2T(
					manager,
					par,
					MDragDrop(
						const(getUniqueIdByIndex(i)),
						it.content,
						concatA([
							[MHoveringId(onHover[i]), MDraggingEnabled(fand(en, itEn)), isDragging],
							grabDelay,
							draggingPlaceholder,
							if (dragLowestPrioriry) [MDragLowestPriority()] else []
						])
					),
					m2t
				)
				|> (\f -> TBorder4(borders, f))
				|> (\f ->
					TConstruct(
						[
							makeSubscribe2(onHover[i], \hov0 -> {
								hov = getIndexByUniqueId(hov0);

								if (hov != -1 && hov != i && hov >= 0 && hov < itemsLength) {
									order0 = getValue(orderIn);
									newOrder = removeIndex(order0, elemIndex(order0, hov, 0));

									nextDistinct(
										orderIn,
										insertArray(
											newOrder,
											if (i == itemsLength - 1)
												1
											else
												elemIndex(if (customDraggingOrder) newOrder else order0, i, 0),
											hov
										)
									);
								}
							})
						],
						f
					)
				)
			}
		)
		|> (\f ->
			if (customDraggingOrder)
				arrayPush(
					f,
					TRectangle(interactiveRectangleStyle, TFillXH(1.))
					|> (\f2 ->
						MDragDrop2T(
							manager,
							par,
							MDragDrop(
								const(getUniqueIdByIndex(itemsLength - 2)),
								f2,
								concat([MHoveringId(onHover[itemsLength - 2]), MDraggingEnabled(const(false))], grabDelay)
							),
							m2t
						)
					)
					|> (\f2 -> TShow(fnot(edgeVisible), f2))
				)
			else
				f
		)
		|> (\f ->
			arrayPush(
				f,
				TRectangle(interactiveRectangleStyle, TFillXH(48.))
				|> (\f2 ->
					MDragDrop2T(
						manager,
						par,
						MDragDrop(
							const(getUniqueIdByIndex(itemsLength - 1)),
							f2,
							concat([MHoveringId(onHover[itemsLength - 1]), MDraggingEnabled(const(false))], grabDelay)
						),
						m2t
					)
				)
				|> (\f2 -> TShow(edgeVisible, f2))
			)
		)
		|> const,
		concat3(extractStructMany(style, CenterAlign()), extractStructMany(style, EndAlign()), extractStructMany(style, TFillersSupport()))
		|> (\st -> if (enableAnimation) arrayPush(st, TAnimationTimer(manager.manager.deltaTimer)) else st)
		|> (\st -> if (verticalGrid) arrayPush(st, TVertical()) else st)
		|> (\st -> arrayPush(st, TWrapNewRow(wrapNewRow)))
		|> (\st -> arrayPush(st, TWrapNewColumn(wrapNewColumn)))
		|> (\st -> if (maxRows > 0) arrayPush(st, TMaxRows(maxRows)) else st)
		|> (\st -> if (alignBaselines) arrayPush(st, TBaselineAlign()) else st)
		|> (\st -> if (isSameSize) arrayPush(st, TSameSize()) else st)
		|> (\st -> arrayPush(st, TOrder(orderIn)))
		|> (\st -> arrayPush(st, TEnabled(en)))
		|> (\st -> arrayPush(st, TLockWidth(lockWidth)))
		|> (\st -> arrayPush(st, extractStruct(style, TSetRTL(par.rtl))))
		|> (\st -> arrayPush(st, TFlexibleGridContentSizeInspector(contentSize)))
	)
	|> (\f -> if (verticalGrid) f else TGroup2(f,
		TRectangle(interactiveRectangleStyle, TFillXHT(fheight(contentSize)))
		|> (\f2 -> {
			hoveringId = make(-1);

			MDragDrop2T(
				manager,
				par,
				MDragDrop(
					const(getUniqueIdByIndex(itemsLength)),
					f2,
					concat([MHoveringId(hoveringId), MDraggingEnabled(const(false))], grabDelay)
				),
				m2t
			)
			|> (\f3 -> TConstruct(
				[makeSubscribe2(hoveringId, \hov0 -> {
					hov = getIndexByUniqueId(hov0);
					if (hov != -1 && hov >= 0 && hov < itemsLength) {
						order0 = getValue(orderIn);
						newOrder = removeIndex(order0, elemIndex(order0, hov, 0));
						nextDistinct(orderIn, arrayPush(newOrder, hov));
					}
				})],
				f3
			))
		})
		|> (\f2 -> TBorderTDir(fwidth(contentSize), zero, zero, zero, f2, par.rtl))
	))
	|> (\f ->
		TConstruct(
			concat([
				makeSubscribe(manager.manager.dragging, \dr0 -> {
					dr = getIndexByUniqueId(dr0);
					nextDistinct(
						draggingItem,
						if (dr >= 0 && dr < itemsLength - 1)
							dr
						else
							-1
					);
				}),
				makeSubscribe(manager.manager.hovering, \hov0 -> {

					hov = getIndexByUniqueId(hov0);
					nextDistinct(
						hoveringItem,
						if (hov >= 0 && hov < itemsLength && getValue(manager.manager.dragging) != -1)
							hov
						else
							-1
					);
				}),
				\ -> fBidirectionalLink(
					orderIn,
					order,
					\o ->
						if (!reorderOnDragEnd)
							nextDistinct(order, tail(o)),
					\o ->
						nextDistinct(
							orderIn,
							concat([itemsLength - 1], generate(0, itemsLength - 1, \i -> if (length(o) > i) o[i] else i))
						)
				)
			],
			if (reorderOnDragEnd) [
				makeSubscribe(draggingItem, \di -> {
					if (di == -1) {
						nextDistinct(
							order,
							tail(fgetValue(orderIn))
						)
					}
				})
			] else []
			),
			f
		)
	))
	|> (\f ->
		MComponentGroup2T(
			manager,
			parent,
			"MReorderGrid",
			[IScriptBehaviour("order", order, ISInputValue([], "[int]", true))],
			f
		)
	)
}

MReorderGrids2T(manager : MaterialManager, parent : MFocusGroup, m : MReorderGrids, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	itemsLength : [int] = map(m.items, length);

	itemVisible : [[MReorderItemVisible]] =
		generate(0, length(m.items), \i -> {
			start = sum(subrange(itemsLength, 0, i));
			end = start + itemsLength[i];

			generate(0, sum(itemsLength), \j -> MReorderItemVisible(make(j >= start && j < end)))
		});

	reorderId : [[MReorderItemId]]=
		generate(0, length(m.items), \__ ->
			generate(0, sum(itemsLength), \__ -> MReorderItemId(ref -1))
		);

	items =
		mapi(m.items, \i, it -> {
			start = sum(subrange(itemsLength, 0, i));
			end = start + itemsLength[i];

			mapi(concatA(m.items), \j, it0 -> MReorderItem(it0.content, replaceStructMany(it0.style, [itemVisible[i][j], reorderId[i][j]])))
		});

	order = generate(0, length(m.items), \i -> make(
			if (length(getValue(m.order)) > i)
				getValue(m.order)[i]
			else {
				start = sum(subrange(itemsLength, 0, i));
				end = start + itemsLength[i];

				fold(generate(0, sum(itemsLength), \k -> k), Pair([], []), \acc, j ->
					if (j >= start && j < end)
						Pair(arrayPush(acc.first, j), acc.second)
					else
						Pair(acc.first, arrayPush(acc.second, j))
				)
				|> (\f -> concat(f.first, f.second))
			}
		));

	style = generate(0, length(m.items), \i -> if (length(m.style) > i) m.style[i] else []);

	dragging0 = generate(0, length(m.items), \i -> extractStruct(style[i], MDraggingItem(make(-1))).item);
	hovering0 = generate(0, length(m.items), \i -> extractStruct(style[i], MHoveringItem(make(-1))).item);

	draggingB = fmerge(dragging0);
	hoveringB = fmerge(hovering0);
	orderB = fmerge(order);

	mapi(items, \i, it ->
		MReorderGrid2T(
			manager,
			parent,
			it,
			order[i],
			replaceStruct(replaceStruct(style[i], MDraggingItem(dragging0[i])), MHoveringItem(hovering0[i])),
			m2t
		)
	)
	|> m.fn
	|> (\f -> m2t(f, parent))
	|> (\f ->
		TConstruct([
			make2Subscribe2(draggingB, hoveringB, \dr, hv -> {
				drId = foldi(dr, -1, \i, acc, dr0 -> if (dr0 != -1) i else acc);
				hvId = foldi(hv, -1, \i, acc, hv0 -> if (hv0 != -1 && i != drId) i else acc);

				if (drId != -1 && hvId != -1 && dr[drId] != -1 && hv[hvId] != -1) {
					next(manager.manager.dragging, ^(reorderId[hvId][dr[drId]].id));

					nextDistinct(itemVisible[drId][dr[drId]].visible, false);
					nextDistinct(itemVisible[hvId][dr[drId]].visible, true);

					newOrder = removeFirst(getValue(order[hvId]), dr[drId]);
					nextDistinct(order[hvId], insertArray(newOrder, elemIndex(newOrder, hv[hvId], 0), dr[drId]));
				}
			}),
			makeSubscribe(orderB, \or ->
				nextDistinct(m.order, mapi(or, \i, or0 -> foldi(or0, [], \j, acc, or1 ->
					if (i < length(itemsLength) && or1 < itemsLength[i] && getValue(itemVisible[i][or1].visible)) arrayPush(acc, or1) else acc)))
			),
			makeSubscribe2(m.order, \or ->
				iteri(or, \i, or0 -> {
					start = sum(subrange(itemsLength, 0, i));
					end = start + itemsLength[i];

					fold(generate(0, sum(itemsLength), \k -> k), [], \acc, j ->
						if (contains(or[i], j)) {
							nextDistinct(itemVisible[i][j].visible, true);
							acc
						} else {
							nextDistinct(itemVisible[i][j].visible, false);
							arrayPush(acc, j)
						}
					)
					|> (\f2 -> nextDistinct(order[i], concat(or0, f2)))
				})
			),
		], f)
	)
}

MDynamicGridContainer(id : DynamicBehaviour<int>, tr : DynamicBehaviour<Point>);

MDynamicGrid2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicGrid, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	items = fguard(m.items, make([]));
	itemsLength = fselect(items.first, length |> FLift);

	boxSize = extractStruct(m.style, MBoxSize(make(zeroWH))).wh;
	contentSize = make(zeroWH);
	position = extractStruct(m.style, TScrollPosition(make(zeroPoint))).position;
	inspectVisible = extractStruct(m.style, TScrollInspectVisible(make(zeroPoint), makeWH()));
	containers = make([]);
	itemSize = extractStruct(m.style, MItemSize(fselect(boxSize, FLift(\cs -> WidthHeight(cs.width / 5., cs.width / 5.))))).wh;

	SelectMGroup2T(manager, parent, containers, \__, __, c, p -> {
		TransformTAcc2(\t2a -> {
				b1 =
					TTranslate(
						c.tr,
						TAvailable(
							TCenter(m2t(MMutable(felementAt(items.first, c.id, const(TEmpty()))), p)),
							TSized(itemSize)
						)
					)
					|> t2a;

				TAcc(
					b1.form,
					TFormMetrics(
						zero,
						zero,
						zero
					),
					zero,
					zero,
					zero,
					zero,
					zero,
					zero,
					const(false),
					false,
					false,
					b1.disposers
				)
			}
		)
	})
	|> (\f -> TFixSize(f, TSized(contentSize)))
	|> (\f ->
		MScroll2T(
			manager,
			parent,
			MScroll(
				f,
				TFillXY(),
				[MScrollPosition(position), MScrollWidthHeight(make(zeroWH), boxSize), inspectVisible]
			),
			m2t
		)
	)
	|> (\f ->
		TConstruct(
			[
				items.second,
				make4Subscribe(boxSize, itemSize, position, itemsLength, \wh, sz, p, il -> {
					// items count in the row
					rowCount = max(floor(wh.width / sz.width), 1);
					// how much items on the screen
					itemCount = min((ceil(wh.height / sz.height) + 1) * rowCount, il);
					// max avaliable value of the first index
					topItemMax = (ceil(i2d(il - itemCount) / i2d(rowCount)) * rowCount) + if ((il - itemCount) % rowCount > 0) rowCount else 0;
					// index of the first shown item
					topItem = min(max(floor(p.y / sz.height) * rowCount, 0), topItemMax);

					if (itemCount > 0) {
						if (length(getValue(containers)) > itemCount) {
							nextDistinct(
								containers,
								subrange(getValue(containers), 0, itemCount)
							);
						} else if (length(getValue(containers)) < itemCount) {
							nextDistinct(
								containers,
								concat(
									getValue(containers),
									generate(length(getValue(containers)), itemCount, \i -> MDynamicGridContainer(make(-1), make(zeroPoint)))
								)
							);
						}

						topId = max(getValue(getValue(containers)[0].id), 0);

						iteri(getValue(containers), \i, c -> {
							idMod = (topId + i - topItem) % itemCount;
							id = (if (idMod >= 0) idMod else itemCount + idMod) + topItem;

							nextDistinct(c.id, id);
							nextDistinct(c.tr, Point(i2d(id % rowCount) * sz.width, i2d(id / rowCount) * sz.height));
						});
					} else {
						nextDistinct(containers, []);
					}

					nextDistinct(contentSize, WidthHeight(i2d(rowCount) * sz.width, dceil(i2d(il) / i2d(rowCount)) * sz.height));
				})
			],
			f
		)
	)
}
