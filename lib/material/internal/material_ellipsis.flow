import material/internal/material_tooltip;
import material/internal/tropic_size_util;

export {
	MEllipsis2T(m : MEllipsis, m2t : (Material) -> Tropic) -> Tropic;
	MEllipsisText2T(manager : MaterialManager, parent : MFocusGroup, m : MEllipsisText, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

getPrettyTextCut(first: int, last : int, wCroped : double, text : string, style : [MTextStyle], m2t : (Material) -> Tropic) -> Tropic {
	if (last <= first + 1)
		m2t(MText(strLeft(text, first + 1), style))
	else {
		mid = first + trunc(i2d(last - first) / 2.0);
		newT = m2t(MText(strLeft(text, mid + 1), style));
		if (getTropicMetrics(newT).width > wCroped)
			getPrettyTextCut(first, mid, wCroped, text, style, m2t)
		else
			getPrettyTextCut(mid, last, wCroped, text, style, m2t)
	}
}

MEllipsis2T(m : MEllipsis, m2t : (Material) -> Tropic) {
	style : [MEllipsisStyle] = m.style;
	showtooltip : Transform<bool> = extractStruct(style, MShowTooltip(const(false))).show;
	full = m.full;
	size = m.size;
	fullT = m2t(full);
	sizeT = m2t(size);
	sm = getTropicMetrics(sizeT);
	disableOnDragging = contains(style, MDisableOnDragging());
	rtl = getDefaultRtl();

	fits = if (isConstantSizeTropic(sizeT) && isConstantSizeTropic(fullT)) {
		fm = getTropicMetrics(fullT);
		fm.width <= sm.width && fm.height <= sm.height;
	} else false;

	if (fits) {
		// We completely fit. No need to do anything
		fullT
		|> makeTropicConstant(None(), None())
	} else {
		textStyle : [MEllipsisStyle] = switch (full) {
			MText(text, s): {
				elStyle : [MEllipsisStyle] = s;
				elStyle;
			}
			default: style;
		};

		tooltipText = switch (full) {
			MText(text, s): MParagraph(text, ifArrayPush(s, sm.width > 0., MWidth(sm.width)));
			default: full;
		};

		ellipsisText = m2t(MText(THREE_DOTS, style2MTextStyle(textStyle)));
		sizeWithoutEllipsis = TSubtractGroup2(sizeT, TWidth(ellipsisText));
		croped = TCropSize(sizeWithoutEllipsis, fullT);

		prettyCroped = switch (full) {
			MText(text, s): {
				wCroped = getTropicMetrics(croped).width;
				if ((strlen(text) > 0) && (wCroped > 0.0) && (wCroped < getTropicMetrics(fullT).width))
					getPrettyTextCut(0, strlen(text) - 1, wCroped, text, s, m2t)
				else
					croped;
			}
			default: croped;
		};

		// If cropped, display the complete content as a tooltip when requested
		truncated : (Tropic) -> Tropic = \tr : Tropic ->
			TCols2A(
				TCenterY(prettyCroped),
				TTweak([TAlign(const(b2d(rtl)), const(1.))], ellipsisText, THeight(prettyCroped))
			)
			|> (\t -> TAvailable(t, tr))
			|> (\t : Tropic ->
				if (showtooltip == ConstBehaviour(false)) t
				else
					MTooltip(t, tooltipText, ifArrayPush([MEnabled(showtooltip)], disableOnDragging, MDisableOnDragging()))
					|> m2t
			);

		TLet("t", fullT,
			TLet("s", sizeT,
				TCase([
					// We make the truncated version one pixel smaller so that if the item fits
					// completely, we never abbreviate it
					TSubtractGroup2(TDisplay("s"), TFixed(1.0, 1.0)),
					// The maximum size of the real thing, as well as the room available
					TGroup2(TGhost("s"), TDisplay("t")),
					// Get the original tropic, if possible
					TGhost("t")
				], [truncated(TGhost("s")), TDisplay("t"), TDisplay("t")])
			)
		)
	}
}

MEllipsisText2T(manager : MaterialManager, parent : MFocusGroup, m : MEllipsisText, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	if (isDomRenderEnabled(m.style)) {
		MEllipsisText2TDom(manager, parent, m, m2t);
	} else {
		MEllipsisText2TMaterial(manager, parent, m, m2t);
	}
}

MEllipsisText2TMaterial(manager : MaterialManager, parent : MFocusGroup, m : MEllipsisText, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	mTextStyle = style2MTextStyle(m.style);
	minWidth = extractStruct(m.style, MMinWidth(-1.)).width;
	tooltipMaxWidth = extractStruct(m.style, MTooltipMaxWidth(400.0)).width;
	textAlignment = maybeMap(tryExtractStruct(m.style, MEllipsisTextAlignment(StartAlign())), \al -> al.alignment);
	rightAlign0 = contains(m.style, RightAlign());
	centerAlign0 = contains(m.style, CenterAlign());
	rightAlign = rightAlign0 || eitherMap(textAlignment, \al -> !centerAlign0 && commonAlignment2abs(al, parent.rtl) == RightAlign(), false);
	centerAlign = centerAlign0 || eitherMap(textAlignment, \al -> !rightAlign0 && al == CenterAlign(), false);
	truncateFromStart = contains(m.style, MTruncateFromStart());
	isTruncated = extractStruct(m.style, MTextIsTruncated(make(false))).isTruncated;
	setIsTruncated = \v -> nextDistinct(isTruncated, v);
	isFullWidth = contains(m.style, MFullWidth());
	interlineSpacing = extractStruct(m.style, InterlineSpacing(0.0));
	escapeHTML = extractStruct(m.style, EscapeHTML(true));
	unescape = if (escapeHTML.escape) idfn else unescapeSpacesIntoTags;
	text = unescape(m.text);

	makeTropic = \ -> {
	charStyle = MTextStyle2TCharacterStyle(parent, text, mTextStyle);
	textT = TText(text, charStyle);
	fullMetrics = getTWordMetrics(textT, makeTree());
	fullWidth = fullMetrics.width;
	maybeApply(tryExtractStruct(m.style, MEllipsisTextGetFullWidth(nop1)), \getfw -> getfw.fn(fullWidth));
	variants = arrayPush(extractStruct(m.style, MTextVariants([])).variants, m.text);
	variantWidths = map(variants, \v -> getTWordMetrics(TText(v, charStyle), makeTree()).width);
	defLines = eitherMap(
		tryExtractStruct(m.style, MMaxHeight(0.)),
		\maxH -> max(1, floor(maxH.height / (fullMetrics.height + interlineSpacing.d))),
		1
	);
	lines = extractStruct(m.style, MMaxLines(defLines)).lines;

	if (minWidth > 0. && minWidth >= fullWidth && strCountOf(text, "\n") < lines) {
		setIsTruncated(false);
		TText(text, charStyle);
	} else {
		availableWidth = make(fullWidth);
		disableOnDragging = contains(m.style, MDisableOnDragging());
		showTooltip0 = extractStruct(m.style, MShowTooltip(const(false))).show;
		showTooltip = fand(showTooltip0, isTruncated);

		dottedTextSize = TSizeOf(TText(substring(text, 0, 1) + THREE_DOTS, charStyle));

		(if (js && !truncateFromStart && false) { // Temporary disabled
			TParagraph(
				const(text),
				concat(
					charStyle,
					[
						ParagraphEllipsis(lines, setIsTruncated)
					]
				)
			)
		} else {
			currentText = make(text);

			(if (lines > 1)
				TParagraph(
					fselect(currentText, FLift(unescape)),
					concat3(charStyle, [ParagraphWidth(availableWidth), interlineSpacing], eitherMap(textAlignment, v2a, []))
				)
			else {
				ttext = TSelect(currentText, \txt -> TText(unescape(txt), charStyle));
				TAvailableWidth(eitherMap(textAlignment, \align -> TAlignHorizontal(align)(ttext), ttext), availableWidth);
			})
			|> (\t ->
				TConstruct(
					[
						\ -> fconnectSelect(availableWidth, currentText, \aw0 -> {
							aw = dceil(aw0 * 100.) / 100.; // To avoid false positive truncations in IE.
							if (aw >= fullWidth && strCountOf(text, "\n") < lines) {
								setIsTruncated(false);
								text;
							} else {
								varIdx = either(lastfindi(variantWidths, (\v -> v <= aw0)), 0);
								varText = variants[varIdx];
								truncatedText =
									getSplittedAndTruncatedByWidthString(if (truncateFromStart) reverse(varText) else varText, aw, charStyle, lines)
									|> if (truncateFromStart) reverse else idfn;
								setIsTruncated(varIdx!=length(variants)-1 || endsWith(truncatedText, THREE_DOTS));
								truncatedText
							}
						})
					],
					t
				)
			)
		})
		|> (\f : Tropic ->
			if (isFConst(showTooltip0) && fgetValue(showTooltip0) == false)
				f
			else
				MTooltip2T(
					manager,
					parent,
					MTooltip(
						f,
						TShowLazy(
							showTooltip,
							\ ->
								MDynamicParagraph2T(
									parent,
									const(if (strlen(text) > 1000) strSubRange(text, 0, 1000) + THREE_DOTS else text),
									[sameLightMColor(parent), MTooltipDesktop(), CropWords(true), MWidth(min(fullWidth, tooltipMaxWidth)), escapeHTML]
								)
						),
						concatA([
							[MEnabled(showTooltip)],
							extractStructMany(m.style, MTooltipExternalFocused(const(false))),
							if (disableOnDragging) [MDisableOnDragging()] else []
						])
					),
					m2t
				)
		)
		|> (\t ->
			if (!isUrlParameterFalse("ellipsis_fix")) {
				align =
					if (centerAlign) 0.5
					else if (rightAlign) {
						if (parent.rtl) -1.0 else 1.0
					} else {
						if (isFullWidth) 0.0 else -1.0
					}
				if (align == -1.0) t
				else TTweak([TAlign(const(align), zero)], t, TFillX())
			} else {
				if (centerAlign) TCenterX(t) else if (rightAlign) TCols2(TFillX(), t) else if (isFullWidth) TCols2(t, TFillX()) else t
			}
		)
		|> (\t -> TGroup2(t, THeight(dottedTextSize)))
		|> (\t ->
			TAttachWidth2(
				(
					if (
						(isFullWidth || centerAlign)
						&& (!getDefaultRtl() || !isUrlParameterFalse("ellipsis_fix"))
					) TFillX()
					else TMinimumGroup2(TFixed(fullWidth, 0.0), TFillX())
				)
				|> (\f ->
					if (strlen(text) > 1)
						TGroup2(TWidth(dottedTextSize), f)
					else
						f
				),
				availableWidth,
				t
			)
		)
	}
	}

	updateOnFontWrapper(parent, makeTropic);
}

MEllipsisText2TDom(manager : MaterialManager, parent : MFocusGroup, m : MEllipsisText, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	mTextStyle = style2MTextStyle(m.style);
	escapeHTML = extractStruct(m.style, EscapeHTML(true));
	unescape = if (escapeHTML.escape) idfn else unescapeSpacesIntoTags;
	text = unescape(m.text);
	charStyle = MTextStyle2TCharacterStyle(parent, text, mTextStyle);
	clipWH = make(WidthHeight(0.0, 0.0));
	avWidth = make(0.0);
	definedTextStyle = getDefinedTextStyle(tcharacterStyle2charStyle(charStyle));
	lineHeight = dceil((if (definedTextStyle.lineHeightPercent > 0.0) definedTextStyle.lineHeightPercent else 1.15) * definedTextStyle.fontSize);

	makeTropic = \ -> TInspect([IAvailableWidth(avWidth)],
		TGroup2(
			TFillX(),
			TNativeForm(fwh(avWidth, const(lineHeight)), \av, zorder, tabOrder -> {
				clip = makeClip();
				setClipTagName(clip, "p");
				setClipStyle(clip, "position", "absolute");
				setClipStyle(
					clip,
					"font-family",
					if (js && length(definedTextStyle.face.families) > 0 && getRendererType() == "html")
						strGlue(definedTextStyle.face.families, ",")
					else
						definedTextStyle.face.family
				);
				setClipStyle(clip, "white-space", "pre");
				setClipStyle(clip, "color", "#" + formatColor(definedTextStyle.fillColor));
				setClipAlpha(clip, definedTextStyle.fillOpacity);
				setClipStyle(clip, "letter-spacing", d2s(definedTextStyle.letterSpacing));
				setClipStyle(clip, "font-weight", i2s(definedTextStyle.face.weight));
				setClipStyle(clip, "font-size", d2s(definedTextStyle.fontSize) + "px");
				setClipStyle(clip, "line-height", d2s(lineHeight) + "px");
				setClipStyle(clip, "overflow", "hidden");
				setClipStyle(clip, "text-overflow", "ellipsis");
				setClipStyle(clip, "max-width", "fit-content");
				setAccessAttributes(clip, [["textContent", text]]);

				d = [
					makeSubscribe(avWidth, \a -> {
						setClipStyle(clip, "width", d2s(a) + "px");
					})(),
				];

				NativeRenderResult(
					[clip],
					\ -> applyall(d)
				);
			})
			|> TFixWidth(TEmpty())
		)
	)

	updateOnFontWrapper(parent, makeTropic);
}

unescapeSpacesIntoTags(s : string) -> string{
	strReplaceCustom2(s, "&#32;", \st, e -> {
		tagsCount = stringFold(strLeft(s, st), 0, \acc, c ->
			if (fromCharCode(c) == "<") acc + 1
			else if (fromCharCode(c) == ">") acc - 1
			else acc
		);
		if (tagsCount > 0) Some(" ") else None()
	})
}