import material/internal/material_textinput;
import material/internal/material_dropdown;

// Internal to material

export {
	MDynamicDataTable2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicDataTable, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
}

MDynamicDataTable2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicDataTable, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	style = m.style;
	rows = m.rows;
	uns = fuse(rows).second; // For internal optimization. Do not use fuse in everyday programming.

	reorderList = tryExtractStruct(style, MListReorder(make([]), const(false)));
	isReorderEnabled = eitherFn(reorderList, \rl : MListReorder -> fgetValue(rl.enabled), \ -> false);
	reorderShowIcon = !contains(style, MListReorderHideIcon());
	// Reserved space for column with drag&drop reorder icon. Icon added at function MListLine2T
	reorderIconSpace = if (isReorderEnabled && reorderShowIcon) 40. else 0.;
	paginationDef = MPagination(make(0), []);
	pagination = if (isSome(reorderList)) paginationDef else extractStruct(style, paginationDef);
	autoPagination = if (isSome(reorderList)) false else pagination.rowsPerPage == [] && contains(style, MPaginationAutoScaling());

	singleSelection = tryExtractStruct(style, MSingleSelection(make(-1)));
	multiSelection = tryExtractStruct(style, MMultiSelection(make(makeSet())));

	headerActions = extractStruct(style, MHeaderActions(TEmpty(), []));
	replaceHeader = contains(headerActions.style, MReplaceHeader());

	currentPage = tryExtractStruct(style, MCurrentPage(make(0)));
	scrollPosition = extractStruct(style, MScrollPosition(make(zeroPoint)));

	selection : MListSelection =
		extractStruct(style, MListSelection(
			const(isSome(singleSelection) || isSome(multiSelection)),
			isSome(multiSelection),
			false,
			make([])
		));
	selectionEnabled : MListSelectionEnabled = extractStruct(style, MListSelectionEnabled([]));
	hoverEnabled : MHoverEnabled = extractStruct(style, MHoverEnabled(const(true)));
	expandRowEnabled = extractStruct(style, MExpandRow(const(mobile))).enabled;
	scrollEnabled = extractStruct(style, TScrollEnabled(const(false))).enabled;
	onClick : MOnListClick = extractStruct(style, MOnListClick(const(false), nop1));
	clickEnabled : MClickEnabled = extractStruct(style, MClickEnabled(const(true)));

	rowHeight = extractStruct(style, MRowHeight(if (extractStruct(style, MCondensed(false)).condensed) 32. else 48.)).height;
	checkboxSelection = tryExtractStruct(style, MCheckBoxSelection([]));
	checkboxColor = eitherMap(checkboxSelection, \cs -> extractStruct(cs.style, parent.theme.primaryColor), parent.theme.primaryColor);
	checkboxBorder = forceRange((rowHeight - 24.) / 2., 0., 12.);
	checkboxColumnWidth = checkboxBorder * 3. / 2. + standardIconSize;

	headerStyle = tryExtractStruct(style, MHeaderStyle([], [], []));
	rowBackgroundStyle = tryExtractStruct(style, MRowBackgroundStyle(\__ -> []));
	selectedBgStyle : Maybe<MSelectedBackgroundStyle> = tryExtractStruct(style, MSelectedBackgroundStyle([]));
	hasExtOrderBy = contains(style, MExternalSort());
	crop = !contains(style, MNoCrop());
	goToIndex = tryExtractStruct(style, MGoToIndex(make(-1)));
	fixedRow = tryExtractStruct(style, MFixedRow(const(false), [], []));
	showRowNumbers = contains(style, MShowRowNumbers());
	mRowsPerPage = tryExtractStruct(style, MRowsPerPage(make(0)));

	widthByContent = contains(style, MWidthByContent());
	columns0 = m.columns
		|> (\arr -> if (widthByContent) map(arr, \col -> MColumnDynamic(col.header, col.tooltip, col.width, arrayPush(col.style, MWidthByContent()))) else arr)
		|> (\arr -> if (showRowNumbers) concat([MColumn("#", "Row number", 80, [MWidthByContent()])], arr) else arr);
	columns = columns0
		|> (\arr -> if (isSome(checkboxSelection)) concat([MColumn("", "", floor(checkboxColumnWidth), [MMaxWidth(floor(checkboxColumnWidth))])], arr) else arr);
	columnsCount = length(columns);
	isAnyWidthByContent = widthByContent || any(map(columns, \col -> contains(col.style, MWidthByContent())));

	defaultSorted : MSortingDefault = extractStruct(style, MSortingDefault(make(MSortingParam(0, true))));
	listSortedCallback = extractStruct(style, MListSorted(nop1)).newOrderCallback;
	sortedByB : DynamicBehaviour<Triple<int, bool, Maybe<MSort>>> = make(Triple(-1, true, None()));
	sortColumnNumberB = make(-1);
	sortUpdating = tryExtractStruct(style, MUpdateSorting(0));

	updatingTrigger = make(true);
	maybeApply(sortUpdating, \su -> updateTrigger(updatingTrigger, su.delay));

	sortedBy = fselect3(
		sortedByB, rows, updatingTrigger,
		\sb : Triple<int, bool, Maybe<MSort>>, rws : [[Material]], __ -> {
			sortingResult =
				if (sb.first >= 0 && sb.first < (columnsCount - (b2i(showRowNumbers) + b2i(isSome(checkboxSelection)))) && !hasExtOrderBy)
					eitherFn(
						sb.third,
						\sc ->
							mergeSort(
								enumFromTo(0, length(rws) - 1),
								\a, b ->
									if (sb.second) sc.colCompares(a, b)
									else sc.colCompares(b, a)
							),
						\ ->
							map(
								sortCustom(
									mapi(rws, \j, r -> Pair(r[sb.first], j)),
									\r -> r.first, sb.second
								),
								\r -> r.second
							)
					)
				else [];
			listSortedCallback(sortingResult);
			Triple(sb.first, sortingResult, sb.second)
		}
	);

	rowsCountB =
		fselect(rows, FLift(length));

	// Width handling

	border = 16.;
	screenW = make(0.);
	rowsBoxH = make(0.);

	fullWidthAdvanced = contains(style, MFullWidthAdvanced());
	fullWidth = contains(style, MFullWidth()) || fullWidthAdvanced;

	minWidths = mapi(columns, \i, c -> i2d(c.width) + border);
	maxWidths =
		mapi(columns, \i, c -> {
			w = extractStruct(c.style, MMaxWidth(-1)).width;
			if (w == -1)
				-1.
			else if (w < c.width)
				i2d(c.width) + border
			else
				i2d(w) + border
		});

	cellWHs : Transform<[[DynamicBehaviour<WidthHeight>]]> =
		fselect(rowsCountB, FLift(\rc ->
			generate(0, rc + b2i(isSome(fixedRow)), \__ ->
				generate(0, columnsCount, \i ->
					makeWH()
				)
			)
		));

	uns2 = fuse(cellWHs).second;

	columnWidths =
		generate(0, columnsCount, \i ->
			if (widthByContent || contains(columns[i].style, MWidthByContent()))
				fsubselect(cellWHs, FLift(\cWHs ->
					faddition(fmaxA(map(cWHs, \row -> fwidth(row[i])), i2d(columns[i].width)), const(border))
				))
			else if (fullWidth)
				make(minWidths[i])
			else
				const(minWidths[i])
		);

	getWidthsSum = \er ->
		if (columnsCount == 0)
			0.
		else
			dsum(mapSplitWidths(
				columnWidths,
				er,
				\w -> either(maxA(map(w, fgetValue)), 0.)
			)) + 2. * border;

	getMinWidthsSum = \er ->
		if (columnsCount == 0)
			0.
		else
			dsum(mapSplitWidths(
				minWidths,
				er,
				\w -> either(maxA(w), 0.)
			));
	minWidthsSum = getMinWidthsSum(1);

	prevER = ref 1;

	countExpandRow = \aw, widthsSum ->
		(if (^prevER == 1 && (aw - border * 2.) < widthsSum * 2. / 3. && aw != 0.)
			2
		else if (^prevER == 2 && (aw - border * 2.) > widthsSum)
			1
		else
			^prevER) |> (\f -> {prevER := f; f});

	expandRow =
		fif(
			expandRowEnabled,
			if (isAnyWidthByContent)
				fselect2(screenW, fmerge(columnWidths), FLift2(\aw, __ -> {
					widthsSum = getWidthsSum(1);
					countExpandRow(aw, widthsSum)
				}))
			else
				fselect(screenW, FLift(\aw -> countExpandRow(aw, minWidthsSum))),
			const(1)
		);

	// Height handling

	separators = !contains(style, MNoSeparators());
	separatorsHeight = b2d(separators);

	noFooter = contains(style, MNoFooter()) && pagination.rowsPerPage == [] && !autoPagination;
	footerHeight = if (noFooter) 0. else 56.;
	footerMinWidth = make(0.);

	minimumRowsDef = 3;
	minTableHeightDef = i2d(minimumRowsDef + b2i(!replaceHeader)) * (rowHeight + separatorsHeight) + footerHeight;
	minTableHeight =
		extractStruct(style, MMinTableHeigth(minTableHeightDef)).height
		|> (\h -> max(h, rowHeight + footerHeight));

	minimumRows = \er -> min(
		ceil((minTableHeight - footerHeight) / ((rowHeight + separatorsHeight) * i2d(er))) - b2i(!replaceHeader),
		fgetValue(rowsCountB)
	);

	countRowHeight = \er : int ->
		rowHeight * i2d(er) + separatorsHeight;

	// Helpers

	makeChkbox = \p, trigger, en, onClk ->
		MCheckBox2T(
			manager,
			p,
			MCheckBox(TEmpty(), trigger, [MIconButtonBorder(checkboxBorder), checkboxColor, MEnabled(en), MOnClick(onClk)]),
			m2t
		)
			|> (\t -> TBorderA(-checkboxBorder / 2., 0., 0., 0., t));

	addCellBorder = \t, size ->
		TBorderA(8., 0., 8., 1., TAvailable(t, TBorderA(-16., 0., 0., -1., size)));

	alignCellContent = \right : bool, center : bool, i : int, content : Tropic ->
		TCopySize(
			TSizedWidth(columnWidths[i], rowHeight),
			\tr ->
				TTweak(
					[TAlign(const(if (center) 0.5 else if (right) 1. else 0.), const(0.5))],
					addCellBorder(content, tr),
					tr
				),
			false
		);

	expandContent = \cells : [Tropic], er : int ->
		map(
			if (length(cells) > 0 && isNone(checkboxSelection))
				splitWidths(cells, er)
			else
				splitByNumber(cells, er),
			TLines);

	fixPaginationCurrent = \current -> forceRange(current, 0, length(pagination.rowsPerPage) - 1);

	page =
		make(
			Pair(
				0,
				if (pagination.rowsPerPage != [])
					pagination.rowsPerPage[fixPaginationCurrent(getValue(pagination.current))]
				else if (autoPagination)
					minimumRows(fgetValue(expandRow))
				else
					fgetValue(rowsCountB)
			)
		);

	updatePageNumber = \newPageNumber ->
		nextDistinct(page, Pair(newPageNumber, getValue(page).second));

	updateRowsPerPage = \newRowsPerPage ->
		nextDistinct(page, Pair(getValue(page).first, newRowsPerPage));

	pageNumber = fFirstOfPair(page);
	rowsPerPage = fSecondOfPair(page);

	pageNumberB = make(0);

	// Header

	tableHeader = \p -> if (replaceHeader) TEmpty() else
		mapi(columns0, \i0, c -> {
			rightAlign = changeBoolIfRTL(contains(c.style, RightAlign()));
			centerAlign = contains(c.style, CenterAlign());
			sortStyle = if (isSome(reorderList)) None() else tryExtractStruct(c.style, MSort(\__, __ -> 0));
			sortable = isNone(reorderList) && (hasExtOrderBy || contains(c.style, MAutoSort()));
			i = i0 - b2i(showRowNumbers);

			makeHeaderTitle = \isSelected : bool ->
				TSelect(c.header, \header ->
					MEllipsisText2T(
						manager,
						p,
						MEllipsisText(
							header,
							eitherMap(headerStyle, \hs -> hs.textStyle, [if (isSelected) MCaptionColumnSelected() else MCaptionColumn()])
						)
					)
				);

			(if (isSome(sortStyle) || sortable) {
				hover = make(false);

				clickColumn = \ -> {
					sb = fgetValue(sortedByB);
					asc = if (fgetValue(sortedBy).first == -1) getValue(defaultSorted.sorting).isAsc else if (sb.first == i) !sb.second else false;

					nextDistinct(defaultSorted.sorting, MSortingParam(i, asc));
					next(sortedByB, Triple(i, asc, sortStyle))
				};

				if (getValue(defaultSorted.sorting).colIndex == i)
					clickColumn();

				mouseDownAround(manager, None(), make(false), hover, clickColumn,
					TIfLazy2(
						fselect(sortedBy, FLift(\sb : Triple<int, [int], bool> -> sb.first == i)),
						\sb ->
							TIfLazy2(
								hover,
								\h ->
									TCols(
										[
											makeHeaderTitle(h)
												|> (\t -> TBorderA(0.0, 0.0, 4.0, 0.0, t)),
											if (sb)
												TIfLazy2(fselect(sortedBy, FLift(\sb2 -> sb2.third)), \sb0 ->
													MIcon2T(p, if (sb0) "arrow_upward" else "arrow_downward", [MIconSize(15.)]),
													true
												)
											else if (h)
												MIcon2T(p, "arrow_downward", [MIconSize(15.), FillOpacity(0.24)])
											else
												TFixed(16., 0.)
										]
											|> (\f2 -> if (rightAlign) reverseA(f2) else f2)
									),
								false
							),
						false
					)
				)
			} else
				makeHeaderTitle(false)
			)
			|> (\f2 -> MTooltip2T(manager, p, MTooltip(f2, MDynamicText(c.tooltip, [MTooltipDesktop(), sameLightMColor(p)]), []), m2t))
			|> (\f2 -> alignCellContent(rightAlign, centerAlign, i0 + b2i(isSome(checkboxSelection)), f2))
		})
		|> (\headers : [Tropic] -> {

			addCheckbox = \colHeaders : Tropic ->
				eitherMap(
					checkboxSelection,
					\cs -> {
						selAll = make(false);
						selPage =
							fselect4(
								fif(scrollEnabled, fselect(rowsCountB, FLift(\rc -> Pair(0, rc))), page),
								sortedBy, rowsCountB, fmerge(selectionEnabled.enabled), \pg, sb, rowsCount, en ->
								fold(
									enumFromTo(pg.first, min(pg.first + pg.second, rowsCount) - 1),
									[],
									\acc, i ->
										ifArrayPush(acc,
											length(en) <= i || en[i],
											if (length(sb.second) > 0) sb.second[i] else i
										)
								)
							);

						makeChkbox(p, selAll,
							if (selection.multiple)
								fand(selection.enabled, fselect(selPage, FLift(\pg -> length(pg) > 0)))
							else
								const(false),
							\ -> nextDistinct(selection.selected,
								if (getValue(selAll))
									concat(getValue(selection.selected), fgetValue(selPage)) |> uniq
								else
									filter(getValue(selection.selected), \x -> !contains(fgetValue(selPage), x))
							)
						)
							|> (\t ->
								if (selection.multiple) {
									TConstruct(
										[
											make2Subscribe(selection.selected, selPage, \sel, pg ->
												nextDistinct(
													selAll,
													length(pg) != 0 && length(sel) >= length(pg)
														&& forall(pg, \i -> contains(sel, i))
												)
											)
										],
										t
									)
								} else
									t
								)
							|> (\chkbox -> TCols2A(chkbox, colHeaders))
					},
					colHeaders
				);

			TSelect(expandRow, \er ->
				expandContent(headers, er)
				|> TColsA
			)
			|> addCheckbox
			|> (\f2 -> TBorderA(border, 0., border + reorderIconSpace, 0., f2))
		})
		|> (\f ->
			eitherMap(
				headerStyle,
				\hs ->
					TCopySize(
						f,
						\tr ->
							TRounded(2., 2., 0., 0., hs.background, tr),
						false
					),
				f
			)
		);

	addHeaderActions = \p -> \t ->
		if (headerActions.actions != TEmpty())
			TLines2(
				m2t(headerActions.actions, p)
				|> (\t2 -> TAvailableHeight(t2, const(rowHeight))),
				t
			)
		else t;

	footer = \p -> (
		if (pagination.rowsPerPage != [] || autoPagination)
			MSelect2T(manager, p, "MDynamicDataTableFooter", [], rowsCountB, \rowsCount, p2 -> {
				makeFooterButton = \title : string, makeUpdatedRow : (Pair<int, int>) -> int, makeEnabled : (Pair<int, int>) -> bool ->
					MIconButton2T(manager, p2,
						MIconButton(
							title,
							\ -> updatePageNumber(makeUpdatedRow(getValue(page))),
							[],
							[MEnabled(fselect(page, FLift(makeEnabled)))]
						)
					)
						|> TCenterY;

				hideFooter = rowsCount < either(minA(pagination.rowsPerPage), fgetValue(rowsPerPage));

				if (hideFooter)
					TEmpty()
				else {
					if (!autoPagination && length(pagination.rowsPerPage) == 1) {
						selectedRowsPerPage = fixPaginationCurrent(getValue(pagination.current));
						updateRowsPerPage(pagination.rowsPerPage[selectedRowsPerPage])
					}

					concatA([
						[TFillX()],
						if (!autoPagination && length(pagination.rowsPerPage) > 1)
							[
								MText2T(p2, "Rows per page:", [MCaption()])
								|> TCenterY
								|> (\t -> TBorder(8., 0., 16., 0., t)),
								MDropDown2T(
									manager,
									p2,
									MDropDown(
										pagination.current,
										"",
										map(pagination.rowsPerPage, toString),
										[MDataTableFooter(), MCroppedButton()]
									),
									m2t
								)
								|> (\f2 ->
									TConstruct([
										makeSubscribe2(pagination.current, \c ->
											updateRowsPerPage(pagination.rowsPerPage[c])
										)
									], f2)
								)
								|> TCenterY
								|> (\t -> TBorder(0., 0., 16., 0., t))
							]
						else
							[],

						[
							TSelect(page, \pg -> {
								text =  formatString(
									if (getDefaultRtl()) _("%1 of %2-%3") else _("%3-%2 of %1"),
									[i2s(rowsCount), i2s(max(min(pg.first + pg.second, rowsCount), 0)), i2s(min(pg.first + 1, rowsCount))]
								);

								MText2T(p2, text, [MCaption()])
							})
								|> TCenterY
								|> (\t -> TBorder(0., 0., 16., 0., t)),
							makeFooterButton(
								"first_page",
								\__ -> 0,
								\pg -> pg.first > 0
							)
								|> hideIfLittlePages(rowsCount, rowsPerPage),
							makeFooterButton(
								"navigate_before",
								\pg -> max(pg.first - pg.second, 0),
								\pg -> pg.first > 0
							),
							makeFooterButton(
								"navigate_next",
								\pg -> min(pg.first + pg.second, rowsCount - if (fgetValue(scrollEnabled)) pg.second else 0),
								\pg -> (rowsCount > 0) && (pg.first + pg.second < rowsCount)
							),
							makeFooterButton(
								"last_page",
								\pg -> (rowsCount - 1) / pg.second * pg.second,
								\pg -> pg.first + pg.second < rowsCount
							)
								|> hideIfLittlePages(rowsCount, rowsPerPage)
						]
					])
					|> TColsA
					|> (\t -> TAvailableHeight(t, const(footerHeight)))
					|> makeTropicConstantHeight(footerHeight |> Some)
				}
			})
		else if (noFooter)
			TEmpty()
		else
			TFixed(0., footerHeight)
	)
	|> (\t -> TransformTAcc2(\t2a -> {
			b1 = t |> t2a;
			TAcc(
				b1.form,
				TFormMetrics(
					b1.metrics.width,
					b1.metrics.height,
					b1.metrics.baseline
				),
				b1.minWidth,
				b1.minHeight,
				b1.maxWidth,
				b1.maxHeight,
				b1.xFillers,
				b1.yFillers,
				b1.addFillers,
				b1.xConstant,
				b1.yConstant,
				arrayPush(b1.disposers, fconnect(b1.minWidth, footerMinWidth))
			)
		}));

	(\p -> {
		// Data rows
		MSelect2T(manager, p, "MDynamicDataTableLines", [], expandRow, \er, p2 ->
			eitherMap(
				fixedRow,
				\fr -> fconcat(const([fr.cols]), rows),
				rows
			)
			|> (\rc -> fmapi(rc, \j, row -> {
					addCheckbox = \p3 : MFocusGroup -> \cells : Tropic ->
						eitherMap(checkboxSelection, \cs -> {
							j0 = j - b2i(isSome(fixedRow));
							if (j0 >= 0) {
								selRow = make(false);
								makeChkbox(p3, selRow,
									if (length(selectionEnabled.enabled) > j0)
										fand(selection.enabled, selectionEnabled.enabled[j0])
									else
										selection.enabled,
									\ -> {
										sel = getValue(selection.selected);

										if (selection.multiple) {
											if (!getValue(selRow))
												nextDistinct(selection.selected, removeAll(sel, j0))
											else if (!contains(sel, j0))
												nextDistinct(selection.selected, arrayPush(sel, j0))
										} else {
											if (getValue(selRow))
												nextDistinct(selection.selected, [j0])
											else if (selection.toggle)
												nextDistinct(selection.selected, [])
										}
									}
								)
								|> (\f4 ->
									TConstruct(
										[
											makeSubscribe(selection.selected, \s : [int] ->
												nextDistinct(selRow, contains(s, j0))
											)
										],
										f4
									))
								|> (\t -> TCols2A(t, cells))
							} else
								TBorderA(checkboxColumnWidth, 0., 0., 0., cells)
							},
							cells
						);
					row2 = if (showRowNumbers) concat([MText(i2s(j + 1), [])], row) else row;
					makeLineContent = \p3 : MFocusGroup ->
						mapi(row2, \i0, cell -> {
							i = i0 + b2i(isSome(checkboxSelection));
							if (i >= length(columns))
								TEmpty()
							else {
								rightAlign = changeBoolIfRTL(contains(columns[i].style, RightAlign()));
								centerAlign = contains(columns[i].style, CenterAlign());
								widthByContentCol = contains(columns[i].style, MWidthByContent());

								(if (widthByContentCol) {
									cellWh = fgetValue(cellWHs)[j][i];
									MAttachBoxCopy(cell, cellWh)
								}
								else cell)
								|> (\m2 ->
									MCellContent2T(
										manager,
										p3,
										m2,
										rowHeight,
										minWidths[i] - border,
										TSizedWidth(columnWidths[i], rowHeight),
										crop,
										m2t
									))
								|> (\t -> alignCellContent(rightAlign, centerAlign, i, t))
							}
						})
							|> (\cells -> expandContent(cells, er))
							|> TColsA
							|> addCheckbox(p3)
							|> (\t -> TBorderA(border, 0., border, 0., t))
							|> (\t -> TFixSize(t, TFillXH(rowHeight * (er |> i2d))))
							|> (\t -> eitherMap(rowBackgroundStyle, \rb ->
										TIfLazy2(
											fselect(selection.selected, FLift(\arr -> contains(arr, j))),
											\v -> if (v && isSome(selectedBgStyle)) t else TFrame(0., 0., rb.fn(j), t),
											true
										), t
									))
							|> (\t -> if (separators) TLines2(t, m2t(MSeparator(true), p3)) else t);

					MCustomLine(
						MGetFocusGroup(makeLineContent),
						[clickEnabled, MHeight(countRowHeight(er))]
					);
				})
			)
			|> (\f2 : Transform<[MCustomLine]> ->
				(if (isSome(fixedRow))
					fselect(f2, tail |> FLift)
				else
					f2)
				|> (\f3 : Transform<[MCustomLine]> ->
					eitherFn(
						reorderList,
						\rl ->
							MSelect2T(
								manager,
								p2,
								"MDataTableContent",
								[],
								f3,
								\f4, p3 ->
									MList2T(
										manager,
										p3,
										MList(
											f4,
											concat(
												[MSideBorders(const(0.)), MAddScroll(None())],
												eitherMap(selectedBgStyle, v2a, [])
											),
											[selection, onClick, rl, hoverEnabled]
											|> (\st : [MListState] -> ifArrayPush(st, !reorderShowIcon, MListReorderHideIcon()))
										),
										m2t
									)
							),
						\ -> {
							listSort = make([]);

							TConstruct(
								[
									makeSubscribe(sortedBy, \sb -> nextDistinct(listSort, sb.second)),
								]
								|> (\arr -> maybeArrayPush(arr,
										maybeMap(mRowsPerPage, \mrpp -> \ -> fconnect(rowsPerPage, mrpp.rowsPerPage)))),
								MDynamicList2T(
									manager,
									p2,
									MDynamicList(
										cast(f3 : Transform<[MCustomLine]> -> Transform<[MListLine]>),
										concat(
											[MSideBorders(const(0.))],
											eitherMap(selectedBgStyle, v2a, [])
										),
										[selection, selectionEnabled, onClick, MListReorder(listSort, const(false)), scrollPosition, TScrollEnabled(scrollEnabled),
											MScrollbars(
												fif(scrollEnabled, getMaterialScrollBar().horz |> const, invisibleScrollBar |> const),
												fif(scrollEnabled, getMaterialScrollBar().vert |> const, invisibleScrollBar |> const)
											), hoverEnabled, MScrollBound(scrollEnabled, scrollEnabled)]
									),
									m2t
								)
							)
						}
					)
				)
				|> (\f3 ->
					eitherMap(
						fixedRow,
						\fr -> {
							bStyle = filtermap(fr.style, \st -> switch (st) {
								TGraphicsStyle() : {a : Maybe<TGraphicsStyle> = Some(st); a};
								default : None()
							});

							onFixedRowClick = tryExtractStruct(fr.style, MOnClick(nop));
								
							TCopySize(
								MSelect2T(manager, p2, "MDataTableFixedRow", [], f2, \fr2, p3 ->
									firstElement(fr2, MCustomLine(TEmpty(), [])).content
									|> (\f4 -> eitherMap(
										onFixedRowClick,
										\onclk -> 
											MFocusClickable([
												MOnClick(onclk.click),
												MRippleType(const(MRippleFill())),
												clickEnabled
											], \__ -> f4),
										f4
									))
									|> (\f4 -> m2t(f4, p3))
								),
								\tr -> TRounded(2., 2., 0., 0., bStyle, tr),
								false
							)
							|> (\t -> TShow(fr.show, t))
							|> (\t -> TLines2(t, f3))
						},
						f3
					)
				)
			)
			|> (\f2 ->
				TAvailableHeight(
					f2,
					fselect3(rowsPerPage, rowsCountB, eitherMap(fixedRow, \fr -> fr.show, const(false)), \rpp, rc, fr ->
						i2d(min(rpp, rc) + b2i(fr)) * countRowHeight(er)
						+ if (cpp) 0.0001 else 0. // workaround for missing bottom separator bug
					)
				)
			)
		)
		|> (\f -> TConstruct([
					make5Subscribe(
						screenW,
						rowsBoxH,
						expandRow,
						eitherMap(fixedRow, \fr -> fr.show, const(false)),
						rowsCountB,
						\width, height, er, fr, rowsCount -> {
							if (pagination.rowsPerPage == [])
								if (autoPagination || fgetValue(scrollEnabled)) {

									newRowsCount =
										forceRange(
											floor(height / countRowHeight(er)),
											minimumRows(er),
											rowsCount + b2i(fr)
										) - b2i(fr);

									pg = getValue(page);
									if (pg.second != newRowsCount)
										next(page,
											Pair(
												forceRange(
													pg.first * newRowsCount / max(pg.second, 1),
													0,
													max(rowsCount - 1, 0)
												),
												newRowsCount
											)
										);
								}
							else
								updateRowsPerPage(rowsCount);

							if (fullWidth && length(columnWidths) > 0) {
								miw =
									mapi(columns, \i, c -> {
										if (contains(c.style, MWidthByContent()))
											fgetValue(columnWidths[i])
										else
											minWidths[i]
									})
									|> (\arr ->
										mapSplitWidths(arr, er,
											\r -> either(maxA(r), -1.)
										)
									);

								maw =
									mapi(columns, \i, c -> {
										if (contains(c.style, MWidthByContent()))
											fgetValue(columnWidths[i])
										else
											maxWidths[i]
									})
									|> (\arr ->
										mapSplitWidths(arr, er,
											\r -> if (contains(r, -1.)) -1. else either(maxA(r), -1.)
										)
									);

								newSizes =
									distributeSizesEvenly3(
										miw,
										maw,
										max(width, getValue(footerMinWidth)) - border * 2. - reorderIconSpace
									);

								iteri(
									concat([[columnWidths[0]]], splitByNumber(columnWidths |> tail, er)),
									\i, r -> iter(r, \r0 ->
										switch (r0) {
											DynamicBehaviour(__, __):
												nextDistinct(r0, newSizes[i]);
											default: {}
										}
									)
								)
							}
						}
					)],
					f
				)
		)
		|> (\t -> TAttachBoxCopyAvailableHeight(t, rowsBoxH))
		|> (\t -> if (separators) TLines2(m2t(MSeparator(true), p), t) else t)
		// Header
		|> (\f -> TLines2(tableHeader(p), f))
		|> addHeaderActions(p)
		// Bottom bar
		|> (\f -> TLines2(f, footer(p)))
		// Card
		|> (\f ->
			if (contains(style, MInCard())) {
				TFrame(0.0, 2.0, [Fill(if (p.theme.lightBackground) white else 0x424242)], f)
				|> (\f2 -> TShadow(const(2.0), p.theme.lightBackground, f2))
			} else
				f)
		|> (\f ->
			TConstruct(
				[
					\ -> \ -> applyall(uns),
					\ -> \ -> applyall(uns2),
					makeSubscribe(selection.selected, \ar -> {
						maybeApply(singleSelection, \ss -> nextDistinct(ss.selected, if (length(ar) > 0) ar[0] else -1));
						maybeApply(multiSelection, \ms -> nextDistinct(ms.selected, buildSet(ar)));
					}),
					make2Subscribe(page, expandRow, \pg, er -> {
						if (autoPagination || pagination.rowsPerPage != [])
							nextDistinct(scrollPosition.position, Point(0., i2d(pg.first) * countRowHeight(er)));

						maybeApply(currentPage, \cp -> nextDistinct(cp.page, pg.first / max(pg.second, 1)));
					}),
					\ -> fBidirectionalLink(sortedByB, sortColumnNumberB, \sb -> nextDistinct(sortColumnNumberB, sb.first), \num -> {
						sortColumn =
							if (num >= 0) {
								if (isSome(reorderList))
									None()
								else
									tryExtractStruct(columns[num].style, MSort(\__, __ -> 0));
							} else
								None();

						nextDistinct(sortedByB, Triple(num, !getValue(defaultSorted.sorting).isAsc, sortColumn));
					}),
					\ -> fBidirectionalLink(page, pageNumberB,
							\pair -> nextDistinct(pageNumberB, pair.first / max(pair.second, 1)),
							\num -> {
								rpp = fgetValue(rowsPerPage);
								rowsCount = fgetValue(rowsCountB);
								updatePageNumber(forceRange(num * rpp, 0, rowsCount - 1))
							})
				]
				|> (\f2 ->
					eitherMap(
						singleSelection,
						\ss ->
							concat([makeSubscribe(ss.selected, \ar -> nextDistinct(selection.selected, if (ar >= 0) [ar] else []))], f2),
						f2
					)
				)
				|> (\f2 ->
					eitherMap(
						multiSelection,
						\ms ->
							concat([makeSubscribe(ms.selected, \ar -> nextDistinct(selection.selected, set2array(ar)))], f2),
						f2
					)
				)
				|> (\f2 ->
					eitherMap(
						currentPage,
						\cp ->
							concat(
								[
									make2Subscribe(cp.page, rowsCountB, \pg, rowsCount -> {
										rpp = fgetValue(rowsPerPage);
										updatePageNumber(forceRange(pg * rpp, 0, rowsCount - 1))
									})
								],
								f2
							),
						f2
					)
				)
				|> (\f2 ->
					eitherMap(
						goToIndex,
						\gti ->
							concat(
								[
									make2Subscribe(gti.index, rowsCountB, \ind, rowsCount ->
										fromArrayIndex([ind],  Pair(0, rowsCount), rowsCount, fgetValue(sortedBy).second)
										|> (\f3 -> if (length(f3) > 0) {
												rpp = fgetValue(rowsPerPage);
												updatePageNumber(if (rpp == 0) 0 else f3[0] / rpp * rpp)
											})
									)
								],
								f2
							),
						f2
					)
				),
				f
			)
		)
		|> (\t -> TAvailableWidth(t, fselect(expandRow, FLift(getWidthsSum))))
		|> (\t -> if (fullWidthAdvanced) TTweak([TSnapSize(8., -1., false)], t, TEmpty()) else t)
		|> (if (pagination.rowsPerPage == [] && !autoPagination && isFConst(m.rows) && !fgetValue(scrollEnabled)) makeTropicConstantHeight(None()) else idfn)
		|> if (fullWidth || isAnyWidthByContent) idfn else makeTropicConstantWidth(None())
	})
	|> (\f ->
		MActivate2T(
			manager,
			parent,
			"MDynamicDataTable",
			[
				IScriptBehaviour("current_page", pageNumberB, ISInputValue([], "int", true)),
				IScriptBehaviour("sort_by", sortColumnNumberB, ISInputValue(map(columns, \c -> fgetValue(c.header)), "index", true))
			],
			f
		)
	)
	|> (\t -> if (isFConst(expandRowEnabled) && !fgetValue(expandRowEnabled) && !fullWidth) t else TGroup2SameMetrics(t, TAttachWidthCopy(TFillX(), screenW)))
}

MCellContent2T(manager : MaterialManager, parent : MFocusGroup, c : Material, rowHeight : double, minWidth : double, tr : Tropic, crop : bool, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	wrapCellContent = \m -> MCellContent2T(manager, parent, m, rowHeight, minWidth, tr, crop, m2t);
	wrapCellContentDef = \m -> MCellContentDefault2T(parent, m, tr, crop, m2t);

	switch (c) {
		MSmallEditDialog(__, __, __): {
			MSmallEditDialog2T(manager, parent, c, m2t)
		}
		MSwitchControl(__, __): {
			TBorder(-4.0, -8., -4.0, -8., MSwitchControl2T(manager, parent, c))
		}
		MDropDown(__, __, __, __): {
			TBorder(-24., 0., -24., 0., MDropDown2T(manager, parent, c, m2t))
		}
		MDropDownMenu(__, __, __): {
			TBorder(-24., 0., -24., 0., MDropDownMenu2T(manager, parent, c, m2t))
		}
		MText(text, style): {
			MEllipsisText2T(manager, parent, MEllipsisText(text, replaceStructMany(style, [MShowTooltip(const(true)), MMinWidth(minWidth)])))
		}
		MIconButton(name, onClick, style, state): {
			MIconButton2T(manager, parent, MCellIconButton(name, onClick, style, state, rowHeight))
		}
		MTooltip(cont, tooltip, st): {
			MTooltip2T(manager, parent, MTooltip(wrapCellContent(cont), tooltip, st), m2t)
		}
		MConstruct(cons, content): {
			TConstruct(cons, wrapCellContent(content));
		}
		MShow(show, content): {
			TShow(show, wrapCellContent(content));
		}
		MVisible(visible, content): {
			TVisible(visible, wrapCellContent(content));
		}
		MMutable(contentT): {
			TMutable(fselect(contentT, FLift(wrapCellContent)))
		}
		MIf(cond, contentA, contentB): {
			TIf(cond, wrapCellContent(contentA), wrapCellContent(contentB))
		}
		MGroup(elements): {
			TGroup(map(elements, wrapCellContent))
		}
		MGroup2(m1, m2): {
			TGroup2(wrapCellContent(m1), wrapCellContent(m2))
		}
		MLines(elements): {
			TLinesA(map(elements, wrapCellContent))
		}
		MLines2(m1, m2): {
			TLines2(wrapCellContent(m1), wrapCellContent(m2))
		}
		MCols(elements): {
			TColsA(map(elements, wrapCellContent))
		}
		MCols2(m1, m2): {
			TCols2(wrapCellContent(m1), wrapCellContent(m2))
		}
		MCenter(m): {
			TCenter(wrapCellContent(m))
		}
		MCenterX(m): {
			TCenterX(wrapCellContent(m))
		}
		MCenterY(m): {
			TCenterY(wrapCellContent(m))
		}
		MMenu(button, items, style): {
			switch (button) {
				MIconButton(name, onClick, iconStyle, state): {
					wrapCellContentDef(MMenu(MCellIconButton(name, onClick, iconStyle, state, rowHeight), items, style))
				}
				default: wrapCellContentDef(c);
			}
		}
		default: {
			wrapCellContentDef(c)
		}
	}
}

MCellIconButton(name : string, onClick : () -> void, style : [MIconButtonStyle], state : [MButtonState], rowHeight : double) -> MIconButton {
	iconSize : MIconSize = MIconSize(min(extractStruct(style, MIconSize(if (rowHeight > 32.) 24. else 18.)).size, rowHeight));
	circleBackground : MCircleBackground =
		extractStruct(style, MCircleBackground(MRed(500), 0.))
		|> (\f -> MCircleBackground(f.color, min(f.size, rowHeight)));
	border : MIconButtonBorder =
		MIconButtonBorder(
			forceRange(
				extractStruct(style, MIconButtonBorder(iconSize.size / 2.)).border,
				0.,
				(rowHeight - iconSize.size) / 2.
			)
		);

	MIconButton(name, onClick, replaceStructMany(style, [iconSize, circleBackground, border]), state);
}

MCellContentDefault2T(parent : MFocusGroup, c : Material, tr : Tropic, crop : bool,  m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	m2t(c, parent)
	|> (\f -> if (crop) TCopySize2(f, \tr2, f3 -> TCropSize(TMinimumGroup2(tr2, tr), f3)) else f)
}

toArrayIndex(arr : [int], pageNumber : int, rowsCount : int, sortedBy : [int]) -> [int] {
	filtermap(arr, \a -> {
		idx = pageNumber + a;
		if (idx >= 0 && idx < rowsCount)
			Some(if (sortedBy != []) sortedBy[idx] else idx)
		else
			None()
	})
}

fromArrayIndex(ar : [int], pg : Pair<int, int>, rowsCount : int, sortedBy : [int]) -> [int] {
	fold(ar, [], \acc, arInd -> {
		if (arInd < rowsCount) {
			a = (if (sortedBy != []) elemIndex(sortedBy, arInd, 0) else arInd) - pg.first;
			if (a >= 0 && a < pg.second)
				arrayPush(acc, a)
			else
				acc
		} else {
			acc
		}
	})
}

hideIfLittlePages(rowsCount : int, rowsPerPage : Transform<int>) -> (Tropic) -> Tropic {
	\t -> TShow(fselect(rowsPerPage, FLift(\rpp -> divCeil(rowsCount, rpp) > 2)), t)
}

divCeil(a : int, b : int) -> int {
	ceil(i2d(a) / i2d(b))
}

distributeSizesEvenly3(
	minWidths : [double],
	maxWidths : [double], // -1. for no limits
	availableWidth : double) -> [double] {

	currentSum = dsum(minWidths);

	newWidths =
		mapi(minWidths, \i, w -> {
			newWidth = w / currentSum * availableWidth;
			if (maxWidths[i] != -1. && newWidth > maxWidths[i])
				Pair(maxWidths[i], true)
			else if (newWidth < minWidths[i])
				Pair(minWidths[i], false)
			else
				Pair(newWidth, false)
		});

	exceedLimits = map(newWidths, secondOfPair);

	if (any(exceedLimits) && !(all(exceedLimits))) {
		filterFixed = \arr -> mapi(arr, \i, item -> if (newWidths[i].second) 0. else item);
		newAvailableWidth = availableWidth - fold(newWidths, 0., \acc, item -> acc + if (item.second) item.first else 0.);

		innerNewSizes =
			distributeSizesEvenly3(
				filterFixed(minWidths),
				filterFixed(maxWidths),
				newAvailableWidth
			);

		mapi(newWidths, \i, item -> if (item.second) item.first else innerNewSizes[i])
	} else
		map(newWidths, firstOfPair);
}

splitWidths(arr : [?], er : int) -> [[?]] {
	concat([[arr[0]]], splitByNumber(arr |> tail, er))
}

mapSplitWidths(arr : [?], er : int, fn : ([?]) -> double) -> [double] {
	map(splitWidths(arr, er), fn)
}

updateTrigger(trigger : DynamicBehaviour<bool>, delay : int) -> void {
	timer(delay, \ -> {
		reverseBehaviour(trigger);
		updateTrigger(trigger, delay)
	})
}