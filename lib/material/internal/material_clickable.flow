import material/internal/material_util;
import material/internal/material_mutable;
import material/internal/material_debug;
import material/material_gui;

export {
	clickable(manager : MaterialManager, parent : MFocusGroup, t : Tropic, onClick : () -> void) -> Tropic;
	clickableDownHover(manager : MaterialManager, parent : MFocusGroup, t : Tropic, point : Maybe<DynamicBehaviour<Point>>,
		down : DynamicBehaviour<bool>, hover : DynamicBehaviour<bool>, onClick : () -> void) -> Tropic;
	downMask(manager : MaterialManager, box : Tropic, onDown : (point : Point, inside : bool) -> void) -> Tropic;
	mouseDownAround(manager : MaterialManager, point : Maybe<DynamicBehaviour<Point>>, down : DynamicBehaviour<bool>, hover : DynamicBehaviour<bool>,
		onClick : () -> void, t : Tropic) -> Tropic;
	mouseDownAroundPassClicks(manager : MaterialManager, point : Maybe<DynamicBehaviour<Point>>, down : DynamicBehaviour<bool>,
		hover : DynamicBehaviour<bool>, onClick : () -> void, t : Tropic) -> Tropic;
	mouseOnDownAroundCommon(manager : MaterialManager, state0 : [MouseOnDownAroundState], t : Tropic) -> Tropic;

	mouseOnDownAround(manager : MaterialManager, state : [MouseOnDownAroundState], t : Tropic) -> Tropic;

	disableScrollInside(manager : MaterialManager, content : Tropic) -> Tropic;
	disableScrollInsideEnabled(manager : MaterialManager, content : Tropic, enabled : Transform<bool>) -> Tropic;
	disableMScrollInside(manager : MaterialManager, content : Material) -> Material;

	materialDoubleClickDelay = ref 500;

	MSwipe2T(manager : MaterialManager, parent : MFocusGroup, m : MSwipe, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
	debugMaterialClicks = isUrlParameterTrue("debug_material_clicks");
	clickOnMouseDown = isUrlParameterTrue("click_on_mouse_down");
	swipeLayersFixEnabled = !isUrlParameterFalse("swipe_layers_fix");
}

mouseDownAround(manager : MaterialManager, point : Maybe<DynamicBehaviour<Point>>, down : DynamicBehaviour<bool>, hover : DynamicBehaviour<bool>,
	onClick : () -> void, t : Tropic) -> Tropic {
	state =
		concat(
			eitherMap(point, \p -> [MMousePosition(p)], []),
			[
				MOnClick(onClick),
				TPressed(down),
				THovering(hover),
			]
		);

	mouseOnDownAroundCommon(manager, state, t)
}

mouseDownAroundPassClicks(manager : MaterialManager, point : Maybe<DynamicBehaviour<Point>>, down : DynamicBehaviour<bool>,
	hover : DynamicBehaviour<bool>, onClick : () -> void, t : Tropic) -> Tropic {

	state =
		concat(
			eitherMap(point, \p -> [MMousePosition(p)], []),
			[
				MPassClicks(),
				MOnClick(onClick),
				TPressed(down),
				THovering(hover),
			]
		);

	mouseOnDownAroundCommon(manager, state, t)
}

downMask(manager : MaterialManager, box : Tropic, onDown : (point : Point, inside : bool) -> void) -> Tropic {
	state = [
		MPassClicks(),
		MOnMouseDown(onDown),
	];

	mouseOnDownAroundCommon(manager, state, box)
}

mouseOnDownAroundCommon(manager : MaterialManager, state0 : [MouseOnDownAroundState], t : Tropic) -> Tropic {
	zorder = ref getValue(manager.manager.zorder);
	state =
		arrayPush(state0,
			MEnabled(fselect(manager.manager.zorder, FLift(\cz -> ^zorder >= cz)))
		);

	TConstruct(
		[\ -> { zorder := getValue(manager.manager.zorder); nop }],
		mouseOnDownAround(manager, state,  t)
	)
}

mouseOnDownAround(manager : MaterialManager, state : [MouseOnDownAroundState], t : Tropic) -> Tropic {
	interactionId = extractStruct(state, MInteractionId(makeTropicTag(manager))).id;

	uns = initUnsM();
	longUns = initUnsM();
	doubleClickUns = initUnsM();
	wasDown = ref false;
	doubleClickWasDown = ref false;
	tripleClickWasDown = ref false;
	longClickWasDown = ref false;
	longDelay = extractStruct(state, MLongTouchDelay(500)).delay;

	down = extractStruct(state, TPressed(make(false))).pressed;
	rightDown = extractStruct(state, MRightDown(make(false))).down;
	middleDown = extractStruct(state, MMiddleDown(make(false))).down;
	hover = extractStruct(state, THovering(make(false))).hovering;
	additionalHover = make(false);
	onClick =
		[extractStruct(state, MOnClick(nop)).click, extractStruct(state, TOnClicked(nop)).clicked]
		|> (\f -> \ -> applyall(f));
	onClickAsync = tryExtractStruct(state, MOnClickAsync(nop1));
	middleClick = tryExtractStruct(state, MOnMiddleClick(nop));
	rightClick = tryExtractStruct(state, MOnRightClick(nop));
	doubleClick = tryExtractStruct(state, MOnDoubleClick(nop));
	tripleClick = tryExtractStruct(state, MOnTripleClick(nop));
	longClick = tryExtractStruct(state, MOnLongClick(nop));
	longTouch =
		tryExtractStruct(state, MOnLongTouch(nop))
		|> (\longTouch ->
			eitherMap(
				longClick,
				\lc ->
					eitherFn(
						longTouch,
						\lt -> Some(MOnLongTouch(\ -> {longClickWasDown := true; lt.touch();})),
						\ -> Some(MOnLongTouch(\ -> longClickWasDown := true))
					),
				longTouch
			)
		);
	onTouch = tryExtractStruct(state, MOnTouch(500, nop));
	point = tryExtractStruct(state, MMousePosition(make(zeroPoint)));
	disableHover = contains(state, MDisableHover());
	blockClicks = !contains(state, MPassClicks());
	additionalRollOutCheck = contains(state, MAdditionalRollOutCheck());
	mobileForceHover = contains(state, MMobileForceHover());
	clickEnabled = make(true);
	enabled =
		fand(extractStruct(state, MEnabled(const(true))).enabled, extractStruct(state, TEnabled(const(true))).enabled)
		|> (\enabled -> if (isSome(onClickAsync)) fand(enabled, clickEnabled) else enabled);
	onDown = tryExtractStruct(state, MOnMouseDown(nop2));
	onUp = tryExtractStruct(state, MOnMouseUp(nop2));
	interactionsOnTop = contains(state, MInteractionsOnTop());
	triggerClickOnMouseDown = clickOnMouseDown && !mobile && isNone(longClick) && isNone(onTouch);

	onClickFn =
		eitherMap(
			longClick,
			\lc -> \ ->
				if (^longClickWasDown) {
					longClickWasDown := false;
					lc.click();
				} else {
					onClick();
				},
			onClick
		);

	callOnClickFn = \ -> {
		(\ -> {
			if (isSome(doubleClick) || isSome(tripleClick)) {
				if (!^doubleClickWasDown) {
					doubleClickWasDown := true;

					dispUnsM(doubleClickUns);
					setUnsM(
						doubleClickUns,
						interruptibleTimer(^materialDoubleClickDelay, \ -> {
							doubleClickWasDown := false;
							onClickFn();
						})
					);
				}
			} else {
				onClickFn()
			}
		})
		|> (\f ->
			eitherMap(
				onClickAsync,
				\clickAsync : MOnClickAsync -> \ -> {
					next(clickEnabled, false);
					f();
					timerDisp = interruptibleTimer(10000, \ -> nextDistinct(clickEnabled, true));

					clickAsync.click(\ -> { timerDisp(); nextDistinct(clickEnabled, true); });
				},
				f
			)
		)
		|> (\f ->
			eitherFn(
				tripleClick,
				\tc ->
					if (^doubleClickWasDown && !^tripleClickWasDown) {
						doubleClickWasDown := false;
						tripleClickWasDown := true;

						dispUnsM(doubleClickUns);
						setUnsM(
							doubleClickUns,
							interruptibleTimer(^materialDoubleClickDelay, \ -> {
								tripleClickWasDown := false;

								eitherFn(
									doubleClick,
									\dc ->
										dc.click(),
									onClickFn
								)
							})
						);
					} else
						f(),
				f
			)
		);
	};

	onDownFn =
		eitherMap(
			point,
			\curPoint ->
				Some(MOnMouseDown(\p, inside -> {
					if (inside)
						nextDistinct(curPoint.point, p);
					maybeApply(onDown, \fn -> fn.down(p, inside));
				})),
			onDown
		);

	release = \ -> {
		if (debugMaterialClicks)
			println("release " + i2s(interactionId));
		dispUnsM(uns);
		nextDistinct(down, false);
	};

	hold = \ -> {
		if (debugMaterialClicks)
			println("hold " + i2s(interactionId));
		dispUnsM(uns);
		addMInteractionItemId(manager, interactionId);
		if (mobile && mobileForceHover) nextDistinct(hover, true);
		nextDistinct(down, true);
		wasDown := true;

		setUnsM(
			uns,
			[
				makeSubscribe(manager.manager.currentInteractionItemID, \ii -> if (!contains(ii, interactionId)) deferred(release))(),
			]
			|> (\f ->
				// TODO: Dispose on hover false
				eitherFn(
					longTouch,
					\lt -> {
						setUnsM(
							longUns,
							interruptibleTimer(longDelay, \ -> { blockOtherMInteractionItemIds(manager, interactionId); lt.touch(); })
						);
						arrayPush(f, \ -> dispUnsM(longUns));
					},
					\ ->
						if (blockClicks)
							arrayPush(f, interruptibleTimer(510, \ -> blockOtherMInteractionItemIds(manager, interactionId)))
						else
							f
				)
			)
			|> (\f ->
				eitherMap(
					onTouch,
					\ot -> {
						longUns1 = either(^longUns, nop);
						longUns2 = interruptibleTimer(ot.delay, \ -> if (isCurrentMInteractionItemId(manager, interactionId)) {
							blockOtherMInteractionItemIds(manager, interactionId);
							ot.touch();
						});
						setUnsM(
							longUns,
							\ -> {
								longUns1();
								longUns2();
							}
						);

						arrayPush(f, \ -> dispUnsM(longUns));
					},
					f
				)
			)
			|> (\f -> \ -> applyall(f))
		);
	};

	(\size -> TInteractive(
		[
			MouseDown(\mi ->
				if (fgetValue(enabled)) {
					if (mi.inside()) {
						if (debugMaterialClicks)
							println("down " + i2s(interactionId));
						maybeApply(onDownFn, \fn -> fn.down(Point(mi.x, mi.y), true));
						hold();

						eitherFn(
							tripleClick,
							\tc ->
								if (^tripleClickWasDown) {
									dispUnsM(uns);
									dispUnsM(doubleClickUns);
									tripleClickWasDown := false;

									tc.click();
								},
							\ ->
								maybeApply(
									doubleClick,
									\dc ->
										if (^doubleClickWasDown) {
											dispUnsM(uns);
											dispUnsM(doubleClickUns);
											doubleClickWasDown := false;

											dc.click();
										}
								)
						);

						if (triggerClickOnMouseDown && isSome(^uns)) {
							callOnClickFn();
						}

						blockClicks
					} else {
						maybeApply(onDownFn, \fn -> fn.down(Point(mi.x, mi.y), false));
						false
					}
				} else {
					false
				}
			),
			MouseUp2(\h, mi -> {
				wasDown := false;

				if (getValue(down)) {
					if (debugMaterialClicks)
						println("up " + i2s(interactionId));

					mouseInfo = mi();
					if (fgetValue(enabled))
						maybeApply(onUp, \fn -> fn.up(Point(mouseInfo.x, mouseInfo.y), mouseInfo.inside));
					next(down, false);

					if (!h && isSome(^uns) && fgetValue(enabled) && mouseInfo.inside) {
						dispUnsM(uns);

						if (!triggerClickOnMouseDown) {
							callOnClickFn();
						}

						blockClicks
					} else {
						h
					}
				} else {
					if (fgetValue(enabled))
						maybeApply(onUp, \fn -> {mouseInfo = mi(); fn.up(Point(mouseInfo.x, mouseInfo.y), mouseInfo.inside);});
					h
				}
			})
		]
		|> (\f ->
			if (disableHover)
				f
			else if (!mobile && isNone(point) && blockClicks)
				concat(
					f,
					[
						RollOver(\__ -> {
							if (debugMaterialClicks)
								println("handled roll over " + i2s(interactionId));

							nextDistinct(hover, true);
						}),
						RollOut(\__ -> {
							if (^wasDown || getValue(hover)) {
								if (debugMaterialClicks)
									println("handled roll out " + i2s(interactionId));

								dispUnsM(longUns);
								// In rare cases, rollout is fired while cursor is still inside the element
								nextDistinct(hover, getValue(additionalHover));
								if (!fgetValue(enabled)) release();
							}

							nextDistinct(rightDown, false);
							nextDistinct(middleDown, false);
						})
					]
					|> (\rollInteractives ->
						if (additionalRollOutCheck)
							concat(
								rollInteractives, [
								MouseMove2(\h, mi -> {
									if (!mi().inside) {
										dispUnsM(longUns);
										nextDistinct(hover, false);
									} else if (!h) {
										nextDistinct(hover, true);
									}

									h
								}),
								TMouseInside(additionalHover, false)
							])
						else
							rollInteractives
					)
				)
			else
				arrayPush(
					f,
					MouseMove2(\h, mi0 ->
						if (fgetValue(enabled) && !h) {
							mi = mi0();

							if (mi.inside) {
								if (debugMaterialClicks)
									println("handled inside " + i2s(interactionId));

								maybeApply(point, \p -> nextDistinct(p.point, Point(mi.x, mi.y)));
								if (!mobile || mobileForceHover) nextDistinct(hover, true);
								blockClicks && getValue(manager.manager.dragging) == -1
							} else {
								if (^wasDown || getValue(hover)) {
									if (debugMaterialClicks)
										println("handled not inside " + i2s(interactionId));

									maybeApply(point, \p -> nextDistinct(p.point, Point(mi.x, mi.y)));
									dispUnsM(longUns);
									if (!mobile || mobileForceHover) nextDistinct(hover, false);
								}

								nextDistinct(rightDown, false);
								nextDistinct(middleDown, false);

								false
							}
						} else {
							if ((^wasDown || getValue(hover)) && !mi0().inside) {
								if (debugMaterialClicks)
									println("not handled not inside " + i2s(interactionId));

								dispUnsM(longUns);
								if (!mobile || mobileForceHover) nextDistinct(hover, false);
								if (^wasDown && !fgetValue(enabled)) release();
							}
							h
						}
					)
				)
		)
		|> (\f ->
			if (!mobile)
				concat(
					f,
					[
						MouseMiddleDown(\mi ->
							if (fgetValue(enabled) && mi.inside()) {
								nextDistinct(middleDown, true);
								true
							} else
								false
						),
						MouseMiddleUp2(\h, mi -> {
							if (getValue(middleDown)) {
								maybeApply(middleClick, \mc -> if (fgetValue(enabled) && mi().inside) mc.click());
								nextDistinct(middleDown, false);

								true
							} else
								h
						}),
						MouseRightDown(\mi ->
							if (fgetValue(enabled) && mi.inside()) {
								if (debugMaterialClicks) println("rightDown true");
								nextDistinct(rightDown, true);
								true
							} else {
								false
							}
						),
						MouseRightUp2(\h, mi -> {
							if (getValue(rightDown)) {
								maybeApply(rightClick, \rc -> if (fgetValue(enabled) && mi().inside) rc.click());
								if (debugMaterialClicks) println("rightDown false");
								nextDistinct(rightDown, false);

								true
							} else {
								h
							}
						})
					]
				)
			else
				f
		),
		size
	))
	|> (\f ->
		if (interactionsOnTop)
			TCopySize(t, f, true)
		else
			f(TFrame(0.0, 0.0, interactiveRectangleStyle, t))
	)
	|> (\f ->
		TDispose(
			[
				\ -> {
					release();
					nextDistinct(hover, false);
				}
			],
			f
		)
	)
	|> (\f ->
		materialLogFn(
			\ ->
				TCopySize(
					f,
					\tr -> TShow(manager.debugMetrics, TRectangle([FillOpacity(0.), Stroke(green)], tr)),
					true
				),
			MaterialInfo(),
			\ -> f
		)
	)
}

clickable(manager : MaterialManager, parent : MFocusGroup, t : Tropic, onClick : () -> void) -> Tropic {
	clickableDownHover(manager, parent, t, None(), make(false), make(false), onClick)
}

clickableDownHover(manager : MaterialManager, parent : MFocusGroup, t : Tropic, point : Maybe<DynamicBehaviour<Point>>,
	down : DynamicBehaviour<bool>, hover : DynamicBehaviour<bool>, onClick : () -> void) -> Tropic {
	click = fn2IScriptBehaviourDisposable(onClick, "click", ISClickEvent());

	MComponentGroup2T(
		manager,
		parent,
		"MClickable",
		[
			IScriptBehaviour("down", down, ISInteractionEvent()),
			IScriptBehaviour("hover", hover, ISInteractionEvent()),
			click.monitorBehaviour
		]
		|> (\f2 ->
			eitherMap(
				point,
				\p -> arrayPush(f2, IScriptBehaviour("point", p, ISInteractionEvent())),
				f2
			)
		),
		\__ ->
			TCursor(FingerCursor(), mouseDownAround(manager, point, down, hover, click.fn, t))
			|> (\f ->
				TConstruct(
					[click.subscribers],
					f
				)
			)
	)
}

disableScrollInside(manager : MaterialManager, content : Tropic) -> Tropic {
	interactiveId = makeTropicTag(manager);
	down = make(false);

	TCopySize(content, \t -> TInteractive([TMouseDownGhost(down)], t), true)
	|> (\f -> TConstruct([makeSubscribe2(down, \__ -> blockOtherMInteractionItemIds(manager, interactiveId))], f))
}

disableScrollInsideEnabled(manager : MaterialManager, content : Tropic, enabled : Transform<bool>) -> Tropic {
	interactiveId = makeTropicTag(manager);

	TCopySize(
		content,
		\sz -> {
			down = make(false);

			TConstruct(
				[
					makeSubscribe(down, \__ -> if (!isUrlParameterFalse("greedyscroll") || fgetValue(enabled)) {
						blockOtherMInteractionItemIds(manager, interactiveId)
					})
				],
				TInteractive([TMouseDownGhost(down)], TRectangle([Fill(black), FillOpacity(0.)], sz))
			)
			|> (\t -> TShow(enabled, t))
		},
		true
	)
}

disableMScrollInside(manager : MaterialManager, content : Material) -> Material {
	interactiveId = makeTropicTag(manager);
	down = make(false);

	MCopySize(content, \t -> MInteractive([TMouseDownGhost(down)], t), true)
	|> (\f -> MConstruct([makeSubscribe2(down, \__ -> blockOtherMInteractionItemIds(manager, interactiveId))], f))
}

MSwipe2T(manager : MaterialManager, parent : MFocusGroup, m : MSwipe, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	swipeId = makeTropicTag(manager);
	currentInteractive = extractStruct(m.state, MCurrentInteractive(make(None()))).current;
	down = extractStruct(m.state, TPressed(make(false))).pressed;
	onSwipeCallback = extractStruct(m.state, MOnSwipe(nop1)).fn;
	onSwipeDown = extractStruct(m.state, MOnSwipeDown(\__, __ -> false)).onSwipe;
	onSwipeUp = extractStruct(m.state, MOnSwipeUp(\__, __ -> false)).onSwipe;
	onSwipeLeft = extractStruct(m.state, MOnSwipeLeft(\__, __ -> false)).onSwipe;
	onSwipeRight = extractStruct(m.state, MOnSwipeRight(\__, __ -> false)).onSwipe;
	enabled = extractStruct(m.state, MEnabled(const(true))).enabled;
	swipeGap = extractStruct(m.state, MSwipeGap(3.)).gap;
	interactionsOnTop = contains(m.state, MInteractionsOnTop());

	lastMouseXY = ref Point(-1., -1.);
	swipeAction = ref -1;

	onSwipe = \p : Point -> {
		if (^lastMouseXY != Point(-1., -1.)) {
			if (^swipeAction >= 0) {
				delta = Point(p.x - (^lastMouseXY.x), p.y - (^lastMouseXY).y);
				lastMouseXY := p;

				if (^swipeAction == 0)
					onSwipeDown(p, delta)
				else if (^swipeAction == 1)
					onSwipeUp(p, delta)
				else if (^swipeAction == 2)
					onSwipeLeft(p, delta)
				else
					onSwipeRight(p, delta);
			} else if (p.y - ^lastMouseXY.y >= swipeGap && onSwipeDown(p, zeroPoint)) {
				swipeAction := 0;
				onSwipeCallback(true);
				true
			} else if (p.y - ^lastMouseXY.y <= -swipeGap && onSwipeUp(p, zeroPoint)) {
				swipeAction := 1;
				onSwipeCallback(true);
				true
			} else if (p.x - ^lastMouseXY.x <= -swipeGap && onSwipeLeft(p, zeroPoint)) {
				swipeAction := 2;
				onSwipeCallback(true);
				true
			} else if (p.x - ^lastMouseXY.x >= swipeGap && onSwipeRight(p, zeroPoint)) {
				swipeAction := 3;
				onSwipeCallback(true);
				true
			} else {
				false
			}
		} else {
			lastMouseXY := p;
			false
		}
	}

	releaseFn = \ -> {
		lastMouseXY := Point(-1., -1.);
		swipeAction := -1;
		nextDistinct(down, false);
		onSwipeCallback(false);
		removeMInteractionItemId(manager, swipeId);
	}

	content = m2t(m.content, parent);
	addInteractions =
		\tr ->
			TShow(
				enabled,
				TInteractive(
					[
						MouseMove(\mi0 -> {
							if (getValue(down) && isCurrentMInteractionItemId(manager, swipeId)) {
								mi = mi0();

								if (mi.inside) {
									point = Point(mi.x, mi.y);
									h = onSwipe(point);

									if (h)
										nextDistinct(currentInteractive, Some(true))
									else if (^swipeAction >= 0)
										nextDistinct(currentInteractive, None());

									h
								} else
									false
							} else {
								nextDistinct(currentInteractive, None());

								false
							}
						}),
						TMouseDownGhost(down)
					],
					TConstruct(
						[
							makeSubscribe2(down, \d ->
								if (d) {
									if (isNone(getValue(currentInteractive)))
										nextDistinct(currentInteractive, Some(false))
								} else {
									nextDistinct(currentInteractive, None())
								}
							),
							makeSubscribe(currentInteractive, \ci ->
								eitherFn(
									ci,
									\bl -> {
										if (!bl) {
											nextDistinct(down, true);
											addMInteractionItemId(manager, swipeId);
										} else {
											nextDistinct(down, true);
											blockOtherMInteractionItemIds(manager, swipeId);
										}
									},
									releaseFn
								)
							),
							\ -> \ -> {
								nextDistinct(currentInteractive, None());
								releaseFn();
							}
						],
						tr
					)
				)
			);

	if (swipeLayersFixEnabled) {
		TCopySize(
			content,
			addInteractions,
			interactionsOnTop
		)
	} else {
		if (interactionsOnTop) {
			TCopySize(
				content,
				addInteractions,
				true
			);
		} else {
			addInteractions(content);
		}
	}
}