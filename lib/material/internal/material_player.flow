import material/internal/material_menu;
import material/internal/material_slider;
import material/internal/material_progress;

export {
	MVideoPlayer2T(manager : MaterialManager, parent : MFocusGroup, m : MVideoPlayer, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;

	MVideoWithAnimatedSubtitles(
		filename : string,
		wh : DynamicBehaviour<WidthHeight>,
		style : [FVideoStyle],
		subtitles : MAnimatedSubtitles
	) -> Material;
}

MVideoPlayer2T(manager : MaterialManager, parent : MFocusGroup, m : MVideoPlayer, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	(\p -> eitherFn2(
		getYouTubeURL(m.filename),
		getVimeoURL(m.filename),
		\url, __ -> MVideoPlayer2MRealHTML(url, p, m, m2t),
		\url -> MVideoPlayer2MRealHTML(url, p, m, m2t),
		\ -> {
			color = extractMColor(p, m.style, MAccentColor());
			backgroundColor = MThemeColor2MColor(p, extractStruct(m.style, MPlayerBackground(MBlack())).color);
			videoHasErrors = make(false);
			enabled =
				fands([
					extractStruct(m.style, MEnabled(const(true))).enabled,
					fnot(videoHasErrors),
					p.enabled
				], true)
				|> MEnabled;

			mPlayerControls = extractStruct(m.style, MPlayerControls([VolumeControl()])).controls;

			controls : [PlayerControl] = concat(
				MPlayerControls2PlayerControls(mPlayerControls),
				extractStruct(m.style, FVideoControls([VolumeControl()])).controls
			);
			volumeControl = contains(controls, VolumeControl());
			fullScreenControl = contains(controls, FullScreenPlayer());
			playbackRateControl = !android() && contains(controls, PlaybackRateControl());
			subtitlesControl = contains(mPlayerControls, SubtitlesControl());
			subtitles = tryExtractStruct(m.style, FVideoSubtitles(const(VideoSubtitle("", []))));
			animatedSubtitles = tryExtractStruct(m.style, MAnimatedSubtitles(const(VideoSubtitle("", [])), []));
			shortcutsEnabled = !containsStruct(m.style, MPlayerDisableKeyboardShortcuts());

			streamStatus = extractStruct(m.style, StreamStatus(nop1)).fn;

			playing = extractStruct(m.style, FVideoPlay(make(false))).play;
			volume = extractStruct(m.style, FVideoVolume(make(1.))).volume;
			fullscreen =
				eitherFn(
					tryExtractStruct(m.style, FVideoFullscreen(make(false))),
					\fs -> Some(fs.fullscreen),
					\ ->
						if (fullScreenControl)
						 	Some(make(false))
						else
							None()
				);
			position = extractStruct(m.style, FVideoPosition(make(0.))).position;
			duration = extractStruct(m.style, FVideoLength(make(1.))).length;
			playbackRate = extractStruct(m.style, FVideoPlaybackRate(make(1.))).rate;
			autoHide = extractStruct(m.style, MPlayerPanelAutoHide(false)).enabled;
			showPanel = extractStruct(m.style, MPlayerShowPanel(const(true))).show;
			visibleRange = extractStruct(m.style, MPlayerVisibleRange(const(0.0), duration));
			visibleDuration = fselect3(visibleRange.start, visibleRange.end, duration, \st, en, d -> min(d, en - st));
			visiblePosition = fselect2(position, visibleRange.start, FLift2(\pos, vs -> max(pos - vs, 0.0)));

			showPanelInner = make(true);

			controlsWH = makeWH();
			videoWH = makeWH();
			availableH = make(0.);
			sliderPosition = make(0.);
			volumeSliderDown = make(false);
			positionSliderDown = make(false);
			volumeSliderHovered = make(false);
			volumeButtonFocused = make(false);
			volumeButtonHovered = make(false);
			playPauseButtonFocused = make(false);
			subtitlesButtonFocused = make(false);
			playbackRateControlButtonFocused = make(false);
			fullScreenControlButtonFocused = make(false);
			volumeSliderFocused = make(false);
			videoFocused = make(false);
			panelHovered = make(false);
			playbackRateSelected = make(3);
			showProgressCircle = make(true);
			subtitlesVisible = make(true);
			subtitlesEnabled = const(isSome(subtitles) || isSome(animatedSubtitles));
			playingPopup = make(None());
			volumePopup = make(None());
			positionPopup = make(None());
			mousePosition = makePoint();
			mouseDown = make(false);
			settingsMenuOpen = make(false);

			makeFadeOutIcon = \icon -> {
				MIcon2T(
					p,
					icon,
					[MCircleBackground(MBlack(), 56.), MIconSize(32.), FillOpacity(1.0)]
				)
				|> (\ic -> TAlpha(const(0.9), ic))
				|> (\ic -> MFadeOutAnimation(manager, p, ic))
				|> (\ic -> TBorder4(48.0, ic))
				|> TCenter
			}

			uns = initUnsM();

			videoStyle =
				m.style
				|> videoPlayerStyle2FVideoStyle
				|> (\st -> removeAllStructs(st, FVideoControls([])))
				|> (\st -> removeAllStructs(st, FVideoFullscreen(make(false))))
				|> (\st ->
					replaceStructMany(
						st,
						[
							FVideoPlay(playing),
							FVideoVolume(volume),
							FVideoPosition(position),
							FVideoLength(duration),
							FVideoPlaybackRate(playbackRate),
							OnVideoLoadingError(\ -> nextDistinct(videoHasErrors, true)),
							StreamStatus(\s -> {nextDistinct(showProgressCircle, false); streamStatus(s)})
						]
					)
				)
				|> (\st ->
					eitherMap(subtitles, \sbtl ->
						replaceStruct(st,
							FVideoSubtitles(fif(subtitlesVisible, sbtl.subtitles, const(VideoSubtitle("", []))))
						),
						st
					)
				)
				|> (\st -> ifArrayPush(
					st,
					containsStruct(m.style, MPlayerVisibleRange(const(0.0), const(0.0))) &&
						!containsStruct(st, FVideoTimeRange(const(0.0), const(0.0))),
					FVideoTimeRange(visibleRange.start, visibleRange.end)
				));

			videoBox =
				MComponent2T(
					manager,
					p,
					"MVideo",
					[MFocused(videoFocused), AccessRole("group"), MCursorShape(const(DefaultCursor()))],
					\foc ->
						TVideoWithAnimatedSubtitles(
							manager,
							p,
							TVideo(m.filename, videoWH, videoStyle),
							maybeMap(animatedSubtitles, \sbtl -> Pair(sbtl, subtitlesVisible)),
							m2t
						)
						|> (\f -> TVisible(parent.ready, f)),
					m2t
				)
				|> (\video ->
					TCopySize(
						video,
						\videoSize ->
							TGroupWithoutMetrics([
								mouseOnDownAround(
									manager,
									[
										MOnClick(\ -> {
											if (!getValue(panelHovered)) {
												next(playingPopup, None());
												reverseBehaviour(playing);
												next(playingPopup, Some(getValue(playing)));
											}
										}),
										TPressed(mouseDown),
										MMousePosition(mousePosition),
										MPassClicks(),
										enabled
									],
									videoSize
								),
								TTweak(
									[TFitZoom(true, false)],
									TGroup([
										TSelect(playingPopup, \plM ->
											eitherMap(
												plM,
												\pl -> {
													nextDistinct(volumePopup, None());
													nextDistinct(positionPopup, None());

													makeFadeOutIcon(
														if (pl)
															"play_arrow"
														else
															"pause"
													);
												},
												TEmpty()
											)
										),
										TSelect(volumePopup, \vM ->
											eitherMap(
												vM,
												\v -> {
													nextDistinct(positionPopup, None());
													nextDistinct(playingPopup, None());

													makeFadeOutIcon(
														if (v == 0.)
															"volume_off"
														else if (v < 0.5)
															"volume_down"
														else
															"volume_up"
													);
												},
												TEmpty()
											)
										),
										TSelect(positionPopup, \posM ->
											eitherMap(
												posM,
												\pos -> {
													nextDistinct(volumePopup, None());
													nextDistinct(playingPopup, None());

													makeFadeOutIcon(
														if (pos)
															"forward_5"
														else
															"replay_5"
													);
												},
												TEmpty()
											)
										)
									])
									|> (\t -> TBorder4(16., t)),
									videoSize
								),
								TCenterIn(
									MProgressCircle2T(manager, p, MProgressCircle([MWhite()])),
									videoSize
								)
								|> (\t2 -> TShow(showProgressCircle, t2))
							]),
						true
					)
				)
				|> (\video -> TVisible(fselect(videoWH, FLift(\vwh -> vwh.width > 0. && vwh.height > 0.)), video))
				|> (\video ->
					TIf(videoHasErrors,
						TGroup2(
							TRectangle([Fill(black)], TFillXY()),
							TCenter(MParagraph2T(p, "An error occurred. Please try again later.", [MWhite(), CenterAlign()]))
						),
						video
					)
				)
				|> addTBackground(backgroundColor);

			slider = \p2 ->
				MSelect2T(
					manager,
					p2,
					"Video Slider Container",
					[],
					duration,
					\d, p3 ->
						MSlider2T(
							manager,
							p3,
							MSlider(
								sliderPosition,
								[
									MCondensed(true),
									MSliderTooltip(\v -> seconds2timelineString(v * fgetValue(visibleDuration))),
									MSliderThumbRadius(const(6.0)),
									MSliderStep(if (d > 0.0) min(max(0.001, 5.0 / d), 0.2) else 0.2),
									MSliderDiscrete(false),
									TPressed(positionSliderDown),
									color,
									MButtonTitle(const(_("Video Slider"))),
									enabled
								]
							),
							m2t
						)
				)
				|> (\f -> TBorder(-2.0, -6.0, -2.0, -2.0, f));

			onKeyDown = \h, ke ->
					if (!h && fgetValue(p.keysEnabled)) {
						if ((ke.utf == " " || ke.utf == "up" || ke.utf == "down" || ke.utf == "right" || ke.utf == "left")
							&& (!fgetValue(videoFocused) && isNone(getCurrentFocusedItem(manager)))) {
							nextDistinct(videoFocused, true);
						}

						if (fgetValue(videoFocused)) {
							if (ke.utf == " " && !getValue(playPauseButtonFocused) && !getValue(subtitlesButtonFocused) && !getValue(fullScreenControlButtonFocused)
								&& !getValue(playbackRateControlButtonFocused) && !getValue(volumeButtonFocused)) {
								next(playingPopup, None());
								reverseBehaviour(playing);
								next(playingPopup, Some(getValue(playing)));
								true;
							} else if (ke.utf == "up") {
								next(volumePopup, None());
								nextDistinct(volume, min(getValue(volume) + 0.1, 1.0));
								next(volumePopup, Some(getValue(volume)));
								true;
							} else if (ke.utf == "down") {
								next(volumePopup, None());
								nextDistinct(volume, max(getValue(volume) - 0.1, 0.0));
								next(volumePopup, Some(getValue(volume)));
								true;
							} else if (ke.utf == "right") {
								next(positionPopup, None());
								nextDistinct(position, min(getValue(position) + min(5.0, max(fgetValue(visibleDuration) / 5.0, 0.1)), fgetValue(visibleRange.end)));
								next(positionPopup, Some(true));
								true;
							} else if (ke.utf == "left") {
								next(positionPopup, None());
								nextDistinct(position, max(getValue(position) - min(5.0, max(fgetValue(visibleDuration) / 5.0, 0.1)), fgetValue(visibleRange.start)));
								next(positionPopup, Some(false));
								true;
							} else {
								false;
							}
						} else {
							false;
						}
					} else {
						h;
					};

			playPauseButton = \p2 ->
				MIconToggle2T(
					manager,
					p2,
					MIconToggle(
						"pause",
						[
							MToggleFalseIcon("play_arrow", [MIconSize(30.)]),
							MIconButtonBorder(3.), MIconSize(30.)
						],
						[
							MToggleValue(playing),
							MButtonTitle(fif(playing, const(_("Pause")), const(_("Play")))),
							MFocused(playPauseButtonFocused),
							enabled
						]
					),
					m2t
				)
				|> (\t -> TBorder(4., 0., 8., 0., t));

			showVolumeSlider =
				if (mobile)
					enabled.enabled
				else
					fselectdelay(
						fand(fOr(fOr(fOr(volumeButtonHovered, volumeSliderHovered), fOr(volumeSliderFocused, volumeButtonFocused)), volumeSliderDown), enabled.enabled),
						\sh -> if (sh) 0 else 100
					);

			panelFillerW = make(0.);
			timeLabelHidden =
				if (mobile) const(false)
				else fselect(showVolumeSlider, FLift(\__ -> getValue(panelFillerW) <= 0.));

			volumeControlButton = \p2 ->
				if (volumeControl)
					TColsYCenter([
						MComponent2T(
							manager,
							p2,
							"MVolumeToggle",
							[
								MButtonTitle(fif(feq(volume, 0.0), const(_("Unmute")), const(_("Mute")))),
								enabled,
								MFocused(volumeButtonFocused),
								MOnClick(\ -> next(volume, if (getValue(volume) == 0.) 1. else 0.)),
								THovering(volumeButtonHovered),
								MTooltipAlignment(MTop()),
								MTooltipText(fif(feq(volume, 0.0), const(_("Unmute")), const(_("Mute")))),
								MRippleType(const(MRippleCenter())),
								MHighlightOnHover(const(true)),
								MHighlightOnFocus(const(true))
							],
							\f ->
								TSelect(
									volume,
									\v ->
										MIcon2T(
											p2,
											if (v == 0.)
												"volume_off"
											else if (v < 0.5)
												"volume_down"
											else
												"volume_up",
											[]
										)
								)
								|> (\f2 -> TBorder4(6.0, f2)),
							m2t
						)
						|> TBorderRight(12.),
						MShow2T(
							manager,
							p2,
							showVolumeSlider,
							\p3 ->
								MSlider2T(
									manager,
									p3,
									MSlider(
										volume,
										[
											MWhite(),
											TPressed(volumeSliderDown),
											MRippleType(const(MNoRipple())),
											MSliderThumbRadius(const(6.0)),
											enabled,
											MWidth(52.0),
											MButtonTitle(const(_("Volume Slider"))),
											MFocused(volumeSliderFocused),
											MHighlightOnHover(const(false)),
											MHighlightOnSelect(const(false))
										]
									),
									m2t
								)
								|> (\volumeSlider ->
									TInteractive([if (mobile) TMouseDownGhost(volumeSliderHovered) else TMouseInside(volumeSliderHovered, false)], volumeSlider)
								)
								|> (\t -> TBorder(-22., 0., 0., 0., t))
								|> TFixWidth(TSizedWidth(fif(timeLabelHidden, zero, const(64.)), 0.))
						)
					])
					|> v2a
				else
					[];

			timeLabel = \p2 ->
				TSelect3(visiblePosition, visibleDuration, enabled.enabled, \pos, dur, en ->
					MText2T(
						p2,
						seconds2timelineString(pos) + " / " + seconds2timelineString(dur),
						if (en)
							[]
						else
							[MTextDisabled()]
					)
				)
				|> TBorderRight(12.)
				|> (\t -> TVisible(fnot(fand(timeLabelHidden, showVolumeSlider)), t));

			playbackRateControlButton = \p2 ->
				if (playbackRateControl)
					MMenu2T(
						manager,
						p2,
						MMenu(
							MIconButton("settings", nop,  [MIconButtonBorder(4.)], [MButtonTitle(const(_("Speed Settings"))), MFocused(playbackRateControlButtonFocused)]),
							[
								MMenuSingleLine("0.25", []),
								MMenuSingleLine("0.5", []),
								MMenuSingleLine("0.75", []),
								MMenuSingleLine("Normal", []),
								MMenuSingleLine("1.25", []),
								MMenuSingleLine("1.5", []),
								MMenuSingleLine("2", [])
							],
							[
								MCondensed(true),
								MSingleSelection(playbackRateSelected),
								enabled,
								MSelectedColor(color),
								MOpenMenu(settingsMenuOpen)
							]
						),
						m2t
					)
					|> v2a
				else
					[];

			subtitlesButton = \p2 ->
				if (subtitlesControl)
					TCopySize(
						MIconToggle2T(manager, p2, MIconToggle("subtitles", [MIconButtonBorder(4.)], [
							MToggleValue(subtitlesVisible),
							MEnabled(subtitlesEnabled),
							MButtonTitle(const(_("Subtitles"))),
							MFocused(subtitlesButtonFocused)
						]), m2t),
						\tr ->
							TRectangle([MFill(MRed(500))], TFillXH(2.))
							|> (\t -> TShow(fand(subtitlesVisible, subtitlesEnabled), t))
							|> TBorderLeftRight(6.)
							|> (\t -> TAvailable(t, tr))
							|> TBorderBottom(4.)
							|> TAlignBottom,
						true
					)
					|> (\t ->
						MTooltip2T(
							manager,
							p2,
							MTooltip(
								t,
								MText(_("Subtitles"), []),
								[
									MTooltipAlignment(MTop()),
									MLightBackground(!getLightBackground(p2)),
									MEnabled(subtitlesEnabled)
								]
							),
							m2t
						)
					)
					|> v2a
				else
					[];

			fullScreenControlButton = \p2 ->
				if (fullScreenControl)
					MIconToggle2T(
						manager,
						p2,
						MIconToggle(
							"fullscreen_exit",
							[
								MToggleFalseIcon("fullscreen", [MIconSize(28.)]),
								MIconButtonBorder(4.), MIconSize(28.)
							],
							eitherMap(
								fullscreen,
								\fs -> [
									MToggleValue(fs),
									MButtonTitle(fif(fs, const(_("Exit Fullsreen")), const(_("Enter Fullscreen")))),
									enabled,
									MFocused(fullScreenControlButtonFocused)
								],
								[enabled, MFocused(fullScreenControlButtonFocused)]
							)
						),
						m2t
					)
					|> v2a
				else
					[];

			panel =
				(\p3 ->
					TLines2(
						slider(p3) |> TFixWidth(TEmpty()),
						[
							[playPauseButton(p3)],
							volumeControlButton(p3),
							[
								timeLabel(p3),
								TAttachWidth(TFillX(), panelFillerW)
							],
							playbackRateControlButton(p3),
							subtitlesButton(p3),
							fullScreenControlButton(p3)
						]
						|> concatA
						|> TColsYCenter
						|> (\videoButtons -> TBorder(12., 4., 12., 4., videoButtons))
					)
					|> (\f ->
						if (autoHide)
							TFrame(0.0, 0.0, [GradientFill(90.0, [GradientPoint(black, 0.0, 0.0), GradientPoint(black, 0.3, 0.3),GradientPoint(black, 0.8, 1.0)])], f)
						else
							addTBackground(MDialogMColor(false))(f)
					)
				)
				|> (\makePanel ->
					(\p2 -> MComponentGroup2T(
						manager,
						p2,
						"MVideoControls",
						[
							MLightBackground(false),
							MSetRTL(false)
						],
						makePanel
					)
					|> (\t -> TAttachBox(t, controlsWH))
				)
				|> (\f -> m2t(MShowAnimation(fOr(fand(showPanel, showPanelInner), fOr(fnot(playing), settingsMenuOpen)), MGetFocusGroup(f), []), p))
				|> TBorderTop(10.0)
				|> (\t -> TInteractive([if (mobile) TMouseDown(panelHovered) else TMouseInside(panelHovered, false)], t)))
				|> (\t -> TLines2(TFillY(), t));

			TGroup2(videoBox, panel)
			|> (\videoPlayer ->
				if (shortcutsEnabled)
					TInteractive([KeyDown2(onKeyDown)], videoPlayer)
				else
					videoPlayer
			)
			|> (\videoPlayer ->
				eitherMap(
					fullscreen,
					\fs ->
						TAttachAvailableHeight(videoPlayer, availableH)
						|> (\t -> TAvailable(t, TIf(fs, TFillXY(), TSized(m.wh))))
						|> (\t -> TFullScreen(fs, t)),
					TAvailable(videoPlayer, TSized(m.wh))
				)
			)
			|> (\t ->
				TConstruct(
					concat(
						[
							\ -> fconnect(fdivide(visiblePosition, visibleDuration), sliderPosition),
							makeSubscribe(
								faddition(fmultiply(sliderPosition, visibleDuration), visibleRange.start),
								\pos -> if (pos <= fgetValue(duration)) nextDistinct(position, pos)
							),
							\ -> fconnect2Select(
								eitherMap(fullscreen, \fs -> fif(fs, availableH, fheight(m.wh)), fheight(m.wh)),
								controlsWH,
								videoWH,
								\vh, cwh -> WidthHeight(cwh.width, max(0., if (autoHide) vh else vh - cwh.height))
							),
							\ -> bidirectionalLink(playbackRate, playbackRateSelected, playbackRate2selected, selected2playbackRate),
						],
						if (autoHide)
							[
								makeSubscribeUns(
									fpair(
										fpair(mousePosition, mouseDown),
										fpair(fpair(playingPopup, fpair(volumePopup, volumeSliderDown)), fpair(positionPopup, positionSliderDown))
									), \__ -> {
									next(showPanelInner, true);

									[
										interruptibleTimer(2000, \ -> next(showPanelInner, false))
									]
								})
							]
						else
							[]
					),
					t
				)
			)
		}
	))
	|> (\f -> MComponentGroup2T(manager, parent, "MVideoContainer", [MSetRTL(false)], f))
}

videoPlayerStyle2FVideoStyle(style : [MVideoPlayerStyle]) -> [FVideoStyle] {
	filtermap(style, \st ->
		switch (st) {
			FVideoStyle() : {a : Maybe<FVideoStyle> = Some(st); a};
			default : None()
		}
	)
}

playbackRate2selected(pr : double) -> int {
	if (pr <= 0.25)
		0
	else if (pr <= 0.5)
		1
	else if (pr <= 0.75)
		2
	else if (pr <= 1.)
		3
	else if (pr <= 1.25)
		4
	else if (pr <= 1.5)
		5
	else
		6
}

selected2playbackRate(rs : int) -> double {
	if (rs == 0)
		0.25
	else if (rs == 1)
		0.5
	else if (rs == 2)
		0.75
	else if (rs == 3)
		1.
	else if (rs == 4)
		1.25
	else if (rs == 5)
		1.5
	else
		2.
}

MVideoPlayer2MRealHTML(url : string, parent : MFocusGroup, m : MVideoPlayer, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	style = eitherMap(
		tryExtractStruct(m.style, MEnabled(const(true))),
		\en -> [MouseDisabled(fnot(en.enabled))],
		[]
	);

	m2t(MRealHTML(url, m.wh, style), parent)
}

MPlayerControls2PlayerControls(mPlayerControls : [MPlayerControl]) -> [PlayerControl] {
	filtermap(mPlayerControls, \cntr : MPlayerControl ->
		switch (cntr) {
			BasicPlayerControl(): {a : Maybe<PlayerControl> = Some(cntr); a};
			default : None();
		}
	)
}

MVideoWithAnimatedSubtitles(
	filename : string,
	wh : DynamicBehaviour<WidthHeight>,
	style : [FVideoStyle],
	subtitles : MAnimatedSubtitles) -> Material {

	MGetManager(\manager -> MGetFocusGroup(\parent -> MGetMaterial2Tropic(\m2t ->
		TVideoWithAnimatedSubtitles(
			manager,
			parent,
			TVideo(filename, wh, style),
			Some(Pair(subtitles, const(true))),
			m2t
		)
	)))
}

TVideoWithAnimatedSubtitles(
	manager : MaterialManager,
	parent : MFocusGroup,
	video : TVideo,
	subtitles : Maybe<Pair<MAnimatedSubtitles, Transform<bool>>>,
	m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {

	videoAreaMetrics = make(FVideoAreaMetrics(zeroPoint, WidthHeight(0., 0.), Factor(1., 1.)));
	style = eitherMap(subtitles, \__ -> arrayPush(video.style, FInspectVideoArea(videoAreaMetrics)), video.style);

	applyRange = \scMode, value ->
		if (scMode.max == -1.) max(scMode.min, value)
		else forceRange(value, scMode.min, scMode.max);

	TGroup2SameMetrics(
		TVideo(video.filename, video.wh, style),
		eitherMap(subtitles, \sbtl -> {
				areaBox = TSized(fselect(videoAreaMetrics, FLift(\vam -> vam.wh)));
				areaTopLeft = fselect(videoAreaMetrics, FLift(\vam : FVideoAreaMetrics -> vam.topLeft));
				scaleMode = tryExtractStruct(sbtl.first.style, FVideoSubtitlesScaleMode(0., 0.));
				customOriginSize = tryExtractStruct(sbtl.first.style, MSubtitlesOriginSize(WidthHeight(0., 0.)));

				TAnimatedSubtitles(manager, parent, sbtl.first, m2t)
				|> (\t -> eitherMap(scaleMode, \scMode -> {
					factor =
						eitherMap(
							customOriginSize,
							\os ->
								fselect(videoAreaMetrics, FLift(\vam ->
									min(vam.wh.width / os.size.width, vam.wh.height / os.size.height)
									|> (\f -> applyRange(scMode, f))
									|> (\f -> Factor(f, f))
								)),
							fselect(videoAreaMetrics, FLift(\vam -> Factor(
								applyRange(scMode, vam.scale.x),
								applyRange(scMode, vam.scale.y)
							)))
						);
						TScale(factor, t)
					},
					t
				))
				|> (\t -> TAvailable(t, areaBox))
				|> (\t -> TTweak([TAlign(const(0.5), const(1.))], t, areaBox))
				|> (\t -> TCropSize(areaBox, t))
				|> (\t -> TTranslate(areaTopLeft, t))
				|> (\t -> TShow(sbtl.second, t))
			},
			TEmpty()
		)
	)
}

TAnimatedSubtitles(manager : MaterialManager, parent : MFocusGroup, subtitles : MAnimatedSubtitles, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	duration = extractStruct(subtitles.style, MDuration(const(0.5)));
	frame = extractStruct(subtitles.style, MSubtitlesFrame(4., 4.));

	percent = make(0.);
	old2LineSize = makeWH();
	oldLineSize = makeWH();
	newLineSize = makeWH();

	boxSize = fselect2(oldLineSize, newLineSize, FLift2(\old, new -> WidthHeight(max(old.width, new.width), old.height + new.height)));

	backgroundColor = make(0);
	backgroundOpacity = make(0.);

	backgroundStyle =
		fsubselect(subtitles.subtitles, FLift(\__ ->
			fselect3(
				fselectWithLast(backgroundColor, FLift2(\old, new -> Pair(old, new))),
				fselectWithLast(backgroundOpacity, FLift2(\old, new -> Pair(old, new))),
				percent,
				\col, opac, prc -> [
					Fill(colorLerp(col.first, col.second, prc)),
					FillOpacity(lerp(opac.first, opac.second, prc))
				]
			)
		));

	background =
		m2t(
			MAnimatedResizer(fwidth(boxSize), fheight(boxSize), [duration, MResizerPercent(percent)]),
			parent
		)
		|> (\sz -> TFrameT(const(frame.border), const(frame.radius), backgroundStyle, sz));

	makeSubtitleLine = \sbtl, size, translatePoint, dSize ->
		MParagraph2T(parent, sbtl.text, concat(MCharacterStyle2MTextStyle(sbtl.style), [MSetRTL(false), extractStruct(sbtl.style, EscapeHTML(true))]))
		|> TBorderBottom(dSize)
		|> (\t -> TAttachBox(t, size))
		|> TBorderTop(frame.border)
		|> (\t -> TTranslate(translatePoint, t))
		|> TCenterX;

	TMutable(fselectWith2Last(subtitles.subtitles, \old2, old, new -> {
		innerPercent = make(0.);
		antiInnerPercent = fsubtract(const(1.), innerPercent);
		old2LineTranslatePoint = flerpPoint(fselect(old2LineSize, FLift(\sz -> Point(0., -sz.height - frame.border))), antiInnerPercent);
		oldLineTranslatePoint = flerpPoint(fselect(old2LineSize, FLift(\sz -> Point(0., sz.height))), innerPercent);
		newLineTranslatePoint = fpointaddition(
			flerpPoint(fselect(old2LineSize, FLift(\sz -> Point(0., sz.height))), innerPercent),
			fselect(oldLineSize, FLift(\sz -> Point(0., sz.height)))
		);

		TGroupWithoutMetrics([
			makeSubtitleLine(new, newLineSize, newLineTranslatePoint, -4.) |> (\t -> TAlpha(innerPercent, t)),
			makeSubtitleLine(old, oldLineSize, oldLineTranslatePoint, 0.),
			makeSubtitleLine(old2, old2LineSize, old2LineTranslatePoint, 0.) |> (\t -> TAlpha(antiInnerPercent, t))
		])
		|> (\t -> TConstruct([
			\ -> fconnectSelect(percent, innerPercent, \perc -> max(getValue(innerPercent), perc)),
			\ -> {
				color = extractStruct(new.style, BackgroundFill(0)).color;
				opacity =
					if (strlen(new.text) == 0) 0.
					else extractStruct(new.style, BackgroundFillOpacity(0.)).opacity;

				nextDistinct(backgroundColor, color);
				nextDistinct(backgroundOpacity, opacity);
				nop;
			}
		], t))
	}, VideoSubtitle("", [])))
	|> (\t -> TGroup2(
		TDisplay("TAnimatedSubtitlesBackground") |> TCenterX,
		TCropSize(TGroup2(TFillX(), TGhost("TAnimatedSubtitlesBackground")), t)
	))
	|> (\t -> TLet("TAnimatedSubtitlesBackground", background, t))
	|> TBorderBottom(8.)
}

fselectWith2Last(b : Transform<??>, fn : (??, ??, ??) -> ?, def : ??) -> Transform<?> {
	lastV : ref Maybe<Pair<??, ??>> = ref None();
	fselect(ftransistor(fneq(b, def), b), FLift(\v ->
		switch (^lastV) {
			Some(prevV): {
				r = fn(prevV.first, prevV.second, v);
				lastV := Some(Pair(prevV.second, v));
				r
			}
			None(): {
				lastV := Some(Pair(def, v));
				fn(def, def, v);
			}
		}
	))
}
