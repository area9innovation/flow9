import material/internal/material_menu;
import material/internal/material_slider;
import material/internal/material_progress;

export {
	MVideoPlayer2T(manager : MaterialManager, parent : MFocusGroup, m : MVideoPlayer, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;

	MVideoWithAnimatedSubtitles(
		filename : string,
		wh : DynamicBehaviour<WidthHeight>,
		style : [FVideoStyle],
		subtitles : MAnimatedSubtitles
	) -> Material;
}

MVideoPlayer2T(manager : MaterialManager, parent : MFocusGroup, m : MVideoPlayer, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	eitherFn2(
		getYouTubeURL(m.filename),
		getVimeoURL(m.filename),
		\url, __ -> MVideoPlayer2MRealHTML(url, parent, m, m2t),
		\url -> MVideoPlayer2MRealHTML(url, parent, m, m2t),
		\ -> {
			color = extractMColor(parent, m.style, MAccentColor());
			backgroundColor = MThemeColor2MColor(parent, extractStruct(m.style, MPlayerBackground(MBlack())).color);
			videoHasErrors = make(false);
			enabled =
				fands([
					extractStruct(m.style, MEnabled(const(true))).enabled,
					fnot(videoHasErrors),
					parent.enabled
				], true)
				|> MEnabled;

			mPlayerControls = extractStruct(m.style, MPlayerControls([VolumeControl()])).controls;

			controls : [PlayerControl] = concat(
				MPlayerControls2PlayerControls(mPlayerControls),
				extractStruct(m.style, FVideoControls([VolumeControl()])).controls
			);
			volumeControl = contains(controls, VolumeControl());
			fullScreenControl = contains(controls, FullScreenPlayer());
			playbackRateControl = !android() && contains(controls, PlaybackRateControl());
			subtitlesControl = contains(mPlayerControls, SubtitlesControl());
			subtitles = tryExtractStruct(m.style, FVideoSubtitles(const(VideoSubtitle("", []))));
			animatedSubtitles = tryExtractStruct(m.style, MAnimatedSubtitles(const(VideoSubtitle("", [])), []));
			shortcutsEnabled = !containsStruct(m.style, MPlayerDisableKeyboardShortcuts());

			streamStatus = extractStruct(m.style, StreamStatus(nop1)).fn;

			playing = extractStruct(m.style, FVideoPlay(make(false))).play;
			volume = extractStruct(m.style, FVideoVolume(make(1.))).volume;
			fullscreen =
				eitherFn(
					tryExtractStruct(m.style, FVideoFullscreen(make(false))),
					\fs -> Some(fs.fullscreen),
					\ ->
						if (fullScreenControl)
						 	Some(make(false))
						else
							None()
				);
			position = extractStruct(m.style, FVideoPosition(make(0.))).position;
			duration = extractStruct(m.style, FVideoLength(make(1.))).length;
			playbackRate = extractStruct(m.style, FVideoPlaybackRate(make(1.))).rate;
			autoHide = contains(m.style, MPlayerPanelAutoHide());
			showPanel = extractStruct(m.style, MPlayerShowPanel(const(true))).show;
			visibleRange = extractStruct(m.style, MPlayerVisibleRange(const(0.0), duration));
			visibleDuration = fselect3(visibleRange.start, visibleRange.end, duration, \st, en, d -> min(d, en - st));
			visiblePosition = fselect2(position, visibleRange.start, FLift2(\p, vs -> max(p - vs, 0.0)));

			showPanelInner = make(true);

			controlsWH = makeWH();
			videoWH = makeWH();
			availableH = make(0.);
			sliderPosition = make(0.);
			volumeSliderVisible = make(false);
			volumeSliderDown = make(false);
			playbackRateSelected = make(3);
			showProgressCircle = make(true);
			subtitlesVisible = make(true);
			subtitlesEnabled = const(isSome(subtitles) || isSome(animatedSubtitles));

			uns = initUnsM();

			videoStyle =
				m.style
				|> videoPlayerStyle2FVideoStyle
				|> (\st -> removeAllStructs(st, FVideoControls([])))
				|> (\st -> removeAllStructs(st, FVideoFullscreen(make(false))))
				|> (\st ->
					replaceStructMany(
						st,
						[
							FVideoPlay(playing),
							FVideoVolume(volume),
							FVideoPosition(position),
							FVideoLength(duration),
							FVideoPlaybackRate(playbackRate),
							OnVideoLoadingError(\ -> nextDistinct(videoHasErrors, true)),
							StreamStatus(\s -> {nextDistinct(showProgressCircle, false); streamStatus(s)})
						]
					)
				)
				|> (\st ->
					eitherMap(subtitles, \sbtl ->
						replaceStruct(st,
							FVideoSubtitles(fif(subtitlesVisible, sbtl.subtitles, const(VideoSubtitle("", []))))
						),
						st
					)
				)
				|> (\st -> ifArrayPush(
					st,
					containsStruct(m.style, MPlayerVisibleRange(const(0.0), const(0.0))) &&
						!containsStruct(st, FVideoTimeRange(const(0.0), const(0.0))),
					FVideoTimeRange(visibleRange.start, visibleRange.end)
				));

			videoBox =
				TVideoWithAnimatedSubtitles(
					manager,
					parent,
					TVideo(m.filename, videoWH, videoStyle),
					maybeMap(animatedSubtitles, \sbtl -> Pair(sbtl, subtitlesVisible)),
					m2t
				)
				|> (\video ->
					TCopySize(
						video,
						\videoSize ->
							TGroupWithoutMetrics([
								mouseOnDownAround(
									manager,
									[
										MOnClick(\ -> reverseBehaviour(playing)),
										MPassClicks(),
										enabled
									],
									TSubtractGroup2(videoSize, TFixed(0., 10.)) // to avoid clicking while changing position
								),
								TZoomOnOverflow(
									TSubscribe2(playing, \pl ->
										TCopySize2(
											MIcon2T(
												parent,
												if (pl)
													"play_arrow"
												else
													"pause",
												[MCircleBackground(MGrey(900), 56.), MIconSize(32.)]
											),
											\size, icon ->
												TFixSize(
													TCenter(MFadeOutAnimation(manager, parent, icon)),
													TCenter(TScale(const(Factor(2., 2.)), size))
												)
										)
									)
									|> (\t -> TBorder4(16., t)),
									videoSize,
									true
								),
								TCenterIn(
									MProgressCircle2T(manager, parent, MProgressCircle([MWhite()])),
									videoSize
								)
								|> (\t2 -> TShow(showProgressCircle, t2))
							]),
						true
					)
				)
				|> (\video -> TVisible(fselect(videoWH, FLift(\vwh -> vwh.width > 0. && vwh.height > 0.)), video))
				|> (\video ->
					TIf(videoHasErrors,
						TGroup2(
							TRectangle([Fill(black)], TFillXY()),
							TCenter(MParagraph2T(parent, "An error occurred. Please try again later.", [MWhite(), CenterAlign()]))
						),
						video
					)
				)
				|> addTBackground(backgroundColor);

			slider = \p ->
				MSlider2T(
					manager,
					p,
					MSlider(
						sliderPosition,
						[
							MMaximizeOnHover(false),
							MOutlineOnMinimum(false),
							MCondensed(true),
							MSliderTooltip(\v -> seconds2timelineString(v * fgetValue(visibleDuration))),
							color,
							enabled
						]
					),
					m2t
				);

			playPauseButton = \p ->
				MIconToggle2T(
					manager,
					p,
					MIconToggle(
						"pause",
						[
							MToggleFalseIcon("play_arrow", [MIconSize(30.)]),
							MIconButtonBorder(3.), MIconSize(30.)
						],
						concat(
							[
								MToggleValue(playing),
								enabled
							],
							if (shortcutsEnabled) [MShortcut(" ")] else []
						)
					),
					m2t
				)
				|> (\t -> TBorder(4., 0., 8., 0., t));

			showVolumeSlider =
				if (mobile)
					enabled.enabled
				else
					fand(fOr(volumeSliderVisible, volumeSliderDown), enabled.enabled);
			
			panelFillerW = make(0.);
			overlapMode =
				if (mobile) const(false)
				else fselect(showVolumeSlider, FLift(\__ -> getValue(panelFillerW) <= 0.));

			volumeControlButton = \p ->
				if (volumeControl)
					TBaselineCols2(
						TSelect(
							volume,
							\v ->
								MTooltip2T(
									manager,
									p,
									MTooltip(
										MIconButton(
											if (v == 0.)
												"volume_off"
											else if (v < 0.5)
												"volume_down"
											else
												"volume_up",
											\ ->
												next(volume, if (v == 0.) 1. else 0.),
											[MIconButtonBorder(6.)],
											[enabled]
										),
										MText(if (v == 0.) _("Unmute") else _("Mute"), []),
										[
											MTooltipAlignment(MTop()),
											MLightBackground(!getLightBackground(parent)),
											enabled
										]
									),
									m2t
								)
						)
						|> TBorderRight(12.),

						MSlider2T(
							manager,
							p,
							MSlider(
								volume,
								[
									MMaximizeOnHover(false),
									MOutlineOnMinimum(false),
									MWhite(),
									MMouseDown(volumeSliderDown),
									enabled,
									MWidth(52.0)
								]
							),
							m2t
						)
						|> (\t -> TBorder(-20., 0., -2., 0., t))
						|> TFixWidth(TSizedWidth(fif(overlapMode, zero, const(60.)), 0.))
						|> (\t -> TShow(showVolumeSlider, t))
					)
					|> (\volumeSlider ->
						TCopySize(
							volumeSlider,
							\volumeSliderSize -> TInteractive([TMouseInside(volumeSliderVisible, false)], volumeSliderSize),
							true
						)
					)
					|> v2a
				else
					[];

			timeLabel = \p ->
				TSelect3(visiblePosition, visibleDuration, enabled.enabled, \pos, dur, en ->
					MText2T(
						p,
						seconds2timelineString(pos) + " / " + seconds2timelineString(dur),
						if (en)
							[]
						else
							[MTextDisabled()]
					)
				)
				|> (\t -> TVisible(fnot(fand(overlapMode, showVolumeSlider)), t));

			playbackRateControlButton = \p ->
				if (playbackRateControl)
					MMenu2T(
						manager,
						p,
						MMenu(
							MIconButton("settings", nop,  [MIconButtonBorder(4.)], []),
							[
								MMenuSingleLine("0.25", []),
								MMenuSingleLine("0.5", []),
								MMenuSingleLine("0.75", []),
								MMenuSingleLine("Normal", []),
								MMenuSingleLine("1.25", []),
								MMenuSingleLine("1.5", []),
								MMenuSingleLine("2", [])
							],
							[
								MCondensed(true),
								MSingleSelection(playbackRateSelected),
								enabled,
								MSelectedColor(color)
							]
						),
						m2t
					)
					|> v2a
				else
					[];

			subtitlesButton = \p ->
				if (subtitlesControl)
					TCopySize(
						MIconToggle2T(manager, p, MIconToggle("subtitles", [MIconButtonBorder(4.)], [
							MToggleValue(subtitlesVisible),
							MEnabled(subtitlesEnabled)
						]), m2t),
						\tr ->
							TRectangle([MFill(MRed(500))], TFillXH(2.))
							|> (\t -> TShow(fand(subtitlesVisible, subtitlesEnabled), t))
							|> TBorderLeftRight(6.)
							|> (\t -> TAvailable(t, tr))
							|> TBorderBottom(4.)
							|> TAlignBottom,
						true
					)
					|> (\t -> 
						MTooltip2T(
							manager,
							p,
							MTooltip(
								t,
								MText(_("Subtitles"), []),
								[
									MTooltipAlignment(MTop()),
									MLightBackground(!getLightBackground(parent)),
									MEnabled(subtitlesEnabled)
								]
							),
							m2t
						)
					)
					|> v2a
				else
					[];

			fullScreenControlButton = \p ->
				if (fullScreenControl)
					MIconToggle2T(
						manager,
						p,
						MIconToggle(
							"fullscreen_exit",
							[
								MToggleFalseIcon("fullscreen", [MIconSize(28.)]),
								MIconButtonBorder(4.), MIconSize(28.)
							],
							eitherMap(
								fullscreen,
								\fs -> [MToggleValue(fs), enabled],
								[enabled]
							)
						),
						m2t
					)
					|> v2a
				else
					[];

			panel =
				(\p ->
					TLines2(
						slider(p) |> TFixWidth(TEmpty()),
						[
							[playPauseButton(p)],
							volumeControlButton(p),
							[
								timeLabel(p),
								TAttachWidth(TFillX(), panelFillerW)
							],
							playbackRateControlButton(p),
							subtitlesButton(p),
							fullScreenControlButton(p)
						]
						|> concatA
						|> TColsYCenter
						|> (\videoButtons -> TBorder(12., 4., 12., 4., videoButtons))
					)
					|> addTBackground(MDialogMColor(false))
					|> (\t -> TAnimatedExpander(manager, p, t, if (autoHide) fand(showPanel, showPanelInner) else showPanel, [MExpandFromStart(), MNoCrop()]))
				)
				|> (\makePanel ->
					MComponentGroup2T(
						manager,
						parent,
						"MVideoControls",
						[
							MLightBackground(false),
						],
						makePanel
					)
				)
				|> (\t -> TAttachBox(t, controlsWH))
				|> (\t -> TLines2(TFillY(), t));

			TGroup2(videoBox, panel)
			|> (\videoPlayer ->
				eitherMap(
					fullscreen,
					\fs ->
						TAttachAvailableHeight(videoPlayer, availableH)
						|> (\t -> TAvailable(t, TIf(fs, TFillXY(), TSized(m.wh))))
						|> (\t -> if (autoHide) TCropSize(TIf(fs, TFillXY(), TSized(fwhmax(m.wh, controlsWH))), t) else t)
						|> (\t -> TFullScreen(fs, t)),
					TAvailable(videoPlayer, TSized(m.wh))
					|> (\t -> if (autoHide) TCrop(const(zeroPoint), fwhmax(m.wh, controlsWH), t) else t)
				)
			)
			|> (\t ->
				TConstruct(
					[
						\ -> fconnect(fdivide(visiblePosition, visibleDuration), sliderPosition),
						makeSubscribe(
							faddition(fmultiply(sliderPosition, visibleDuration), visibleRange.start),
							\pos -> if (pos <= fgetValue(duration)) nextDistinct(position, pos)
						),
						\ -> fconnect2Select(
							eitherMap(fullscreen, \fs -> fif(fs, availableH, fheight(m.wh)), fheight(m.wh)),
							controlsWH,
							videoWH,
							\vh, cwh -> WidthHeight(cwh.width, max(0., vh - cwh.height))
						),
						\ -> bidirectionalLink(playbackRate, playbackRateSelected, playbackRate2selected, selected2playbackRate)
					]
					|> (\arr -> ifArrayPush(arr, autoHide, makeSubscribe(playing, \pl -> {
							dispUnsM(uns);
							nextDistinct(showPanelInner, true);

							if (pl) {
								setUnsM(
									uns,
									interruptibleTimer(1000, \ -> nextDistinct(showPanelInner, false))
								)
							}
						}))),
					t
				)
			)
		}
	)
}

videoPlayerStyle2FVideoStyle(style : [MVideoPlayerStyle]) -> [FVideoStyle] {
	filtermap(style, \st ->
		switch (st) {
			FVideoStyle() : {a : Maybe<FVideoStyle> = Some(st); a};
			default : None()
		}
	)
}

playbackRate2selected(pr : double) -> int {
	if (pr <= 0.25)
		0
	else if (pr <= 0.5)
		1
	else if (pr <= 0.75)
		2
	else if (pr <= 1.)
		3
	else if (pr <= 1.25)
		4
	else if (pr <= 1.5)
		5
	else
		6
}

selected2playbackRate(rs : int) -> double {
	if (rs == 0)
		0.25
	else if (rs == 1)
		0.5
	else if (rs == 2)
		0.75
	else if (rs == 3)
		1.
	else if (rs == 4)
		1.25
	else if (rs == 5)
		1.5
	else
		2.
}

MVideoPlayer2MRealHTML(url : string, parent : MFocusGroup, m : MVideoPlayer, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	style = eitherMap(
		tryExtractStruct(m.style, MEnabled(const(true))),
		\en -> [MouseDisabled(fnot(en.enabled))],
		[]
	);

	m2t(MRealHTML(url, m.wh, style), parent)
}

MPlayerControls2PlayerControls(mPlayerControls : [MPlayerControl]) -> [PlayerControl] {
	filtermap(mPlayerControls, \cntr : MPlayerControl ->
		switch (cntr) {
			BasicPlayerControl(): {a : Maybe<PlayerControl> = Some(cntr); a};
			default : None();
		}
	)
}

MVideoWithAnimatedSubtitles(
	filename : string,
	wh : DynamicBehaviour<WidthHeight>,
	style : [FVideoStyle],
	subtitles : MAnimatedSubtitles) -> Material {

	MGetManager(\manager -> MGetFocusGroup(\parent -> MGetMaterial2Tropic(\m2t ->
		TVideoWithAnimatedSubtitles(
			manager,
			parent,
			TVideo(filename, wh, style),
			Some(Pair(subtitles, const(true))),
			m2t
		)
	)))
}

TVideoWithAnimatedSubtitles(
	manager : MaterialManager,
	parent : MFocusGroup,
	video : TVideo,
	subtitles : Maybe<Pair<MAnimatedSubtitles, Transform<bool>>>,
	m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {

	videoAreaMetrics = make(FVideoAreaMetrics(zeroPoint, WidthHeight(0., 0.), Factor(1., 1.)));
	style = eitherMap(subtitles, \__ -> arrayPush(video.style, FInspectVideoArea(videoAreaMetrics)), video.style);

	applyRange = \scMode, value ->
		if (scMode.max == -1.) max(scMode.min, value)
		else forceRange(value, scMode.min, scMode.max);

	TGroup2SameMetrics(
		TVideo(video.filename, video.wh, style),
		eitherMap(subtitles, \sbtl -> {
				areaBox = TSized(fselect(videoAreaMetrics, FLift(\vam -> vam.wh)));
				areaTopLeft = fselect(videoAreaMetrics, FLift(\vam : FVideoAreaMetrics -> vam.topLeft));
				scaleMode = tryExtractStruct(sbtl.first.style, FVideoSubtitlesScaleMode(0., 0.));
				customOriginSize = tryExtractStruct(sbtl.first.style, MSubtitlesOriginSize(WidthHeight(0., 0.)));

				TAnimatedSubtitles(manager, parent, sbtl.first, m2t)
				|> (\t -> eitherMap(scaleMode, \scMode -> {
					factor =
						eitherMap(
							customOriginSize,
							\os ->
								fselect(videoAreaMetrics, FLift(\vam ->
									min(vam.wh.width / os.size.width, vam.wh.height / os.size.height)
									|> (\f -> applyRange(scMode, f))
									|> (\f -> Factor(f, f))
								)),
							fselect(videoAreaMetrics, FLift(\vam -> Factor(
								applyRange(scMode, vam.scale.x),
								applyRange(scMode, vam.scale.y)
							)))
						);
						TScale(factor, t)
					},
					t
				))
				|> (\t -> TAvailable(t, areaBox))
				|> (\t -> TTweak([TAlign(const(0.5), const(1.))], t, areaBox))
				|> (\t -> TCropSize(areaBox, t))
				|> (\t -> TTranslate(areaTopLeft, t))
				|> (\t -> TShow(sbtl.second, t))
			},
			TEmpty()
		)
	)
}

TAnimatedSubtitles(manager : MaterialManager, parent : MFocusGroup, subtitles : MAnimatedSubtitles, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	duration = extractStruct(subtitles.style, MDuration(const(0.5)));
	frame = extractStruct(subtitles.style, MSubtitlesFrame(4., 4.));

	percent = make(0.);
	old2LineSize = makeWH();
	oldLineSize = makeWH();
	newLineSize = makeWH();

	boxSize = fselect2(oldLineSize, newLineSize, FLift2(\old, new -> WidthHeight(max(old.width, new.width), old.height + new.height)));

	backgroundColor = make(0);
	backgroundOpacity = make(0.);

	backgroundStyle = 
		fsubselect(subtitles.subtitles, FLift(\__ ->
			fselect3(
				fselectWithLast(backgroundColor, FLift2(\old, new -> Pair(old, new))),
				fselectWithLast(backgroundOpacity, FLift2(\old, new -> Pair(old, new))),
				percent,
				\col, opac, prc -> [
					Fill(colorLerp(col.first, col.second, prc)),
					FillOpacity(lerp(opac.first, opac.second, prc))
				]
			)
		));

	background =
		m2t(
			MAnimatedResizer(fwidth(boxSize), fheight(boxSize), [duration, MResizerPercent(percent)]),
			parent
		)
		|> (\sz -> TFrameT(const(frame.border), const(frame.radius), backgroundStyle, sz));

	makeSubtitleLine = \sbtl, size, translatePoint, dSize -> 
		MParagraph2T(parent, sbtl.text, arrayPush(MCharacterStyle2MTextStyle(sbtl.style), extractStruct(sbtl.style, EscapeHTML(true))))
		|> TBorderBottom(dSize)
		|> (\t -> TAttachBox(t, size))
		|> TBorderTop(frame.border)
		|> (\t -> TTranslate(translatePoint, t))
		|> TCenterX;

	TMutable(fselectWith2Last(subtitles.subtitles, \old2, old, new -> {
		innerPercent = make(0.);
		antiInnerPercent = fsubtract(const(1.), innerPercent);
		old2LineTranslatePoint = flerpPoint(fselect(old2LineSize, FLift(\sz -> Point(0., -sz.height - frame.border))), antiInnerPercent);
		oldLineTranslatePoint = flerpPoint(fselect(old2LineSize, FLift(\sz -> Point(0., sz.height))), innerPercent);
		newLineTranslatePoint = fpointaddition(
			flerpPoint(fselect(old2LineSize, FLift(\sz -> Point(0., sz.height))), innerPercent),
			fselect(oldLineSize, FLift(\sz -> Point(0., sz.height)))
		);

		TGroupWithoutMetrics([
			makeSubtitleLine(new, newLineSize, newLineTranslatePoint, -4.) |> (\t -> TAlpha(innerPercent, t)),
			makeSubtitleLine(old, oldLineSize, oldLineTranslatePoint, 0.),
			makeSubtitleLine(old2, old2LineSize, old2LineTranslatePoint, 0.) |> (\t -> TAlpha(antiInnerPercent, t))
		])
		|> (\t -> TConstruct([
			\ -> fconnectSelect(percent, innerPercent, \p -> max(getValue(innerPercent), p)),
			\ -> {
				color = extractStruct(new.style, BackgroundFill(0)).color;
				opacity =
					if (strlen(new.text) == 0) 0.
					else extractStruct(new.style, BackgroundFillOpacity(0.)).opacity;

				nextDistinct(backgroundColor, color);
				nextDistinct(backgroundOpacity, opacity);
				nop;
			}
		], t))
	}, VideoSubtitle("", [])))
	|> (\t -> TGroup2(
		TDisplay("TAnimatedSubtitlesBackground") |> TCenterX,
		TCropSize(TGroup2(TFillX(), TGhost("TAnimatedSubtitlesBackground")), t)
	))
	|> (\t -> TLet("TAnimatedSubtitlesBackground", background, t))
	|> TBorderBottom(8.)
}

fselectWith2Last(b : Transform<??>, fn : (??, ??, ??) -> ?, def : ??) -> Transform<?> {
	lastV : ref Maybe<Pair<??, ??>> = ref None();
	fselect(ftransistor(fneq(b, def), b), FLift(\v ->
		switch (^lastV) {
			Some(prevV): {
				r = fn(prevV.first, prevV.second, v);
				lastV := Some(Pair(prevV.second, v));
				r
			}
			None(): {
				lastV := Some(Pair(def, v));
				fn(def, def, v);
			}
		}
	))
}
