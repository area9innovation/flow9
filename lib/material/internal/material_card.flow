import material/internal/material_menu;

export {
	MCard2T(manager : MaterialManager, parent : MFocusGroup, m : MCard, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
	MDynamicCard2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicCard, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;
	
	defaultCardBorder = 4.0;
	defaultCardMargin = 16.0;
	defaultCardMarginBorders = 24.0;
	defaultCardMarginActions = 8.0;
	defaultCardElevation = 2.0;
}


MCard2T(manager : MaterialManager, parent : MFocusGroup, m : MCard, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	style = fixMCardStyle(parent, m.style);

	MCardBlock2T(manager, parent, MCard(m.blocks, style, []), TEmpty(), TEmpty(), [], false, m2t)
	|> (\f -> MCardAddShadow(manager, parent, f, m.state, const(style), m2t))
}

MDynamicCard2T(manager : MaterialManager, parent : MFocusGroup, m : MDynamicCard, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	style = fselect(m.style, FLift(\st -> fixMCardStyle(parent, st)));

	MCardBlock2T(manager, parent,
		MDynamicBlock(fselect2(m.blocks, style, FLift2(\blcks, st -> [MCard(blcks, st, [])]))),
		TEmpty(), TEmpty(), [], false, m2t
	)
	|> (\f -> MCardAddShadow(manager, parent, f, m.state, style, m2t))
}

fixMCardStyle(parent : MFocusGroup, style : [MCardStyle]) -> [MCardStyle] {
	wd = extractStruct(style, MCardWidth(0.)).width;
	hgt = extractStruct(style, MCardHeight(0.)).height;

	ifArrayPush(style,
		!containsStruct(style, MWhite()) && !containsStruct(style, MBackgroundImage("", false)),
		MDialogMColor(getLightBackground(parent))
	)
	|> (\f -> replaceStruct(f, MCardWidth(wd)))
	|> (\f -> replaceStruct(f, MCardHeight(hgt)));
}

MCardAddShadow(
	manager : MaterialManager,
	parent : MFocusGroup,
	card : Tropic,
	state : [MButtonState],
	style : Transform<[MCardStyle]>,
	m2t : (Material, MFocusGroup) -> Tropic
) -> Tropic {
	onClick = tryExtractStruct(state, MOnClick(nop));
	highlightOnFocus = tryExtractStruct(state, MHighlightOnFocus(const(false)));
	elevation = \hover, focused -> extractStruct(state, MElevation(fif(fOr(hover, focused), const(6.), const(2.)))).elevation;
	outerBorders = fif(fcontains(style, const(MCardNoOuterBorders())), zero, const(defaultCardBorder));
	focusState = concatStyles(
		removeAllStructs(state, MElevation(zero)),
		[AccessRole("button"), MRippleType(const(MRippleFill())), MRippleShape("MCard")]
	);
	cardCorners = fselect(style, FLift(\st ->
		tryExtractStruct(st, MCardCorners(RoundedCorner(0.), RoundedCorner(0.), RoundedCorner(0.), RoundedCorner(0.)))
	));

	(\p ->
	TCopySize2(
		card,
		\cardSize, cardTropic ->
			TMask(cardTropic, MShape(p, [Fill(black)], cardSize, "MCard"))
	)
	|> (\f -> \hover, focused, enabled ->
		TCopySize(
			TEasedShadowShape(manager, p, elevation(hover, focused), f, 0.3, RectangleShape("MCard")),
			\tr ->
				TShowLazy(
					fnot(enabled),
					\ ->
						MShape(p, [Fill(white), FillOpacity(0.36)], tr, "MCard")
				),
			true
		)
	)
	|> (\makeShadowedCard ->
		if (isSome(onClick) || isSome(highlightOnFocus))
			MComponent2T(manager, p, "MCard", focusState, \focus -> makeShadowedCard(focus.hover, focus.focused, focus.enabled), m2t)
		else
			makeShadowedCard(const(false), const(false), const(true))
	)
	|> (\f -> TBorderT(outerBorders, outerBorders, outerBorders, outerBorders, f))
	)
	|> (\makeCard -> TSelect(cardCorners, \ccorners -> eitherMap(ccorners, \cc ->
		MComponentGroup2T(manager, parent, "MCardWrapper", [
			MUpdateShapeCorners("card", MaterialShape(cc.leftTop, cc.rightTop, cc.rightBottom, cc.leftBottom))
		], makeCard),
		makeCard(parent)
	)))
}

MCardBlock2T(manager : MaterialManager, parent : MFocusGroup, block : MCardBlock, above : MCardBlock, below : MCardBlock, belowStyle : [MCardStyle],
	lines : bool, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	l = ref defaultCardMargin;
	t = ref if (above == TEmpty()) defaultCardMargin else defaultCardMarginBorders;
	r = ref defaultCardMargin;
	b = ref if (below == TEmpty()) defaultCardMargin else defaultCardMarginBorders;

	isRTL = parent.rtl;

	borders = \st ->
		extractStruct(st, extractStruct(belowStyle, MContentBorders(true))).borders;

	itemsColor = \st ->
		normalizeColor(MCardTextColor2MColor(parent, st, belowStyle));

	cardWidth = \st, bord ->
		extractStruct(concat(belowStyle, st), MCardWidth(-1.)).width - (if (bord) ^l + ^r else 0.);

	makeParagraph = \text, fontStyle : MFontStyle, iColor : MColor, cw ->
		MDynamicParagraph2T(
			parent,
			const(text),
			[fontStyle, iColor, MWidth(cw), InterlineSpacing(0.), ParagraphMargin(0., 0.)]
		);

	wrapToFrame = \bord, style ->
		\f -> MCardStyle2Frame(
				manager,
				parent,
				f,
				ifArrayPush(style, contains(belowStyle, MWidthByContent()), MWidthByContent()),
				if (bord) {
					bb = extractStruct(style, MCardBlockBorders(^l, ^t, ^r, ^b));
					Quadruple(bb.start, bb.top, bb.end, bb.bottom);
				} else Quadruple(0., 0., 0., 0.),
				itemsColor(style),
				m2t
			);

	handleBlocks = \blocks, style, isLines -> {
		if (length(blocks) == 1) {
			MCardBlock2T(
				manager,
				parent,
				blocks[0],
				TEmpty(),
				TEmpty(),
				style,
				isLines,
				m2t
			)
		} else {
			mapi(blocks, \i, bl ->
				MCardBlock2T(
					manager,
					parent,
					bl,
					if (i > 0) blocks[i - 1] else TEmpty(),
					if (i < length(blocks) - 1) blocks[i + 1] else TEmpty(),
					style,
					isLines,
					m2t
				)
			)
			|> (\f -> if (isLines) TLinesDir(f, isRTL) else TColsDir(f, isRTL))
		}
	}

	switch (block) {
		MRichMediaBlock(content, style): {
			m2t(content, parent)
			|> wrapToFrame(borders(style), style)
		}
		MActionBlock(left, right, style0): {
			style = replaceStruct(style0, extractStruct(style0, MCardHeight(52.0)));

			l := defaultCardMarginActions;
			t := 0.;
			r := defaultCardMarginActions;
			b := 0.;

			actionFn = \item -> MCardAction2T(manager, parent, item, style, belowStyle, m2t);

			concat3(
				map(left, actionFn),
				[TFillX()],
				map(right, actionFn)
			)
			|> (\f -> TColsDir(f, isRTL))
			|> (\f -> TAvailable(f, TFillX()))
			|> wrapToFrame(borders(style), style)
		}
		MPrimaryTextBlock(title, text, style): {
			bord = borders(style);
			iColor = itemsColor(style);

			if (lines)
				switch (above) {
					MRichMediaBlock(__, __): t := defaultCardMarginBorders;
					default: {}
				};
			cw = cardWidth(style, bord);

			TLines2(
				makeParagraph(title, MHeadline(), iColor, cw)
				|> TBorderBottom(4.),
				makeParagraph(text, MBody(), iColor, cw)
			)
			|> wrapToFrame(bord, style)
		}
		MSupportingTextBlock(text, style): {
			bord = borders(style);

			if (lines)
				switch (above) {
					MRichMediaBlock(__, __): t := defaultCardMarginBorders;
					MPrimaryTextBlock(__, __, __): t := 0.0;
					default: {}
				};
			makeParagraph(text, MBody(), itemsColor(style), cardWidth(style, bord))
			|> wrapToFrame(bord, style)
		}
		MCard(blocks, style, state): {
			handleBlocks(blocks, fold(style, belowStyle, \acc, s -> replaceStruct(acc, s)), !lines)
			|> wrapToFrame(false, style)
		}
		MDynamicBlock(blocksT): {
			TSelect(blocksT, \blocks -> handleBlocks(blocks, belowStyle, lines))
		}
		MExpanderBlock(header, details, expander, style): {
			MCardBlock2T(
				manager,
				parent,
				MRichMediaBlock(
					extractStruct(belowStyle, MCardWidth(-1.)).width
					|> (\f ->
						MExpander(
							MBorder(24., 0., 0., 0., header)
							|> (\m -> if (f > 0.) MAvailable(m, TFixed(f - 64., 0.)) else m),

							if (f > 0.)
								\ -> MAvailable(details(), TFixed(f, 0.))
							else
								details,
							expander,
							style
						)
					),
					[MContentBorders(false)]
				),
				above,
				below,
				belowStyle,
				lines,
				m2t
			)
		}
		MSeparatorBlock(): {
			TRectangle(
				[MFill(MCardTextColor2MColor(parent, belowStyle, belowStyle)), FillOpacity(0.12)],
				(if (lines) TFillXH(1.0) else TFillWY(1.0))
			)
		}
		TEmpty(): {
			TEmpty();
		}
	}
}

MCardTextColor2MColor(parent : MFocusGroup, style : [MCardStyle], belowStyle : [MCardStyle]) -> MColor {
	eitherFn(
		tryExtractStruct(style, MCardTextColor(MWhite())),
		\col -> MThemeColor2MColor(parent, col.color),
		\ ->
			eitherFn(
				tryExtractStruct(belowStyle, MCardTextColor(MWhite())),
				\col -> MThemeColor2MColor(parent, col.color),
				\ ->
					eitherFn(
						tryExtractStruct(style, MBackgroundImage("", false)),
						\im -> MDefaultTextColor(im.light),
						\ ->
							eitherFn(
								tryExtractMColor(parent, style),
								MTextMColor,
								\ ->
									eitherFn(
										tryExtractStruct(belowStyle, MBackgroundImage("", false)),
										\bi -> MDefaultTextColor(bi.light),
										\ -> MTextMColor(extractMColor(parent, belowStyle, MWhite()))
									)
							)
					)
			)
	)
}

MCardAction2T(manager : MaterialManager, parent : MFocusGroup, action : Material, style : [MCardStyle], belowStyle : [MCardStyle],
	m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	switch (action) {
		MTextButton(text, onClick, style2, state2): {
			buttonStyle : [MTextButtonStyle] =
				if (containsStruct(style2, MCardTextColor(MWhite())))
					style2
				else
					eitherFn(
						tryExtractStruct(style, MCardTextColor(MWhite())),
						\col -> arrayPush(style2, normalizeColor(MThemeColor2MColor(parent, col.color))),
						\ ->
							ifArrayPush(
								style2,
								containsStruct(style, MBackgroundImage("", false)),
								normalizeColor(MCardTextColor2MColor(parent, style, belowStyle))
							)
					);

			MTextButton2T(
				manager,
				parent,
				MTextButton(
					text,
					onClick,
					buttonStyle,
					state2
				),
				m2t
			)
			|> (\t -> TBorder(0.0, defaultCardMarginActions, 0.0, defaultCardMarginActions, t))
		}
		default: {
			TCenterY(m2t(action, parent));
		}
	}
}

MCardStyle2Frame(manager : MaterialManager, parent : MFocusGroup, content : Tropic, style : [MCardStyle], borders : Quadruple<double, double, double, double>,
	itemsColor : MColor, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {

	normalizeVal = \f -> if (f > 0.) i2d(floor(f / 4.0)) * 4.0 else f;

	color = tryExtractMColor(parent, style);
	image = extractStruct(style, MBackgroundImage("", false));
	opacity = extractStruct(style, FillOpacity(1.0));
	widthByContent = contains(style, MWidthByContent());
	cardWidth = if (widthByContent) -1.0 else normalizeVal(extractStruct(style, MCardWidth(-1.0)).width);
	cardHeight = extractStruct(style, MCardHeight(-1.0)).height |> normalizeVal;
	moreMenu = extractStruct(style, MMoreMenu([], [], []));
	alignBottom = contains(style, MCardAlignBottom());
	isRTL = parent.rtl;

	content
	|> (\t -> TAlignStartDir(t, isRTL))
	|> (\f -> if (alignBottom) TLines2(TFillY(), f) else f)
	|> (\f -> if (widthByContent) TGroup2(TFillY(), f) else TGroup2(TFillXY(), f))
	|> (\f -> TBorder(borders.first, borders.second, borders.third, borders.fourth, f))
	|> (\f -> if (cardWidth < 0.0 && cardHeight < 0.0) f else TAvailable(f, wh2Tropic(WidthHeight(cardWidth, cardHeight))))
	|> (\f ->
		TCopySize(
			f,
			\tr ->
				if (image.image != "")
					TAlpha(const(opacity.opacity), TZoomToFill(TPicture(image.image, []), tr, true))
					|> (\f2 -> TGroup2(TRectangle([MFill(MBackgroundMColor(image.light))], tr), f2))
				else
					eitherMap(color, \col -> TRectangle([MFill(col), opacity], tr), TEmpty()),
			false
		)
	)
	|> (\f ->
		if (moreMenu.items != []) {
			mmenu =
				MMenu2T(
					manager,
					parent,
					MMenu(
						MIconButton(
							"more_vert",
							nop,
							concat([itemsColor], moreMenu.buttonStyle),
							[MButtonTitle(const("More Options"))]
						),
						moreMenu.items,
						moreMenu.state
					),
					m2t
				);

			TCopySize(f, \tr -> TAvailable(TAlignEndDir(mmenu, isRTL), tr), true)
		} else
			f
	)
}

normalizeColor(col : MColor) -> MColor {
	if (col == MWhite()) MWhite() else MBlack()
}