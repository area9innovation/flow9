import material/internal/material_edit_grid;
import material/internal/filebrowser/material_filebrowser_internal;
import material/iscript/material_iscript_util;
import material/iscript/material_iscript_filesystem;

export {
	getIScriptRecordButtons(
		manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic, script : Pair<double, IScriptRecord>,
		addPlay : bool, addFn : (Pair<double, IScriptRecord>) -> void, replaceFn : (Pair<double, IScriptRecord>) -> void,
		removeFn : () -> void
	) -> [Material];
	addIScriptRecordHover(manager : MaterialManager, script : Pair<double, IScriptRecord>, hover : DynamicBehaviour<bool>) -> (Material) -> Material;

	addIScriptEventDialog(manager : MaterialManager, focus : MaterialFocus, onOk : (Pair<double, IScriptRecord>) -> void,
		onCancel : () -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void;
	editIScriptRecordDialog(manager : MaterialManager, editRecord : Pair<double, IScriptEditableRecord>,
		onOk : (Pair<double, IScriptRecord>) -> void, onCancel : () -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void;

	defineGlobalAliasOnFocusDialog(manager : MaterialManager, focus : MaterialFocus, onOk : (string) -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void;
	defineGlobalAliasDialog(manager : MaterialManager, componentName : string, id : List<IScriptIdentifier>, onOk : (string) -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void;

	loadBatchTestsDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void;

	loadIScriptDialog(manager : MaterialManager, appendToCurrent : bool, m2t : (Material, MFocusGroup) -> Tropic) -> void;
	saveIScriptDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void;

	extendedLoadSaveIScriptDialog(manager : MaterialManager, storedIScript : StoredIScript, m2t : (Material, MFocusGroup) -> Tropic, isRunningB : Transform<bool>, onClose : () -> void) -> void;
	viewIScriptLogicalScreenshotDialog(manager : MaterialManager, screenshot : string, size : WidthHeight,
		m2t : (Material, MFocusGroup) -> Tropic) -> void;

	showRecordingModeDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void;
	showRecordSettingsDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void;
	showReplaySettingsDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void;

	takeSubscribersSnapshot(manager : MaterialManager, categoryTag : ?, m2t : (Material, MFocusGroup) -> Tropic, subsInfo : DynamicBehaviour<bool>) -> void;
	makeSubscribe2focusHovered(manager : MaterialManager, focus : () -> Maybe<MaterialFocus>, hover : DynamicBehaviour<bool>) -> () -> () -> void;

	showLogicalScreenshotCompareDialog(manager : MaterialManager, batchTestsFolder : string,
		batchTestResults : ref [IScriptReplayResult], m2t : (Material, MFocusGroup) -> Tropic) -> void;

	loadDBIScriptDialog(manager : MaterialManager, appendToCurrent : bool, m2t : (Material, MFocusGroup) -> Tropic) -> void;
	saveDBIScriptDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void;
	loadDBBatchTestsDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void;
}



addIScriptRecordHover(manager : MaterialManager, script : Pair<double, IScriptRecord>, hover : DynamicBehaviour<bool>) -> (Material) -> Material {
	eitherMap(
		switch (script.second : IScriptRecord) {
			IScriptBehaviourRecord(descriptor, value, type, callstack) : {
				getMaterialFocusByIScriptId(manager.focus, descriptor.component.id);
			}
			IScriptInputRecord(descriptor, __, __, __) : {
				getMaterialFocusByIScriptId(manager.focus, descriptor.component.id);
			}
			IScriptOutputRecord(descriptor, __) : {
				getMaterialFocusByIScriptId(manager.focus, descriptor.component.id);
			}
			default:
				None();
		},
		\f -> \content ->
			MConstruct(
				[
					makeSubscribe2focusHovered(manager, \ -> Some(f), hover)
				],
				content
			),
		idfn
	)
}

makeIScriptArrow(mainLength : double, mainHeight : double, arrowLength : double, arrowHeight : double, style : [TGraphicsStyle]) -> TGraphics {
	x1 = 0.;
	y1 = (arrowHeight - mainHeight) / 2.;
	x2 = x1;
	y2 = y1 + mainHeight;
	x3 = x1 + mainLength;
	y3 = y2;
	x4 = x3;
	y4 = y3 + y1;
	x5 = x4 + arrowLength;
	y5 = arrowHeight / 2.;
	x6 = x4;
	y6 = 0.;
	x7 = x6;
	y7 = y1;

	TGraphics(
		[
			MoveTo(x1, y1),
			LineTo(x2, y2),
			LineTo(x3, y3),
			LineTo(x4, y4),
			LineTo(x5, y5),
			LineTo(x6, y6),
			LineTo(x7, y7),
			ClosePath()
		],
		style
	)
}

makeSubscribe2focusHovered(manager : MaterialManager, focus : () -> Maybe<MaterialFocus>, hover : DynamicBehaviour<bool>) -> () -> () -> void {
	makeSubscribe2Uns(hover, \h ->
		if (h)
			eitherMap(
				focus(),
				\f -> {
					fillColor = switchMaterialFocus(f, \__ -> MGreen(500), \__ -> MPurple(300));

					pos = f.position().pos;
					wh = f.widthHeight();
					stage = getRealStageWidthHeight(manager);

					xTranscendMin = pos.x + wh.width < 0.;
					xTranscendMax = pos.x >= stage.width;
					yTranscendMin = pos.y + wh.height < 0.;
					yTranscendMax = pos.y >= stage.height;

					[
						trender(
							if (xTranscendMin || xTranscendMax || yTranscendMin || yTranscendMax) {
								TTranslate(
									const(Point(min(max(pos.x + wh.width / 2., 0.), stage.width), min(max(pos.y + wh.height / 2., 0.), stage.height))),
									TRotate(
										const(if (xTranscendMin) {
											if (yTranscendMin) {
												180. + 45.
											} else if (yTranscendMax) {
												180. - 45.
											} else {
												180.
											}
										} else if (xTranscendMax) {
											if (yTranscendMin) {
												0. + 45.
											} else if (yTranscendMax) {
												0. - 45.
											} else {
												0.
											}
										} else if (yTranscendMin) {
											270.
										} else {
											90.
										}),
										TTranslate(
											const(Point(-40., -10.)),
											makeIScriptArrow(
												20.,
												8.,
												10.,
												20.,
												[
													MFill(fillColor),
													FillOpacity(0.8),
													MStroke(fillColor),
													StrokeWidth(1.),
													StrokeOpacity(1.)
												]
											)
										)
									)
								)
							} else {
								TTranslate(
									const(pos),
									TRectangle(
										[
											MFill(fillColor),
											FillOpacity(0.3),
											MStroke(fillColor),
											StrokeWidth(1.),
											StrokeOpacity(1.)
										],
										TSized(const(wh))
									)
								)
							},
							manager.manager.renderStyle
						)
					]
				},
				[]
			)
		else
			[]
	)
}

getIScriptRecordButtons(
	manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic, script : Pair<double, IScriptRecord>,
	addPlay : bool, addFn : (Pair<double, IScriptRecord>) -> void, replaceFn : (Pair<double, IScriptRecord>) -> void,
	removeFn : () -> void
) -> [Material] {
	time = firstOfPair(script);
	r = secondOfPair(script);

	deleteButton = MIconButton(
		"delete",
		removeFn,
		[MRed(500), MIconSize(20.)],
		[MTooltipText(const(_("Delete action")))]
	);

	editButton = MIconButton(
		"edit",
		\ -> {
			editIScriptRecordDialog(
				manager,
				cast(script : Pair<double, IScriptRecord> -> Pair<double, IScriptEditableRecord>),
				replaceFn,
				nop,
				m2t
			)
		},
		[MIconSize(20.)],
		[MTooltipText(const(_("Edit action")))]
	);

	buttons =
		switch (r : IScriptRecord) {
			IScriptBehaviourRecord(descriptor, value, type, callstack) : {
				playButton =
					if (addPlay) {
						MIconButton(
							"play_arrow",
							\ ->
								repeatBehaviourById(manager, descriptor, value) |> ignore,
							[MGreen(500), MIconSize(20.)],
							[MTooltipText(const(_("Fire action")))]
						)
					} else
						MEmpty();

				transformIntoInputButton =
					switch (type) {
						ISInputValue(pv, th, canInput) : {
							if (canInput) {
								MIconButton(
									"input",
									\ -> {
										transformIntoInputDialog(
											manager,
											\name -> {
												newRecord = Pair(time, IScriptInputRecord(descriptor, name, pv, th));
												replaceFn(newRecord);
											},
											m2t
										)
									},
									[MIconSize(20.)],
									[MTooltipText(const(_("Transform into script input")))]
								)
							} else
								MEmpty();
						}
						default : MEmpty();
					};

				[
					playButton,
					transformIntoInputButton,
					editButton,
				]
			}
			IScriptRequestRecord(url, post, headers, params, delay, response) : [];
			IScriptScreenshot(__, __) : [
				if (addPlay) {
					MIconButton(
						"play_arrow",
						\ ->
							applyIScriptScreenshot(manager, r),
						[MGreen(500), MIconSize(20.)],
						[MTooltipText(const(_("Apply screenshot")))]
					)
				} else {
					MEmpty()
				}
			];
			IScript(recordingStarted, stack) : [
				// if (addPlay) {
				// 	MIconButton(
				// 		"play_arrow",
				// 		\ -> applyIScriptScreenshot(manager, r),
				// 		[MGreen(500), MIconSize(20.)],
				// 		[MTooltipText("Apply screenshot")]
				// 	)
				// } else {
				// 	MEmpty()
				// }
			];
			IScriptLogicalScreenshot(screenshot, size) : [
				// if (addPlay) {
				// 	MIconButton(
				// 		"play_arrow",
				// 		\ ->
				// 			applyIScriptScreenshot(manager, r),
				// 		[MGreen(500), MIconSize(20.)],
				// 		[MTooltipText("Apply screenshot")]
				// 	)
				// } else {
				// 	MEmpty()
				// },
				MIconButton(
					"pageview",
					\ -> viewIScriptLogicalScreenshotDialog(manager, screenshot, size, m2t),
					[MIconSize(20.)],
					[MTooltipText(const(_("View screenshot")))]
				)
			];
			IScriptVisualScreenshot(screenshot) : [
				MIconButton(
					"pageview",
					\ -> viewIScriptVisualScreenshotDialog(manager, screenshot, m2t),
					[MIconSize(20.)],
					[MTooltipText(const(_("View screenshot")))]
				)
			];
			IScriptInputRecord(descriptor, name, pv, th) : {
				transformIntoEventButton = MIconButton(
					"transform",
					\ -> {
						inputValue = findDef(getValue(manager.iscript.inputValuesB), \input -> input.name == name, IScriptInputValue("", "", None()));

						transformIntoEventDialog(
							manager,
							inputValue.value,
							pv,
							th,
							\value -> {
								newRecord = Pair(time, IScriptBehaviourRecord(descriptor, value, ISInputValue(pv, th, true), ""));
								replaceFn(newRecord);
							},
							m2t
						)
					},
					[MIconSize(20.)],
					[MTooltipText(const(_("Transform into event")))]
				);

				addOutputButton = MIconButton(
					"launch",
					\ -> {
						freeIndex = countUntil(1, 100000, \i -> isOutputNameUnique(manager, name + "_output_" + i2s(i)));
						newRecord = Pair(time + 1.0, IScriptOutputRecord(descriptor, name + "_output_" + i2s(freeIndex)));
						addFn(newRecord);
					},
					[MIconSize(20.)],
					[MTooltipText(const(_("Add output nearby")))]
				);

				[
					transformIntoEventButton,
					addOutputButton,
					editButton,
				]
			}
			IScriptOutputRecord(__, __) : [editButton];
			default:
				[]
		}

	arrayPush(buttons, deleteButton);
}

transformIntoInputDialog(manager : MaterialManager, onOk : (string) -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhenB = make(false);
	nameIsOkB = make(false);
	nameErrorTextB = make(None());
	assignedNameB = make("");

	elements = [
		MParagraph("You are trying to transform event record into input record. Please, assign the unique name for it.", []),
		MFixed(0.0, 16.0),
		MConstruct([
				makeSubscribe2(assignedNameB, \un -> {
					if (isInputNameUnique(manager, un))
						nextDistinct(nameErrorTextB, None())
					else
						next(nameErrorTextB, Some(Pair("This name is already used", true)));
				})
			],
			MTextInput(assignedNameB,
				[MLabel("Assign name"), MFloatingLabel()],
				[MInputError(nameErrorTextB, [MInputIsOk(nameIsOkB), MRequiredField()])]
			)
		)
	];

	content = MGetFocusGroup(\p ->
		MLines(map(elements, \it -> MEGItem2T(p, it, [MWidth(400.)], m2t)))
	);

	dialogActions = MDialogActions([
		MTextButton("CANCEL", \ -> next(closeWhenB, true), [], [MShortcut("esc")]),
		MTextButton("TRANSFORM", \ -> {
			next(closeWhenB, true);
			onOk(getValue(assignedNameB));
		}, [], [MShortcut("enter"), MEnabled(feq(nameIsOkB, true))])
	]);

	dialogStyle = [
		MDialogTitle("Record transform"),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		content,
		m2t
	);
}

transformIntoEventDialog(manager : MaterialManager, maybeInitial : Maybe<flow>, possibleValues : [flow], typeHelper : string, onOk : (flow) -> void,
	m2t : (Material, MFocusGroup) -> Tropic) -> void {

	closeWhenB = make(false);

	pair = makeValueInputByTypeHint(possibleValues, typeHelper, None(), const(true));
	valueB = pair.first;
	valueInputMaterial = MGetFocusGroup(\p -> MEGItem2T(p, pair.second, [], m2t));

	maybeApply(maybeInitial, \initial -> next(valueB, initial));

	elements = [
		MParagraph("You are trying to transform input record into event record. Please, enter the value for it.", []),
		MFixed(0.0, 16.0),
		valueInputMaterial
	];

	content = MGetFocusGroup(\p ->
		MLines(map(elements, \it -> MEGItem2T(p, it, [MWidth(400.)], m2t)))
	);

	dialogActions = MDialogActions([
		MTextButton("CANCEL", \ -> next(closeWhenB, true), [], [MShortcut("esc")]),
		MTextButton("TRANSFORM", \ -> {
			next(closeWhenB, true);
			onOk(getValue(valueB));
		}, [], [MShortcut("enter"),])
	]);

	dialogStyle = [
		MDialogTitle("Record transform"),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		content,
		m2t
	);
}

addIScriptEventDialog(manager : MaterialManager, focus : MaterialFocus, onOk : (Pair<double, IScriptRecord>) -> void,
	onCancel : () -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void {

	closeWhenB = make(false);

	behaviourNames = getTreeKeys(focus.behaviours);
	behaviourTypes : [IScriptRecordType] = map(getTreeValues(focus.behaviours), \pair : Pair<DynamicBehaviour<flow>, IScriptRecordType>, -> pair.second);

	getBehaviourValue = \bid -> {
		if (bid >= 0)
			getValue(either(lookupTree(focus.behaviours, behaviourNames[bid]), Pair(make(""), ISMiscEvent())).first)
		else
			flow("")
	}

	recordTimeB = make(0.0);
	uniqueNameB = make("");

	nameIsOkB = make(true);
	nameErrorTextB = make(None());
	eventDurationB = make(100.0);

	focId = getFullMFocusId(focus);

	availableEventsIndexes = filtermapi(behaviourTypes, \i, type -> {
		if (isSameStructType(type, ISMiscEvent()))
			None()
		else
			Some(i);
	});
	availableInputsIndexes = filtermapi(behaviourTypes, \i, type -> {
		switch (type) {
			ISInputValue(__, __, canBeInput) : if (canBeInput) Some(i) else None();
			default : None();
		}
	});
	availableOutputsIndexes = filtermapi(behaviourTypes, \i, type -> {
		isAvailableForOutput = isSameStructType(type, ISInputValue([], "", false)) || isSameStructType(type, ISMiscEvent());

		if (isAvailableForOutput)
			Some(i)
		else
			None();
	});

	behaviourIdB = make(if (length(availableEventsIndexes) > 0) availableEventsIndexes[0] else -1);
	valueB : DynamicBehaviour<flow> = make(getBehaviourValue(getValue(behaviourIdB)));

	actionIndexB = make(0);
	actions = [
		Pair(0, "Event"),
		Pair(1, "Input"),
		Pair(2, "Output")
	];

	eventElements = [
		MEGDropDown("Event", behaviourIdB, Pair(-1, "No events available"), map(availableEventsIndexes, \i -> Pair(i, behaviourNames[i])), []),
		MEGMutable(fselect(behaviourIdB, FLift(\bid -> {
			if (bid >= 0) {
				durationInput = MEGTextInput("Event duration (ms)", eventDurationB, [MInputFilter(\s, foc -> d2s(max(s2d(s), 0.)))]);

				switch (behaviourTypes[bid] : IScriptRecordType) { // TODO
					ISDetailedTextEvent() : MEmpty();
					ISMouseEvent() : durationInput;
					ISKeyEvent() : MEmpty();
					ISInteractionEvent() : MEmpty();
					ISClickEvent() : MEmpty();
					ISAppearanceEvent() : MEmpty();
					ISValueEvent() : MEmpty();
					ISEnableEvent() : MEmpty();
					ISSelectionEvent() : MEmpty();

					ISMiscEvent() : {
						next(valueB, getBehaviourValue(bid));
						MEGSwitchControlOrTextInput("Input value", valueB, [MHelperText(const(Some(getTypeString(getValue(valueB)))), true)]);
					}
					ISWigiEvent() : MEmpty();
					ISPebbleEvent() : MEmpty();
					ISInputValue(pv, typeHint, __) : {
						next(valueB, getBehaviourValue(bid));
						pair = makeValueInputByTypeHint(pv, typeHint, None(), const(true));

						MConstruct([
								\ -> fconnect(pair.first, valueB)
							],
							MGetFocusGroup(\p -> MEGItem2T(p, pair.second, [], m2t))
						)
					}
					ISCustomIScriptRecordType(__) : MEmpty();
				}
			} else
				TEmpty()
		})))
	];

	makeNameInput = \checkUniqueFn -> {
		MConstruct([
				makeSubscribe2(uniqueNameB, \un -> {
					if (checkUniqueFn(manager, un))
						nextDistinct(nameErrorTextB, None())
					else
						next(nameErrorTextB, Some(Pair("This name is already used", true)));
				})
			],
			MTextInput(uniqueNameB,
				[MLabel("Assign name"), MFloatingLabel()],
				[MInputError(nameErrorTextB, [MInputIsOk(nameIsOkB), MRequiredField()])]
			)
		)
	}

	inputElements = [
		MEGDropDown("Select Input", behaviourIdB, Pair(-1, "No inputs available"), map(availableInputsIndexes, \i -> Pair(i, behaviourNames[i])), []),
		makeNameInput(isInputNameUnique)
	];

	outputElements = [
		MEGDropDown("Select value to output", behaviourIdB, Pair(-1, "No outputs available"), map(availableOutputsIndexes, \i -> Pair(i, behaviourNames[i])), []),
		makeNameInput(isOutputNameUnique)
	];

	elements = [
		MParagraph("Id: [" + IScriptId2s(focId) + "]", [MSubheading(), MWidth(-1.)]),
		MEGTextInput("Delay after beginning (ms)", recordTimeB, [MInputFilter(\s, foc -> d2s(max(s2d(s), 0.)))]),
		MEGDropDown("Action type", actionIndexB, Pair(-1, "Select Action type"), actions, []),
		MEGMutable(fselect(actionIndexB, FLift(\actionIndex -> {
			next(uniqueNameB, "");

			actionElements =
				if (actionIndex == 0) {
					eventElements
				} else if (actionIndex == 1) {
					bid = getValue(behaviourIdB);
					idExists = exists(availableInputsIndexes, \idx -> idx == bid);

					if (!idExists) {
						newBid = if (length(availableInputsIndexes) > 0) availableInputsIndexes[0] else -1;
						next(behaviourIdB, newBid);
					}

					inputElements
				} else if (actionIndex == 2) {
					bid = getValue(behaviourIdB);
					idExists = exists(availableOutputsIndexes, \idx -> idx == bid);

					if (!idExists) {
						newBid = if (length(availableOutputsIndexes) > 0) availableOutputsIndexes[0] else -1;
						next(behaviourIdB, newBid);
					}

					outputElements
				} else
					[];

			MGetFocusGroup(\p -> MLines(map(actionElements, \it -> MEGItem2T(p, it, [MWidth(400.)], m2t))))
		})))
	];

	content = MGetFocusGroup(\p ->
		MLines(map(elements, \it -> MEGItem2T(p, it, [MWidth(400.)], m2t)))
	);

	dialogActions = MDialogActions([
		MTextButton("CANCEL", \ -> {
			next(closeWhenB, true);
			onCancel()
		}, [], [MShortcut("esc")]),
		MTextButton("ADD TO SCRIPT", \ -> {
			actionIndex = getValue(actionIndexB);
			bid = getValue(behaviourIdB);
			recordTime = getValue(recordTimeB);
			uniqueName = getValue(uniqueNameB);
			eventDuration = getValue(eventDurationB);

			descriptor = IScriptBehaviourDescriptor(
				IScriptComponentDescriptor(
					focus.name,
					focId
				),
				behaviourNames[bid]
			);

			result : Pair<double, IScriptRecord> =
				if (actionIndex == 0) {
					callstackString = getCallstackIfNeeded(manager);

					res : Pair<double, IScriptRecord> =
						switch (behaviourTypes[bid]) {
							// ISMouseEvent() :
							// 	Pair(
							// 		recordTime,
							// 		IScriptEventRecord(
							// 			descriptor,
							// 			true,
							// 			false,
							// 			eventDuration
							// 		)
							// 	);
							default : Pair(
								recordTime,
								IScriptBehaviourRecord(
									descriptor,
									getValue(valueB),
									behaviourTypes[bid],
									callstackString
								)
							);
						}

					res
				} else if (actionIndex == 1) {
					valuesAndHint : Pair<[flow], string> =
						switch (behaviourTypes[bid]) {
							ISInputValue(pv, typeHint, __) : Pair(pv, typeHint);
							default : Pair([], "");
						};

					Pair(
						recordTime,
						IScriptInputRecord(
							descriptor,
							uniqueName,
							valuesAndHint.first,
							valuesAndHint.second
						)
					)
				} else {
					Pair(
						recordTime,
						IScriptOutputRecord(
							descriptor,
							uniqueName
						)
					)
				}

			next(closeWhenB, true);
			onOk(result);
		}, [], [MShortcut("enter"), MEnabled(fand(fneq(behaviourIdB, -1), feq(nameIsOkB, true)))])
	]);

	dialogStyle = [
		MDialogTitle(focus.name),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		content,
		m2t
	);
}

editIScriptRecordDialog(manager : MaterialManager, editRecord : Pair<double, IScriptEditableRecord>,
	onOk : (Pair<double, IScriptRecord>) -> void, onCancel : () -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void {

	closeWhenB = make(false);
	record = editRecord.second;
	recordTimeB = make(editRecord.first);
	uniqueNameB = make("");
	nameIsOkB = make(true);
	nameErrorTextB = make(None());
	eventDurationB = make(100.0);
	valueB : DynamicBehaviour<flow> = make(flow(""));

	makeNameInput = \checkUniqueFn, initialName -> {
		MConstruct([
				makeSubscribe2(uniqueNameB, \un -> {
					if (un == initialName || checkUniqueFn(manager, un))
						nextDistinct(nameErrorTextB, None())
					else
						next(nameErrorTextB, Some(Pair("This name is already used", true)));
				})
			],
			MTextInput(uniqueNameB,
				[MLabel("Assigned name"), MFloatingLabel()],
				[MInputError(nameErrorTextB, [MInputIsOk(nameIsOkB), MRequiredField()])]
			)
		)
	}

	actionElement =
		switch (record : IScriptEditableRecord) {
			IScriptInputRecord(descriptor, name, __, __) : {
				next(uniqueNameB, name);

				makeNameInput(isInputNameUnique, name);
			}
			IScriptOutputRecord(descriptor, name) : {
				next(uniqueNameB, name);

				makeNameInput(isOutputNameUnique, name);
			}
			IScriptBehaviourRecord(descriptor, value, type, __) : {
				next(valueB, value);

				switch (type) {
					ISMiscEvent() : MEGTextInput("Input value", valueB, [MHelperText(const(Some(getTypeString(value))), true)]);
					ISInputValue(pv, typeHint, __) : {
						pair = makeValueInputByTypeHint(pv, typeHint, None(), const(true));

						MConstruct([
								\ -> fconnect(pair.first, valueB)
							],
							MGetFocusGroup(\p -> MEGItem2T(p, pair.second, [], m2t))
						)
					}
					default : MEmpty();
				}
			}
			default: TEmpty()
		}

	triple =
		switch (record : IScriptEditableRecord) {
			IScriptInputRecord(descriptor, __, __, __) : Triple("Input \"" + descriptor.name + "\"", descriptor.component.id, descriptor.component.name);
			IScriptOutputRecord(descriptor, __) : Triple("Output \"" + descriptor.name + "\"", descriptor.component.id, descriptor.component.name);
			IScriptBehaviourRecord(descriptor, __, __, __) : Triple("Event \"" + descriptor.name + "\"", descriptor.component.id, descriptor.component.name);
			default : Triple("", makeList(), "");
		}

	recordAction = triple.first;
	id = triple.second;
	title = triple.third;

	elements = [
		MParagraph("Id: [" + IScriptId2s(id) + "]", [MSubheading(), MWidth(-1.)]),
		MFixed(0.0, 8.0),
		MText(recordAction, [MSubheading()]),
		MFixed(0.0, 8.0),
		MEGTextInput("Delay after beginning (ms)", recordTimeB, [MInputFilter(\s, foc -> d2s(max(s2d(s), 0.)))]),
		actionElement
	];

	content = MGetFocusGroup(\p ->
		MLines(map(elements, \it -> MEGItem2T(p, it, [MWidth(400.)], m2t)))
	);

	dialogActions = MDialogActions([
		MTextButton("CANCEL", \ -> {
			next(closeWhenB, true);
			onCancel()
		}, [], [MShortcut("esc")]),
		MTextButton("APPLY", \ -> {
			recordTime = getValue(recordTimeB);
			uniqueName = getValue(uniqueNameB);
			eventDuration = getValue(eventDurationB);
			value = getValue(valueB);

			result : Pair<double, IScriptRecord> = Pair(recordTime,
				switch (record : IScriptEditableRecord) {
					IScriptInputRecord(descriptor, __, pv, helper) : IScriptInputRecord(descriptor, uniqueName, pv, helper);
					IScriptOutputRecord(descriptor, __) : IScriptOutputRecord(descriptor, uniqueName);
					IScriptBehaviourRecord(descriptor, __, type, cs) : IScriptBehaviourRecord(descriptor, value, type, cs);
					default: record;
				}
			);

			next(closeWhenB, true);
			onOk(result);
		}, [], [MShortcut("enter"), MEnabled(feq(nameIsOkB, true))])
	]);

	dialogStyle = [
		MDialogTitle(title),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		content,
		m2t
	);
}

showRecordSettingsDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhenB = make(false);

	content = MAvailable(
		MLines([
			MBaselineCols([
				MText("Capture callstack", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureCallstack, []),
			]),

			MBaselineCols([
				MText("Detailed capture for text inputs", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureTextDetails, []),
			]),
			MBaselineCols([
				MText("Capture HTTP requests", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureHttpRequests, []),
			]),
			MBaselineCols([
				MText("Capture mouse events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureMouseEvents, []),
			]),
			MBaselineCols([
				MText("Capture keystroke events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureKeyEvents, []),
			]),
			MBaselineCols([
				MText("Capture interaction events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureInteractionEvents, []),
			]),
			MBaselineCols([
				MText("Capture hover events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureHover, [
					MEnabled(manager.iscript.settings.captureInteractionEvents)
				]),
			]),
			MBaselineCols([
				MText("Capture click events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureClickEvents, []),
			]),
			MBaselineCols([
				MText("Capture appearance events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureAppearanceEvents, []),
			]),
			MBaselineCols([
				MText("Capture value events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureValueEvents, []),
			]),
			MBaselineCols([
				MText("Capture enable events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureEnableEvents, []),
			]),
			MBaselineCols([
				MText("Capture selection events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureSelectionEvents, []),
			]),
			MBaselineCols([
				MText("Capture input events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureInputValues, []),
			]),
			MBaselineCols([
				MText("Capture wigi events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureWigiEvents, []),
			]),
			MBaselineCols([
				MText("Capture pebble events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.capturePebbleEvents, []),
			]),
			MBaselineCols([
				MText("Capture custom events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureCustomTypes, []),
			]),
			MBaselineCols([
				MText("Capture misc events", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.captureMiscEvents, []),
			]),
		]),
		MFixed(400.0, 0.0)
	);

	dialogActions = MDialogActions([
		MTextButton("CLOSE", \ -> next(closeWhenB, true), [], [MShortcut("esc")]),
	]);

	dialogStyle = [
		MDialogTitle("Record settings"),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		content,
		m2t
	);
}

showReplaySettingsDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhenB = make(false);
	currentCursorScheme = make(getIScriptReplayCurrentCursorSchemeId(manager));

	content = MAvailable(
		MLines([
			// MBaselineCols([
			// 	MText("Apply first screenshot (reset state)", [MSubheading()]),
			// 	TFillX(),
			// 	MSwitchControl(manager.iscript.settings.replayApplyFirstScreenshot, []),
			// ]),
			MBaselineCols([
				MText("Check errors", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.replayCheckErrors, []),
			]),
			// MBaselineCols([
			// 	MText("Check all behaviours", [MSubheading()]),
			// 	TFillX(),
			// 	MSwitchControl(
			// 		manager.iscript.settings.replayCheckAllBehaviours,
			// 		[MEnabled(manager.iscript.settings.replayCheckErrors)]
			// 	),
			// ]),
			MBaselineCols([
				MText("Verbose output", [MSubheading()]),
				TFillX(),
				MSwitchControl(manager.iscript.settings.replayVerbose, []),
			]),
			MBaselineCols([
				MText("Speed", [MSubheading()]),
				// TFillX(),
				MSlider(manager.iscript.settings.replaySpeed, [MSliderRange(0.1, 10.0), MSliderDisplayValue(true)]),
			]),
			MBaselineCols([
				MText("Current cursor theme", [MSubheading()]),
				TFillX(),
				MDropDownMenu(
					mapi(getIScriptReplayCursorSchemes(manager), \i, v ->
						MMenuCustomLine(
							MBaselineCols(
								[
									MCenterIn(v.defaultCursor.shape, TFixed(32.0, 32.0)),
									MFixedX(16.0),
									MCenterY(MText(v.name, []))
								]
							),
							[MOnClick(\ -> setIScriptReplayCurrentCursorSchemeId(manager, i))]
							)
						),
					currentCursorScheme,
					[])
			]),

		]),
		MFixed(400.0, 0.0)
	);

	dialogActions = MDialogActions([
		MTextButton("CLOSE", \ -> next(closeWhenB, true), [], [MShortcut("esc")]),
	]);

	dialogStyle = [
		MDialogTitle("Replay settings"),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		content,
		m2t
	);
}

defineGlobalAliasOnFocusDialog(manager : MaterialManager, focus : MaterialFocus, onOk : (string) -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	defineGlobalAliasDialog(
		manager,
		focus.name,
		getFullMFocusId(focus),
		onOk,
		m2t
	);
}

defineGlobalAliasDialog(manager : MaterialManager, componentName : string, id : List<IScriptIdentifier>, onOk : (string) -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhenB = make(false);
	aliasB = make("");

	content = MAvailable(
		MLines([
			MParagraph("You are defining new alias for component", []),
			MFixed(0.0, 8.0),
			MParagraph(componentName + " [" + IScriptId2s(id) + "]", [MSubheading()]),
			MFixed(0.0, 24.0),
			MTextInput(aliasB, [], [])
		]),
		MFixed(560.0, 0.0)
	);

	dialogActions = MDialogActions([
		MTextButton("CLOSE", \ -> next(closeWhenB, true), [], [MShortcut("esc")]),
		MTextButton("DEFINE", \ -> {
				next(closeWhenB, true);
				onOk(getValue(aliasB));
			}, [], [MShortcut("enter"), MEnabled(fneq(aliasB, ""))]
		),
	]);

	dialogStyle = [
		MDialogTitle("Component alias definition"),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		content,
		m2t
	);
}

loadBatchTestsDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	// DO WE NEED TO REQUEST OF SAVING OF THE CURRENT SCRIPT?
	storeFilesystemExtAPI = getFilesystemExtAPI();

	restoreFilesystemExtAPI = \ -> setFilesystemExtAPI(storeFilesystemExtAPI);

	setLocalFilesystemExtAPI();

	directoryNameB = make("");

	onOpen = \fileObjects -> {
		if (fileObjects != []) {
			folderName = fileObjects[0].fullPath;
			if (folderName != "") {
				folderName0 = if (!(endsWith(folderName, "\\") || endsWith(folderName, "/"))) folderName +"\\" else folderName;
				updateBatchTestFilenames(manager, folderName0);
				next(manager.iscript.currentBatchTestsDirectoryB, folderName0);
			}
		} else {
			// Should never happens
			iScriptInfo(manager, "Warning: no folders selected", None());
		}
		restoreFilesystemExtAPI();
	};

	onCancel = \ -> restoreFilesystemExtAPI();

	renderCustomMFileBrowser(manager, manager.focus, "Open Batch Tests Folder", [],
		[
			OpenExtFBB(onOpen),
			CancelExtFBB(onCancel)
		],
		[
			FbFolderSelection(),
			FbAllowPathInput(),
			FbModalWindow(true),
			FbCurrentPath(make("")),
			FbStartDir(getCurrentDirectory())
		],
		m2t
	);

}

loadIScriptDialog(manager : MaterialManager, appendToCurrent : bool, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	storeFilesystemExtAPI = getFilesystemExtAPI();

	restoreFilesystemExtAPI = \ -> setFilesystemExtAPI(storeFilesystemExtAPI);

	setLocalFilesystemExtAPI();

	fileNameB = make("");
	recordTimeB = make(0.);
	recordTimeInputB = make(d2s(getValue(recordTimeB)));

	closeDelayDialog = make(false);
	saveDelayEnable = fneq(recordTimeInputB, "");

	onOpen = \fileObjects -> {
		if (fileObjects != []) {
			fileName = fileObjects[0].fullPath;
			next(manager.iscript.currentFileNameB, fileName);
			if (appendToCurrent)
				addIScriptRecordWithDelay(manager, getValue(recordTimeB), iScriptFromFile2(manager, fileName))
			else
				iScriptFromFile(manager, fileName);
		} else {
			// Should never happens
			iScriptInfo(manager, "Warning: no files selected", None());
		}
		restoreFilesystemExtAPI();
	};

	onCancel = \ -> restoreFilesystemExtAPI();

	delayDialog = \__, __ -> {
		renderMDialog(manager, closeDelayDialog,
			[
				MDialogTitle("Enter delay value"),
				MDialogUseFrame(),
				MDialogActions([
					MTextButton(_("CANCEL"), \-> next(closeDelayDialog, true), [], [MShortcut("esc")]),
					MTextButton(_("SET"), \-> {
						next(recordTimeB, s2d(getValue(recordTimeInputB)));
						next(closeDelayDialog, true);
					}, [], [MShortcut("enter"), MEnabled(saveDelayEnable)])
				]),
			],
			MConstruct(
				[],
				MCols([
					MFixSize(MText("Delay (ms):", []), TFixed(75.0, 20.0)),
					TFixed(8.0, 0.0),
					MTextInput(
						recordTimeInputB,
						[TextInputType(NumericType()), MWidth(400.0), MMaxLines(1)],
						[MEnabled(const(true))]
					)
				])
			),
			m2t
		)
	}

	renderCustomMFileBrowser(manager, manager.focus, "Open file", [".iscript"],
		concat(
			if (appendToCurrent) [CustomExtFBB("Delay (ms)", delayDialog)] else [],
			[
				OpenExtFBB(onOpen),
				CancelExtFBB(onCancel)
			]
		),
		[
			FbModalWindow(true),
			FbCurrentPath(make("")),
			FbStartDir(getCurrentDirectory())
		], m2t);
}

saveIScriptDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	fileNameB = make(getValue(manager.iscript.currentFileNameB));

	storeFilesystemExtAPI = getFilesystemExtAPI();

	restoreFilesystemExtAPI = \ -> setFilesystemExtAPI(storeFilesystemExtAPI);

	setLocalFilesystemExtAPI();
	onSave = \fileObjects -> {
		if (fileObjects != []) {
			filename = if (strContains(fileObjects[0].fullPath, ".iscript"))
				fileObjects[0].fullPath
			else
				concatStrings([fileObjects[0].fullPath, ".iscript"]);
			next(fileNameB, filename);
			iScriptInfo(manager, filename, None());
			saveIScript(manager, getValue(fileNameB), m2t);
		} else {
			// Should never happen
			iScriptInfo(manager, "Warning: no files selected", None());
		}
		restoreFilesystemExtAPI();
	};

	onCancel = \ -> restoreFilesystemExtAPI();

	renderCustomMFileBrowser(manager, manager.focus, "Save file as...", [".iscript"],
		[
			SaveExtFBB(onSave),
			CancelExtFBB(onCancel)
		],
		[
			FbModalWindow(true),
			FbCurrentPath(make("")),
			FbStartDir(getCurrentDirectory())
		], m2t);
}

extendedLoadSaveIScriptDialog(
	manager : MaterialManager,
	storedIScript : StoredIScript,
	m2t : (Material, MFocusGroup) -> Tropic,
	isRunningB : Transform<bool>,
	onClose : () -> void
) -> void {
	loadChoice = make(0);
	loadChoices =
		[
			"Replace current IScript",
			"Add to current IScript",
			"Save to file"
		];

	fileName = make("mtest.txt");
	recordTime = make(0.);

	MGetFocusGroup(\p ->
		[
			MEGDropDown("Load Type", loadChoice, Pair(-1, "Select Load Type"), mapi(loadChoices, \i, bn -> Pair(i, bn)), []),
			MEGMutable(fselect(loadChoice, FLift(\lc -> {
				if (lc == 1)
					MEGTextInput("Delay (ms)", recordTime, [MInputFilter(\s, foc -> d2s(max(s2d(s), 0.)))])
				else if (lc == 2)
					MEGTextInput("File Name", fileName, [])
				else
					TEmpty()
			})))
		]
		|> (\f -> map(f, \it -> MEGItem2T(p, it, [MWidth(400.)], m2t)))
		|> MLines
	)
	|> (\f ->
		renderMDialog(
			manager,
			fnot(isRunningB),
			[
				MDialogTitle("Load IScript"),
				MDialogUseFrame(),
				MDialogScroll(),
				MDialogActions([
					MTextButton("CANCEL", \ -> {
						onClose();
					}, [], [MShortcut("esc")]),
					MTextButton("OK", \ -> {
						onClose();
						lc = getValue(loadChoice);

						if (lc == 0)
							loadStoredIScript(manager, storedIScript)
						else if (lc == 1)
							addIScriptRecordWithDelay(manager, getValue(recordTime), storedIScript.script)
						else
							iScriptToFile2(manager, storedIScript, getValue(fileName)) |> ignore
					}, [], [MShortcut("enter")])
				]),
				IScriptRecordingEnabled(const(false))
			],
			f,
			m2t
		)
	);
}

viewIScriptLogicalScreenshotDialog(manager : MaterialManager, screenshot : string, size : WidthHeight, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhenB = make(false);

	TFixSize(
		TFForm(deserializeLogicalSnapshot(manager, screenshot, true, m2t), zeroTMetrics),
		TFixed(size.width, size.height)
	)
	|> (\f ->
		renderMDialog(
			manager,
			closeWhenB,
			[
				MDialogTitle("Right click any text to add it to record stack"),
				MDialogUseFrame(),
				MDialogScroll(),
				MDialogActions([
					// MTextButton("CANCEL", \ -> {
					// 	next(closeWhenB, true);
					// }, [], [MShortcut("esc")]),
					MTextButton("OK", \ -> {
						next(closeWhenB, true);
						// lc = getValue(loadChoice);

						// if (lc == 0)
						// 	next(manager.iscript.iScriptB, iScript)
						// else if (lc == 1)
						// 	addRecordToIScript(manager, Pair(getValue(recordTime), iScript), getValue(fileName))
						// else
						// 	iScriptToFile2(manager, iScript, getValue(fileName)) |> ignore
					}, [], [MShortcut("enter")])
				]),
				IScriptRecordingEnabled(const(false))
			],
			f,
			m2t
		)
	)
}

viewIScriptVisualScreenshotDialog(manager : MaterialManager, screenshot : string, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhenB = make(false);
	availViewSizeB = make(WidthHeight(0.0, 0.0));

	MAttachBoxCopy(
		MSelect(availViewSizeB,
			\vs -> MGroup2(
				MZoom(TPicture(screenshot, []), TFixed(vs.width, vs.height), true),
				MFillXY()
			)
		),
		availViewSizeB
	)
	|> (\f ->
		renderMDialog(
			manager,
			closeWhenB,
			[
				MDialogTitle("Visual screenshot"),
				MDialogUseFrame(),
				MDialogScroll(),
				MDialogActions([
					MTextButton("OK", \ -> {
						next(closeWhenB, true);
					}, [], [MShortcut("enter")])
				]),
				IScriptRecordingEnabled(const(false))
			],
			f,
			m2t
		)
	)
}

showRecordingModeDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhenB = make(false);
	material = MText("You already have recorded script. Do you want to continue recording or start a new record?", []);
	captureOptions = manager.iscript.getCaptureOptions();

	dialogStyle = [
		MDialogTitle("Choose record mode"),
		MDialogUseFrame(),
		MDialogScroll(),
		MDialogActions([
			MTextButton("CANCEL", \ -> {
					next(closeWhenB, true);
					next(manager.iscript.iScriptStateB, IScriptDisabled());
				},
				[], [MShortcut("esc")]
			),
			MTextButton("CONTINUE", \ -> {
					next(closeWhenB, true);
					next(manager.iscript.iScriptStateB, IScriptRecording(captureOptions, true));
				},
				[], []
			),
			MTextButton("START NEW", \ -> {
					next(closeWhenB, true);
					next(manager.iscript.iScriptStateB, IScriptRecording(captureOptions, false));
				},
				[], [MShortcut("enter")]
			)
		]),
		IScriptRecordingEnabled(const(false))
	];

	content = MGetFocusGroup(\p -> {
		MLines(map([
				material
			],
			\it -> MEGItem2T(p, it, [MWidth(400.)], m2t)
		))
	});

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		content,
		m2t
	);
}

getFunctionIdsFromCallstack(callstack : string) -> [string] {
	strSplit(callstack, "#")
	|> (\f -> subrange(f, 2, length(f) - 2))
	|> (\f ->
		fold(f, Pair([], []), \acc, st ->
			strSplit(st, " ")
			|> (\s -> filter(s, \v -> v != ""))
			|> (\s ->
				if (length(s) > 1 && s[1] != "" && startsWith(s[1], "0x")) {
					if (length(s) > 3) {
						if (strContains(toLowerCase(s[3]), "fuse") || strContains(toLowerCase(s[3]), "subscribe"))
							Pair(concat(firstOfPair(acc), arrayPush(secondOfPair(acc), s[1])), [])
						else
							Pair(firstOfPair(acc), arrayPush(secondOfPair(acc), s[1]))
					} else {
						Pair(firstOfPair(acc), arrayPush(secondOfPair(acc), s[1]))
					}
				} else {
					acc
				}
			)
		)
	)
	|> firstOfPair
}

maxChain(a : [?], ai : int, bi : int, tree : Tree<?, [int]>, counter : int) -> int {
	if (ai < length(a))
		eitherMap(
			lookupTree(tree, a[ai]),
			\v ->
				either(
					maxA(map(v, \v0 ->
						if (bi < 0)
							maxChain(a, ai + 1, v0, tree, counter + 1)
						else if (v0 == bi + 1)
							maxChain(a, ai + 1, v0, tree, counter + 1)
						else
							counter
					)),
					counter
				),
			counter
		)
	else
		counter
}

arraysIntersect(a : [?], b : [?]) -> int {
	atree = foldi(a, makeTree(), \i, acc, v -> {
		treePushToArrayValue(acc, v, i);
	});

	either(
		maxA(mapi(b, \i, v -> maxChain(b, i, -1, atree, -1))),
		-1
	);
}

takeSubscribersSnapshot(manager : MaterialManager, categoryTag : ?, m2t : (Material, MFocusGroup) -> Tropic, subsInfo : DynamicBehaviour<bool>) -> void {
	closeWhenB = make(false);
	closeWhen2B = make(false);
	showAddedAndRemovedFunctionsIds = make(false);
	material = MText("You already have recorded script. Do you want to continue recording or start a new record?", []);

	subsSnapshot = ref getDebuggedSubscribers(categoryTag);
	removedSubsSnapshot = ref getRemovedDebuggedSubscribers(categoryTag);

	functionsIds =
		map(^subsSnapshot, \snapshot -> Pair(snapshot, getFunctionIdsFromCallstack(callstack2string(snapshot.callstack))));
	removedFunctionsIds = ref
		map(^removedSubsSnapshot, \snapshot -> Pair(snapshot, getFunctionIdsFromCallstack(callstack2string(snapshot.callstack))));

	addedAndRemovedFunctionsIds =
		foldi(functionsIds, [], \i, acc, id -> {
			if (length(^removedFunctionsIds) > 0) {
				j =
					foldi(^removedFunctionsIds, Pair(0, 0), \j, acc2 : Pair<int, int>, rid -> {
						intersects = arraysIntersect(secondOfPair(id), secondOfPair(rid));

						if (intersects > firstOfPair(acc2)) {
							Pair(intersects, j)
						} else {
							acc2;
						}
					}).second;

				fn = firstOfPair(functionsIds[i]);
				rfn = firstOfPair(^removedFunctionsIds[j]);

				subsSnapshot := removeIndex(^subsSnapshot, i);
				removedSubsSnapshot := removeFirst(^removedSubsSnapshot, rfn);
				removedFunctionsIds := removeIndex(^removedFunctionsIds, j);

				arrayPush(acc, Pair(fn, rfn));
			} else {
				acc;
			}
		});

	dialogStyle = [
		MDialogTitle("Subscribers Snapshot"),
		MDialogUseFrame(),
		// MDialogResizable(),
		MDialogActions([
			MTextButton("CLOSE", \ -> {
					next(closeWhenB, true);
				},
				[], [MShortcut("esc")]
			)
		]),
		IScriptRecordingEnabled(const(false))
	];

	dialogStyle2 = \title -> [
		MDialogTitle(title),
		MDialogUseFrame(),
		MDialogScroll(),
		MDialogActions([
			MTextButton("CLOSE", \ -> {
					next(closeWhen2B, true);
				},
				[], [MShortcut("esc")]
			)
		]),
		IScriptRecordingEnabled(const(false))
	];

	content =
		MDynamicDataTable(
			[
				MColumn("Disposed", "Disposed subscriber", 196, []),
				MColumn("Subscribed", "Subscriber that was created", 196, []),
				MColumn("Related", "Related material components", 196, []),
				MColumn("Callstack", "Show subscriber callstack", 48, [])
			],
			fconcat(
				fconcat(
					const(map(
						^subsSnapshot,
						\sbs ->
							[
								TEmpty(),
								MText(behaviourValueToString(sbs.behValue), []),
								TEmpty(),
								MIconButton(
									"format_list_numbered",
									\ ->
										renderMDialog(
											manager,
											closeWhen2B,
											dialogStyle2(behaviourValueToString(sbs.behValue)),
											MGetFocusGroup(\p2 -> {
												MParagraph(
													callstack2string(sbs.callstack)
													|> (\f ->
														if (f == "")
															"Callstack only with flowcpp in debug or profiling mode"
														else
															f
													),
													[]
												)
												|> (\f -> MEGItem2T(p2, f, [MWidth(400.)], m2t))
											}),
											m2t
										),
									[],
									[]
								)
							]
					)),
					const(map(
						^removedSubsSnapshot,
						\sbs ->
							[
								MText(behaviourValueToString(sbs.behValue), []),
								TEmpty(),
								TEmpty(),
								MIconButton(
									"format_list_numbered",
									\ ->
										renderMDialog(
											manager,
											closeWhen2B,
											dialogStyle2(behaviourValueToString(sbs.behValue)),
											MGetFocusGroup(\p2 -> {
												MParagraph(
													callstack2string(sbs.callstack)
													|> (\f ->
														if (f == "")
															"Callstack only with flowcpp in debug or profiling mode"
														else
															f
													),
													[]
												)
												|> (\f -> MEGItem2T(p2, f, [MWidth(400.)], m2t))
											}),
											m2t
										),
									[],
									[]
								)
							]
					))
				),
				fif(
					showAddedAndRemovedFunctionsIds,
					const(map(
						addedAndRemovedFunctionsIds,
						\sbs ->
							[
								MText(behaviourValueToString(secondOfPair(sbs).behValue), []),
								MText(behaviourValueToString(firstOfPair(sbs).behValue), []),
								TEmpty(),
								MIconButton(
									"format_list_numbered",
									\ ->
										renderMDialog(
											manager,
											closeWhen2B,
											dialogStyle2(behaviourValueToString(firstOfPair(sbs).behValue)),
											MGetFocusGroup(\p2 -> {
												MParagraph(
													callstack2string(firstOfPair(sbs).callstack)
													|> (\f ->
														if (f == "")
															"Callstack only with flowcpp in debug or profiling mode"
														else
															f
													),
													[]
												)
												|> (\f -> MEGItem2T(p2, f, [MWidth(400.)], m2t))
											}),
											m2t
										),
									[],
									[]
								)
							]
					)),
					const([])
				)
			),
			[TScrollEnabled(const(true)), MFullWidth(), MCondensed(true)]
		)
		|> (\f -> MGroup2(TFillXY(), f))
		|> (\f -> MLines2(MSwitchControl(showAddedAndRemovedFunctionsIds, [MOnOffText("Show added and removed", "Show added and removed")]), f));

	renderMDialog(
		manager,
		closeWhenB,
		dialogStyle,
		MConstruct(
			[
				\ -> {
					tempSubsInfo = getValue(subsInfo);
					nextDistinct(subsInfo, false);
					\ -> {
						nextDistinct(subsInfo, tempSubsInfo);
					}
				}
			],
			content
		),
		m2t
	);
}

showLogicalScreenshotCompareDialog(
	manager : MaterialManager,
	batchTestsFolder : string,
	batchTestResults : ref [IScriptReplayResult],
	m2t : (Material, MFocusGroup) -> Tropic) -> void {

	if (length(^batchTestResults) > 0) {
		closeWhenB = make(false);

		resultDataTable = make([]);

		updateResultDataTable = \ -> {
			res = mapi(
				^batchTestResults,
				\i : int, v : IScriptReplayResult -> {
					iconSuccess = MIcon("done", [MGreen(500)]);
					iconCurrent = MIcon("done_all", [MGreen(500)]);
					iconError = MIcon("clear", [MRed(500)]);
					[
						MText(i2s(i), []),
						MText(v.filename, []),
						if (v.isReplaySuccessful) iconSuccess else iconError,
						if (isSome(v.iscriptVisualScreenshotM))
							MZoom(TPicture(either(v.iscriptVisualScreenshotM, IScriptVisualScreenshot("")).screenshot, []), TFixed(120.0, 90.0), true)
						else
							MCenterIn(iconError, TFixed(120.0, 90.0)),
						MZoom(TPicture(v.afterReplayVisualScreenshot.screenshot, []), TFixed(120.0, 90.0), true),
						if (isSome(v.iscriptLogicalScreenshotM)) {
							if (either(v.iscriptLogicalScreenshotM, IScriptLogicalScreenshot("", WidthHeight(0., 0.))).screenshot == v.afterReplayLogicalScreenshot.screenshot)
								iconSuccess
							else
								iconError
						} else iconError
					]
				}
			);
			next(resultDataTable, res)
		}

		updateResultDataTable();

		compareFn = \rowId -> {
			closeWhen2B = make(false);
			transparentB = make(0.5);
			availViewSizeB = make(WidthHeight(0.0, 0.0));

			prevRR = (^batchTestResults)[rowId];

			iscriptVisualScreenshot = make(either(prevRR.iscriptVisualScreenshotM, IScriptVisualScreenshot("")).screenshot);
			replayVisualScreenshot = prevRR.afterReplayVisualScreenshot.screenshot;

			dialogStyle2 = [
				MDialogTitle("Compare screenshots"),
				MDialogUseFrame(),
				MDialogActions([
					MTextButton("SET REPLAY SCREENSHOT AS CORRECT IN ISCRIPT", \ -> {
						next(iscriptVisualScreenshot, prevRR.afterReplayVisualScreenshot.screenshot);
						batchTestResults := replace(^batchTestResults, rowId,
							IScriptReplayResult(
								prevRR.filename,
								prevRR.isReplaySuccessful,
								Some(prevRR.afterReplayLogicalScreenshot),
								Some(prevRR.afterReplayVisualScreenshot),
								prevRR.afterReplayLogicalScreenshot,
								prevRR.afterReplayVisualScreenshot
							)
						);
						updateIScriptFile(manager, batchTestsFolder + prevRR.filename,
							\storedIScript : StoredIScript -> {
								storedIScript.script.stack
								|> (\stack -> switch (findIScriptLastRecordKeyValue(stack, IScriptLogicalScreenshot("", WidthHeight(0., 0.)))) {
									Some(v) : replaceRecordInIScriptStack(stack, v.first, v.second, prevRR.afterReplayLogicalScreenshot);
									None() : addIScriptStackRecord(stack, prevRR.afterReplayLogicalScreenshot);
								})
								|> (\stack -> switch (findIScriptLastRecordKeyValue(stack, IScriptVisualScreenshot(""))) {
									Some(v) : replaceRecordInIScriptStack(stack, v.first, v.second, prevRR.afterReplayVisualScreenshot);
									None() : addIScriptStackRecord(stack, prevRR.afterReplayVisualScreenshot);
								})
								|> (\stack ->
								StoredIScript(
									storedIScriptCurrentVersion,
									storedIScript.additionalInfo,
									IScript(
										storedIScript.script.recordingStarted,
										stack
									)
								))
							}
						);
						updateResultDataTable();
					}, [], []),
					MTextButton("CLOSE", \ -> next(closeWhen2B, true), [], [MShortcut("esc")])
				]),
				IScriptRecordingEnabled(const(false))
			];

			content2 =
				MLines([
					MSlider(transparentB, [MSliderRange(0.0, 1.0), MSliderStep(0.1), MSliderDisplayValue(true), MSliderIconLeft("brightness_1"), MSliderIconRight("brightness_5")]),
					MAttachBoxCopy(
						MGroup([
							MSelect(fpair(availViewSizeB, iscriptVisualScreenshot),
								\vs -> MGroup2(
									if (vs.second != "")
										MZoom(TPicture(vs.second, []), TFixed(vs.first.width, vs.first.height), true)
									else
										TFixed(vs.first.width, vs.first.height),
									MFillXY()
								)
							),
							MSelect(availViewSizeB,
								\vs -> MGroup2(
									MZoom(MAlpha(transparentB, TPicture(replayVisualScreenshot, [])), TFixed(vs.width, vs.height), true),
									MFillXY()
								)
							),
						]),
						availViewSizeB
					)
				]);

			renderMDialog(
				manager,
				closeWhen2B,
				dialogStyle2,
				content2,
				m2t)
		};

		dialogStyle = [
			MDialogTitle("Resulting screenshots"),
			MDialogUseFrame(),
			MDialogScroll(),
			MDialogActions([MTextButton("CLOSE", \ -> next(closeWhenB, true), [], [MShortcut("esc")])]),
			IScriptRecordingEnabled(const(false))
		];

		content =
			MLines([
				MFixedY(16.0),
				MDynamicDataTable(
					[
						MColumn("#", "", 48, [CenterAlign()]),
						MColumn("Filename", "IScript filename", 196, []),
						MColumn("Replay result", "", 80, [CenterAlign()]),
						MColumn("IScript screenshot", "", 144, [CenterAlign()]),
						MColumn("Replay screenshot", "", 144, [CenterAlign()]),
						MColumn("Screenshots are equal", "", 120, [CenterAlign()])
					],
					resultDataTable,
					[TScrollEnabled(const(true)), MFullWidth(), MRowHeight(90.0), MOnListClick(const(true), compareFn)]
				)
				|> (\f -> MGroup2(TFillXY(), f))
			]);

		renderMDialog(
			manager,
			closeWhenB,
			dialogStyle,
			content,
			m2t
		);
	}
}

loadDBIScriptDialog(manager : MaterialManager, appendToCurrent : bool, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	if (isIScriptFilesystemDBInitialized(manager)) {
		storeCurrentAndSetIScriptDBPartition(manager);

		recordTimeB = make(0.);
		recordTimeInputB = make(d2s(getValue(recordTimeB)));

		closeDelayDialog = make(false);
		saveDelayEnable = fneq(recordTimeInputB, "");

		onOpen = \fileObjects -> {
			if (fileObjects != []) {
				fileName = fileObjects[0].fullPath;
				next(manager.iscript.currentFileNameB, fileName);
				if (appendToCurrent)
					iScriptFromDB2(
						manager,
						fileName,
						\storedIScript -> addIScriptRecordWithDelay(manager, getValue(recordTimeB), storedIScript.script),
						\err_msg -> iScriptInfo(manager, err_msg, None())
					)
				else
					iScriptFromDB(manager, fileName);
			} else {
				// Should never happens
				iScriptInfo(manager, "Warning: no files selected", None());
			}
			restoreCurrentDBPartition(manager);
		};

		onCancel = \ -> restoreCurrentDBPartition(manager);

		delayDialog = \__, __ -> {
			renderMDialog(manager, closeDelayDialog,
				[
					MDialogTitle("Enter delay value"),
					MDialogUseFrame(),
					MDialogActions([
						MTextButton(_("CANCEL"), \-> next(closeDelayDialog, true), [], [MShortcut("esc")]),
						MTextButton(_("SET"), \-> {
							next(recordTimeB, s2d(getValue(recordTimeInputB)));
							next(closeDelayDialog, true);
						}, [], [MShortcut("enter"), MEnabled(saveDelayEnable)])
					]),
				],
				MConstruct(
					[],
					MCols([
						MFixSize(MText("Delay (ms):", []), TFixed(75.0, 20.0)),
						TFixed(8.0, 0.0),
						MTextInput(
							recordTimeInputB,
							[TextInputType(NumericType()), MWidth(400.0), MMaxLines(1)],
							[MEnabled(const(true))]
						)
					])
				),
				m2t
			)
		}

		renderCustomMFileBrowser(manager, manager.focus, "Open file", [".iscript"],
			concat(
				if (appendToCurrent) [CustomExtFBB("Delay (ms)", delayDialog)] else [],
				[
					OpenExtFBB(onOpen),
					CancelExtFBB(onCancel)
				]
			),
			[
				FbModalWindow(true),
			], m2t);
	}
}

saveDBIScriptDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	if (isIScriptFilesystemDBInitialized(manager)) {
		storeCurrentAndSetIScriptDBPartition(manager);

		fileNameB = make(getValue(manager.iscript.currentFileNameB));

		onSave = \fileObjects -> {
			if (fileObjects != []) {
				filename = fileObjects[0].fullPath;
				next(fileNameB, filename);
				iScriptInfo(manager, filename, None());
				saveDBIScript(manager, getValue(fileNameB), m2t);
			} else {
				// Should never happen
				iScriptInfo(manager, "Warning: no files selected", None());
			}
			restoreCurrentDBPartition(manager);
		};

		onCancel = \ -> restoreCurrentDBPartition(manager);

		renderCustomMFileBrowser(manager, manager.focus, "Save file as...", [".iscript"],
			[
				SaveExtFBB(onSave),
				CancelExtFBB(onCancel)
			],
			[
				FbModalWindow(true),
			], m2t
		);
	}
}

loadDBBatchTestsDialog(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	// // DO WE NEED TO REQUEST OF SAVING OF THE CURRENT SCRIPT?
	if (isIScriptFilesystemDBInitialized(manager)) {
		storeCurrentAndSetIScriptDBPartition(manager);

		onOpen = \fileObjects -> {
			if (fileObjects != []) {
				folderName = fileObjects[0].fullPath;
				if (folderName != "") {
					folderName0 = if (!(endsWith(folderName, "\\") || endsWith(folderName, "/"))) folderName +"\\" else folderName;
					updateBatchTestFilenames(manager, folderName0);
					next(manager.iscript.currentBatchTestsDirectoryB, folderName0);
				}
			} else {
				// Should never happens
				iScriptInfo(manager, "Warning: no folders selected", None());
			}
			restoreCurrentDBPartition(manager);
		};

		onCancel = \ -> restoreCurrentDBPartition(manager);

		renderCustomMFileBrowser(manager, manager.focus, "Open Batch Tests Folder", [],
			[
				OpenExtFBB(onOpen),
				CancelExtFBB(onCancel)
			],
			[
				FbFolderSelection(),
				FbModalWindow(true),
			],
			m2t
		);
	}
}
