import material/iscript/material_iscript_filesystem_local;
import material/iscript/material_iscript_filesystem_db;

export {
	updateBatchTestFilenames(manager : MaterialManager, path : string) -> void;
	playIScriptFromFile(manager : MaterialManager, fileName : string, onFinish : ([IScriptReplayResult]) -> void) -> void;
	playIScriptIfNeeded(manager : MaterialManager, onFinish : ([IScriptReplayResult]) -> void) -> void;

	IScriptReplayStyles ::= ISRSourceLocal, ISRSourceDB, ISRDelay;
		ISRSourceLocal();
		ISRSourceDB();
		ISRDelay(defaultDelay : int);

	playIScriptIfNeededWithStyles(manager : MaterialManager, styles : [IScriptReplayStyles], onFinish : ([IScriptReplayResult]) -> void) -> void;
	playBatchTestsFromDirectory(manager : MaterialManager, directory : string, onFinish : ([IScriptReplayResult]) -> void) -> void;

	updateIScriptFile(manager : MaterialManager, currentScriptFilename : string,
		updateFn : (storedIScript : StoredIScript) -> StoredIScript) -> void;

	loadIScriptFile(manager : MaterialManager, name : string, onSuccess : () -> void, onError : () -> void) -> () -> void;
}

updateBatchTestFilenames(manager : MaterialManager, path : string) -> void {
	getFilesystemExtAPI().readDirectory(
		path,
		\__, files -> {
			testFiles = sort(filter(files, \v1 -> endsWith(v1, ".iscript")));
			next(manager.iscript.batchTestFileNamesB, testFiles);
		},
		\error -> iScriptInfo(manager, "Too few elements. " + error, None())
	)
}

playIScriptFromFile(manager : MaterialManager, fileName : string, onFinish : ([IScriptReplayResult]) -> void) -> void {
	iScriptFromFile(manager, fileName);
	setIScriptState(manager, IScriptReplaying(onFinish));
}

playIScriptIfNeeded(manager : MaterialManager, onFinish : ([IScriptReplayResult]) -> void) -> void {
	playIScriptIfNeededWithStyles(manager, [ISRSourceLocal(), ISRSourceDB()], onFinish)
}

playIScriptIfNeededWithStyles(manager : MaterialManager, styles : [IScriptReplayStyles], onFinish : ([IScriptReplayResult]) -> void) -> void {
	scriptFileName = getUrlParameter(UP_ISCRIPTPLAY);
	batchTestsDirectory = getUrlParameter(UP_ISCRIPTPLAYBATCH);

	isrReplayLocalSource = containsStruct(styles, ISRSourceLocal());
	isrReplayDBSource = containsStruct(styles, ISRSourceDB());

	replayDelay =
		if (isDigits(getUrlParameter(UP_ISCRIPTSTARTDELAY)))
			s2i(getUrlParameter(UP_ISCRIPTSTARTDELAY))
		else
			eitherMap(tryExtractStruct(styles, ISRDelay(0)), \v -> v.defaultDelay, 0);

	if (isrReplayLocalSource) {
		if (!js && scriptFileName != "" && !isIScriptDBPath(scriptFileName)) {
			storeFilesystemExtAPI = getFilesystemExtAPI();
			setLocalFilesystemExtAPI();
			if (fileExists(scriptFileName)) {
				iScriptInfo(manager, "playiscript: " + scriptFileName, None());
				timer(replayDelay, \ -> playIScriptFromFile(manager, scriptFileName, onFinish));
			} else {
				iScriptInfo(manager, "IScript file not found: " + scriptFileName, None());
			}
			setFilesystemExtAPI(storeFilesystemExtAPI);
		} else if (!js && batchTestsDirectory != "" && !isIScriptDBPath(batchTestsDirectory)) {
			storeFilesystemExtAPI = getFilesystemExtAPI();
			setLocalFilesystemExtAPI();
			if (isDirectory(batchTestsDirectory)) {
				folderName = if (!endsWith(batchTestsDirectory, "\\")) batchTestsDirectory + "\\" else batchTestsDirectory;
				iScriptInfo(manager, "playbatchiscript: " + folderName, None());
				timer(replayDelay, \ -> playBatchTestsFromDirectory(manager, folderName, onFinish));
			} else {
				iScriptInfo(manager, "Batch directory not found: " + batchTestsDirectory, None());
			}
			setFilesystemExtAPI(storeFilesystemExtAPI);
		}
	} else if (isrReplayDBSource && isIScriptFilesystemDBInitialized(manager)) {
		if (isIScriptDBPath(scriptFileName)) {
			storeCurrentAndSetIScriptDBPartition(manager);
			iScriptFromDB2(manager, scriptFileName,
				\v : StoredIScript -> {
					restoreCurrentDBPartition(manager);
					iScriptInfo(manager, "playiscript: " + scriptFileName, None());
					timer(replayDelay,
						\ -> {
							next(manager.iscript.script, v.script);
							setIScriptState(manager, IScriptReplaying(onFinish));
						}
					);
				},
				\err_msg -> {
					restoreCurrentDBPartition(manager);
					iScriptInfo(manager, err_msg, None());
				}
			)
		} else if (isIScriptDBPath(batchTestsDirectory)) {
			storeCurrentAndSetIScriptDBPartition(manager);
			getFilesystemExtAPI().isDirectory(batchTestsDirectory,
				\b -> if (b) {
						folderName = if (!endsWith(batchTestsDirectory, "\\")) batchTestsDirectory + "\\" else batchTestsDirectory;
						iScriptInfo(manager, "playbatchiscript: " + folderName, None());
						next(manager.iscript.currentBatchTestsDirectoryB, folderName);
						updateBatchTestFilenames(manager, folderName);
						restoreCurrentDBPartition(manager);
						timer(replayDelay, \ -> setIScriptState(manager, IScriptBatchReplaying(onFinish)));
					} else {
						restoreCurrentDBPartition(manager);
						iScriptInfo(manager, "Batch directory not found: " + batchTestsDirectory, None());
					}
			)
		}
	}
}

playBatchTestsFromDirectory(manager : MaterialManager, folderName : string, onFinish : ([IScriptReplayResult]) -> void) -> void {
	if (folderName != "") {
		next(manager.iscript.currentBatchTestsDirectoryB, folderName);
		updateBatchTestFilenames(manager, folderName);
		setIScriptState(manager, IScriptBatchReplaying(onFinish));
	}
}

updateIScriptFile(
	manager : MaterialManager,
	currentScriptFilename : string,
	updateFn : (storedIScript : StoredIScript) -> StoredIScript) -> void {

	if (startsWith(currentScriptFilename, "DB:/")) {
		storeCurrentAndSetIScriptDBPartition(manager);

		iScriptFromDB2(manager, currentScriptFilename,
			\currentScript : StoredIScript -> {
				resultScript = updateFn(currentScript);
				iScriptToDB2(
					manager,
					currentScriptFilename,
					resultScript,
					\ -> restoreCurrentDBPartition(manager),
					\err_msg -> {
						restoreCurrentDBPartition(manager);
						iScriptInfo(manager, err_msg, None());
					}
				)
			},
			\err_msg -> {
				restoreCurrentDBPartition(manager);
				iScriptInfo(manager, err_msg, None());
			}
		)
	}
	else {
		currentScript = storedIScriptFromFile(currentScriptFilename);
		resultScript = updateFn(currentScript);
		res = iScriptToFile2(manager, resultScript, currentScriptFilename);
		if (!res) {
			iScriptInfo(manager, "Update of local iScript file failed.", None());
		}
	}
}

loadIScriptFile(manager : MaterialManager, name : string, onSuccess : () -> void, onError : () -> void) -> () -> void {
	if (isIScriptDBPath(name)) {
		storeCurrentAndSetIScriptDBPartition(manager);
		disposed = ref false;

		iScriptFromDB2(
			manager,
			name,
			\v : StoredIScript -> if (!^disposed) {
				restoreCurrentDBPartition(manager);
				next(manager.iscript.script, v.script);

				onSuccess();
			},
			\err_msg -> if (!^disposed) {
				restoreCurrentDBPartition(manager);
				iScriptError(manager, err_msg, None());

				onError();
			}
		);

		\ -> disposed := true;
	} else {
		interruptibleDeferUntilNextFrameRendered(\ -> {
			iScriptFromFile(manager, name);
			onSuccess();
		});
	}
}