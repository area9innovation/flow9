import material/iscript/material_iscript_dialog;
import material/iscript/material_iscript_test_combinations;
import material/iscript/material_iscript_filter;
import material/iscript/material_iscript_cursors;
import material/iscript/material_focus_tree;
import material/internal/material_splitter;
import material/internal/material_textinput;
import material/internal/material_tabs;
import material/internal/material_datatable;
import material/internal/material_dialog_internal;
import fusion_utils;

export {
	MIScriptPanels2T(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;

	MIScriptStack(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Material;
	MInputOutputValues(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Material;
	MAliases(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Material;
	MIScriptErrors(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Material;

	playIScriptIfNeeded(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void;
	playIScriptIfNeededWithStyles(manager : MaterialManager, styles : [IScriptReplayStyles], m2t : (Material, MFocusGroup) -> Tropic) -> void;
}

MIScriptPanels2T(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	subsInfo = make(false);
	nextsInfo = make(false);
	zorderInfo = make(true);
	focusInfo = make(true);

	MSplitter(
		TFillXY(),
		(\p ->
			MDynamicTabs(
				[
					MTab(MIScriptStack(manager, m2t), const(_("Script")), []),
					MTab(MInputOutputValues(manager, m2t), const(_("Input/Output")), []),
					MTab(MAliases(manager, m2t), const(_("Aliases")), []),
					MTab(MFocusTree(manager, m2t), const(_("UI Tree")), []),
					MTab(MIScriptErrors(manager, m2t), const(_("Errors")), [])
				]
				|> const,
				make(0),
				[]
			)
			|> (\f2 -> MFrame(0., 0., [MThemeFill(p, MBackgroundColor())], f2))
		)
		|> MGetFocusGroup
		|> disableMInteractivesBelow, // Prevent capturing mouse events by IScript on Script Panel
		TFillXY(),
		[
			MEnabled(fmin(manager.iscript.showSidebar, fnot(isIScriptReplayingOrRecordingB(manager)))),
			MSplitterWidthHeight(manager.iscript.available, make(zeroWH)),
		]
	)
	|> (\f ->
		MLines2(
			MCreate(\ ->
				MBaselineCols([
					MBaselineCols([
						MSelect(
							isIScriptFilesystemDBPresentB(manager),
							\v ->
								MMenu(
									MIconButton("folder", nop, [], [MTooltipText(const(_("Save or Load Scripts")))]),
									concatA([
										[
											MMenuSingleLine("New script",
												[MOnClick(\ -> {
													clearCurrentIScript(manager);
													setIScriptState(manager, IScriptStopped());
												}), MTooltipText(const(_("Clear Current Script")))]),
											MMenuSingleLine("New scenario",
												[MOnClick(\ -> {
													// clearCurrentIScript(manager);
													// setIScriptState(manager, IScriptStopped());
												}), MTooltipText(const(_("Clear Current Scenario")))]),
											MSeparatorLine(),
										],
										if (cpp)
											[
												MMenuSingleLine("Open script",
													[MOnClick(\ -> loadLocalIScriptDialog(manager, false, m2t)), MTooltipText(const(_("Replace current script with script from file")))]),
												MMenuSingleLine("Save script",
													[MOnClick(\ -> saveLocalIScript(manager, getValue(manager.iscript.currentFileNameB), m2t, false)),
														MEnabled(fselect(manager.iscript.scriptChangedB, FLift(\scriptChanged -> scriptChanged && getValue(manager.iscript.currentFileNameB) != "")))]),
												MMenuSingleLine("Save script as ...",
													[MOnClick(\ -> saveIScriptDialog(manager, m2t, false)), MEnabled(fnot(isIScriptEmptyB(manager)))]),
												MMenuSingleLine("Save script as (UrlEncode)...",
													[MOnClick(\ -> saveIScriptDialog(manager, m2t, true)), MEnabled(fnot(isIScriptEmptyB(manager)))]),
												MSeparatorLine(),
												MMenuSingleLine("Open scenario",
													[MOnClick(\ -> loadLocalIScriptScenarioDialog(manager, m2t)), MTooltipText(const(_("Replace current scenario with scenario from file")))]),
												MMenuSingleLine("Save scenario",
													[MOnClick(\ -> saveLocalIScriptScenario(manager, getValue(manager.iscript.scenario.pathB), m2t)),
														MEnabled(fselect(manager.iscript.scenario.scenarioChangedB, FLift(\scenarioChanged -> scenarioChanged && getValue(manager.iscript.scenario.pathB) != "")))]),
												MMenuSingleLine("Save scenario as ...",
													[MOnClick(\ -> saveLocalIScriptScenarioDialog(manager, m2t)), MEnabled(fnot(isIScriptScenarioEmptyB(manager)))]),
											]
										else
											[],
										if (cpp && v)
											[MSeparatorLine()]
										else
											[],
										if (v)
											[
												MMenuSingleLine("Open script from DB",
													[MOnClick(\ -> loadDBIScriptDialog(manager, false, m2t)), MTooltipText(const(_("Replace current script with script from file")))]),
												MMenuSingleLine("Save script to DB",
													[MOnClick(\ -> saveDBIScript(manager, getValue(manager.iscript.currentFileNameB), m2t)),
														MEnabled(fselect(manager.iscript.scriptChangedB, FLift(\scriptChanged -> scriptChanged && getValue(manager.iscript.currentFileNameB) != "")))]),
												MMenuSingleLine("Save script to DB as ...",
													[MOnClick(\ -> saveDBIScriptDialog(manager, m2t)), MEnabled(fnot(isIScriptEmptyB(manager)))]),
												MSeparatorLine(),
												MMenuSingleLine("Open scenario from DB",
													[MOnClick(\ -> loadDBIScriptScenarioDialog(manager, m2t)), MTooltipText(const(_("Replace current scenario with scenario from file")))]),
												MMenuSingleLine("Save scenario to DB",
													[MOnClick(\ -> saveDBIScriptScenario(manager, getValue(manager.iscript.scenario.pathB), m2t)),
														MEnabled(fselect(manager.iscript.scenario.scenarioChangedB, FLift(\scenarioChanged -> scenarioChanged && getValue(manager.iscript.scenario.pathB) != "")))]),
												MMenuSingleLine("Save scenario to DB as ...",
													[MOnClick(\ -> saveDBIScriptScenarioDialog(manager, m2t)), MEnabled(fnot(isIScriptScenarioEmptyB(manager)))]),
											]
										else
											[],
										if (v && js)
											[MSeparatorLine()]
										else
											[],
										if (js)
											[
												MMenuSingleLine(
													"Open local script",
													[
														MOnClick(\ ->
															openFileDialog(1, ["*.iscript"], \s -> if (length(s) > 0) {
																readFileClient(
																	s[0],
																	"text",
																	\data : string -> {
																		storedIScript : StoredIScript = unpackStoredIScript(data);

																		loadAdditionalIScriptInfo(manager, storedIScript.additionalInfo);

																		next(manager.iscript.currentFileNameB, fileNameClient(s[0]));
																		next(manager.iscript.script, storedIScript.script);
																	},
																	nop1
																);
															} else {})
														),
														MTooltipText(const(_("Replace current script with script from file")))
													]
												),
												MMenuSingleLine(
													"Save local script",
													[
														MOnClick(\ -> {
															setFileContentBinary(
																if (strlen(getValue(manager.iscript.currentFileNameB)) > 0)
																	changeFileExt(getValue(manager.iscript.currentFileNameB), ".iscript")
																else
																	"script.iscript",
																flow(packStoredIScript(prepareCurrentIScriptForSave(manager)))
															)
															|> ignore;
														}),
														MEnabled(fnot(isIScriptEmptyB(manager)))
													]
												),
												MMenuSingleLine("Save local script (UrlEncode)...",
													[
														MOnClick(\ -> {
															iScriptToLocalFileUrlEncodeJS(
																manager,
																if (strlen(getValue(manager.iscript.currentFileNameB)) > 0)
																	changeFileExt(getValue(manager.iscript.currentFileNameB), ".iscriptue")
																else
																	"script.iscriptue"
															)
														}),
														MEnabled(fnot(isIScriptEmptyB(manager)))
													]
												),

											]
										else
											[],
										[
											MSeparatorLine(),
											MMenuSingleLine(
												"Generate iscript",
												[
													MOnClick(\ ->
														openFileDialog(1, ["*.iscriptg"], \s -> if (length(s) > 0) {
															readFileClient(
																s[0],
																"text",
																\data : string -> {
																	generateIScriptFromCommands(manager, data);
																},
																nop1
															);
														} else {})
													),
												]
											)
										]
									]),
									[MOpenOnClick(), MEnabled(isIScriptStoppedB(manager))]
								)
						),
						MFixedX(16.0),
						MShow(
							subsInfo,
							MConstruct(
								[
									\ -> {
										setLeakingSubscribersHandler(100, 50, \subscribersCount, value -> {
											if (subscribersCount != subscribersCountBelowThreshold) {
												dumpValue = value |> getValue |> behaviourValueToString;
												println("ERROR: Too many subscribers (" + toString(subscribersCount) + ") to " + dumpValue);
												materialLogFn(
													printCallstack,
													MaterialWarning(),
													nop
												)
											}
										});

										clearLeakingSubscribersHandler;
									}
								],
								MCols2(
									MText("Subscribers: ", []),
									MSelect(
										{
											pscb = ref 0;
											cts = ref 0.0;
											fselect(getCurrentSubscribersCountBehaviour(), FLift(\scb -> {
												ts = timestamp();
												if (ts - ^cts > 33.0) {
													cts := ts;
													pscb := scb;
												}

												^pscb;
											}));
										},
										\t -> MText(i2s(t), [])
									)
								)
							)
						),
						MShow(
							nextsInfo,
							MConstruct(
								[
									\ -> {
										setCountNexts(true);

										\ -> setCountNexts(false);
									}
								],
								MCols2(
									MText("  Nexts: ", []),
									MSelect(getCurrentNextCountBehaviour(), \t -> MText(i2s(t), [])),
								)
							)
						),
						MShow(
							zorderInfo,
							MCols2(
								MText("  Z-order: ", []),
								MSelect(manager.manager.zorder, \i -> MText(i2s(i), [])),
							)
						),
						MShow(
							focusInfo,
							MCols2(
								MText("  Focus: ", []),
								MSelect(
									getCurrentFocusedItemBehaviour(manager),
									\focM ->
										MEllipsisText(
											eitherMap(
												focM,
												\foc -> {
													pos : Point = applyTransformMatrix(fgetValue(foc.transformMatrix), zeroPoint);
													IScriptId2s(getFullMFocusId(foc)) + " " + toString(pos)
												},
												"-"
											),
											[MShowTooltip(const(true))]
										)
								)
							)
						),
						MMenu(
							MIconButton("more_vert", nop, [MIconSize(16.0)], []),
							[
								MMenuSingleLine(
									"Subscribers",
									[
										MPrimaryAction(MMutable(fif(subsInfo, MIcon("check", []) |> const, TEmpty() |> const))),
										MOnClick(\ -> next(subsInfo, !getValue(subsInfo)))
									]
								),
								MMenuSingleLine(
									"Nexts",
									[
										MPrimaryAction(MMutable(fif(nextsInfo, MIcon("check", []) |> const, TEmpty() |> const))),
										MOnClick(\ -> next(nextsInfo, !getValue(nextsInfo)))
									]
								),
								MMenuSingleLine(
									"Z-order",
									[
										MPrimaryAction(MMutable(fif(zorderInfo, MIcon("check", []) |> const, TEmpty() |> const))),
										MOnClick(\ -> next(zorderInfo, !getValue(zorderInfo)))
									]
								),
								MMenuSingleLine(
									"Focus",
									[
										MPrimaryAction(MMutable(fif(focusInfo, MIcon("check", []) |> const, TEmpty() |> const))),
										MOnClick(\ -> next(focusInfo, !getValue(focusInfo)))
									]
								)
							],
							[MFocusEnabled(const(false))]
						),
						MSelect2(manager.iscript.currentFileNameB, isIScriptEmptyB(manager),
							\name, isEmpty ->
								if (name == "") {
									if (isEmpty)
										MText("", [])
									else
										MText("New script", []);
								} else {
									MText(name, []);
								}
						)
					])
					|> (\f2 -> MGroup2(TFillX(), f2)),
					MIconToggle(
						"stop",
						[MToggleFalseIcon("fiber_manual_record", [MRed(500)])],
						[
							MToggleValueLink(
								isIScriptRecordingB(manager),
								\v -> setIScriptState(
									manager,
									if (v) {
										if (!isIScriptEmpty(manager)) {
											showRecordingModeDialog(manager, m2t);
											IScriptStopped();
										} else {
											IScriptRecording(manager.iscript.getCaptureOptions(), false);
										}
									} else {
										addBufferedMouseDownAndDispose(true);

										// The latest entries in the script should be screenshots.
										// The order of the function call is important.
										addIScriptRecordWithDelayStyle(manager, ISRecLast(), getIScriptScreenshot(manager, true));
										addIScriptRecordWithDelayStyle(manager, ISRecLast(), getIScriptLogicalScreenshot(manager));
										addIScriptRecordWithDelayStyle(manager, ISRecLast(), getIScriptVisualScreenshot(manager, true));
										IScriptStopped();
									}
								)
							),
							MEnabled(fnot(fmax(isIScriptReplayingB(manager), isIScriptScenarioReplayingB(manager)))),
							MTooltipText(fselect(isIScriptRecordingB(manager), FLift(\rec -> if (rec) _("Stop Recording") else _("Start Recording")))),
							MShortcut("alt+r")
						]
					),
					MIconToggle(
						"stop",
						[
							MToggleFalseIcon("play_arrow", [MGreen(500)])
						],
						[
							MToggleValueLink(
								isIScriptReplayingB(manager),
								\v ->
									setIScriptState(
										manager,
										if (v)
											IScriptReplaying(
												onIScriptReplayFinish(
													manager,
													\b ->
														showLogicalScreenshotCompareDialog(
															manager,
															b,
															m2t
														)
												)
											)
										else
											IScriptStopped()
									)
							),
							MEnabled(fmin(fnot(fmax(isIScriptScenarioReplayingB(manager), isIScriptRecordingB(manager))), isIScriptReplayReadyB(manager))),
							MTooltipText(fselect(isIScriptReplayingB(manager), FLift(\v -> if(v) _("Stop Replaying") else _("Replay Recording")))),
							MShortcut("alt+p")
						]
					),
					MIconToggle(
						"stop",
						[
							MToggleFalseIcon("playlist_play", [MGreen(500)])
						],
						[
							MToggleValueLink(
								isIScriptScenarioReplayingB(manager),
								\v ->
									setIScriptState(
										manager,
										if (v)
											IScriptScenarioReplaying(
												onIScriptReplayFinish(
													manager,
													\b ->
														showLogicalScreenshotCompareDialog(
															manager,
															b,
															m2t
														)
												)
											)
										else
											IScriptStopped()
									)
							),
							MEnabled(fmin(fnot(fmax(isIScriptReplayingB(manager), isIScriptRecordingB(manager))), isIScriptScenarioReplayReadyB(manager))),
							MTooltipText(fselect(isIScriptScenarioReplayingB(manager), FLift(\v -> if(v) _("Stop Scenario") else _("Replay Scenario"))))
						]
					),
					MIconButton(
						"comment",
						\ -> scenarioViewDialog(manager, m2t),
						[],
						[
							// MEnabled(fnot(isIScriptEmptyB(manager))),
							MTooltipText(const(_("Open Scenario Edit Dialog")))
						]
					),

					// MIconToggle(
					// 	"grid_off",
					// 	[MToggleFalseIcon("grid_on", [])],
					// 	[
					// 		MToggleValue(manager.iscript.showGrid),
					// 		MTooltipText(const(_("Show Grid")))
					// 	]
					// ),
					MIconButton(
						"note",
						\ -> showCombinationsDialog(manager, m2t),
						[],
						[
							MEnabled(fnot(isIScriptEmptyB(manager))),
							MTooltipText(const(_("Open Test Combinations Dialog")))
						]
					),
					MIf(
						isIScriptAddingB(manager),
						MIconButton("close", \ -> nextDistinct(manager.iscript.state, IScriptStopped()), [], []),
						MMenu(
							MIconButton("add", nop, [], [MTooltipText(const(_("Add Behaviour Record")))]),
							[
								MMenuSingleLine(
									"Capture single interaction",
									[
										MOnClick(\ ->
											nextDistinct(
												manager.iscript.state,
												IScriptAddMFocusRecord(\focus : MFocus ->
													addIScriptEventDialog(
														manager,
														focus,
														\rec -> addIScriptRecordWithDelay(manager, rec.first, rec.second),
														nop,
														m2t
													)
												)
											)
										),
										MEnabled(isIScriptStoppedB(manager))
									]
								),
								MMenuSingleLine(
									"Capture group of interaction elements",
									[
										MOnClick(\ ->
											nextDistinct(
												manager.iscript.state,
												IScriptAddMFocusGroupRecord(\focus ->
													addIScriptEventDialog(
														manager,
														focus,
														\rec -> addIScriptRecordWithDelay(manager, rec.first, rec.second),
														nop,
														m2t
													)
												)
											)
										),
										MEnabled(isIScriptStoppedB(manager))
									]
								),
								MMenuSingleLine(
									"Capture screenshot",
									[
										MOnClick(\ -> addIScriptScreenshot(manager, false))
									]
								),
								MMenuSingleLine(
									"Capture snapshot of logical UI state",
									[
										MOnClick(\ -> addIScriptLogicalScreenshot(manager))
									]
								),
								MMenuSingleLine(
									"Define alias for the element",
									[
										MOnClick(\ ->
											nextDistinct(
												manager.iscript.state,
												IScriptAddMFocusAlias(\foc ->
													defineGlobalAliasOnFocusDialog(
														manager,
														foc,
														\alias -> addGlobalAliasInIScript(manager, foc, alias),
														m2t
													)
												)
											)
										),
										MEnabled(isIScriptStoppedB(manager))
									]
								),
								MMenuSingleLine(
									"Define alias for the group of elements",
									[
										MOnClick(\ ->
											nextDistinct(
												manager.iscript.state,
												IScriptAddMFocusAlias(\foc ->
													defineGlobalAliasOnFocusDialog(
														manager,
														foc,
														\alias -> addGlobalAliasInIScript(manager, foc, alias),
														m2t
													)
												)
											)
										),
										MEnabled(isIScriptStoppedB(manager))
									]
								),
								MMenuSingleLine(
									"Import script",
									[
										MOnClick(\ -> loadLocalIScriptDialog(manager, true, m2t)),
										MTooltipText(const(_("Add script from file to the current script")))
									]
								)
							],
							[MOpenOnClick()]
						)
					),
					MIconButton(
						"settings",
						\ -> showRecordSettingsDialog(manager, m2t), [],
						[
							MTooltipText(const(_("Open Record Settings"))),
							MEnabled(fnot(fmax(isIScriptReplayingB(manager), isIScriptScenarioReplayingB(manager))))
						]
					),
					MIconButton(
						"settings_backup_restore",
						\ -> showReplaySettingsDialog(manager, m2t),
						[],
						[
							MTooltipText(const(_("Open Replay Settings")))
						]
					),
					MIconToggle(
						"arrow_forward",
						[
							MToggleFalseIcon("menu", [])
						],
						[
							MToggleValue(manager.iscript.showSidebar),
							MEnabled(fnot(isIScriptReplayingOrRecordingB(manager))),
							MTooltipText(fselect(manager.iscript.showSidebar, FLift(\v -> if(v) _("Hide Recording Sidebar") else _("Show Recording Sidebar"))))
						]
					),
				])
				|> (\f2 -> MBorder(16., 0., 8., 0., f2))
				|> (\f2 -> MFrame(0., 0., [MThemeFill(manager.focus, MBackgroundColor())], f2))
				|> (\f2 -> MScroll(f2, TFillXH(48.), []))
				|> disableMInteractivesBelow // Prevent capturing mouse events by IScript on Script Panel
			),
			f
		)
	)
	|> (\f ->
		MComponentGroup(
			f,
			[IScriptRecordingEnabled(const(false)), MZorder(manager.manager.zorder), MFocusName("MIScriptPanel")]
		)
	)
	|> (\f ->
		MSelect(manager.iscript.showPanel,
			\sp -> {
				if (sp) {
					if (isIScriptFilesystemDBInitFnSet() && !isIScriptFilesystemDBInitialized(manager)) {
						initIScriptFilesystemDB()
					}
					//fixIScriptUniqueId(manager.focus);
					f;
				} else {
					MAttachBox(TFillXY(), manager.iscript.available);
				}
			}
		)
	)
	|> (\f -> {
		if (js) MConstruct(
			[
				\ -> addMessageEventListener(\message, origin -> {
					maybeApply(
						iScriptFromUrlEncode(manager, message),
						\parameters -> {
							if (!getValue(manager.iscript.showPanel)) {
								nextDistinct(manager.iscript.showPanel, true);
								fixAllIScriptUniqueIds(manager.focus);
							}
							setIScriptState(manager, IScriptReplaying(onIScriptReplayFinish(
								manager,
								\b -> {
									status = fold(b, true, \acc, replay -> acc && replay.isReplaySuccessful);
									errors = map(uniq(list2array(getValue(manager.iscript.errors))), \e -> JsonString(toString(e)));
									result = json2string(JsonObject(
										concatA([
											[
												Pair("callid", JsonString(parameters.first)),
												Pair("status", JsonString(if (status) "OK" else "ERROR"))
											],
											if (status) [] else [Pair("errors", JsonArray(errors))]
										])
									));
									hostCall("window.postMessage", [result, origin]);
									if (parameters.second) {
										showLogicalScreenshotCompareDialog(
											manager,
											b,
											m2t
										)
									}
								}
							)))
						}
					)
				})
			],
			f
		) else f
	})
	|> (\f -> m2t(f, manager.focus))
}

compressScript(script : IScript) -> IScript {
	newStack = filterIScriptStackTree(script.stack, \delay, record -> {
		switch (record) {
			IScriptBehaviourRecord(__, __, type, __) : {
				switch (type) {
					ISMouseEvent() : false;
					ISMiscEvent() : false;
					default : true;
				}
			}
			default : true;
		}
	});

	// TODO: Reimplement
	// filteredInputs = foldIScriptStackTree(newStack, [], \acc : [IScriptRecord], delay, record -> {
	// 	if (length(acc) > 0) {
	// 		last = acc[length(acc) - 1];

	// 		switch (last) {
	// 			IScriptBehaviourRecord(lastDescriptor, __, lastType, __) : {
	// 				switch (record) {
	// 					IScriptBehaviourRecord(descriptor, __, type, __) : {
	// 						if (isSameStructType(type, lastType) && isSameStructType(type, ISInputValue([], "", false)) && IScriptId2s(descriptor.component.id) == IScriptId2s(lastDescriptor.component.id))
	// 							replace(acc, length(acc) - 1, record)
	// 						else
	// 							arrayPush(acc, record);
	// 					}
	// 					default : arrayPush(acc, record);
	// 				}
	// 			}
	// 			default : arrayPush(acc, record);
	// 		}
	// 	} else {
	// 		[record]
	// 	}
	// });

	IScript(script.recordingStarted, newStack);
}

MIScriptStack(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Material {
	searchB = make("");
	filterTypesB = make([FTNoIScript()]);
	filterPopupOpenedB = make(true);
	caseSensitiveB = make(false);
	posScaleB = make(zeroPositionScale);

	inputEnabled = make(false);

	visibleRecordsB = fselect4(
		manager.iscript.script,
		searchB,
		filterTypesB,
		caseSensitiveB,
		\rs : IScript, sb, ft, cs -> {
			iScriptStack = mapIScriptStack(rs.stack, \delay, record -> Pair(delay, record));

			strFiltered = if (strlen(sb) > 0) {
				aliases = getValue(manager.iscript.aliases);
				filter(iScriptStack, \r0: Pair<double, IScriptRecord> -> {
					caption = getIScriptRecordCaption(aliases, r0);
					if (cs)
						strContains(caption, sb)
					else
						strContains(toLowerCase(caption), toLowerCase(sb))
				});
			} else {
				iScriptStack;
			}

			recordFiltered = if (containsStruct(ft, FTIScriptRecords([]))) {
				recordFilter = extractStruct(ft, FTIScriptRecords([]));
				filter(strFiltered, \r0: Pair<double, IScriptRecord> -> exists(recordFilter.records, \v -> isSameStructType(v, r0.second)))
			} else strFiltered;

			recordTypeFiltered = if (containsStruct(ft, FTIScriptRecordTypes([]))) {
				recordTypeFilter = extractStruct(ft, FTIScriptRecordTypes([]));
				filter(recordFiltered, \r0: Pair<double, IScriptRecord> -> {
					var = r0.second;
					switch(var) {
						IScriptCheck(__, __, __) : exists(recordTypeFilter.types, \v -> isSameStructType(v, var.type));
						IScriptBehaviourRecord(__, __, __, __) : exists(recordTypeFilter.types, \v -> isSameStructType(v, var.type));
						default: false;
					}
				})
			} else recordFiltered;

			behaviourFiltered = if (containsStruct(ft, FTIScriptBehaviourDescriptors("", "", ""))) {
				behaviourFilter = extractStruct(ft, FTIScriptBehaviourDescriptors("", "", ""));
				filter(recordTypeFiltered, \r0: Pair<double, IScriptRecord> -> {
					var = r0.second;
					switch(var) {
						IScriptCheck(__, __, __) : checkBehaviour(var.behaviourDescriptor, behaviourFilter, cs);
						IScriptBehaviourRecord(__, __, __, __) : checkBehaviour(var.descriptor, behaviourFilter, cs);
						IScriptInputRecord(__, __, __, __) : checkBehaviour(var.behaviourDescriptor, behaviourFilter, cs);
						IScriptOutputRecord(__, __) : checkBehaviour(var.behaviourDescriptor, behaviourFilter, cs);
						default: false;
					}
				})
			} else recordTypeFiltered;

			behaviourFiltered;
		}
	);

	// fselect2(manager.iscript.aliases, visibleRecordsB, FLift2(\aliases, visibleRecords -> {
		fmap(visibleRecordsB, \r0 : Pair<double, IScriptRecord> -> {
			time = r0.first;
			r = r0.second;

			caption = getIScriptRecordCaption(getValue(manager.iscript.aliases), r0);

			if (caption != "") {
				hover = make(false);
				buttons =
					getIScriptRecordButtons(
						manager,
						m2t,
						r0,
						true,
						\nr -> addIScriptRecordWithDelay(manager, nr.first, nr.second),
						\mr -> replaceRecordInIScript(manager, Pair(time, r), mr),
						\ -> removeRecordFromIScript(manager, Pair(time, r))
					);

				MSingleLine(caption, [MSecondaryAction(MBaselineCols(buttons) |> addIScriptRecordHover(manager, r0, hover)), THovering(hover)])
			} else {
				MSeparatorLine()
			}
		})
	// }))
	|> (\f -> MDynamicList(f, [], []))
	|> (\f ->
		MLines([
			MBorder(16., 0., 16., 16.,
				MBaselineCols([
					MPositionScale(posScaleB,
						MGetFocusGroup(\parent ->
							MIconButton(
								"filter_list",
								\ -> {
									renderFilterPopup(manager, parent, m2t, filterTypesB, searchB, filterPopupOpenedB, posScaleB, caseSensitiveB)
								},
								[],
								[MShortcut("ctrl+f")]
							)
						)
					),
					TFillX(),
					MTextButton(
						"COMPRESS SCRIPT",
						\ -> {
							next(manager.iscript.script, compressScript(getValue(manager.iscript.script)));
							next(manager.iscript.scriptChangedB, true);
						},
						[],
						[MEnabled(fnot(isIScriptEmptyB(manager)))]
					)
				])
			),
			renderSearchChip(manager, searchB, caseSensitiveB),
			renderFilterChips(manager, filterTypesB),
			f
		])
	)
}

MInputOutputValues(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Material {
	MProportionCols(
		[
			MComponentGroupSelect(
				fmap(
					fselect(manager.iscript.script, getMInputValueRecords |> FLift),
					\r -> \p -> {
						typeHelper = r.second.valueTypeHelper;
						inputName = r.second.name;
						inputValues = getValue(manager.iscript.input);
						inputIndex = findiDef(inputValues, \input -> input.name == inputName, -1);
						askB = if (inputIndex == -1) make(false) else make(isNone(inputValues[inputIndex].value));

						pair = makeValueInputByTypeHint(r.second.possibleValues, r.second.valueTypeHelper, None(), fnot(askB));
						valueB = pair.first;
						item = pair.second;
						askSwitch = MEGSwitchControl("Ask for value during the replay", askB, []);

						TConstruct([
								makeSubscribe2(fpair(valueB, askB), \vaPair -> {
									value = vaPair.first;
									ask = vaPair.second;
									iScript = getValue(manager.iscript.script);
									inputValue = IScriptInputValue(
										inputName,
										typeHelper,
										if (ask) None() else Some(value)
									);

									next(manager.iscript.input, replace(inputValues, inputIndex, inputValue));
								})
							],
							TBorder(0.0, 8.0, 0.0, 8.0, TFrame(1.0, 0.0, [Stroke(black)], TPad(8.0, 8.0, TLines([
								MEGItem2T(p, MText("\"" + inputName + "\"", [MSubheading()]), [], m2t),
								TFixed(0.0, 8.0),
								MEGItem2T(p, askSwitch, [], m2t),
								MEGItem2T(p, item, [], m2t)
							]))))
						)
					}
				),
				\rs ->
					map(rs, \r -> MGetFocusGroup(r))
					|> (\f2 ->
						concat(
							[MEllipsisText("Input Values:", [MShowTooltip(const(true))])],
							f2
						)
					)
					|> MLines,
				[MFocusName("MInputValueRecords")]
			)
			|> (\f2 -> MBorder(16., 16., 0., 0., f2))
			|> (\f2 -> MGroup2(f2, TFillX()))
			|> (\f2 -> Pair(const(0.5), f2)),
			MComponentGroupSelect(
				fmap(
					manager.iscript.output,
					\r -> \p -> {
						if (r.visible) {
							material = MLines2(
								MText(r.name, []),
								MText(eitherMap(r.value, toString, "No such behaviour"), [])
							);

							MEGItem2T(p, material, [], m2t)
						} else
							TEmpty();
					}
				),
				\rs ->
					map(rs, \r -> MGetFocusGroup(r))
					|> (\f2 ->
						concat(
							[MEllipsisText("Output Values:", [MShowTooltip(const(true))])],
							f2
						)
					)
					|> MLines,
				[MFocusName("MOutputValueRecords")]
			)
			|> (\f2 -> MBorder(16., 16., 0., 0., f2))
			|> (\f2 -> MGroup2(f2, TFillX()))
			|> (\f2 -> Pair(const(0.5), f2))
		]
	)
}

getMInputValueRecords(iScript : IScript) -> [Pair<double, IScriptInputRecord>] {
	filtermapIScriptStack(iScript.stack, \delay, record -> {
		switch (record : IScriptRecord) {
			IScriptInputRecord(__, __, __, __) : Some(Pair(delay, record));
			default: None();
		}
	});
}

getMOutputValueRecords(iScript : IScript) -> [Pair<double, IScriptOutputRecord>] {
	filtermapIScriptStack(iScript.stack, \delay, record -> {
		switch (record : IScriptRecord) {
			IScriptOutputRecord(__, __) : Some(Pair(delay, record));
			default: None();
		}
	});
}

MAliases(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Material {
	elementNameWidth = 400;
	aliasWidth = 360;
	columns = [
		MColumn("Element name", "", elementNameWidth, []),
		MColumn("Alias", "", aliasWidth, []),
		MColumn("", "", 48, []),
		MColumn("", "", 48, [])
	];

	fselect(manager.iscript.aliases, FLift(\aliases -> {
		foldTree(aliases, [], \descriptor, alias, acc -> {
			elementTitle = descriptor.name + " [" + IScriptId2s(descriptor.id) + "]";

			arrayPush(acc, [
				MParagraph(elementTitle, [MWidth(i2d(elementNameWidth))]),
				MParagraph(alias, [MWidth(i2d(aliasWidth))]),
				MIconButton("edit", \ -> {
						defineGlobalAliasDialog(
							manager,
							descriptor.name,
							descriptor.id,
							\newAlias -> next(manager.iscript.aliases, setTree(aliases, descriptor, newAlias)),
							m2t
						)
					}, [], []
				),
				MIconButton("delete", \ -> next(manager.iscript.aliases, removeFromTree(aliases, descriptor)), [], [])
			])
		})
	}))
	|> (\m -> MDynamicDataTable(columns, m, []))

	// fselect(manager.iscript.aliases, FLift(\aliases -> {
	// 	foldTree(aliases, [], \descriptor, alias, acc -> {
	// 		elementTitle = descriptor.componentName + " [" + IScriptId2s(descriptor.id) + "]";
	// 		lineTitle = elementTitle + " = \"" + alias + "\"";

	// 		arrayPush(acc, MSingleLine(lineTitle, []));
	// 	})
	// }))
	// |> (\f -> MDynamicList(f, [], []))
	// |> (\f -> MDynamicList2T(manager, parent, f, m2t))


	// |> (\f ->
	// 	TLines2(
	// 		TBorder(16., 0., 16., 16.,
	// 			TBaselineCols([
	// 				MTextInput2T(
	// 					manager,
	// 					parent,
	// 					MTextInput(manager.iscript.panelsSearchInputContent, [MLabel("Search"), MFilterAction(\c -> nextDistinct(searchB, c))],
	// 						[
	// 							MEnabled(fselect(manager.iscript.state, FLift(\iss -> {
	// 								en = !isSameStructType(iss, IScriptRecording(makeCaptureOptions(), false));

	// 								c = getValue(manager.iscript.panelsSearchInputContent);

	// 								deferred(\ -> next(manager.iscript.panelsSearchInputContent, c));
	// 								if (!en) nextDistinct(searchB, "");

	// 								en
	// 							})))
	// 						]
	// 					),
	// 					m2t
	// 				),
	// 				TFixed(16.0, 0.0),
	// 				MTextButton2T(
	// 					manager,
	// 					parent,
	// 					MTextButton("COMPRESS SCRIPT", \ -> {
	// 							next(manager.iscript.script, compressScript(getValue(manager.iscript.script)));
	// 							next(manager.iscript.scriptChangedB, true);
	// 						},
	// 						[], [MEnabled(fselect(manager.iscript.script, FLift(\__ -> !isIScriptEmpty(manager))))]
	// 					)
	// 				)
	// 			])
	// 		),
	// 		f
	// 	)
	// )

	// |> (\f ->
	// 	TConstruct(
	// 		[
	// 			makeSubscribe(manager.iscript.state, \iss -> {
	// 				nextDistinct(inputEnabled, !isSameStructType(iss, IScriptRecording(makeCaptureOptions(), false)));
	// 				c = getValue(manager.iscript.panelsSearchInputContent);
	// 				deferred(\ -> next(manager.iscript.panelsSearchInputContent, c));
	// 				if (!getValue(inputEnabled)) nextDistinct(searchB, "");
	// 			})
	// 		],
	// 		f
	// 	)
	// )
}

MIScriptErrors(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> Material {
	MGetFocusGroup(\parent ->
		SelectTLines(
			fselect(manager.iscript.errors, list2array |> FLift),
			\index, id, e -> {
				MText(e.error, [MCaption()])
				|> (\f ->
					eitherMap(
						e.id,
						\identifier -> {
							hover = make(false);

							MConstruct(
								[
									makeSubscribe2focusHovered(manager, \ -> getMaterialFocusByIScriptId(manager.focus, identifier), hover)
								],
								MInteractive(
									[TMouseInside(hover, false)],
									MFrame(0., 0., [Fill(black), FillOpacity(0.)], f)
								)
							)
						},
						f
					)
				)
				|> (\f -> m2t(f, parent))
			}
		)
	)
}

playIScriptIfNeeded(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	playIScriptIfNeededWithStyles(
		manager,
		[ISRSourceLocal(), ISRSourceDB()],
		m2t
	)
}

playIScriptIfNeededWithStyles(manager : MaterialManager, styles : [IScriptReplayStyles], m2t : (Material, MFocusGroup) -> Tropic) -> void {
	playIScriptIfNeededWithStylesOnFinish(
		manager,
		styles,
		onIScriptReplayFinish(
			manager,
			\b ->
				showLogicalScreenshotCompareDialog(
					manager,
					b,
					m2t
				)
		)
	)
}