import material/iscript/material_iscript_form;
import material/iscript/material_iscript_screenshot;
import material/iscript/material_iscript_cursors;
import http_request_callback;

export {
	replayIScript(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic, onFinished : (bool) -> void, handleInputOutput : bool) -> () -> void;

	getBehaviourById(manager : MaterialManager, behaviourDescriptor : IScriptBehaviourDescriptor, focusExists : bool, enabled : bool, focusable : bool, click : bool) -> Maybe<DynamicBehaviour<flow>>;
	repeatBehaviourById(manager : MaterialManager, behaviourDescriptor : IScriptBehaviourDescriptor, value : flow) -> bool;

	subscribeToIScriptBehavioursFocus(manager : MaterialManager, captureHttp : bool) -> () -> void;

	substituteHttpRequest(manager : MaterialManager) -> () -> void;
	makeValueInputByTypeHint(possibleValues : [flow], typeHelper : string, maybeInitial : Maybe<flow>, enabledB : Transform<bool>) -> Pair<DynamicBehaviour<flow>, MEGItem>;
	getDefaultValueByTypeHint(typeHint : string) -> flow;

	loadStoredIScript(manager : MaterialManager, storedIScript : StoredIScript) -> void;
	loadAdditionalIScriptInfo(manager : MaterialManager, additionalInfo : IScriptAdditionalInfo) -> void;

	prepareCurrentIScriptForSave(manager : MaterialManager) -> StoredIScript;
}

stopIScriptReplay(manager : MaterialManager) -> void {
	next(manager.iscript.panelsReplayToggleValue, false);
}

replayIScript(manager : MaterialManager, m2t : (Material, MFocusGroup) -> Tropic, onFinished : (bool) -> void, handleInputOutput : bool) -> () -> void {
	iScriptInfo(manager, "Replay IScript", None());
	replayIScript2(manager, getValue(manager.iscript.iScriptB), m2t, onFinished, handleInputOutput);
}

continiousTimer(tree : Tree<double, [() -> bool]>, onFinished : (bool) -> void) -> () -> void {
	isFinished = ref false;
	delays = ref getTreeKeys(tree);
	maxDelay = either(maxA(^delays), 0.0);
	r = repeatable(1);
	started = timestamp();
	uns = ref [];

	uns :=
		[
			r.stop,
			\ -> if (!(^isFinished)) {
				onFinished(length(^delays) == 0);
				isFinished := true
			},
			makeSubscribe(r.value, \ts -> {
				if (length(^delays) == 0) {
					applyall(^uns)
				}

				foldWhile(^delays, ts - started, \t, d -> {
					if (d < t) {
						maybeApply(lookupTree(tree, d), \records -> iter(records, \fn -> if (!fn()) applyall(^uns)));
						delays := subrange(^delays, 1, length(^delays));

						Some(t)
					} else {
						None()
					}
				})
				|> ignore
			})()
		];

	\ -> applyall(^uns)

}

replayIScript2(manager : MaterialManager, iScript : IScript, m2t : (Material, MFocusGroup) -> Tropic, onFinished : (bool) -> void, handleInputOutput : bool) -> () -> void {
	stopDelay = either(maxA(getTreeKeys(iScript.stack)), 1.0);

	foldTree(iScript.stack, makeTree(), \delay, records, acc ->
		setTree(
			acc,
			delay / getValue(manager.iscript.settings.replaySpeed),
			map(records, \r -> \ -> replayIScriptRecord(manager, r, m2t, handleInputOutput))
		)
	)
	|> (\t -> continiousTimer(t, onFinished))
}

replayIScriptRecord(manager : MaterialManager, record : IScriptRecord, m2t : (Material, MFocusGroup) -> Tropic, handleInputOutput : bool) -> bool {
	switch (record : IScriptRecord) {
		IScriptBehaviourRecord(descriptor, value, type, callstack): {
			repeatBehaviourById(manager, descriptor, value) || descriptor.name == "focused"
		}
		IScriptInputRecord(descriptor, name, possibleValues, __) : {
			if (handleInputOutput) {
				inputValues = getValue(manager.iscript.inputValuesB);
				idx = findiDef(inputValues, \input -> input.name == name, -1);

				eitherMap(
					inputValues[idx].value,
					\v -> {
						repeatBehaviourById(manager, descriptor, v)
					},
					false
					// \ -> {
					// 	showValueInputDialog(
					// 		manager,
					// 		record,
					// 		\value -> repeatBehaviourById(manager, descriptor, value) |> ignore,
					// 		m2t
					// 	);
					// }
				);
			} else {
				true;
			}
		}
		IScriptOutputRecord(descriptor, name) : {
			if (handleInputOutput) {
				value : Maybe<flow> = eitherFn(
					getBehaviourById(manager, descriptor, true, false, false, false),
					\beh -> Some(getValue(beh)),
					\ -> None()
				);

				outputValues = getValue(manager.iscript.outputValuesB);
				idx = findiDef(outputValues, \output -> output.name == name, -1);

				next(manager.iscript.outputValuesB, replace(outputValues, idx, IScriptOutputValue(name, true, value)));
			}

			true;
		}
		IScriptStageWidthHeight(wh): {
			nextDistinct(stageWidthHeight, Some(wh));

			true;
		}
		IScriptRequestRecord(url, post, headers, params, delay, response): {
			requestCreated = timestamp();

			if (getValue(manager.iscript.settings.replayCheckErrors) /*&& containsList(getValue(manager.iscript.requests), Pair(time, record))*/) {
				uns = ref [];

				checkRequestFn = \rq -> if (^uns != []) {
					// if (containsList(rq, Pair(time, record))) {
					// 	dispUnsA(uns);
					// 	// Only as info for now
					// 	iScriptInfo(manager, "Request error " + url + " : isn't executed", None());
					// } else {
						dispUnsA(uns);
					// }
				};

				uns :=
					[
						interruptibleTimer(max(ceil(delay), 1000), \ -> checkRequestFn(getValue(manager.iscript.requests))),
						makeSubscribe2(manager.iscript.requests, checkRequestFn)()
					];
			}

			true;
		}
		IScriptScreenshot(__, check): {
			if (!check) {
				applyIScriptScreenshot(manager, record);

				true;
			} else {
				record2 = getIScriptScreenshot(manager, check);
				d = diffIScriptScreenshot(record, record2);

				if (sizeTree(d) > 0) {
					iScriptError(manager, "IScriptScreenshot error : screenshots aren't equal", None());
					traverseInOrder(d, \k, v ->
						iScriptError(
							manager,
							foldTree(v, "\t" + toString(k) + " :", \k2, v2, acc2 ->
								acc2 + "\n\t\t" + toString(k2) + " : " + eitherMap(v2.first, toString, "") + " - " + eitherMap(v2.second, toString, "")
							),
							None() // todo:
							// Some(k)
						)
					);

					false;
				} else {
					true;
				}
			}
		}
		IScriptCheck(descriptor, checkFn, type): {
			checkBehaviourById(manager, descriptor, checkFn)
		}
		IScriptNested(componentName, id, iScriptNested): {
			// replayIScript2(manager, iScriptNested, m2t, false);
			true;
		}
		IScript(recordingStarted, stack): {
			// replayIScript2(manager, record, m2t, false);
			true;
		}
		IScriptUrlRecord(url) : {
			if (url != loaderUrl()) {
				getUrlN(url, "");

				false;
			} else {
				true;
			}
		}
		default: {
			true;
		}
	}
}

getBehaviourById(manager : MaterialManager, behaviourDescriptor : IScriptBehaviourDescriptor, focusExists : bool, enabled : bool, focusable : bool, click : bool) -> Maybe<DynamicBehaviour<flow>> {
	idString = IScriptId2s(behaviourDescriptor.component.id);
	noSuchFocusString = "No such focus: [" + idString + "] " + behaviourDescriptor.name;

	eitherFn(
		getMaterialFocusByIScriptId(manager.focus, behaviourDescriptor.component.id),
		\fc ->
			if (fc.name == behaviourDescriptor.component.name) {
				if (focusable && fgetValue(fc.focusEnabled) == false) {
					iScriptError(manager, "Component focus is disabled: " + behaviourDescriptor.name + " in focus:  [" + IScriptId2s(behaviourDescriptor.component.id) + "]",
						Some(behaviourDescriptor.component.id));
					None()
				} else if ((click || enabled) && fgetValue(fc.enabled) == false) {
					iScriptError(manager, "Component is disabled: " + behaviourDescriptor.name + " in focus:  [" + IScriptId2s(behaviourDescriptor.component.id) + "]",
						Some(behaviourDescriptor.component.id));
					None()
				} else if (click && fgetValue(switchMaterialFocus(fc, \f -> f.parent.zorderEnabled, \f -> f.zorderEnabled)) == false) {
					iScriptError(manager, "Component clicks are disabled: " + behaviourDescriptor.name + " in focus:  [" + IScriptId2s(behaviourDescriptor.component.id) + "]",
						Some(behaviourDescriptor.component.id));
					None()
				} else {
					eitherFn(
						lookupTree(fc.behaviours, behaviourDescriptor.name),
						\beh ->
							Some(beh.first),
						\ -> {
							if (focusExists) iScriptError(manager, "No such behaviour: " + behaviourDescriptor.name + " in focus:  [" + IScriptId2s(behaviourDescriptor.component.id) + "]",
								Some(behaviourDescriptor.component.id));
							None()
						}
					)
				}
			} else {
				if (focusExists) iScriptError(manager, noSuchFocusString, Some(behaviourDescriptor.component.id));
				None()
			},
		\ -> {
			if (focusExists) iScriptError(manager, noSuchFocusString, Some(behaviourDescriptor.component.id));
			None()
		}
	)
}

repeatBehaviourById(manager : MaterialManager, behaviourDescriptor : IScriptBehaviourDescriptor, value : flow) -> bool {
	name = behaviourDescriptor.name;
	componentName = behaviourDescriptor.component.name;

	iscriptReplayCheckCursorShape(manager, name, value);

	addHeight =
		if (contains(manager.style, MIScriptPanel()))
			getValue(manager.iscript.topPanelWidthHeightB).height
			|> (\hgt -> \pnt -> Point(pnt.x, pnt.y + hgt))
		else
			idfn;
	checkStr =
		if (getValue(manager.iscript.settings.replayCheckErrors))
			">> Check " + componentName + " " + name + " value " + toString(value)
		else
			">> Replay " + componentName + " " + name + " value " + toString(value);

	emitMouseEv = \event, point -> {
		emitMouseEvent(currentClip(), event, point.x, point.y);

		next(manager.iscript.replayCursorPositionB,
			Point(point.x - getValue(manager.iscript.replayCursorClickPointB).x, point.y - getValue(manager.iscript.replayCursorClickPointB).y)
		);

		iScriptInfo(
			manager,
			checkStr + " = " + event + " at (" + toString(point.x) + ", " + toString(point.y) + ")",
			Some(behaviourDescriptor.component.id)
		);
	}

	if (getValue(manager.iscript.settings.replayCheckErrors)) { // replay option "Check errors" is on
		if ((name == "mousedown" || name == "mouseup" || name == "mousemove") && isSameStructType(value, zeroPoint)) {
			emitMouseEv(name, cast(value : flow -> Point) |> addHeight);
			true
		} else if ((name == "keypress" || name == "keydown" || name == "keyup") && isSameStructType(value, KeyEventRecord("", false, false, false, false, 0))) {
			println(name);

			kst = cast(value : flow -> KeyEventRecord);
			iScriptInfo(
				manager,
				checkStr,
				Some(behaviourDescriptor.component.id)
			);
			emitKeyEvent(currentClip(), name, kst.utf, kst.ctrl, kst.shift, kst.alt, kst.meta, kst.keycode);
			true
		} else {
			checkErrors = contains(getValue(manager.iscript.settings.replayReplayBehaviours), name);
			checkEnabled = checkErrors;
			checkFocused = checkErrors && name == "focused";
			checkClickable = checkErrors && ((name == "down" && flow(value) != None()) || (name == "click" && flow(value) == true)
				|| (name == "hover" && flow(value) == true) || name == "position");
			checkExists = checkFocused || checkClickable;

			eitherMap(
				getBehaviourById(manager, behaviourDescriptor, checkExists, checkEnabled, checkFocused, checkClickable),
				\b -> {
					if (checkErrors) {
						// Behaviours we need to replay
						if (name == "point" && false) {
							eitherFn(
								getMaterialFocusByIScriptId(manager.focus, behaviourDescriptor.component.id),
								\f -> {
									down = getValue(either(
										getBehaviourById(
											manager,
											IScriptBehaviourDescriptor(behaviourDescriptor.component, "down"),
											false,
											false,
											false,
											false
										),
										make(None())
									));

									if (flow(down) == true || (flow(down) != false && isSome(down))) {
										p = f.position();
										emitMouseEv("mousemove", Point(p.pos.x + value.x, p.pos.y + value.y));
									};

									true
								},
								\ -> {
									iScriptError(
										manager,
										"Check Errors. No such focus: " + name,
										Some(behaviourDescriptor.component.id)
									);
									false
								}
							)
						} else if (name == "down" && false) {
							mouseEvent = if (flow(value) != None() && flow(value) != false) "mousedown" else "mouseup";

							eitherFn(
								getMaterialFocusByIScriptId(manager.focus, behaviourDescriptor.component.id),
								\f -> {
									pnt =
										eitherFn(
											if (flow(value) == true || flow(value) == false) {
												None()
											} else {
												value
											},
											\pnt -> {
												p = f.position();
												mx = p.pos.x + pnt.x;
												my = p.pos.y + pnt.y;
												Point(mx, my);
											},
											\ -> {
												eitherFn(
													getBehaviourById(
														manager,
														IScriptBehaviourDescriptor(behaviourDescriptor.component, "point"),
														false,
														false,
														false,
														false
													),
													\pnt -> {
														p = f.position();
														mx = p.pos.x + getValue(pnt).x;
														my = p.pos.y + getValue(pnt).y;
														Point(mx, my);
													},
													\ -> {
														wh = f.widthHeight();
														p = f.position();
														mx = p.pos.x + wh.width / 2.0;
														my = p.pos.y + wh.height / 2.0;
														Point(mx, my);
													}
												);
											}
										);

									emitMouseEv(mouseEvent, pnt);
									true
								},
								\ -> {
									iScriptError(
										manager,
										"Check Errors. No such focus: " + name,
										Some(behaviourDescriptor.component.id)
									);
									false
								}
							)
						} else {
							next(b, value);
							true
						}
					} else {
						if (contains(getValue(manager.iscript.settings.replayCheckedBehaviours), name) && name != "position") {
							// Checked behaviours
							if (getValue(b) != flow(value)) {
								iScriptError(manager, "Check Errors. " + componentName + " " + name + " in focus:  [" + IScriptId2s(behaviourDescriptor.component.id) + "] doesn't match: " +
									toString(getValue(b)) + " != " + toString(value), Some(behaviourDescriptor.component.id));
								false
							} else {
								true
							}
						} else // All unchecked behaviours
						{
							true
						}
					}
				},
				!checkExists
			)
		}
	} else { // replay option "Check errors" is off
		if (countList(behaviourDescriptor.component.id) == 0) {
			if ((name == "mousedown" || name == "mouseup" || name == "mousemove") && isSameStructType(value, zeroPoint)) {
				emitMouseEv("mousemove", cast(value : flow -> Point) |> addHeight);
				true
			} else if ((name == "keypress" || name == "keydown" || name == "keyup") && isSameStructType(value, KeyEventRecord("", false, false, false, false, 0))) {
				if (componentName == "canvas") {
					kst = cast(value : flow -> KeyEventRecord);
					iScriptInfo(
						manager,
						checkStr + " = set",
						Some(behaviourDescriptor.component.id)
					);
					emitKeyEvent(currentClip(), name, kst.utf, kst.ctrl, kst.shift, kst.alt, kst.meta, kst.keycode);
				}

				true
			} else {
				false
			}
		} else if (!strContains(name, "pebble")) {
			mv = getBehaviourById(manager, behaviourDescriptor, name != "click" && name != "hover" && name != "point",
				name != "click" && name != "hover" && name != "point", name == "focused", false);
			switch (mv) {
				None(): {
					true;
				}
				Some(bv): {
					iScriptInfo(
						manager,
						checkStr + " = set",
						Some(behaviourDescriptor.component.id)
					);
					next(bv, flow2b(value));
					true;
				}
			}
		} else {
			true;
		}
	}
}

checkBehaviourById(manager : MaterialManager, behaviourDescriptor : IScriptBehaviourDescriptor, fn : (flow) -> bool) -> bool {
	eitherMap(
		getMaterialFocusByIScriptId(manager.focus, behaviourDescriptor.component.id),
		\fc ->
			if (fc.name == behaviourDescriptor.component.name)
				eitherMap(
					lookupTree(fc.behaviours, behaviourDescriptor.name),
					\beh -> {
						fn(getValue(beh.first));
					},
					false
				)
			else
				false,
		false
	)
}

subscribeToIScriptBehavioursFocus(manager : MaterialManager, captureHttp : bool) -> () -> void {
	subscribeToIScriptBehavioursFocus2(manager, manager.focus)
	|> (\uns -> arrayPush(uns, subscribeToStageWidthHeight(manager)))
	|> (\uns -> if (captureHttp) arrayPush(uns, subscribeToHttpRequest(manager)) else uns)
	|> (\uns -> \ -> applyall(uns))
}

subscribeToStageWidthHeight(manager : MaterialManager) -> () -> void {
	deferUntilNextFrameRendered(\ -> addIScriptStageWidthHeight(manager));

	addEventListener(currentClip(), "resize", \ -> addIScriptStageWidthHeight(manager))
}

substituteHttpRequest(manager : MaterialManager) -> () -> void {
	next(
		manager.iscript.requests,
		foldIScriptStack(getValue(manager.iscript.iScriptB).stack, makeList(), \acc, delay, record ->
			switch (record : IScriptRecord) {
				IScriptRequestRecord(__, __, __, __, __, __): Cons(Pair(delay, record), acc);
				default: acc;
			}
		)
	);

	if (countList(getValue(manager.iscript.requests)) > 0) {
		setHttpRequestCallback(\url, post, headers, params2 -> {
			params = filter(params2, \p -> p.key != "t" && p.key != "jwt");

			eitherFn(
				foldList(getValue(manager.iscript.requests), None(), \acc, r -> {
					if (isNone(acc)) {
						rec = r.second;

						switch (rec : IScriptRecord) {
							IScriptRequestRecord(_url, _post, _headers, _params, delay, response): {
								if (_url == url && _post == post && _headers == headers && _params == params) {
									Some(rec);
								} else {
									acc;
								}
							}
							default: acc;
						}
					} else {
						acc;
					}
				}),
				\rq : IScriptRequestRecord -> {
					HttpRequestCallbacks(
						\data -> {
							if (rq.response == IScriptRequestOk(data)) {
								next(manager.iscript.requests, filterList(getValue(manager.iscript.requests), \r -> r.second != rq));
							} else {
								iScriptError(manager, "Request error " + url + " : wrong response", None());
							}
						},
						\error -> {
							if (rq.response == IScriptRequestError(error)) {
								next(manager.iscript.requests, filterList(getValue(manager.iscript.requests), \r -> r.second != rq));
							} else {
								iScriptError(manager, "Request error " + url + " : wrong response", None());
							}
						},
						\status -> {
							// if (rq.response == IScriptRequestStatus(status)) {
							// 	next(manager.iscript.requests, filterList(getValue(manager.iscript.requests), \r -> r.second == rq));
							// } else {
							// 	println("Request error");
							// 	println("status");
							// 	println(status);
							// }
						}
					)
				},
				\ -> {
					HttpRequestCallbacks(
						\data -> {
							iScriptError(manager, "Request error " + url + " : unknown request", None());
						},
						\error -> {
							iScriptError(manager, "Request error " + url + " : unknown request", None());
						},
						\status -> {
							// println("Unknown request");
							// println("status");
							// println(status);
						}
					)
				}
			)
		});
	};

	\ -> {
		clearHttpRequestCallback();
		if (countList(getValue(manager.iscript.requests)) > 0) {
			iScriptError(manager, "Requests error : " + i2s(countList(getValue(manager.iscript.requests))) + " requests left in queue", None());
			next(manager.iscript.requests, makeList());
		}
	}
}

subscribeToHttpRequest(manager : MaterialManager) -> () -> void {
	setHttpRequestCallback(\url, post, headers, params2 -> {
		params = filter(params2, \p -> p.key != "t" && p.key != "jwt");
		time = timestamp();

		HttpRequestCallbacks(
			\data -> {
				addIScriptRequestRecord(manager, url, post, headers, params, timestamp() - time, IScriptRequestOk(data));
			},
			\error -> {
				addIScriptRequestRecord(manager, url, post, headers, params, timestamp() - time, IScriptRequestError(error));
			},
			\status -> {
				// addRecord(IScriptRequestStatus(status));
			}
		);
	});

	clearHttpRequestCallback;
}

subscribeToIScriptBehavioursFocus2(manager : MaterialManager, focus : MFocusGroup) -> [() -> void] {
	foldTree(getValue(focus.focusIds), [], \k, v, acc ->
		switchMaterialFocus(
			v,
			\f ->
				if (fgetValue(f.recordingEnabled))
					arrayPush(acc, initIScriptRecording(manager, f, false))
				else
					acc,
			\f ->
				if (fgetValue(f.recordingEnabled))
					concat(acc, subscribeToIScriptBehavioursFocus2(manager, f))
				else
					acc
		)
	)
	|> (\f -> arrayPush(f, initIScriptRecording(manager, focus, false)))
}

makeValueInputByTypeHint(possibleValues : [flow], typeHelper : string, maybeInitial : Maybe<flow>, enabledB : Transform<bool>) -> Pair<DynamicBehaviour<flow>, MEGItem> {
	result : Pair<DynamicBehaviour<flow>, MEGItem> =
		if (typeHelper == "bool" && length(possibleValues) > 1) {
			valueB = make(either(maybeInitial, flow(false)));

			Pair(
				valueB,
				MEGSwitchControl(
					"Choose value",
					cast(valueB : DynamicBehaviour<flow> -> DynamicBehaviour<bool>),
					[
						MOnOffText(flow2s(possibleValues[0]), flow2s(possibleValues[1])),
						MEnabled(enabledB),
					]
				)
			);
		} else if (typeHelper == "index") {
			valueB = make(either(maybeInitial, flow(-1)));

			Pair(
				valueB,
				MEGDropDown(
					"Choose value",
					valueB,
					Pair(-1, "Select value"),
					mapi(possibleValues, \i, pv -> Pair(i, pv)),
					[MEnabled(enabledB)]
				)
			);
		} else if (typeHelper == "int") {
			valueB = make(either(maybeInitial, flow(0)));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true),
						TextInputType(NumericType())
					]
				)
			);
		} else if (typeHelper == "double") {
			valueB = make(either(maybeInitial, flow(0.0)));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true),
						TextInputType(NumericType())
					]
				)
			);
		} else if (typeHelper == "string" || typeHelper == "") {
			valueB = make(either(maybeInitial, flow("")));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true)
					]
				)
			);
		} else if (startsWith(typeHelper, "[")) {
			valueB = make(either(maybeInitial, flow([])));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true)
					]
				)
			);
		} else {
			valueB = make(either(maybeInitial, flow(deserialize(typeHelper, IllegalStruct()))));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true)
					]
				)
			);
		}

	result
}

getDefaultValueByTypeHint(typeHint : string) -> flow {
	result : flow =
		if (typeHint == "bool")
			flow(false)
		else if (typeHint == "index")
			flow(-1)
		else if (typeHint == "int")
			flow(0)
		else if (typeHint == "double")
			flow(0.0)
		else if (typeHint == "string" || typeHint == "")
			flow("")
		else if (startsWith(typeHint, "["))
			flow([])
		else
			flow(deserialize(typeHint, IllegalStruct()));

	result
}

showValueInputDialog(manager : MaterialManager, record : IScriptInputRecord, onEnter : (flow) -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhen = make(false);

	pair = makeValueInputByTypeHint(record.possibleValues, record.valueTypeHelper, None(), const(true));
	valueB = pair.first;
	content = MGetFocusGroup(\p -> MEGItem2T(p, pair.second, [MWidth(400.)], m2t));

	dialogActions = MDialogActions([
		// MTextButton("CANCEL", \ -> {
		// 	next(closeWhen, true);
		// 	onCancel()
		// }, [], [MShortcut("esc")]),
		MTextButton("PROCEED", \ -> {
			next(closeWhen, true);
			onEnter(getValue(valueB));
		}, [], [MShortcut("enter")])
	]);

	dialogStyle = [
		MDialogTitle("Please, enter value for input \"" + record.name + "\""),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhen,
		dialogStyle,
		content,
		m2t
	);
}

loadStoredIScript(manager : MaterialManager, storedIScript : StoredIScript) -> void {
	loadAdditionalIScriptInfo(manager, storedIScript.additionalInfo);
	next(manager.iscript.iScriptB, storedIScript.script);
}

loadAdditionalIScriptInfo(manager : MaterialManager, additionalInfo : IScriptAdditionalInfo) -> void {
	next(manager.iscript.inputValuesB, map(additionalInfo.inputs, \pair -> IScriptInputValue(pair.first, pair.second, None())));
	next(manager.iscript.outputValuesB, map(additionalInfo.outputs, \outputName -> IScriptOutputValue(outputName, false, None())));
	next(manager.iscript.aliasesB, mergeTree(getValue(manager.iscript.aliasesB), pairs2tree(additionalInfo.aliases)));
}

prepareCurrentIScriptForSave(manager : MaterialManager) -> StoredIScript {
	prepareIScriptFromIScriptManagerForSave(manager.iscript)
}

prepareIScriptFromIScriptManagerForSave(ism : IScriptManager) -> StoredIScript {
	iScript = getValue(ism.iScriptB);
	aliases = getValue(ism.aliasesB);
	inputValues = getValue(ism.inputValuesB);
	outputValues = getValue(ism.outputValuesB);
	prepareIScriptForSave(iScript, aliases, inputValues, outputValues);
}

prepareIScriptForSave(
	iScript : IScript,
	aliases : Tree<IScriptComponentDescriptor, string>,
	inputValues : [IScriptInputValue],
	outputValues : [IScriptOutputValue]) {

	checkDescriptor = \descriptor : IScriptBehaviourDescriptor -> {
		alias = lookupTreeDef(aliases, descriptor.component, "");

		if (alias != "")
			Some(Pair(descriptor.component, alias))
		else
			None();
	}

	usedAliases = filtermapIScriptStack(iScript.stack, \delay, record -> {
		switch (record) {
			IScriptBehaviourRecord(descriptor, __, __, __) : checkDescriptor(descriptor);
			default : None();
		}
	});
	usedInputs = map(inputValues, \inputValue -> Pair(inputValue.name, inputValue.typeHelper));
	usedOutputs = map(outputValues, \outputValue -> outputValue.name);

	StoredIScript(
		storedIScriptCurrentVersion,
		IScriptAdditionalInfo(
			usedInputs,
			usedOutputs,
			usedAliases
		),
		IScript(
			iScript.recordingStarted,
			filtermapIScriptStackTree(
				iScript.stack,
				\delay, record ->
					switch (record : IScriptRecord) {
						IScriptScreenshot(__, __): {
							Some(filterIScriptScreenshot(record))
						}
						default: {
							if (!checkIsFn(serialize(record)))
								Some(record)
							else
								None()
						}
					}
			)
		)
	)
}
