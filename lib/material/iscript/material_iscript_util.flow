import material/iscript/material_iscript_screenshot;
import material/iscript/material_iscript_filesystem;
import material/iscript/material_iscript_replay_record;
import net/http_request_callback;
import net/http_request_iscript;
import formats/base64;

export {
	replayIScript(manager : MaterialManager, onFinished : (bool) -> void, handleInputOutput : bool) -> () -> void;

	subscribeToIScriptBehavioursFocus(manager : MaterialManager, captureHttp : bool) -> () -> void;

	substituteHttpRequest(manager : MaterialManager) -> () -> void;
	makeValueInputByTypeHint(possibleValues : [flow], typeHelper : string, maybeInitial : Maybe<flow>, enabledB : Transform<bool>) -> Pair<DynamicBehaviour<flow>, MEGItem>;
	getDefaultValueByTypeHint(typeHint : string) -> flow;

	loadStoredIScript(manager : MaterialManager, storedIScript : StoredIScript) -> void;

	onIScriptReplayFinish(manager : MaterialManager, compareDialog : ([IScriptReplayResult]) -> void) -> ([IScriptReplayResult]) -> void;

	handleIScriptState(manager : MaterialManager) -> () -> void;
}

stopIScriptReplay(manager : MaterialManager) -> void {
	next(manager.iscript.state, IScriptStopped());
}

replayIScript(manager : MaterialManager, onFinished : (bool) -> void, handleInputOutput : bool) -> () -> void {
	iScriptInfo(manager, "Replay IScript", None());
	replayIScript2(manager, getValue(manager.iscript.script), onFinished, handleInputOutput);
}

ContinuousTimer(
	start : () -> void,
	stop : () -> void,
	pause : () -> void,
);

continuousTimer(tree : Tree<double, [Pair<() -> bool, bool>]>, timeout : double, speed : double, onFinished : (bool) -> void) -> ContinuousTimer {
	isFinished = ref true;
	delays : ref [Pair<double, double>] = ref [];
	r = repeatable2(1, false);
	started = ref 0.0;
	uns = ref [];

	deferredFn : ref Maybe<() -> bool> = ref None();
	deferredTime = ref 0.0;
	deferredTimeInner = ref 0.0;
	currentRepeat = ref 0;

	startFn = \ -> {
		started := timestamp() - either(minA(map(^delays, \v -> v.first)), 0.0);
		r.start();
	};
	stopFn = \ -> {
		dispUnsA(uns);
	};
	pauseFn = \ -> {
		r.stop();
	};
	initFn = \ -> {
		isFinished := false;
		delays := map(getTreeKeys(tree), \v -> Pair(v, v)); // Pair(current replaying time with timeouts, recorded time)
		startNext = ref 0;

		uns :=
			[
				r.stop,
				\ -> if (!(^isFinished)) {
					isFinished := true;
					onFinished(length(^delays) == 0 && isNone(^deferredFn));
				},
				makeSubscribe2(r.value, \ts -> {
					if (length(^delays) == 0 && isNone(^deferredFn)) {
						dispUnsA(uns);
					}

					offset = ts - ^started;
					eitherFn(
						^deferredFn,
						\fn -> {
							if (offset > ^deferredTime + ^deferredTimeInner) {
								if(fn()) deferredFn := None();

								if (isNone(^deferredFn)) {
									delays := map(^delays, \v -> Pair(v.first + (offset - ^deferredTime) / speed, v.second));
								} else if (offset > ^deferredTime + 30000.) {
									println("IScript timeout");
									stopFn();
								} else {
									deferredTimeInner := ^deferredTimeInner + timeout;
								}
							}
						},
						\ -> {
							foldWhile(^delays, offset, \t, d -> {
								if (^delays[0].first < offset) {
									eitherFn(
										lookupTree(tree, ^delays[0].second),
										\records -> {
											tailedRecords = tailFrom(records, ^startNext);
											result0 = foldWhile(tailedRecords, Pair([], 0),
												\acc, fn -> {
													if (fn.first()) {
														Some(Pair(arrayPush(acc.first, fn), acc.second + b2i(fn.second)))
													} else {
														None()
													}
												}
											);
											result = result0.first;

											if (result == tailedRecords) {
												delays := tail(^delays);
												startNext := 0;
												if (result0.second == 0) Some(t) else None()
											} else {
												startNext := length(result) + 1;
												if (length(result) + 1 == length(tailedRecords)) {
													delays := tail(^delays);
													startNext := 0;
												}
												deferredFn := Some(subtractA(tailedRecords, result)[0].first);
												deferredTime := offset;
												deferredTimeInner := timeout;
												None()
											}
										},
										\ -> {
											delays := tail(^delays);
											Some(t)
										}
									);
								} else {
									None()
								}
							}) |> ignore
						}
					)
				})()
			];
	}

	ContinuousTimer(
		\ -> {
			if (^isFinished == true || length(^delays) == 0) {
				initFn();
			}

			startFn();
		},
		stopFn,
		pauseFn,
	)
}

replayIScript2(manager : MaterialManager, iScript : IScript, onFinished : (bool) -> void, handleInputOutput : bool) -> () -> void {
	defaultTimeout = getValue(manager.iscript.settings.replayTimeout) / getValue(manager.iscript.settings.replaySpeed);

	foldTree(iScript.stack, makeTree(), \delay, records, acc ->
		setTree(
			acc,
			delay / getValue(manager.iscript.settings.replaySpeed),
			map(records, \r -> Pair(
				\ -> replayIScriptRecord(manager, r, handleInputOutput),
				isMouseDownEvent(r)
			))
		)
	)
	|> (\t -> foldTree(
			t,
			Pair(makeTree(), 0.),
			\k, v, acc -> {
				offset = i2d(countA(v, secondOfPair) * 250) + acc.second;
				Pair(
					setTree(
						acc.first,
						k + offset,
						v
					),
					offset
				)
			}
		).first
	)
	|> (\t -> continuousTimer(t, defaultTimeout, getValue(manager.iscript.settings.replaySpeed), onFinished))
	|> (\t -> {
		if (getValue(manager.iscript.settings.replayProfile)) {
			startProfile(getValue(manager.iscript.currentFileNameB));
		}
		t.start();

		\ -> {
			t.stop();
			endProfile();
		}
	})
}

isMouseDownEvent(record : IScriptRecord) -> bool {
	eitherMap(
		getRecordDescriptor(record),
		\descriptor -> descriptor.name == "mousedown" || descriptor.name =="focused",
		false
	)
}

getRecordDescriptor(record : IScriptRecord) -> Maybe<IScriptBehaviourDescriptor> {
	switch (record : IScriptRecord) {
		IScriptBehaviourRecord(descriptor, __, __, __): if (descriptor.component.id != makeList()) Some(descriptor) else None();
		default: None();
	}
}

replayIScriptRecord(manager : MaterialManager, record : IScriptRecord, handleInputOutput : bool) -> bool {
	switch (record : IScriptRecord) {
		IScriptBehaviourRecord(descriptor, value, type, callstack): {
			repeatBehaviourById(manager, descriptor, value)
		}
		IScriptInputRecord(descriptor, name, possibleValues, __) : {
			if (handleInputOutput) {
				inputValues = getValue(manager.iscript.input);
				idx = findiDef(inputValues, \input -> input.name == name, -1);

				eitherMap(
					inputValues[idx].value,
					\v -> {
						repeatBehaviourById(manager, descriptor, v)
					},
					false
					// \ -> {
					// 	showValueInputDialog(
					// 		manager,
					// 		record,
					// 		\value -> repeatBehaviourById(manager, descriptor, value) |> ignore,
					// 		m2t
					// 	);
					// }
				);
			} else {
				true;
			}
		}
		IScriptOutputRecord(descriptor, name) : {
			if (handleInputOutput) {
				value : Maybe<flow> = eitherFn(
					getBehaviourById(manager, descriptor, true, false, false, false),
					\beh -> Some(getValue(beh)),
					\ -> None()
				);

				outputValues = getValue(manager.iscript.output);
				idx = findiDef(outputValues, \output -> output.name == name, -1);

				next(manager.iscript.output, replace(outputValues, idx, IScriptOutputValue(name, true, value)));
			}

			true;
		}
		IScriptStageWidthHeight(wh): {
			iScriptInfo(manager, "canvas width/height: " + toString(wh), None());
			nextDistinct(stageWidthHeight, Some(wh));

			true;
		}
		IScriptRequestRecord(url, post, headers, params, delay, response): {
			requestCreated = timestamp();

			if (getValue(manager.iscript.settings.replayCheckErrors) /*&& containsList(getValue(manager.iscript.requests), Pair(time, record))*/) {
				uns = ref [];

				checkRequestFn = \rq -> if (^uns != []) {
					// if (containsList(rq, Pair(time, record))) {
					// 	dispUnsA(uns);
					// 	// Only as info for now
					// 	iScriptInfo(manager, "Request error " + url + " : isn't executed", None());
					// } else {
						dispUnsA(uns);
					// }
				};

				uns :=
					[
						interruptibleTimer(max(ceil(delay), 1000), \ -> checkRequestFn(getValue(manager.iscript.requests))),
						makeSubscribe2(manager.iscript.requests, checkRequestFn)()
					];
			}

			true;
		}
		IScriptScreenshot(__, check): {
			if (!check) {
				if (getValue(manager.iscript.settings.replayApplyScreenshot))
					applyIScriptScreenshot(manager, record);

				true;
			} else {
				record2 = getIScriptScreenshot(manager, check);
				d = diffIScriptScreenshot(record, record2);

				if (sizeTree(d) > 0) {
					iScriptError(manager, "IScriptScreenshot error : screenshots aren't equal", None());
					traverseInOrder(d, \k, v ->
						iScriptError(
							manager,
							foldTree(v, "\t" + toString(k) + " :", \k2, v2, acc2 ->
								acc2 + "\n\t\t" + toString(k2) + " : " + toString(v2.first) + " - " + toString(v2.second)
							),
							None() // todo:
							// Some(k)
						)
					);

					false;
				} else {
					true;
				}
			}
		}
		IScriptCheck(descriptor, checkFn, type): {
			checkBehaviourById(manager, descriptor, checkFn)
		}
		IScriptNested(componentName, id, iScriptNested): {
			// replayIScript2(manager, iScriptNested, m2t, false);
			true;
		}
		IScript(recordingStarted, stack): {
			// replayIScript2(manager, record, m2t, false);
			true;
		}
		IScriptUrlRecord(url) : {
			if (url != loaderUrl()) {
				getUrlN(url, "");

				false;
			} else {
				true;
			}
		}
		IScriptVisualScreenshot(__) : true;
		IScriptLogicalScreenshot(__, __) : true;
		IScriptGeneratedRecord(): replayGeneratedRecord(manager, record);
	}
}

checkBehaviourById(manager : MaterialManager, behaviourDescriptor : IScriptBehaviourDescriptor, fn : (flow) -> bool) -> bool {
	eitherMap(
		getMaterialFocusByIScriptId(manager.focus, behaviourDescriptor.component.id),
		\fc ->
			if (fc.name == behaviourDescriptor.component.name)
				eitherMap(
					lookupTree(fc.behaviours, behaviourDescriptor.name),
					\beh -> {
						fn(getValue(beh.first));
					},
					false
				)
			else
				false,
		false
	)
}

subscribeToIScriptBehavioursFocus(manager : MaterialManager, captureHttp : bool) -> () -> void {

	if (captureHttp) {
		if (isNone((manager.iscript).httpRequestDisposer))
		(manager.iscript).httpRequestDisposer ::= Some(subscribeToHttpRequest(manager));
	};

	subscribeToIScriptBehavioursFocus2(manager, manager.focus)
	|> (\uns -> arrayPush(uns, subscribeToStageWidthHeight(manager)))
	|> (\uns -> \ -> applyall(uns))
}

subscribeToStageWidthHeight(manager : MaterialManager) -> () -> void {
	deferUntilNextFrameRendered(\ -> addIScriptStageWidthHeight(manager));

	addEventListener(currentClip(), "resize", \ -> addIScriptStageWidthHeight(manager))
}

substituteHttpRequest(manager : MaterialManager) -> () -> void {
	next(
		manager.iscript.requests,
		foldIScriptStack(getValue(manager.iscript.script).stack, makeList(), \acc, delay, record ->
			switch (record : IScriptRecord) {
				IScriptRequestRecord(__, __, __, __, __, __): Cons(Pair(delay, record), acc);
				default: acc;
			}
		)
	);

	if (countList(getValue(manager.iscript.requests)) > 0) {
		setHttpRequestCallback(\url, post, headers2, params2 -> {
			headers = filterRequestRecordHeaders(headers2);
			params = filterRequestRecordParams(params2);
			received = IScriptRequestRecord(url, post, headers, params, 0., IScriptRequestOk(""));
			compareFns : HttpRequestResponseCompareFns = getHttpRequestResponseCompareFns(received);

			eitherFn(
				foldList(getValue(manager.iscript.requests), None(), \acc, r -> {
					if (isNone(acc)) {
						rec = r.second;
						switch (rec : IScriptRecord) {
							IScriptRequestRecord(_url, _post, _headers, _params, delay, response): {
								compareFns.requestsCompareFn(received, rec)
								|> (\v -> if (v) Some(rec) else acc)
							}
							default: acc;
						}
					} else {
						acc;
					}
				}),
				\rq : IScriptRequestRecord -> {
					HttpRequestCallbacks(
						\data -> {
							compareFns.responseCompareFn(IScriptRequestOk(data), rq.response)
							|> (\v -> if (v) {
								next(manager.iscript.requests, filterList(getValue(manager.iscript.requests), \r -> r.second != rq));
							}
							else {
								iScriptError(manager, "Request error " + url + " : wrong response", None());
							})
						},
						\error -> {
							compareFns.responseCompareFn(IScriptRequestError(error), rq.response)
							|> (\v -> if (v) {
								next(manager.iscript.requests, filterList(getValue(manager.iscript.requests), \r -> r.second != rq));
							}
							else {
								iScriptError(manager, "Request error " + url + " : wrong response", None());
							})
						},
						\status -> {
							// if (rq.response == IScriptRequestStatus(status)) {
							// 	next(manager.iscript.requests, filterList(getValue(manager.iscript.requests), \r -> r.second == rq));
							// } else {
							// 	println("Request error");
							// 	println("status");
							// 	println(status);
							// }
						}
					)
				},
				\ -> {
					HttpRequestCallbacks(
						\data -> {
							iScriptError(manager, "Request error " + url + " " + toString(params2) + " : unknown request", None());
						},
						\error -> {
							iScriptError(manager, "Request error " + url + " " + toString(params2) + " : unknown request", None());
						},
						\status -> {
							// println("Unknown request");
							// println("status");
							// println(status);
						}
					)
				}
			)
		});
	};

	\ -> {
		clearHttpRequestCallback();
		if (countList(getValue(manager.iscript.requests)) > 0) {
			if (getUrlParameter(UP_ISCRIPTTEST) != "") {
				iter(list2array(getValue(manager.iscript.requests)), \v -> println(v))
			}
			iScriptError(manager, "Requests error : " + i2s(countList(getValue(manager.iscript.requests))) + " requests left in queue", None());
			next(manager.iscript.requests, makeList());
		}
	}
}

subscribeToHttpRequest(manager : MaterialManager) -> () -> void {
	setHttpRequestCallback(\url, post, headers, params2 -> {
		params = filter(params2, \p -> p.key != "t" && p.key != "jwt");
		time = timestamp();

		HttpRequestCallbacks(
			\data -> {
				addIScriptRequestRecord(manager, url, post, headers, params, timestamp() - time, IScriptRequestOk(data));
			},
			\error -> {
				addIScriptRequestRecord(manager, url, post, headers, params, timestamp() - time, IScriptRequestError(error));
			},
			\status -> {
				// addRecord(IScriptRequestStatus(status));
			}
		);
	});

	clearHttpRequestCallback;
}

subscribeToIScriptBehavioursFocus2(manager : MaterialManager, focus : MFocusGroup) -> [() -> void] {
	foldTree(getValue(focus.focusIds), [], \k, v, acc ->
		switchMaterialFocus(
			v,
			\f ->
				if (fgetValue(f.recordingEnabled))
					arrayPush(acc, initIScriptRecording(manager, f, false))
				else
					acc,
			\f ->
				if (fgetValue(f.recordingEnabled))
					concat(acc, subscribeToIScriptBehavioursFocus2(manager, f))
				else
					acc
		)
	)
	|> (\f -> arrayPush(f, initIScriptRecording(manager, focus, false)))
}

makeValueInputByTypeHint(possibleValues : [flow], typeHelper : string, maybeInitial : Maybe<flow>, enabledB : Transform<bool>) -> Pair<DynamicBehaviour<flow>, MEGItem> {
	result : Pair<DynamicBehaviour<flow>, MEGItem> =
		if (typeHelper == "bool" && length(possibleValues) > 1) {
			valueB = make(either(maybeInitial, flow(false)));

			Pair(
				valueB,
				MEGSwitchControl(
					"Choose value",
					cast(valueB : DynamicBehaviour<flow> -> DynamicBehaviour<bool>),
					[
						MOnOffText(flow2s(possibleValues[0]), flow2s(possibleValues[1])),
						MEnabled(enabledB),
					]
				)
			);
		} else if (typeHelper == "index") {
			valueB = make(either(maybeInitial, flow(-1)));

			Pair(
				valueB,
				MEGDropDown(
					"Choose value",
					valueB,
					Pair(-1, "Select value"),
					mapi(possibleValues, \i, pv -> Pair(i, pv)),
					[MEnabled(enabledB)]
				)
			);
		} else if (typeHelper == "int") {
			valueB = make(either(maybeInitial, flow(0)));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true),
						TextInputType(NumericType())
					]
				)
			);
		} else if (typeHelper == "double") {
			valueB = make(either(maybeInitial, flow(0.0)));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true),
						TextInputType(NumericType())
					]
				)
			);
		} else if (typeHelper == "string" || typeHelper == "") {
			valueB = make(either(maybeInitial, flow("")));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true)
					]
				)
			);
		} else if (startsWith(typeHelper, "[")) {
			valueB = make(either(maybeInitial, flow([])));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true)
					]
				)
			);
		} else {
			valueB = make(either(maybeInitial, flow(deserialize(typeHelper, IllegalStruct()))));

			Pair(
				valueB,
				MEGTextInput(
					"Enter value",
					valueB,
					[
						MNativeInput(true),
						MEnabled(enabledB),
						MHelperText(const(Some(typeHelper)), true)
					]
				)
			);
		}

	result
}

getDefaultValueByTypeHint(typeHint : string) -> flow {
	result : flow =
		if (typeHint == "bool")
			flow(false)
		else if (typeHint == "index")
			flow(-1)
		else if (typeHint == "int")
			flow(0)
		else if (typeHint == "double")
			flow(0.0)
		else if (typeHint == "string" || typeHint == "")
			flow("")
		else if (startsWith(typeHint, "["))
			flow([])
		else
			flow(deserialize(typeHint, IllegalStruct()));

	result
}

showValueInputDialog(manager : MaterialManager, record : IScriptInputRecord, onEnter : (flow) -> void, m2t : (Material, MFocusGroup) -> Tropic) -> void {
	closeWhen = make(false);

	pair = makeValueInputByTypeHint(record.possibleValues, record.valueTypeHelper, None(), const(true));
	valueB = pair.first;
	content = MGetFocusGroup(\p -> MEGItem2T(p, pair.second, [MWidth(400.)], m2t));

	dialogActions = MDialogActions([
		// MTextButton("CANCEL", \ -> {
		// 	next(closeWhen, true);
		// 	onCancel()
		// }, [], [MShortcut("esc")]),
		MTextButton("PROCEED", \ -> {
			next(closeWhen, true);
			onEnter(getValue(valueB));
		}, [], [MShortcut("enter")])
	]);

	dialogStyle = [
		MDialogTitle("Please, enter value for input \"" + record.name + "\""),
		MDialogUseFrame(),
		MDialogScroll(),
		dialogActions,
		IScriptRecordingEnabled(const(false))
	];

	renderMDialog(
		manager,
		closeWhen,
		dialogStyle,
		content,
		m2t
	);
}

loadStoredIScript(manager : MaterialManager, storedIScript : StoredIScript) -> void {
	loadAdditionalIScriptInfo(manager, storedIScript.additionalInfo);
	next(manager.iscript.script, storedIScript.script);
}

startIScriptRecording(manager : MaterialManager, options : CaptureOptions, continueRec : bool) -> [() -> void] {
	if (continueRec) {
		resumeCurrentIScript(manager);
	} else {
		clearCurrentIScript(manager);
	}

	// addIScriptUrlRecord(manager, loaderUrl());
	addIScriptScreenshot(manager, false);

	[
		subscribeToIScriptBehavioursFocus(manager, options.captureHttpRequests),
		\ -> {
			next(manager.iscript.scriptChangedB, true);
		}
	]
}

startIScriptReplaying(
	manager : MaterialManager,
	onFinish : ([IScriptReplayResult]) -> void
) -> [() -> void] {
	if (!isIScriptEmpty(manager)) {
		tempStageWidthHeight = getValue(stageWidthHeight);

		nextDistinct(manager.iscript.output, map(getValue(manager.iscript.output), \ov -> IScriptOutputValue(ov.name, false, None())));
		nextDistinct(manager.iscript.errors, makeList());

		showIScriptReplayCursor(manager);

		(manager.iscript).httpRequestDisposer ::= Some(substituteHttpRequest(manager));
		[
			replayIScript(
				manager,
				\b -> {
					onFinish(
						[
							IScriptReplayResult(
								getValue(manager.iscript.currentFileNameB),
								b,
								extractLastLogicalScreenshot(manager),
								extractLastVisualScreenshot(manager),
								getIScriptLogicalScreenshot(manager),
								getIScriptVisualScreenshot(manager, true)
							)
						]
					)
				},
				true
			),
			\ -> nextDistinct(stageWidthHeight, tempStageWidthHeight)
		]
		|> (\d -> \ -> applyall(d))
		|> (\f -> (manager.iscript).replayDisposer ::= Some(f))
	};
	[]
}

startIScriptScenarioReplaying(
	manager : MaterialManager,
	onFinish : ([IScriptReplayResult]) -> void
) -> [() -> void] {
	if (!isIScriptScenarioEmpty(manager)) {
		nextDistinct(manager.iscript.output, map(getValue(manager.iscript.output), \ov -> IScriptOutputValue(ov.name, false, None())));
		nextDistinct(manager.iscript.errors, makeList());
		scenarioTestResults = ref [];

		iScriptInfo(manager, "=== Start Scenario Tests ===", None());
		next(manager.iscript.scenario.runningIScriptIdxB, 0);

		stopScenarioReplay = \ -> {
			onFinish(^scenarioTestResults);
		};
		[
			makeSubscribeUns(
				fselect2(
					manager.iscript.scenario.scriptsB,
					manager.iscript.scenario.runningIScriptIdxB,
					FLift2(\scriptRecords, id ->
						if (id >= 0 && id < length(scriptRecords))
							Some(scriptRecords[id])
						else
							None()
					)
				),
				\scriptRecordM -> {
					eitherFn(
						scriptRecordM,
						\scriptRecord -> {
							uns = ref [];

							scriptPath = scriptRecord.scriptPath;

							tempStageWidthHeight = getValue(stageWidthHeight);

							iScriptInfo(manager, "Replaying " + scriptPath, None());

							next(manager.iscript.currentFileNameB, scriptPath);

							onSuccess = \ -> {
								showIScriptReplayCursor(manager);

								(manager.iscript).httpRequestDisposer ::= Some(substituteHttpRequest(manager));
								[
									replayIScript(manager,
										\b -> {
											hideIScriptReplayCursor(manager);

											if (!b) {
												iScriptError(manager, scriptPath + " finished with errors", None());
											}

											iscriptLogicalScreenshotM = extractLastLogicalScreenshot(manager);
											iscriptVisualScreenshotM = extractLastVisualScreenshot(manager);

											afterReplayLogicalScreenshot = getIScriptLogicalScreenshot(manager);
											afterReplayVisualScreenshot = getIScriptVisualScreenshot(manager, true);

											refArrayPush(
												scenarioTestResults,
												IScriptReplayResult(
													scriptPath,
													b,
													iscriptLogicalScreenshotM,
													iscriptVisualScreenshotM,
													afterReplayLogicalScreenshot,
													afterReplayVisualScreenshot
												)
											);

											outputValues : [Pair<string, Maybe<flow>>] = map(getValue(manager.iscript.output), \ov : IScriptOutputValue -> Pair(ov.name, ov.value));
											if (length(outputValues) > 0) {
												setFileContent("iscript_output.txt", getFileContent("iscript_output.txt") + serialize(outputValues)) |> ignore;
											}
											iScriptInfo(manager, "", None());

											if (isIScriptScenarioReplaying(manager)) {
												next(manager.iscript.scenario.runningIScriptIdxB, getValue(manager.iscript.scenario.runningIScriptIdxB) + 1);
											} else {
												stopScenarioReplay();
											}
										},
										true
									),
									\ -> nextDistinct(stageWidthHeight, tempStageWidthHeight)
								]
								|> (\d -> \ -> applyall(d))
								|> (\f -> (manager.iscript).replayDisposer ::= Some(f))
							};

							onError = \ -> {
								disposeIScriptHandler(manager);
								next(manager.iscript.scenario.runningIScriptIdxB, getValue(manager.iscript.scenario.runningIScriptIdxB) + 1);
							};

							uns := [
								\ -> disposeIScriptHandler(manager),
								\ -> clearIScriptAdditionalInfo(manager),
								loadIScriptFile(manager, scriptPath, onSuccess, onError)
							];

							[\ -> dispUnsA(uns)];
						},
						\ -> {
							stopScenarioReplay();
							[];
						}
					);
				}
			)()
		]
		|> (\d -> \ -> applyall(d))
		|> (\f -> (manager.iscript).replayScenarioDisposer ::= Some(f));
		[]
	} else {
		[]
	}
}

disposeIScriptHandler(manager : MaterialManager) -> void {
	maybeApply((manager.iscript).httpRequestDisposer, apply0);
	(manager.iscript).httpRequestDisposer ::= None();
	maybeApply((manager.iscript).replayDisposer, apply0);
	(manager.iscript).replayDisposer ::= None();
}

disposeIScriptScenarioHandler(manager : MaterialManager) -> void {
	maybeApply((manager.iscript).replayScenarioDisposer, apply0);
	(manager.iscript).replayScenarioDisposer ::= None();
}

handleIScriptState(manager : MaterialManager) -> () -> void {
	makeSubscribe2Uns(manager.iscript.state, \state -> {
		switch (state) {
			IScriptRecording(options, continue): {
				disposeIScriptHandler(manager);
				startIScriptRecording(manager, options, continue);
			}
			IScriptRecordingPaused(options, continue): {
				[]
			}
			IScriptReplaying(onFinish): {
				disposeIScriptHandler(manager);
				startIScriptReplaying(manager, onFinish);
			}
			IScriptReplayingPaused(): {
				[]
			}
			IScriptScenarioReplaying(onFinish): {
				disposeIScriptHandler(manager);
				disposeIScriptScenarioHandler(manager);
				startIScriptScenarioReplaying(manager, onFinish);
			}
			IScriptScenarioReplayingPaused(): {
				[]
			}
			IScriptStopped(): {
				disposeIScriptHandler(manager);
				disposeIScriptScenarioHandler(manager);
				[]
			}
			IScriptAddMFocusRecord(addDialog): {
				[
					make2SubscribeUns(getCurrentHoveredItemBehaviour(manager), getCurrentPressedItemBehaviour(manager), \hb, pb -> {
						eitherFn(
							pb,
							\p -> {
								setIScriptState(manager, IScriptStopped());
								addDialog(p);

								[]
							},
							\ -> {
								eitherMap(
									hb,
									\h -> [highlightMaterialFocus(manager, h)],
									[]
								);
							}
						);
					})()
				]
			}
			IScriptAddMFocusGroupRecord(addDialog): {
				[
					make2SubscribeUns(getCurrentHoveredItemBehaviour(manager), getCurrentPressedItemBehaviour(manager), \hb, pb -> {
						eitherFn(
							pb,
							\p -> {
								setIScriptState(manager, IScriptStopped());
								addDialog(p.parent);

								[]
							},
							\ -> {
								eitherMap(
									hb,
									\h -> [highlightMaterialFocus(manager, h.parent)],
									[]
								);
							}
						);
					})()
				]
			}
			IScriptAddMFocusAlias(addDialog): {
				[
					make2SubscribeUns(getCurrentHoveredItemBehaviour(manager), getCurrentPressedItemBehaviour(manager), \hb, pb -> {
						eitherFn(
							pb,
							\p -> {
								setIScriptState(manager, IScriptStopped());
								addDialog(p);

								[]
							},
							\ -> {
								eitherMap(
									hb,
									\h -> [highlightMaterialFocus(manager, h)],
									[]
								);
							}
						);
					})()
				]
			}
			IScriptAddMFocusGroupAlias(addDialog): {
				[
					make2SubscribeUns(getCurrentHoveredItemBehaviour(manager), getCurrentPressedItemBehaviour(manager), \hb, pb -> {
						eitherFn(
							pb,
							\p -> {
								setIScriptState(manager, IScriptStopped());
								addDialog(p.parent);

								[]
							},
							\ -> {
								eitherMap(
									hb,
									\h -> [highlightMaterialFocus(manager, h.parent)],
									[]
								);
							}
						);
					})()
				]
			}
		}
	})()
}

onIScriptReplayFinish(manager : MaterialManager, compareDialog : ([IScriptReplayResult]) -> void) -> ([IScriptReplayResult]) -> void {
	\b -> {
		hideIScriptReplayCursor(manager);

		if (countA(b, \v -> v.isReplaySuccessful) < length(b)) {
			iScriptError(manager, "IScript replay finished with errors", None());
		} else {
			iScriptInfo(manager, "IScript replay completed successfully", None());
		}

		outputValues = map(getValue(manager.iscript.output), \ov : IScriptOutputValue -> Pair(ov.name, ov.value));

		if (length(outputValues) > 0) {
			setFileContent("iscript_output.txt", serialize(outputValues)) |> ignore;
		}

		nextDistinct(manager.iscript.state, IScriptStopped());

		if (getUrlParameter(UP_ISCRIPTGENERATESCREENSHOTS) != "") {
			iter(b, \result -> writeIScriptScreenshot(
				getIScriptScreenshotName(result.filepath, Some(getIScriptScenarioFileName(manager)), None()),
				result.afterReplayVisualScreenshot.screenshot
			) |> ignore);
		}

		// If there was a run with the parameter "playiscript", then we exit with the result return.
		if (length(b) > 1) {
			iScriptInfo(manager, "=== End Scenario ===", None());
			if (getUrlParameter(UP_ISCRIPTGENERATESCENARIOHTMLREPORT) != "") {
				writeIScriptReport(
					getIScriptReportName(getIScriptScenarioFileName(manager)),
					getIscriptReportResults(b)
				) |> ignore;
			}
			if (getUrlParameter(UP_ISCRIPTPLAYSCENARIO) != "")
				quit(countA(b, \v -> !v.isReplaySuccessful))
			else
				compareDialog(b);
		} else {
			if (
				getUrlParameter(UP_ISCRIPTPLAY) != "" ||
				getUrlParameter(UP_ISCRIPTPLAYSCENARIO) != "" ||
				getUrlParameter(UP_ISCRIPTGENERATEPLAY) != ""
			)
				quit(b2i(countA(b, \v -> v.isReplaySuccessful) < length(b)))
			else
				compareDialog(b);
		}
	}
}

getIScriptScenarioFileName(manager : MaterialManager) -> string {
	scenarioPath = getValue(manager.iscript.scenario.pathB);
	strRight(scenarioPath, strLastIndexOf(scenarioPath, "/") + 1);
}

getIScriptReportName(scenarioName : string) -> string {
	"iscripts/report_" + scenarioName + ".html"
}

getIScriptScreenshotName(scriptPath : string, scenarioNameM : Maybe<string>, indexM : Maybe<int>) -> string {
	scenarioName = eitherMap(scenarioNameM, \sn -> sn + "_", "");
	index = eitherMap(indexM, \i -> i2s(i) + "_", "");
	"iscripts/screenshot_after_" + scenarioName + index + strRight(scriptPath, strLastIndexOf(scriptPath, "/") + 1) + ".png";
}

writeIScriptScreenshot(path : string, content : string) -> bool {
	setFileContentBytes(
		path,
		decodeBase64(
			strRight(content, 22)
		)
	);
}

writeIScriptReport(path : string, content : string) -> bool {
	setFileContent(
		path,
		"<html><head></head><body>" + content + "</body></html>"
	);
}

getIscriptReportResults(results : [IScriptReplayResult]) -> string {
	strGlue(map(results, \result -> {
		savedScreenshoot = eitherMap(result.iscriptVisualScreenshotM, \ivs -> ivs.screenshot, "");
		formatString(
			"<h2>%1 with result %2</h2><img src=\"%3\">%4",
			[
				result.filepath,
				b2s(result.isReplaySuccessful),
				result.afterReplayVisualScreenshot.screenshot,
				if (!result.isReplaySuccessful && savedScreenshoot != "")
					formatString("<p>but was</p><img src=\"%1\">", [savedScreenshoot])
				else ""
			]
		)
	}), "\n")
}
