import form/paragraphtypes;
import form/line;
import form/baseline;
import text/bidi_text;

import text/blueprint;

export {
	makeAlphabetAwareTextField(txt : string, style : [CharacterStyle]) -> native;
	renderParagraph(p : [ParagraphElement], s : [ParagraphStyle], rtl : bool, widthManager : ParagraphWidthManager) -> Form;
	getLineIndent(paraIndent : double, lineIndex : int) -> double;
	makeZeroSpaceElement(style : [CharacterStyle]) -> Form;
	general2WrapElement(elem : GeneralWrapElement, style : [CharacterStyle]) -> WrapElement;
	makeWrapElementsFromGeneralTextFragments(fragments : GeneralTextFragments) -> List<WrapElement>;
	generalEmptyLineInspectElement(id : int) -> GeneralInspectElement;
	keepFormTogether(form : Form) -> bool;

	StoryWidthManager(
		paragraphManagers : [ParagraphWidthManager],
		getParagraphManagerAt: (int) -> ParagraphWidthManager,
		width : Behaviour<double>,
		infoOnly : bool, /* do not control width of paragraphs, just gather their widths */
		constructor : () -> () -> void,
	);
	ParagraphWidthManager(
		storyWidth : Behaviour<double>,
		width : DynamicBehaviour<double>,
		infoOnly : bool
	);

	makeStoryWidthManager(paragraphs : int, infoOnly : bool) -> StoryWidthManager;
	dummyParagraphWidthManager = ParagraphWidthManager(const(0.0), make(0.0), true);
	dummyStoryWidthManager = StoryWidthManager([], \__ -> dummyParagraphWidthManager, zero, true, \ -> nop);

	INF_METRICS = 100000.0;

	isUrlParameterEscUnescHTMLTrue() -> bool;
	// Helper to pass accessibility properties from material to forms
	// Used in material2form
	accessibilityParent : ref Maybe<flow> = ref None();
}

// TODO:
// - When joining texts, wait with calculating the new metrics until all texts have been joined up
// - Unsubscribe when reflowing

/*
To render a paragraph, we first collect individual words which need to stay together on the same line.
That happens in expandGlueFragments. Each of the words is then categorized as a text or ghost element.
Ghost elements are rendered once, and then moved around when reflows happen. Text elements are
reconstructed on each reflow, because we combine them together to have as few sprites for text as
possible, and because rendering words in the same sprite improves the interword spacing.
*/

// Intermediately used typed as a union of ParagraphElement and WrapElement and ParaOneWord
ParaElement ::= ParagraphElement, WrapElement, ParaAtomic;

// After expanding elements to words
ParaAtomic ::= Form, ParaOneWord, TextElement, InteractiveParaAtomic;
	// A range of elements that need to be on the same line
	ParaOneWord(elements : [Form]);
	InteractiveParaAtomic(elem : ParaAtomic, interactivityId : int);

GhostForm ::= Form, Ghosted; // It is important that GhostForm is a super type of Ghosted
	Ghost(xc : DynamicBehaviour<double>, yc : DynamicBehaviour<double>, form : Form);

// For dynamic elements, we use a ghost to allow reuse of the physical forms
Ghosted ::= Empty, Ghost, CoordinateInspectElement, InspectGhost;
	CoordinateInspectElement(inspector : ParaElementInspector, form : Form);
	InspectGhost(
		inspector : ParaElementInspector,
		form : Form
	);

// Each word to be placed in the paragraph, including the metrics and as well as any ghost constructed
ParaWord(
	word : ParaAtomic,
	metrics : Behaviour<FormMetrics>,
	ghosted : Ghosted,
	form: GhostForm,
	interactivityIdM : Maybe<int>
);

ParaLine(words : [ParaWord], indent : double);
ParaLineAcc(lines : List<Form>, y : double, baseline : double);

OptimizedLineElement(f : GhostForm, metrics : Behaviour<FormMetrics>, interactivityIdM : Maybe<int>);
ParaLineInfo(optimizedLine : [OptimizedLineElement], lineAsc : double, lineHeight : double, lineWidth : double, lineIndent : double);
ParaLinesInfos(lines : [ParaLineInfo], maxLineWidth : double);
ParaLineResult(form : Form, width : double, height : double, baseline : double);

makeWrapElementsFromGeneralTextFragments(fragments : GeneralTextFragments) -> List<WrapElement> {
	mapList(fragments.elements, \elem -> general2WrapElement(elem, fragments.style))
}

makeZeroSpaceElement(style : [CharacterStyle]) -> Form {
	spaceMetrics = getStaticFormSize(Text(" ", style));
	fixBaseline(spaceMetrics.baseline, Fixed(0.0, spaceMetrics.height))
}

generalEmptyLineInspectElement(id : int) -> GeneralInspectElement {
	GeneralInspectElement(
		ParaElementInspector(make(id), make(0.), make(0.), makeWH(), make(0.), make(0.), make(0), make(false)),
		EmptyLineElement()
	)
}

general2WrapElement(elem : GeneralWrapElement, style : [CharacterStyle]) -> WrapElement {
	switch (elem) {
		GeneralText(txt) : {
			Text(txt, applyAlphabetStyles(detectAlphabet(txt), style));
		}
		GeneralDynamicText(txt) : {
			Text(txt, applyAlphabetStyles(detectAlphabet(txt), style));
		}
		GeneralSpace(txt, alBef, alAft) : {
			Space(Text(txt, apply2AlphabetsScaling(alBef, alAft, style)));
		}
		NewLine() : NewLine();
		GeneralLinePart(f, m, e) : LinePart(Text(f, style), Text(m, style), Text(e, style));
		EmptyLineElement() : makeZeroSpaceElement(style);
		GeneralInspectElement(inspector, element) : InspectElement(
			inspector,
			cast(general2WrapElement(element, style) : WrapElement -> InspectableElement)
		);
		NonTextElement() : Empty();
	}
}

makeAlphabetAwareTextField(txt : string, style : [CharacterStyle]) -> native {
	overridden = applyAlphabetStyles(detectAlphabet(txt), style);
	textfield = makeTextfield(getMappedFontFamily(overridden));
	rtl = extractStruct(style, SetRTL(getDefaultRtl())).rtl;
	setTextDirection(textfield, if (rtl) "rtl" else "ltr");
	dstyle = getDefinedTextStyle(overridden);
	setTextAndStyle(
		textfield,
		if (isUrlParameterEscUnescHTMLTrue() && cpp) {
			esc = extractStruct(style, EscapeHTML(true)).escape;
			if (esc) xmlEscapeText(txt) else txt;
		} else txt,
		dstyle.face.family,
		dstyle.fontSize,
		dstyle.face.weight,
		dstyle.face.slope,
		dstyle.fillColor,
		dstyle.fillOpacity,
		dstyle.letterSpacing,
		dstyle.backgroundColor,
		dstyle.backgroundOpacity
	);
	textfield
}

renderParagraph(
	p : [ParagraphElement],
	s : [ParagraphStyle],
	rtl : bool,
	widthManager : ParagraphWidthManager
) -> Form {

	alignment : ParaLineAlignment = extractParaLineAlignment(s);
	genIndent = extractStruct(s, GeneralIndent(0.0)).indent;
	needIndent = genIndent != 0.0;

	interactiveStyles = extractStruct(s, ParagraphInteractiveStyleTree(makeTree())).styleTree;

	interlineSpacing = extractStruct(s, InterlineSpacing(0.0)).d;
	interLineHighlighting = containsStruct(s, InterlineHighlighting());
	fitLongWords = containsStruct(s, ParagraphFitLongWords());

	tightWidth = contains(s, TightWidth());

	fill = filtermap(s, \st -> switch (st) {
		Fill(__): Some(st);
		FillOpacity(__): Some(st);
		default: None();
	});

	topLineBaseline = contains(s, TopLineBaseline());

	indent = extractStruct(s, FirstLineIndent(0.0)).indent;
	linesCountB = extractStruct(s, ParagraphLinesCount(make(0))).count;

	isSingleLine = containsStruct(s, ParagraphSingleLine());
	isParagraphMarked = containsStruct(s, ParagraphMarked());
	paragraphBorder = extractStruct(s, ParagraphBorder(0.0, 0.0));

	// Expand elements into word-units
	glued : List<ParaAtomic> = expandGlueFragments(p, false, makeList(), makeList(), 0);
	// Construct our ParaWords
	gluedArr = list2array(glued);
	words : [ParaWord] = mapi(gluedArr, \i, word -> makeParaWord(
		word,
		i + 1 == length(gluedArr) || containsStruct(s, IgnoreLetterspacingOnReflow())
	));

	aw = make(0.0);
	update = make(0);
	linesB : DynamicBehaviour<ParaLinesInfos> = make(ParaLinesInfos([], 0.0));

	renderParaLinesFn = \lines, storyWidth -> {
		renderParaLines(
			storyWidth, lines, tightWidth, alignment, interlineSpacing,
			topLineBaseline, interactiveStyles, rtl, isParagraphMarked,
			interLineHighlighting
		);
	}
	paragraphAccessibilityParent = ^accessibilityParent;

	// it renders all words but for dynamics it just reserves space and updates coordinates
	renderedParagraph = if (widthManager == dummyParagraphWidthManager) {
		Select(linesB, \lines -> {
			prevAccessibilityParent = ^accessibilityParent;
			accessibilityParent := paragraphAccessibilityParent;
			rpl = renderParaLinesFn(lines, getValue(aw));
			accessibilityParent := prevAccessibilityParent;
			rpl
		});
	} else if (widthManager.infoOnly) {
		// in this non-TightWidth case we just inform WidthManager about our width
		// we will not take story width into account in this case, but use availableWidth as usual
		Select(linesB, \lines -> {
			prevAccessibilityParent = ^accessibilityParent;
			accessibilityParent := paragraphAccessibilityParent;
			rendered = renderParaLinesFn(lines, getValue(aw));
			nextDistinct(widthManager.width, lines.maxLineWidth);
			accessibilityParent := prevAccessibilityParent;
			rendered
		});
	} else {
		Select2(linesB, widthManager.storyWidth, \lines, storyWidth -> {
			prevAccessibilityParent = ^accessibilityParent;
			accessibilityParent := paragraphAccessibilityParent;
			rendered = renderParaLinesFn(
				lines,
				if (storyWidth > 0.0) storyWidth else getValue(aw),
			);
			// we have to notify story about changes in our width.
			// it could alter widthManager.storyWidth if we are new winner so we have to defer
			deferUntilNextFrameRendered(\->nextDistinct(widthManager.width, lines.maxLineWidth));
			accessibilityParent := prevAccessibilityParent;
			rendered
		});
	}

	// If there is less or equal then upper limit of dynamic elements, we do not need to postpone updates
	dynamicWords = filter(words, \w -> !isConst(w.metrics));
	ndynamic = length(dynamicWords);
	ndymamicUpper = extractStruct(s, DynamicBlockDelay(getParagraphDynamicBlockDelay())).n;

	contentGroup = Group(concat(
		[if (fill != []) Background(fill, renderedParagraph) else renderedParagraph],
		filtermap(words, \word -> {
			applyStyles = \form -> eitherMap(
				lookupTree(interactiveStyles, either(word.interactivityIdM, -1)),
				\wordStyles -> applyListenersAndModifiers(wordStyles, form),
				form
			);
			switch (word.ghosted : Ghosted) {
				Empty(): None();
				Ghost(x, y, f): Some(Translate(x, y, applyStyles(f)));
				InspectGhost(inspector, f): Some(Translate(inspector.x, inspector.y, applyStyles(f)));
				CoordinateInspectElement(__, __): None();
			}
		})
	));

	scaleB = make(1.0);
	scaledView = if (fitLongWords) Scale(scaleB, scaleB, contentGroup) else contentGroup;

	Constructor(
		Inspect(
			[AvailableWidth2(aw)],
			if (paragraphBorder.top > 0.0 || paragraphBorder.bottom > 0.0) {
				Border(0.0, paragraphBorder.top, 0.0, paragraphBorder.bottom, scaledView);
			} else {
				scaledView;
			}
		),
		\ -> {
			updateFn = \wi-> {
				// Ignore negative widths
				// It's the safest way to simulate single line, because reflowParaWords2 does a lot of useful staff.
				w = if (isSingleLine) INF_METRICS else max(0.0, wi);
				infosAndScale = getParaLineInfosAndScale(
					alignment,
					words,
					w,
					indent,
					isParagraphMarked,
					rtl,
					fitLongWords,
					extractStruct(s, FontSize(14.0)).size
				);

				nextDistinct(linesCountB, length(infosAndScale.first));
				// We have to rerender even when lines are identical, at minimum to move things because of potential size changes
				next(linesB, ParaLinesInfos(infosAndScale.first, fold(infosAndScale.first, 0.0, \acc, info -> max(info.lineWidth, acc))));
				maybeApply(infosAndScale.second, \scaleFactor -> nextDistinct(scaleB, scaleFactor));
			};

			uns = concatA([
				[
					subscribe(aw, \v -> updateFn(v)),
				],
				if (ndynamic == 0 || ndynamic == ndymamicUpper) [] else
				[
					subscribe2(update, \u -> {
						// When an update from a dynamic element is requested, we do it in the next frame
						if (u == 1) deferUntilNextFrameRendered(\ -> {
							next(update, 0);
							updateFn(getValue(aw))
						})
					})
				],
				mapi(dynamicWords, \i, w -> subscribe2(w.metrics, \m -> if (i < ndymamicUpper) updateFn(getValue(aw)) else nextDistinct(update, 1)))
			]);

			\ -> applyall(uns)
		}
	)
	|> (\f -> {
		if (needIndent) {
			if (rtl) Border(0.0, 0.0, genIndent, 0.0, f) else Border(genIndent, 0.0, 0.0, 0.0, f)
		} else {
			f
		}
	})
}

getParaLineInfosAndScale(
	alignment : ParaLineAlignment,
	words : [ParaWord],
	availableWidth : double,
	indent : double,
	isParagraphMarked : bool,
	rtl : bool,
	fitLongWords : bool,
	fontSize : double
) -> Pair<[ParaLineInfo], Maybe<double>> {
	reflowResult = reflowParaWords(words, availableWidth, indent, isParagraphMarked, rtl, fitLongWords, fontSize);
	switch (reflowResult) {
		ReflowedLines(newLines) : {
			infos = map(newLines, \line -> {
				optimizedLine = getOptimizedLine(line.words, alignment);
				metrics = map(optimizedLine, \ole -> getValue(ole.metrics));
				lineAsc = fold(metrics, 0.0, \ac, m -> max(ac, m.baseline));
				ParaLineInfo(
					optimizedLine,
					lineAsc,
					fold(metrics, 0.0, \ac, m -> max(ac, m.height + lineAsc - m.baseline)),
					fold(metrics, line.indent, \ac, m -> ac + m.width),
					line.indent,
				)
			});
			Pair(infos, None());
		}
		OverflowCase(availWidth, overflowWidth) : {
			// We have word that is too long to fit paragraph width so
			// We are reflowing paragraph to respect the width of the long word
			// And scaling paragrph to fit it to available width
			p = getParaLineInfosAndScale(
				alignment,
				words,
				overflowWidth,
				indent,
				isParagraphMarked,
				rtl,
				fitLongWords,
				fontSize
			);
			Pair(p.first, Some(availWidth / overflowWidth))
		}
	}
}

// Introduce a ParaOneWord that group the words together, so that we do not have to worry about joins
expandGlueFragments(
	elements : [ParaElement],
	glueNext : bool,
	running : List<ParaAtomic>,
	acc : List<ParaAtomic>,
	runLength : int,
) -> List<ParaAtomic> {

	flush = \ -> {
		if (runLength == 0) {
			acc;
		} else if (runLength == 1) {
			Cons(headList(running, Empty()), acc);
		} else {
			wds = foldList(running, makeList(), \acc2, ra : ParaAtomic -> {
				switch (ra : ParaAtomic) {
					ParaOneWord(wds): acc2;
					NewLine(): acc2;
					Space(f): Cons(f, acc2);
					LinePart(pr, i, po): Cons(i, acc2);
					InspectElement(inspector, e): {
						switch (e : InspectableElement) {
							Space(f): Cons(f, acc2);
							Text(t, s): Cons(e, acc2);
							default: Cons(cast(e : InspectableElement -> Form), acc2);
						}
					}
					InteractiveParaAtomic(ia, id) : {
						Cons(cast(ia : ParaAtomic -> Form), acc2);
					}
					default: {
						Cons(cast(ra : ParaAtomic -> Form), acc2);
					}
				}
			});
			Cons(ParaOneWord(list2array(reverseList(wds))), acc)
		};
	};

	n = length(elements);
	if (n == 0) {
		flush();
	} else {
		rest = tail(elements);
		e = elements[0];
		switch (e : ParaElement) {
			GlueFragments(): expandGlueFragments(rest, true, running, acc, runLength);
			TextFragments(parts): {
				p : [WrapElement] = list2array(parts);
				expandGlueFragments(concat(p, rest), glueNext, running, acc, runLength);
			}
			// TODO rewrite via switch subunion syntax when implemented.
			default: {
				en = switch (e : ParaElement) {
					WordWrapInteractive(wElem, id): {
						InteractiveParaAtomic(cast(wElem : WrapElement -> ParaAtomic), id)
					}
					default: cast(e : ParaElement -> ParaAtomic);
				}
				if (glueNext) {
					expandGlueFragments(rest, false, Cons(en, running), acc, runLength + 1);
				} else {
					expandGlueFragments(rest, false, Cons(en, makeList()), flush(), 1);
				}
			}
		}
	}
}

makeInteractiveParaWord(
	w : ParaAtomic,
	interactivityIdM : Maybe<int>,
	ignoreLetterSpacing : bool
) -> ParaWord {
	s2w = \s : Pair<Form, Behaviour<FormMetrics>> -> {
		if (!isGhostable(s.first)) {
			ParaWord(s.first, s.second, Empty(), Empty(), interactivityIdM);
		} else {
			x = make(0.0);
			y = make(0.0);
			ParaWord(Empty(), s.second, Ghost(x, y, s.first), Empty(), interactivityIdM);
		}
	}
	switch (w : ParaAtomic) {
		NewLine(): ParaWord(w, const(zeroMetrics), Empty(), Empty(), interactivityIdM);
		// We choose the middle part
		LinePart(f, p, e): {
			dynFormSizePair = getDynamicFormSize2(p, true);
			ParaWord(
				LinePart(f, p, e),
				dynFormSizePair.second,
				CoordinateInspectElement(
					ParaElementInspector(
						make(0),
						make(0.),
						make(0.),
						makeWH(),
						make(0.),
						make(0.),
						make(0),
						make(false)
					),
					dynFormSizePair.first,
				),
				Empty(),
				interactivityIdM
			);
		}
		Space(s): {
			dynFormSizePair = getJoinableTextMetrics(s, true, ignoreLetterSpacing);
			ParaWord(Space(dynFormSizePair.first), dynFormSizePair.second, Empty(), Empty(), interactivityIdM);
		}
		ParaOneWord(wd): {
			oc = optimizeCols(wd);
			line : Form = Line(oc);
			getDynamicFormSize(line) |> s2w;
		}
		InspectElement(inspector, e): {
			makeWord = \word, form -> ParaWord(
				word,
				getJoinableTextMetrics(form, false, ignoreLetterSpacing).second,
				CoordinateInspectElement(inspector, form),
				Empty(),
				interactivityIdM
			);
			switch (e : InspectableElement) {
				Text(t, s): {
					makeWord(e, e);
				}
				LinePart(f, m, l): {
					makeWord(LinePart(f, m, l), m);
				}
				Space(t): {
					makeWord(Space(t), t);
				}
				default: {
					s = getJoinableTextMetrics(
						cast(e : InspectableElement -> Form),
						false,
						ignoreLetterSpacing
					);
					ParaWord(Empty(), s.second, InspectGhost(inspector, s.first), Empty(), interactivityIdM);
				}
			}
		}
		InteractiveParaAtomic(word, id) : {
			makeInteractiveParaWord(word, Some(id), ignoreLetterSpacing);
		}
		default: {
			opt = optimizeForm(cast(w : ParaAtomic -> Form));
			getJoinableTextMetrics(opt, false, ignoreLetterSpacing) |> s2w;
		}
	}
}

getJoinableTextMetrics(
	form: Form,
	getExactTopPoint : bool,
	ignoreLetterSpacing : bool
) -> Pair<Form, Behaviour<FormMetrics>> {
	if (ignoreLetterSpacing) {
		getDynamicFormSize2(form, getExactTopPoint)
	} else switch (form) {
		Text(__, st) : {
			letterSpacing = extractStruct(st, LetterSpacing(0.0)).spacing;
			metrics = getStaticFormSizeCached2(form, getExactTopPoint);
			Pair(
				form,
				const(FormMetrics(metrics with
					width = metrics.width + letterSpacing
				))
			);
		}
		default : getDynamicFormSize2(form, getExactTopPoint)
	}
}

makeParaWord(w : ParaAtomic, ignoreLetterSpacing : bool) -> ParaWord {
	switch (w) {
		InteractiveParaAtomic(word, id): makeInteractiveParaWord(
			word,
			Some(id),
			ignoreLetterSpacing
		);
		default : makeInteractiveParaWord(
			w,
			None(),
			ignoreLetterSpacing
		);
	}
}

isGhostable(f : Form) -> bool {
	switch (f) {
		Text(t, s): false;
		default: {
			true;
		}
	}
}

// We indent the first line if paragraph's first-line indent is positive
// or all of the other lines if the indent is negative.
getLineIndent(paraIndent : double, lineIndex : int) -> double {
	if (paraIndent > 0.0 && lineIndex == 0 || paraIndent < 0.0 && lineIndex != 0 ) abs(paraIndent)
	else 0.0;
}

ReflowResult ::= ReflowedLines, OverflowCase;
	ReflowedLines(lines : [ParaLine]);
	OverflowCase(availableWidt: double, overflowWidth : double);

// Given a bunch of words, a constant amount of width for paragraph, a first-line indent, this does the line breaking
reflowParaWords(
	words : [ParaWord],
	availableParaWidth : double,
	paraIndent : double,
	isFirstMark : bool,
	rtl : bool,
	catchOverflow : bool,
	fontSize : double
) -> ReflowResult {

	firstLineWidth = availableParaWidth - getLineIndent(paraIndent, 0);
	reflowResult = reflowParaWords2(
		words,
		firstLineWidth,
		firstLineWidth,
		[],
		ReflowedLines([]),
		paraIndent,
		isFirstMark,
		catchOverflow,
		fontSize
	);

	if (rtl) switch (reflowResult) {
		ReflowedLines(lines) : ReflowedLines(
			zipWith(
				lines,
				reflowTextToBiDi(map(lines, \l -> l.words), isWordEmpty, getWordText, setWordText, idfn, rtl),
				\l, ws -> ParaLine(ws, l.indent),
			)
		);
		OverflowCase(__, __) : reflowResult;
	} else reflowResult;
}

getParaWordWidth(word : ParaWord) -> double {
	getValue(word.metrics).width;
};

// here availableWidth is an amount of width for current line
reflowParaWords2(
	words : [ParaWord],
	availableWidth : double,
	remainingWithSpacing : double,
	currentWords : [ParaWord],
	result : ReflowResult,
	paraIndent : double,
	isFirstMark : bool,
	catchOverflow : bool,
	fontSize : double
) -> ReflowResult {
	addLine = \cw, indent -> switch (result) {
		ReflowedLines(lines) : ReflowedLines(
			if (cw != []) arrayPush(lines, ParaLine(cw, indent)) else lines
		);
		OverflowCase(__, __) : result;
	}

	addOverflow = \overflowWidth : double -> switch (result) {
		ReflowedLines(__) : OverflowCase(availableWidth, overflowWidth);
		OverflowCase(aWidth, oWidth) : OverflowCase(aWidth, max(oWidth, overflowWidth));
	}

	remaining = remainingWithSpacing - getUserDefinedWordSpacingPercent() * fontSize;

	detachWord = \w : ParaWord, detach : bool -> {
		switch (w.ghosted) {
			CoordinateInspectElement(inspector, __): nextDistinct(inspector.detached, detach);
			default: {
				switch (w.form) {
					CoordinateInspectElement(inspector, __): nextDistinct(inspector.detached, detach);
					default: {}
				}
			}
		}
	}

	lineIndex = switch (result) {
		ReflowedLines(lines) : length(lines);
		OverflowCase(__, __) : 1;
	};

	lineIndent = getLineIndent(paraIndent, lineIndex);
	nextLineWidth = availableWidth + lineIndent - getLineIndent(paraIndent, lineIndex + 1);

	if (words == []) {
		addLine(currentWords, lineIndent);
	} else {
		word = words[0];
		detachWord(word, false);
		rest = subrange(words, 1, length(words) - 1);
		extractParaAtomicForm = \word1 -> {
			w : ParaAtomic = word1.word;
			f : Form = switch(w : ParaAtomic) {
				ParaOneWord(__): Empty();
				InspectElement(__, __): Empty();
				LinePart(first, inline, last): inline;
				Space(s): s;
				default: cast(w : ParaAtomic -> Form);
			};
			f
		};
		getWordGhostedForm = \word1 -> {
			g = word1.ghosted;
			switch (g : Ghosted) {
				Empty(): extractParaAtomicForm(word1);
				Ghost(xc, yc, fm): g;
				InspectGhost(__, fm): g;
				CoordinateInspectElement(__, __): g;
			}
		};
		makeParaWord2 = \word2, form2 -> {
			g = word2.ghosted;
			replaceWordGhosted = \ -> {
				r : Ghosted = switch (g : Ghosted) {
					Empty(): Empty();
					Ghost(xc, yc, __): Ghost(xc, yc, form2);
					InspectGhost(inspector, __): InspectGhost(inspector, form2);
					CoordinateInspectElement(inspector, __): CoordinateInspectElement(inspector, form2);
				}
				r;
			};

			ghosted = replaceWordGhosted();
			ghostedForm = {
				switch (g : Ghosted) {
					Empty(): extractParaAtomicForm(word2);
					default: replaceWordGhosted();
				};
			};
			ParaWord(word2.word, const(getStaticFormSize(form2)), ghosted, ghostedForm, word2.interactivityIdM);
		};

		switch (word.word : ParaAtomic) {
			NewLine(): {
				reflowParaWords2(
					rest,
					nextLineWidth,
					nextLineWidth,
					[],
					addLine(currentWords, lineIndent),
					paraIndent,
					false,
					catchOverflow,
					fontSize
				);
			}
			LinePart(f, p, e): {
				width = getParaWordWidth(word);
				nextWordWidth = if (rest != []) getParaWordWidth(rest[0]) else 0.0;

				if (currentWords == []) {
					// If it is the first item on the line
					paraword = makeParaWord2(word, f);
					reflowParaWords2(
						rest,
						availableWidth,
						remaining - width,
						arrayPush(currentWords, paraword),
						result,
						paraIndent,
						false,
						catchOverflow,
						fontSize
					);
				} else if (width + nextWordWidth < remaining) {
					// If it is the middle item on the line
					paraword = makeParaWord2(word, p);
					reflowParaWords2(
						rest,
						availableWidth,
						remaining - width,
						arrayPush(currentWords, paraword),
						result,
						paraIndent,
						false,
						catchOverflow,
						fontSize
					);
				} else {
					// If it is the last item on the line
					paraword = makeParaWord2(word, e);
					reflowParaWords2(
						rest,
						nextLineWidth,
						nextLineWidth,
						[],
						addLine(arrayPush(currentWords, paraword), lineIndent),
						paraIndent,
						false,
						catchOverflow,
						fontSize
					);
				}
			}
			Space(s): {
				// LSTSBU-454
				// Spaces at the beginning of a line had been dropped before, but this block has been removed in order to have identical view in wigi editor and preview
				width = getParaWordWidth(word);
				if (width < remaining) {
					reflowParaWords2(
						rest,
						availableWidth,
						remaining - width,
						arrayPush(currentWords, setWordForm(word, getWordGhostedForm(word))),
						result,
						paraIndent,
						false,
						catchOverflow,
						fontSize
					);
				} else {
					detachWord(word, true);
					reflowParaWords2(
						rest,
						nextLineWidth,
						nextLineWidth,
						[],
						addLine(currentWords, lineIndent),
						paraIndent,
						false,
						catchOverflow,
						fontSize
					);
				}
			}
			default: {
				width = ref 0.0;
				getWordForm = \word1 -> {
					g = word1.ghosted;
					switch (g: Ghosted) {
						Empty(): extractParaAtomicForm(word1);
						Ghost(xc, yc, f): f;
						InspectGhost(__, f): f;
						CoordinateInspectElement(__, f): f;
					};
				};

				form = getWordForm(word);

				// Number of words that should be on the same line
				keepFormTogetherOrWithMark = \form1, i -> keepFormTogether(form1) || (isFirstMark && i <= 1);
				wordsCount = if (keepFormTogetherOrWithMark(form, 0)) {
					iteriUntil(words, \i, _word -> {
						switch (_word.word) {
							NewLine(): true;
							Space(__): true;
							LinePart(__, __, __): true;
							default: {
								f = getWordForm(_word);
								// it is no use to make line with zero-width content, so we continue counting if the width is 0
								// For example, single WigiRecursive in line get surrounded by WigiText("",[]), WigiRecursive, WigiText("",[])
								// such construction becomes [Empty, form, Empty] and we should keep them on the same line
								if (keepFormTogetherOrWithMark(f, i) || ^width == 0.0) {
									width := ^width + getParaWordWidth(_word);
									false;
								} else {
									true
								}
							};
						};
					});
				} else {
					width := getParaWordWidth(word);
					1;
				};

				if (^width <= remaining || currentWords == []) { // Overflow situation where a word is longer than the available width
					_words = subrange(words, 0, wordsCount);
					_rest = subrange(words, wordsCount, length(words) - wordsCount);

					newCurrentWords = fold(_words, currentWords, \acc, _word -> {
						arrayPush(acc, setWordForm(
							_word,
							getWordGhostedForm(_word)
						));
					});

					reflowParaWords2(
						_rest,
						availableWidth,
						remaining - ^width,
						newCurrentWords,
						if (catchOverflow && ^width > remaining) addOverflow(^width) else result,
						paraIndent,
						false,
						catchOverflow,
						fontSize
					);
				} else {
					reflowParaWords2(
						words,
						nextLineWidth,
						nextLineWidth,
						[],
						addLine(currentWords, lineIndent),
						paraIndent,
						false,
						catchOverflow,
						fontSize
					);
				}
			}
		}
	}
}

// This function gets the form and decides, should the next form kept together in the same line or should it be placed on the next line
keepFormTogether(form : Form) -> bool {
	switch (form) {
		Empty(): true;
		Text(t, __): t != " ";
		Alpha(__, f): keepFormTogether(f);
		Size2(__, f): keepFormTogether(f);
		Access(__, f): keepFormTogether(f);
		Rotate(__, f): keepFormTogether(f);
		Cursor(__, f): keepFormTogether(f);
		Visible(__, f): keepFormTogether(f);
		Inspect(__, f): keepFormTogether(f);
		Baseline(__, f): keepFormTogether(f);
		Scale(__, __, f): keepFormTogether(f);
		Mask2(f, __, __): keepFormTogether(f);
		SetPending(__, f): keepFormTogether(f);
		Available2(__, f): keepFormTogether(f);
		Filter2(__, f, __): keepFormTogether(f);
		Interactive(__, f): keepFormTogether(f);
		Constructor(f, __): keepFormTogether(f);
		Create2(current, fn): keepFormTogether(if (^current != Empty()) ^current else fn());
		Translate(__, __, f): keepFormTogether(f);
		Mutable2(f, __): keepFormTogether(getValue(f));
		Border(__, __, __, __, f): keepFormTogether(f);
		Crop2(__, __, __, __, f, __): keepFormTogether(f);
		Group(fs): any(map(fs, \f -> keepFormTogether(f)));
		Switch(i, fs): keepFormTogether(if (getValue(i) < length(fs)) fs[getValue(i)] else { println("STRANGE CASE"); Empty(); });
		Grid(__): false; // Unlikely to be a part of a word. But maybe should go recursive.
		Picture(__, __): false;
		Graphics(__, __): false;
		Video(__, __, __, __): false;
		TextInput(__, __, __): false;
		Camera(__, __, __, __): false;
		RealHTML2(__, __, __): false;
		ControlFocus(__, f): keepFormTogether(f);
		FullWindow(__, f): keepFormTogether(f);
		FullScreen(__, f): keepFormTogether(f);
		NativeForm(__, __, __, __): false;
		DynamicGroup2(__, __, __, __) : false; // Placeholder
	};
}

// renders lines and returns resulting forms along with their tight width at the rendering time
renderParaLines(
	availableWidth : double,
	lines : ParaLinesInfos,
	tightWidth : bool,
	alignment: ParaLineAlignment,
	interlineSpacing : double,
	topLineBaseline : bool,
	styleTree : Tree<int, [ParaElementInteractiveStyle]>,
	rtl : bool,
	isMarked : bool,
	interLineHighlighting : bool,
) -> Form {
	lasti = length(lines.lines) - 1;
	a : ParaLineAcc = foldi(lines.lines, ParaLineAcc(EmptyList(), 0.0, 0.0), \i : int, acc : ParaLineAcc, lineInfo : ParaLineInfo -> {
		// TODO: IF the line ends with NewLine (which we do not track here), we should consider it a last line and not justify
		lastLine = i == lasti;
		// On the last line, we do not justify.
		// Also if the line is single with TightWidth, any alignment is equal to StartAlign
		lineAlignment =
			if (alignment == Justify() && lastLine) {
				StartAlign()
			} else {
				alignment;
			}
		f = RenderLine(
			acc.y, lineInfo,
			availableWidth, lineAlignment,
			i, styleTree, rtl, isMarked,
			if (interLineHighlighting) {
				if (i == 0) 0. else - interlineSpacing / 2.0
			} else 0.0,
			if (interLineHighlighting){
				if (lastLine || i == 0) interlineSpacing / 2.0 else interlineSpacing
			} else 0.0
		);
		ParaLineAcc(
			Cons(Size2(const(zeroWH), f.form), acc.lines),
			acc.y + f.height + if (lastLine) 0. else interlineSpacing,
			if (i == 0 || !topLineBaseline) f.baseline else acc.baseline
		);
	});
	Baseline(const(a.baseline), Size2(const(WidthHeight(if (tightWidth) lines.maxLineWidth else max(availableWidth, lines.maxLineWidth), a.y)),
		Group(list2array(a.lines))
	))
}

getOptimizedLine(words : [ParaWord], alignment : ParaLineAlignment) -> [OptimizedLineElement]{
	if (isBiDiEnabled() || alignment == Justify()) {
		map(words, \w -> OptimizedLineElement(w.form, w.metrics, w.interactivityIdM));
	} else {
		optimizeLine(words);
	};
}

// Aligns all elements to the baseline, as well as calculates the height and the baseline
// Send out the result, width, height
RenderLine(
	y : double,
	info : ParaLineInfo,
	width : double,
	alignment : ParaLineAlignment,
	lineNumber : int,
	interactiveStyles : Tree<int, [ParaElementInteractiveStyle]>,
	rtl : bool,
	isMarked : bool,
	highLightOffset : double,
	interlineSpacing : double,
) -> ParaLineResult {
	// Alignment offset
	remaining = width - info.lineWidth;
	startOffset = if (rtl) max(remaining, 0.) else 0.;
	endOffset = if (rtl) 0. else max(0., remaining);
	alignmentOffset = ref switch (alignment : ParaLineAlignment) {
		LeftAlign(): if (rtl) endOffset else startOffset;
		RightAlign(): if (rtl) startOffset else endOffset;
		CenterAlign(): max(0.0, remaining / 2.0);
		StartAlign(): startOffset;
		EndAlign(): endOffset;
		Justify(): 0.0;
	} + (if (rtl) 0. else info.lineIndent);

	// Inter-word spacing for justification
	interWord = if (alignment == Justify() && length(info.optimizedLine) > 1) {
		nwords = length(info.optimizedLine);
		min(10.0, remaining / i2d(nwords - 1));
	} else 0.0;

	// The width and the individual forms
	lo : [Form] = filtermapi(info.optimizedLine, \i : int, elem : OptimizedLineElement -> {
		f = elem.f;
		m = getValue(elem.metrics);
		intStyleContains = \st : ParaElementInteractiveStyle -> eitherMap(
			elem.interactivityIdM,
			\id -> containsStruct(
				lookupTreeDef(interactiveStyles, id, []),
				st
			),
			false
		);

		isSuperscript = intStyleContains(ParaElementSuperscript());
		isSubscript = intStyleContains(ParaElementSubscript());

		dy = if (isSuperscript) -1.0 else info.lineAsc - m.baseline / (if (isSubscript) 2.0 else 1.0);
		applyStylesAndOffset = \fm : Form -> eitherMap(
			elem.interactivityIdM,
			\id -> applyIntStylesAndHighlighting(
				fm,
				lookupTreeDef(interactiveStyles, id, []),
				^alignmentOffset,
				y,
				dy,
				m.width,
				info.lineHeight,
				interWord,
				highLightOffset,
				interlineSpacing
			),
			Translate(const(^alignmentOffset), const(y + dy), fm)
		);

		justifyOffset = if (isMarked && i == 0 && lineNumber == 0) 0.0 else interWord;

		r = switch (f : GhostForm) {
			Empty(): None();
			Ghost(xc, yc, fm): {
				nextDistinct(xc, ^alignmentOffset);
				nextDistinct(yc, y + dy);
				Some(applyStylesAndOffset(Empty()));
			}
			InspectGhost(inspector, fm): {
				nextDistinct(inspector.x, ^alignmentOffset);
				nextDistinct(inspector.y, y + dy);
				nextDistinct(inspector.size, WidthHeight(m.width, m.height));
				nextDistinct(inspector.lineHeight, info.lineHeight);
				nextDistinct(inspector.lineNumber, lineNumber);
				Some(applyStylesAndOffset(Empty()));
			}
			CoordinateInspectElement(inspector, fm): {
				fo = applyStylesAndOffset(fm);
				nextDistinct(inspector.x, ^alignmentOffset);
				nextDistinct(inspector.y, y + dy);
				nextDistinct(inspector.size, WidthHeight(m.width, m.height));
				nextDistinct(inspector.lineHeight, info.lineHeight);
				nextDistinct(inspector.lineNumber, lineNumber);
				Some(fo);
			}
			default: Some(applyStylesAndOffset(cast(f : GhostForm -> Form)))
		}

		alignmentOffset := ^alignmentOffset + if (m.width == 0.0 || intStyleContains(IgnoreMetrics())) 0.0 else m.width + justifyOffset;
		r;
	});
	ParaLineResult(
		Group(lo),
		info.lineWidth,
		info.lineHeight,
		y + info.lineAsc
	);
}

applyIntStylesAndHighlighting(
	form : Form,
	styles : [ParaElementInteractiveStyle],
	xOffset : double,
	yOffset : double,
	baselineShift : double,
	width : double,
	height : double,
	interWord : double,
	highLightOffset : double,
	interlineSpacing : double,
) -> Form {
	getStyleForBackgroundFromCharStyle = \s -> eitherMap(
		tryExtractStruct(s, BackgroundFill(white)),
		\backFill -> [
			Fill(backFill.color),
			FillOpacity(extractStruct(s, BackgroundFillOpacity(1.0)).opacity)
		],
		[]
	);

	makeBackRect = \defStyle, dynStyle -> Rectangle(
		width + interWord,
		height + interlineSpacing + 0.1,
		replaceStructMany(
			getStyleForBackgroundFromCharStyle(defStyle),
			getStyleForBackgroundFromCharStyle(dynStyle)
		)
	);

	dynamicCharStyleBM = tryExtractStruct(styles, DynamicCharStyle(make([])));

	textAndBackPair = switch(form) {
		Text(txt, txtStyle) : {

			makeTxt = \dynStyle -> Text(
				txt,
				removeAllStructsMany(
					replaceStructMany(txtStyle, dynStyle),
					[BackgroundFill(0), BackgroundFillOpacity(0.0)]
				)
			);

			// We draw custom highlighting to fix gaps for cases of justifying alignment
			// And to have more straight and pretty highlighting.
			eitherMap(
				dynamicCharStyleBM,
				\dynCharStyle -> Pair(
					Select(dynCharStyle.styleB, \chStyle -> makeTxt(chStyle)),
					Select(dynCharStyle.styleB, \chStyle -> makeBackRect(txtStyle, chStyle))
				),
				Pair(makeTxt([]), makeBackRect(txtStyle, []))
			)
		}
		default : Pair(
			form,
			eitherMap(
				dynamicCharStyleBM,
				\dynCharStyle -> Select(dynCharStyle.styleB, \chStyle -> makeBackRect([], chStyle)),
				Empty()
			)
		);
	}

	modifiedForm = applyListenersAndModifiers(styles, textAndBackPair.first);

	Group([
		Translate(const(xOffset - interWord / 2.), const(yOffset + highLightOffset), textAndBackPair.second),
		Translate(const(xOffset), const(yOffset + baselineShift), modifiedForm),
	]);
}

applyListenersAndModifiers(styles : [ParaElementInteractiveStyle], form : Form) -> Form {
	if (form == Empty()) form else {
		formModifiersM = tryExtractStruct(styles, FormModifiers([]));
		modifiedForm = eitherMap(
			formModifiersM,
			\modifiers -> fold(modifiers.fns, form, \acc, fn -> fn(acc)),
			form
		);

		eventListenersM = tryExtractStruct(styles, EventListeners([]));
		eitherMap(
			eventListenersM,
			\el -> Interactive(el.listeners, modifiedForm),
			modifiedForm
		);	
	}
}


// Joins texts together to a single text element, along with the new metrics
optimizeLine(words : [ParaWord]) -> [OptimizedLineElement] {
	fold(words, makeList(), \acc : List<OptimizedLineElement>, word : ParaWord -> {
		f = word.form;
		metrics = word.metrics;
		intIdM = word.interactivityIdM;
		switch(acc){
			EmptyList(): Cons(OptimizedLineElement(f, metrics, intIdM), acc);
			Cons(p, tail_): {
				switch (p.f : GhostForm) {
					Text(t1, s1): {
						switch (f : GhostForm) {
							Text(t2, s2): {
								join = joinText(t1, s1, t2, s2);
								if (length(join) == 1 && intIdM == p.interactivityIdM) {
									t : Form = join[0];
									// TODO: Do not do this until at the end of the joining fest
									m = getStaticFormSize(t);

									joinedWordMetrics = FormMetrics(m with
										width = m.width + extractStruct(s2, LetterSpacing(0.0)).spacing
									);

									// Replace the last element with the newly joined
									Cons(
										OptimizedLineElement(t, const(joinedWordMetrics), intIdM),
										tail_
									);
								} else Cons(OptimizedLineElement(f, metrics, intIdM), acc);
							}
							default: {
								Cons(OptimizedLineElement(f, metrics, intIdM), acc);
							}
						}
					}
					default: Cons(OptimizedLineElement(f, metrics, intIdM), acc);
				}
			}
		}
	}) |> list2array
}

isWordEmpty(w: ParaWord) -> bool {
	switch(w.word) {
		Text(t, __): t == "";
		default: false;
	}
}

getWordText(w: ParaWord) -> string {
	switch(w.word) {
		Text(t, __): t;
		Empty(): switch (w.form) {
			InspectGhost(__, form): getFormText(form);
			Ghost(__, __, form): getFormText(form);
			default: "";
		};
		default: "";
	}
}

setWordText(w: ParaWord, t: string) -> ParaWord {
	switch(w.word) {
		Text(__, s): ParaWord(
			Text(t, s), w.metrics, w.ghosted,
			switch(w.form) {
				CoordinateInspectElement(inspector, form): CoordinateInspectElement(
					inspector,
					Text(t, getFormTextStyle(form))
				);
				Text(__, fs): Text(t, fs);
				default: w.form;
			},
			w.interactivityIdM
		);
		default: w;
	};
}

setWordForm(word: ParaWord, form: GhostForm) -> ParaWord {
	ParaWord(word.word, word.metrics, word.ghosted, form, word.interactivityIdM);
};

makeStoryWidthManager(paragraphs : int, infoOnly : bool) -> StoryWidthManager {
	paragraphWidths = generate(0, paragraphs, \__ -> make(0.0));
	storyWidth = make(0.0);

	paragraphManagers = map(paragraphWidths, \pw -> ParagraphWidthManager(storyWidth, pw, infoOnly));
	StoryWidthManager(
		paragraphManagers,
		\i -> elementAt(paragraphManagers, i, dummyParagraphWidthManager),
		storyWidth,
		infoOnly,
		\ -> {
			if (infoOnly) {
				nop
			} else {
				maxWidth = arrayMaxu(paragraphWidths);
				uns = [
					maxWidth.second,
					connectDistinct(maxWidth.first, storyWidth)
				];
				\ -> applyall(uns);
			}
		}
	)
}

// Not with others because referenced from flow9 repo.
isUrlParameterEscUnescHTMLTrue() -> bool {
	isUrlParameterTrue("753");
}
