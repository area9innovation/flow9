preprocessor = ws block*:b lastlinecomment?:lc { Preprocess(concat(:b, :lc)) };

block = conditionalBlock | content;

conditionalBlock = '#ifdef' wsOrComments conditionalPair:pif ('#elif' wsOrComments conditionalPair)*:pelif ('#else' wsOrComments block*:p { :p })?:else '#endif' linecomment? comment? ws { ConditionalBlock(:pif, :pelif, unarray(:else)) };
conditionalPair = name:n block*:p { ConditionalPair(:n, :p) };

content = (!keywords !twoslashes !slashstar char)+$str { Content($str) }
	| linecomment$c { Content($c) }
	| comment$c { Content($c) };

keywords = '#ifdef' | '#elif' | '#else' | '#endif';
name = nameNoWs$n wsOrComments {$n};
nameNoWs = letter (letterOrDigit*);
letter = 'a'-'z' | 'A'-'Z' | '_';
letterOrDigit = digit | letter;
digit = '0'-'9';
ws = whitespace*;
wsOrComments = (whitespace | comment | linecomment)*;
whitespace = ' ' | '\n' | '\t' | '\u000d';
comment = slashstar nonstarchar* '*/';
nonstarchar = !'*/' char;
twoslashes = '//';
slashstar = '/*';
lastlinecomment = (twoslashes nonlinebreakchar*)$c { Content($c) };
linecomment = twoslashes nonlinebreakchar* '\n';
nonlinebreakchar = !'\n' char;
char = '\u0000' - '\uffff';