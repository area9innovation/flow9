
export {
	// Returns current url using loaderInfo
	native loaderUrl : io () -> string = Native.loaderUrl;

	// Open url in target window
	native getUrlN : io (url : string, target : string) -> void = Native.getUrl;

	// Open url in target window
	native getUrl2 : io (url : string, target : string) -> bool = Native.getUrl2;

	// Open url in target window and close it after delay ms
	native getUrlNAutoclose : io (url : string, target : string, delay : int) -> void = Native.getUrlAutoclose;

	// Opens a new window with url or redirects current window to url (if target == "_self")
	// See possible values of target in https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#target
	getUrl : (url : string, target : string) -> void;
	getUrlAutoclose : (url : string, target : string, delay : int /*ms*/) -> void;

	// Sets additional redirect function for getUrl.
	//
	// Example:
	//	setRedirectAdditionalFn(
	//		\url, onDone -> showWarning("You are about to be redirected", \ -> onDone(transformUrl(url)))
	//	)
	setRedirectAdditionalFn(fn : (string, (string) -> void) -> void) -> void;
}

//redirectFn
redirectAdditionalFn : ref (string, (string) -> void) -> void = ref \url, fn -> { fn(url) };

setRedirectAdditionalFn(fn : (string, (string) -> void) -> void) -> void {redirectAdditionalFn := fn;}

getUrl(url : string, target : string) {
	^redirectAdditionalFn(url, \transformedUrl -> {getUrlN(transformedUrl, target)});
}

getUrlAutoclose(url : string, target : string, delay : int /*ms*/) {
	^redirectAdditionalFn(url, \transformedUrl -> {getUrlNAutoclose(transformedUrl, target, delay)});
}

// native fallback body
getUrl2(url : string, target : string) { getUrl(url, target); true; }
getUrlNAutoclose(url : string, target : string, delay : int) { getUrlN(url, target); }