import ds/treeutils;
// import algorithms; // For tests only

export {
	// Create a sparse array. Provide a function which can produce a range of values
	makeSparseArray(
		retrieveRange : (start : int, end : int, cb : ([?]) -> void) -> void,
		retrieveLength : (cb : (int) -> void) -> void
	)-> SparseArray<?>;

	// If we have a complete, static array, we do not need a generating function
	array2SparseArray(vals : [?]) -> SparseArray<?>;
	sparseArray2array(s : SparseArray<?>, cb : ([?]) -> void) -> void;

	// What are the values in this given range? Will call the function to generate values if we do
	// not have them cached. Up to, but not including end.
	getSparseArrayRange(s : SparseArray<?>, start : int, end : int, cb : ([?]) -> void) -> void;
	getSparseArrayLength(s : SparseArray<?>, cb : (int) -> void) -> void;
	getSparseArrayIndex(s : SparseArray<?>, index : int, cb : (?) -> void) -> void;

	removeSparseArray(s : SparseArray<?>, index : int, len : int) -> SparseArray<?>;
	insertSparseArray(s1 : SparseArray<?>, index : int, s2 : SparseArray<?>) -> SparseArray<?>;
	removeSparseArrayIndex(s : SparseArray<?>, index : int) -> SparseArray<?>;
	insertSparseArrayIndex(s : SparseArray<?>, index : int, value : ?) -> SparseArray<?>;

	iterSparseArray(s : SparseArray<?>, fn : (?) -> void) -> void;
	mapSparseArray(s : SparseArray<?>, fn : (?) -> ??) -> SparseArray<??>;
	mapWithPreviousSparseArray(s : SparseArray<?>, first : (?) -> ??, rest : (?, ?) -> ??) -> SparseArray<??>;
	subrangeSparseArray(s : SparseArray<?>, from : int, len : int) -> SparseArray<?>;
	concatSparseArray(s1 : SparseArray<?>, s2 : SparseArray<?>) -> SparseArray<?>;
	reverseSparseArray(s : SparseArray<?>) -> SparseArray<?>;

	sparseArray2string(s : SparseArray<?>, value2string : (v : ?, level : int) -> string, level : int) -> string;

	emptySparseArray = array2SparseArray([]);
}

// A sparse array where we can retrieve values in a given range up to, but not including end
SparseArray(
	// This is a cache of the values in the array. Key is the index in the array, value
	// is a range of values we have from that point on. We have to maintain the
	// invariant that our ranges are continuous in the sense that it would be wrong
	// to represent an array ["a", "b"] as {0: ["a"], 1: ["a"]}. Instead, we should
	// combine and have {0: ["a", "b"]}
	mutable ranges : Tree<int, SparseRange<?>>,
	// The function which can generate/retrieve values at the given point.
	retrieveRange : (start : int, end : int, cb : ([?]) -> void) -> void,
	// The function which can generate/retrieve array length at the given point.
	retrieveLength : (cb : (int) -> void) -> void
);

	SparseRange<?> ::= SparseArrayRange<?>, SparseArrayRangeRequest;

		SparseArrayRange(start : int, end : int, range : [?]);
		SparseArrayRangeRequest(start : int, end : int, mutable cb : [() -> void]);

makeSparseArray(
	retrieveRange : (start : int, end : int, cb : ([?]) -> void) -> void,
	retrieveLength : (cb : (int) -> void) -> void
) -> SparseArray<?> {
	SparseArray(makeTree(), retrieveRange, retrieveLength);
}

array2SparseArray(vals : [?]) -> SparseArray<?> {
	SparseArray(
		makeTree1(0, SparseArrayRange(0, length(vals), vals)),
		\f, t, cb ->
			cb(subrange(vals, f, t - f)),
		\cb ->
			cb(length(vals))
	);
}

sparseArray2array(s : SparseArray<?>, cb : ([?]) -> void) -> void {
	getSparseArrayLength(s, \l -> getSparseArrayRange(s, 0, l, \r -> cb(r)))
}

/*
TODO:
- Add an option to define how many elements are worth caching. I.e.
  if we have numbers 10-20 in cache, and request 0-100, maybe it is not worth
  it to reuse the cache, coompared to retrieving all of the elements in one request
  instead of 2.
*/

emptySparseArrayRange = SparseArrayRange(0, 0, []);

getSparseArrayRange(s : SparseArray<?>, start : int, end : int, cb : ([?]) -> void) -> void {
	if (start < end) {
		// Do we have anything before we can reuse?
		lb = findTreeLowerKey(s.ranges, start, -1);

		// Maybe there is a part in the middle we can reuse?
		ub = findTreeUpperKey(s.ranges, start + 1, -1);

		// What do we have below us?
		lower0 = if (lb == -1) emptySparseArrayRange else lookupTreeDef(s.ranges, lb, emptySparseArrayRange);

		switch (lower0 : SparseRange) {
			SparseArrayRange(__, __, lower1): {
				// Find how much of the end of what is below us we can reuse, and extract that
				lowerReuse = if (lb == -1 || lb + length(lower1) < start) -1 else max(0, start - lb);
				lower = if (lb == -1 || lowerReuse == -1) [] else subrange(lower1, lowerReuse, end - lowerReuse);

				// OK, there might be a part after the start, which we can use
				upper0 = if (ub == -1 || ub >= end) emptySparseArrayRange else lookupTreeDef(s.ranges, ub, emptySparseArrayRange);

				switch (upper0 : SparseRange) {
					SparseArrayRange(__, __, upper1): {
						upper = subrange(upper1, 0, max(0, end - ub));
						// We can push the start by reusing things from below
						newStart = start + length(lower);

						if (upper == []) {
							// We have something in the start we can reuse, so just retrieve the end bit
							// If newStart is bigger than end just subrange already retieved lower part
							if (newStart >= end) {
								cb(subrange(lower, 0, end - start));
							} else {
								r = SparseArrayRangeRequest(newStart, end, []);
								s.ranges ::= setTree(s.ranges, newStart, r);

								s.retrieveRange(newStart, end, \rang -> {
									res = concat(lower, rang);
									updateSpareArrayCache(s, start, res);
									cb(res);
									applyall(r.cb);
								});
							}
						} else {
							// OK, we have a middle part. First, let us get what is after the first part, but before the mid part
							// If upper bound is bigger than end just retrieve missing range
							if (ub > end) {
								if (newStart >= end) {
									cb(lower)
								} else {
									r = SparseArrayRangeRequest(newStart, end, []);
									s.ranges ::= setTree(s.ranges, newStart, r);

									s.retrieveRange(newStart, end, \beforeMid -> {
										res = concat(lower, beforeMid);
										updateSpareArrayCache(s, start, res);
										cb(res);
										applyall(r.cb);
									})
								}
							} else {
								afterMid = ub + length(upper);

								if (newStart >= ub) {
									if (afterMid >= end) {
										res = concat(lower, upper);
										updateSpareArrayCache(s, start, res);
										cb(res);
									} else {
										// Recursively check for other mid parts
										getSparseArrayRange(s, afterMid, end, \after -> {
											res = concat3(lower, upper, after);
											updateSpareArrayCache(s, start, res);
											cb(res);
										});
									}
								} else {
									r = SparseArrayRangeRequest(newStart, ub, []);
									s.ranges ::= setTree(s.ranges, newStart, r);

									s.retrieveRange(newStart, ub, \beforeMid -> {
										if (afterMid >= end) {
											res = concat3(lower, beforeMid, upper);
											updateSpareArrayCache(s, start, res);
											cb(res);
											applyall(r.cb);
										} else {
											// Recursively check for other mid parts
											getSparseArrayRange(s, afterMid, end, \after -> {
												res = concatA([lower, beforeMid, upper, after]);
												updateSpareArrayCache(s, start, res);
												cb(res);
												applyall(r.cb);
											});
										}
									})
								}
							}
						}
					}
					SparseArrayRangeRequest(__, __, __): {
						upper0.cb ::= arrayPush(upper0.cb, \ -> getSparseArrayRange(s, start, end, cb));
					}
				}
			}
			SparseArrayRangeRequest(__, __, __): {
				lower0.cb ::= arrayPush(lower0.cb, \ -> getSparseArrayRange(s, start, end, cb));
			}
		}
	} else {
		cb([]);
	}
}

getSparseArrayLength(s : SparseArray<?>, cb : (int) -> void) -> void {
	s.retrieveLength(cb);
}

getSparseArrayIndex(s : SparseArray<?>, index : int, cb : (?) -> void) -> void {
	getSparseArrayRange(s, index, index + 1, \v -> if (length(v) > 0) cb(v[0]));
}

// OK, we have a new entry that has to go into the cache.
// We have the invariant that we know this is not colliding with other entries
updateSpareArrayCache(s : SparseArray<?>, start0 : int, values0 : [?]) -> void {
	start = ref start0;
	values = ref values0;

	// println("updateSpareArrayCache: " + toString(start0) + " " + toString(length(values0)));
	// println(s.ranges);

	// Filter out all entries which are dominated by this one
	// Concat intersecting parts tp values
	s.ranges ::= filterTree(s.ranges, \s2, v -> {
		end = ^start + length(^values);
		e2 = v.end;

		if (s2 >= ^start) {
			switch (v : SparseRange) {
				SparseArrayRange(__, __, rang): {
					if (e2 <= end) {
						false;
					} else if (s2 <= end) {
						values := concat(^values, subrange(rang, end - s2, v.end - v.start));
						false;
					} else {
						true;
					}
				}
				SparseArrayRangeRequest(__, __, __): s2 != ^start;
			}
		} else if (e2 <= end) {
			switch (v : SparseRange) {
				SparseArrayRange(__, __, rang): {
					if (e2 >= ^start) {
						values := concat(subrange(rang, 0, v.end - v.start + ^start - e2), ^values);
						start := s2;

						false;
					} else {
						true;
					}
				}
				SparseArrayRangeRequest(__, __, __): s2 != ^start;
			}
		} else {
			true;
		}
	});

	s.ranges ::= setTree(s.ranges, ^start, SparseArrayRange(^start, ^start + length(^values), ^values));

	// println("finish updateSpareArrayCache: " + toString(start0) + " " + toString(length(values0)));
	// println(s.ranges);
}

removeSparseArray(s : SparseArray<?>, index : int, len : int) -> SparseArray<?> {
	concatSparseArray(subrangeSparseArray(s, 0, index), subrangeSparseArray(s, index + len, -1))
}

insertSparseArray(s1 : SparseArray<?>, index : int, s2 : SparseArray<?>) -> SparseArray<?> {
	concatSparseArray(concatSparseArray(subrangeSparseArray(s1, 0, index), s2), subrangeSparseArray(s1, index, -1))
}

removeSparseArrayIndex(s : SparseArray<?>, index : int) -> SparseArray<?> {
	removeSparseArray(s, index, 1);
}

insertSparseArrayIndex(s : SparseArray<?>, index : int, value : ?) -> SparseArray<?> {
	insertSparseArray(s, index, array2SparseArray([value]))
}

iterSparseArray(s : SparseArray<?>, fn : (?) -> void) -> void {
	getSparseArrayLength(s, \l -> getSparseArrayRange(s, 0, l, \v -> iter(v, fn)));
}

mapSparseArray(s : SparseArray<?>, fn : (?) -> ??) -> SparseArray<??> {
	makeSparseArray(
		\f, t, cb ->
			getSparseArrayRange(s, f, t, \r -> cb(map(r, fn))),
		\cb ->
			getSparseArrayLength(s, cb)
	)
}

mapWithPreviousSparseArray(s : SparseArray<?>, first : (?) -> ??, rest : (?, ?) -> ??) -> SparseArray<??> {
	makeSparseArray(
		\f, t, cb ->
			if (f > 0)
				getSparseArrayRange(s, f - 1, t, \r -> cb(mapWithPrevious(tail(r), \v -> rest(r[0], v), rest)))
			else
				getSparseArrayRange(s, f, t, \r -> cb(mapWithPrevious(r, first, rest))),
		\cb ->
			getSparseArrayLength(s, cb)
	)
}

subrangeSparseArray(s : SparseArray<?>, from : int, len : int) -> SparseArray<?> {
	makeSparseArray(
		\f, t, cb ->
			getSparseArrayRange(s, f + from, min(t, len) + from, cb),
		\cb ->
			getSparseArrayLength(s, \l -> cb(if (len >= 0) min(l - from, len) else l - from))
	)
}

concatSparseArray(s1 : SparseArray<?>, s2 : SparseArray<?>) -> SparseArray<?> {
	makeSparseArray(
		\f, t, cb ->
			getSparseArrayLength(s1, \l1 ->
				if (t < l1)
					getSparseArrayRange(s1, f, t, cb)
				else if (f >= l1)
					getSparseArrayRange(s2, f - l1, t - l1, cb)
				else
					getSparseArrayRange(s1, f, t, \r1 -> getSparseArrayRange(s2, f - l1, t - l1, \r2 -> cb(concat(r1, r2))))
			),
		\cb ->
			getSparseArrayLength(s1, \l1 -> getSparseArrayLength(s2, \l2 -> cb(l1 + l2)))
	)
}

reverseSparseArray(s : SparseArray<?>) -> SparseArray<?> {
	makeSparseArray(
		\f, t, cb ->
			getSparseArrayLength(s, \l -> getSparseArrayRange(s, l - t, l - f, \v -> cb(reverseA(v)))),
		\cb ->
			getSparseArrayLength(s, cb)
	)
}

sparseArray2string(s : SparseArray<?>, value2string : (v : ?, level : int) -> string, level : int) -> string {
	strGlue(
		map(tree2pairs(s.ranges), \v ->
			switch (v.second : SparseRange) {
				SparseArrayRange(__, __, rang): {
					strRepeat("\t", level) + toString(v.first) + ": [\n" + concatStrings(map(rang, \vv -> value2string(vv, level + 1) + "\n"))
				}
				SparseArrayRangeRequest(__, __, __): {
					strRepeat("\t", level) + toString(v.first) + ": RangeRequest"
				}
			}
		),
		"]\n"
	)
}


// TESTS

/*
dumpSparseArray(s : SparseArray<?>) -> void {
	traverseInOrder(s.ranges, \k, v -> {
		println(toString(k) + ":" + toString(v))
	});
}

testSparseArrayRange(s : SparseArray<int>, from : int, to : int) -> void {
	getSparseArrayRange(s, from, to, \v -> assertEquals(v, enumFromTo(from, to - 1)));
}

makeTestSparseArray() -> SparseArray<int> {
	cache = ref [];

	makeSparseArray(
		\s, e, cb -> {
			// println("range locked: " + toString(s) + " " + toString(e));

			timer(round(random() * 5.0), \ -> {
				println("retrieving from " + i2s(s) + " to " + i2s(e));
				r = generate(s, e, \i -> i);

				assert(!exists(r, \v -> contains(^cache, v)), "already in cache: " + toString(r) + " " + toString(^cache));

				println(r);

				// println("updating cache: " + toString(r) + " " + toString(^cache));
				cache := uniq(concat(^cache, r));

				cb(r);

				// println("range unlocked: " + toString(s) + " " + toString(e));
			})
		},
		\cb ->
			timer(round(random() * 5.0), \ -> cb(500))
	);
}

main() {
	a = makeTestSparseArray();

	testSparseArrayRange(a, 0, 10);
	testSparseArrayRange(a, 30, 40);
	testSparseArrayRange(a, 50, 60);
	testSparseArrayRange(a, 0, 100);
	testSparseArrayRange(a, 50, 60);
	testSparseArrayRange(a, 30, 40);
	testSparseArrayRange(a, 0, 1);
	testSparseArrayRange(a, 50, 200);

	// Randomized tests
	generate(0, 1000, \__ -> {
		b = makeTestSparseArray();

		testSparseArrayRange(b, 0, 10);
		testSparseArrayRange(b, 30, 40);
		testSparseArrayRange(b, 50, 60);
		testSparseArrayRange(b, 0, 100);
		testSparseArrayRange(b, 50, 60);
		testSparseArrayRange(b, 30, 40);
		testSparseArrayRange(b, 0, 1);
		testSparseArrayRange(b, 50, 200);

		generate(0, 100, \__ -> {
			from = round(random() * 100.0);
			to = from + round(random() * 100.0);

			testSparseArrayRange(b, from, to);
		});
	});

	println("ALL TESTS PASSED");

	quit(0);
}
*/