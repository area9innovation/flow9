// Â© Copyright 2014 Area9 Technologies.

import ds/tree;
import math/math;

export {
	// Compares two arrays, and returns a list of operations to apply to change the old to the new
	arrayDiff(old : [?], new : [?], fullProtocol : bool) -> [ArrayOperation<?>];
	arrayDiffWithSwapp(old : [?], new : [?], fullProtocol : bool) -> [ArrayOperationWithSwapp<?>];

	// reverse protocol, for backward compatibility
	arrayDiffReversed(old : [?], new : [?], fullProtocol : bool) -> [ArrayOperation<?>];

	// Livenshtein-based protocol
	applyProtocol(
		source : [?],
		protocol : [ArrayOperation<?>],
		insertFn : (newValue : ?) -> ?, //
		replaceFn : (oldValue : ?, newValue : ?) -> ?,
		removeFn : (oldValue : ?) -> Maybe<?> // if None() then element will be removed, if Some - replaced
	) -> [?];

	// Damerhau-Livenshtein - based protocol (accepts Livenshtein-based protocol too because it is subset)
	applyProtocolWithSwapp(
		source : [?],
		protocol : [ArrayOperationWithSwapp<?>],
		insertFn : (newValue : ?) -> ?, //
		replaceFn : (oldValue : ?, newValue : ?) -> ?,
		removeFn : (oldValue : ?) -> Maybe<?>, // if None() then element will be removed, if Some - replaced
		swapFn : (Pair<?, ?>) -> Pair<?, ?>
	) -> [?];

	applyProtocolInvariant(source : [?], protocol : [ArrayOperationWithSwapp<?>]) -> [?];

	// Levenshtein, see https://en.wikipedia.org/wiki/Levenshtein_distance
	ArrayOperation<?> ::= ArrayNop, ArrayInsert<?>, ArrayReplace<?>, ArrayRemove;
		ArrayNop(oldindex : int, newindex : int);
		ArrayInsert(oldindex : int, newindex : int, value : ?);
		ArrayReplace(oldindex : int, newindex : int, value : ?);
		ArrayRemove(oldindex : int, newindex : int);

	// Damerau-Levenshtein, see https://en.wikipedia.org/wiki/Damerau%E2%80%93Levenshtein_distance
	ArrayOperationWithSwapp<?> ::= ArrayOperation<?>, ArraySwap<?>;
		ArraySwap(oldindex : int, newindex : int, value : Pair<?, ?>);
}

arrayDiff(s : [?], t : [?], fullProtocol : bool) -> [ArrayOperation<?>] {
	if (s == t) []
	else filtermap(
		arrayDiffExt(s, t, fullProtocol, false),
		\p : ArrayOperationWithSwapp<?> -> {
			switch (p) {
				ArrayOperation(): {
					m : Maybe<ArrayOperation> = Some(p);
					m;
				}
				default: None();
			}
		}
	)
}

arrayDiffWithSwapp(s : [?], t : [?], fullProtocol : bool) -> [ArrayOperationWithSwapp<?>] {
	arrayDiffExt(s, t, fullProtocol, true)
}

// Constructs a protocol "script" which changes one array to another.
// fullProtocol leaves ArrayNop operations in the result (false by default)
// Basically, this is Levenshtein
arrayDiffExt(s : [?], t : [?], fullProtocol : bool, swapp : bool) -> [ArrayOperationWithSwapp<?>] {
	m = length(s);
	n = length(t);

	// Initialize the matrix of costs and operations
	d : [[ref Pair<int, ArrayOperationWithSwapp<?>>]] = generate(0, m + 1, \i -> {
		generate(0, n + 1, \j -> {
			p : Pair<int, ArrayOperationWithSwapp<?>> = if (i == 0 && j != 0) {
				Pair(j, ArrayInsert(0, j - 1, t[j - 1]));
			} else {
				if (j == 0 && i != 0) {
					Pair(i, ArrayRemove(i - 1, 0));
				} else {
					Pair(0, ArrayNop(-1, -1));
				}
			};
			ref p
		});
	});

	dij = \ii, jj -> {
		^((d[ii])[jj]).first;
	}

	fori(1, m, \i -> { // s
		si = s[i - 1];
		fori(1, n, \j -> { // t
			tj = t[j - 1];
			if (si == tj) {
				// Equal as is. No change
				d[i][j] := Pair(dij(i - 1, j - 1), ArrayNop(i-1, j-1));
			} else {
				delcost = dij(i - 1, j) + 1;
				inscost = dij(i, j - 1) + 1;
				repcost = dij(i - 1, j - 1) + 1;

				cost = min3(
					delcost,  // Deletion
					inscost, // Insertion
					repcost // Replacement
				);

				d[i][j] := Pair(cost,
					if (cost == repcost) {
						ArrayReplace(i - 1, j - 1, tj)
					} else if (cost == inscost) {
						ArrayInsert(i, j - 1, tj);
					} else if (cost == delcost) {
						ArrayRemove(i - 1, j)
					} else {
						ArrayNop(i-1, j-1)
					}
				);

				if (swapp && i > 1 && j > 1) {
					si2 = s[i - 2];
					tj2 = t[j - 2];
					if (si == tj2 && si2 == tj) {
						swapcost = dij(i - 2, j - 2) + 1;
						if (swapcost <= cost) {
							d[i][j] := Pair(swapcost, ArraySwap(i - 2, j - 2, Pair(si, tj)));
						}
					}
				}
			}
		});
	});
/*
	getName = \op : ArrayOperationWithSwapp -> {
		switch (op) {
			ArrayNop(oldindex, newindex): "N";
			ArrayInsert(oldindex, newindex, value): "I";
			ArrayReplace(oldindex, newindex, value): "R";
			ArrayRemove(oldindex, newindex): "D";
			ArraySwap(oldindex, newindex, value): "S";
		} + "(" + i2s(op.oldindex) + ", " + i2s(op.newindex) + ")";
	}

	println(foldi(d[0], lpad("", " ", 6 + 14), \i, acc, __ -> {
		acc + lpad(if (i == 0) "" else toString(t[i - 1]), " ", 12) + ", "
	}));

	iteri(d, \y, row : [ref Pair<int, ArrayOperationWithSwapp>] -> {
		println(rpad(i2s(y), " ", 2) + " (" +  rpad((if (y > 0) toString(s[y - 1])  else ""), " ", 13) + "): " + fold(row, "", \acc, r : ref Pair<int, ArrayOperationWithSwapp> -> {
			p = ^r;
			acc + lpad(i2s(p.first), " ", 2) + " " + lpad(getName(p.second), " ", 9) + ", "
		}));
	});
*/
	makeArrayDiffOps(d, m, n, [], fullProtocol);
}

arrayDiffReversed(old : [?], new : [?], fullProtocol : bool) -> [ArrayOperation<?>] {
	reverseA(arrayDiff(old, new, fullProtocol))
}

makeArrayDiffOps(d : [[ref Pair<int, ArrayOperationWithSwapp<?>>]], y : int, x : int, acc : [ArrayOperationWithSwapp<?>], fullProtocol : bool) -> [ArrayOperationWithSwapp<?>] {
	c = ^(d[y][x]);
	// println(i2s(y) + "," + i2s(x) + ": " + toString(c));
	if (c.second.oldindex == -1 || c.second.newindex == -1) acc
	else {
		nacc = if (!fullProtocol && isSameStructType(c.second, ArrayNop(0, 0))) {
			// Skip the NOPs
			acc;
		} else {
			arrayPush(acc, c.second);
		};
		makeArrayDiffOps(d, c.second.oldindex, c.second.newindex, nacc, fullProtocol)
	}
}

applyProtocol(
	source : [?],
	protocol : [ArrayOperation<?>],
	insertFn : (?) -> ?,
	replaceFn : (?, ?) -> ?,
	removeFn : (?) -> Maybe<?>
) -> [?] {
	applyProtocolWithSwapp(source, protocol, insertFn, replaceFn, removeFn, idfn)
}

applyProtocolWithSwapp(
	source : [?],
	protocol : [ArrayOperationWithSwapp<?>],
	insertFn : (?) -> ?,
	replaceFn : (?, ?) -> ?,
	removeFn : (?) -> Maybe<?>,
	swapFn : (Pair<?, ?>) -> Pair<?, ?>
) -> [?] {
	fold(protocol, source, \acc, op -> {
		switch (op : ArrayOperationWithSwapp) {
			ArrayNop(__, __): acc;
			ArrayInsert(i, __, v): insertArray(acc, i, insertFn(v));
			ArrayReplace(i, __, v): replace(acc, i, replaceFn(acc[i], v));
			ArrayRemove(i, __): {
				eitherFn(
					removeFn(acc[i]),
					\newVal -> replace(acc, i, newVal),
					\ -> removeIndex(acc, i)
				)
			}
			ArraySwap(i, __, v): {
				swapped = swapFn(v);
				replace(
					replace(acc, i + 1, swapped.second),
					i,
					swapped.first
				);
			}
		}
	});
}

applyProtocolInvariant(source : [?], protocol : [ArrayOperationWithSwapp<?>]) -> [?] {
	applyProtocolWithSwapp(source, protocol,
		idfn,
		\__, newValue -> newValue,
		\__ -> None(),
		idfn
	);
}
