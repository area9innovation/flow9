// © Copyright 2011 Area9 Technologies.

import maybe;

// Single-linked list. For a double-linked list, see dlist
export {

	List<?> ::= EmptyList, Cons<?>;
		EmptyList();
		// Add head to the list "tail"
		Cons(head : ?, tail : List<?>);
	// O(1)
	makeList() -> List<?>;
	makeList1(v : ?) -> List<?>;
	// O(n). Tail recursion
	listAt(xs : List<?>, i : int, x0 : ?) -> ?;
	// O(1)
	headList(l : List<?>, default : ?) -> ?;
	// O(1)
	tailList(l : List<?>) -> List<?>;

	// O(n). Tail recursion
	countList(l : List<?>) -> int;

	// O(n). Tail recursion. Warning: elements are generated from the end
	// Range: [from..to)
	generateList(from : int, to : int, fn : (int) -> ?) -> List<?>;
	generateListRec(start : int, end : int, fn : (int) -> ?, acc : List<?>) -> List<?>;

	// O(n) in l1. Tail recursion
	concatList(l1 : List<?>, l2 : List<?>) -> List<?>;

	// O(n). Tail recursion
	reverseList(xs : List<?>) -> List<?>;

	// Convert a list to a string - notice, it does this in reverse order!  O(n)
	native list2string :(l : List<string>) -> string = Native.list2string;
	native list2array : (l : List<?>) -> [?] = Native.list2array;

	// Apply function to list elements. O(n). Tail recursion
	applyList : (l : List<?>, fn : (?) -> void) -> void;

	// With a list of functions, call them all. O(n). Tail recursion
	callList : (l : List<() -> void>) -> void;

	// Apply function to list elements. O(n). Tail recursion
	mapList : (l : List<?>, fn : (?) -> ??) -> List<??>;
	mapiList : (l : List<?>, fn : (int, ?) -> ??) -> List<??>;

	// Creates a new list, whose elements are selected from 'l' with a function 'fn' returns Some(v). O(n). Tail recursion
	filtermapList : (l : List<?>, fn : (?) -> Maybe<??>) -> List<??>;

	// Apply a collecting function. O(n). Tail recursion
	foldList : (l : List<?>, init : ??, fn : (??, ?) -> ??) -> ??;
	foldiList : (l : List<?>, init : ??, fn : (int, ??, ?) -> ??) -> ??;

	// Apply a collecting function passing Cons containing the element. O(n). Tail recursion
	foldpList : (l : List<?>, init : ??, fn : (List<?>, ??, ?)->??) -> ??;

	// Iterate a function over a list. O(n). Tail recursion
	iterList : (l : List<?>, fn : (?) -> void) -> void;
	iteriList : (l : List<?>, fn : (int, ?) -> void) -> void;

	// Does the list contain the given element? O(n). Tail recursion
	containsList : (l : List<?>, v : ?) -> bool;

	existsList(l : List<?>, fn : (?) -> bool) -> bool;

	// Find an element in a list. O(n). Tail recursion
	findList : (l : List<?>, fn : (?) -> bool) -> Maybe<?>;

	// Same, returning sublist with head contains matching element. O(n). Tail recursion
	findpList : (l : List<?>, fn : (List<?>, ?) -> bool) -> List<?>;

	// Reduce the list to those that match the criteria. O(n). Tail recursion
	filterList(l : List<?>, fn : (?) -> bool) -> List<?>;

	// Add a value to the head of a list reference. O(1)
	rlistPush(l : ref List<?>, s : ?) -> void;

	// O(1)
	isEmptyList : (xs : List<?>) -> bool;
}

makeList() -> List<?> {
	EmptyList();
}

makeList1(v : ?) -> List<?> {
	Cons(v, EmptyList())
}

listAt(xs, i, x0) {
	if (i < 0) x0
	else switch (xs : List) {
				EmptyList(): x0;
				Cons(h, t): if (i == 0) h else listAt(t, i - 1, x0);
	};
}

native headList : (l : List<?>, default : ?) -> ? = Native.headList;

headList(l, default) {
	switch (l : List) {
		EmptyList(): default;
		Cons(h, t): h;
	}
}

native tailList : (l : List<?>) -> List<?> = Native.tailList;
tailList(l) {
	switch (l : List) {
		EmptyList(): l;
		Cons(h, t): t;
	}
}

countList(l) {
	countListRec(l, 0)
}
countListRec(l : List<?>, count : int) -> int {
	switch (l : List<?>) {
		EmptyList(): count;
		Cons(__, tail0): countListRec(tail0, count + 1);
	}
}

generateList(start : int, end : int, fn : (int) -> ?) -> List<?> {
	generateListRec(start, end, fn, EmptyList())
}

generateListRec(start : int, end : int, fn : (int) -> ?, acc : List<?>) -> List<?> {
	if (start < end) {
		generateListRec(start, end - 1, fn, Cons(fn(end-1), acc));
	} else {
		acc
	}
}

concatList(l1 : List<?>, l2 : List<?>) -> List<?> {
	switch (l2 : List<?>) {
		EmptyList(): l1;
		//#39534 reverseList increases const in O(n) but allows concatToNonEmptyList to be tail rec
		Cons(h, t): concatToNonEmptyList(reverseList(l1), l2);
	}
}
concatToNonEmptyList(l1 : List<?>, l2 : List<?>) -> List<?> {
	switch (l1 : List<?>) {
		EmptyList(): l2;
		Cons(h, t): {
			concatToNonEmptyList(t, Cons(h, l2));
		}
	}
}

reverseList(xs) {
	reverseAcc(xs, makeList());
}

reverseAcc(xs : List<?>, acc : List<?>) -> List<?> {
	switch (xs : List<?>) {
		EmptyList(): acc;
		Cons(x, ys): reverseAcc(ys, Cons(x, acc));
	}
}

applyList(l : List<?>, fn : (?) -> void) -> void {
	switch (l : List<?>) {
		EmptyList(): {}
		Cons(h, t): {
			fn(h);
			applyList(t, fn);
		}
	}
}

callList(l : List<() -> void>) -> void {
	switch (l : List<() -> void>) {
		EmptyList(): {}
		Cons(h, t): {
			h();
			callList(t);
		}
	}
}

mapList(l : List<?>, fn : (?) -> ??) -> List<??> {
	reverseList(doMapList(l, fn, EmptyList()))
}

doMapList(l : List<?>, fn : (?) -> ??, acc : List<??>) -> List<??> {
	switch (l : List<?>) {
		EmptyList(): acc;
		Cons(h, t): doMapList(t, fn, Cons(fn(h), acc));
	}
}

mapiList(l : List<?>, fn : (int, ?) -> ??) -> List<??> {
	reverseList(doMapiList(l, 0, fn, EmptyList()))
}

doMapiList(l : List<?>, i : int, fn : (int, ?) -> ??, acc : List<??>) -> List<??> {
	switch (l : List<?>) {
		EmptyList(): acc;
		Cons(h, t): doMapiList(t, i + 1, fn, Cons(fn(i, h), acc));
	}
}

filtermapList(l : List<?>, fn : (?) -> Maybe<??>) -> List<??> {
	reverseList(doFiltermapList(l, fn, EmptyList()))
}

doFiltermapList(l : List<?>, fn : (?) -> Maybe<??>, acc : List<??>) -> List<??> {
	switch (l : List<?>) {
		EmptyList(): acc;
		Cons(h, t): {
			switch (fn(h)) {  // Explicit switch, by advise from Asger, to eliminate exponentiality.
				None(): doFiltermapList(t, fn, acc);
				Some(subh): doFiltermapList(t, fn, Cons(subh, acc));
			}
		}
	}
}

// Apply a collecting function.
foldList(l : List<?>, init : ??, fn : (??, ?)->??) -> ?? {
	switch (l : List<?>) {
		EmptyList(): init;
		Cons(h, t): foldList(t, fn(init, h), fn);
	}
}

foldiList(l : List<?>, init : ??, fn : (int, ??, ?) -> ??) -> ?? {
	doFoldiList(l, init, 0, fn);
}

doFoldiList(l : List<?>, init : ??, i : int, fn : (int, ??, ?) -> ??) -> ?? {
	switch (l : List<?>) {
		EmptyList(): init;
		Cons(h, t): doFoldiList(t, fn(i, init, h), i + 1, fn);
	}
}


// Apply a collecting function passing Cons containing the element;
// «p» is for pointer, as «i» in foldi is for index.
foldpList(l : List<?>, init : ??, fn : (List<?>, ??, ?)->??) -> ?? {
	switch(l : List<?>) {
		EmptyList(): init;
		Cons(h, t): foldpList(t, fn(l, init, h), fn);
	}
}

iterList(l : List<?>, fn : (?) -> void) -> void {
	switch (l : List<?>) {
		EmptyList(): {};
		Cons(h, t): {
			fn(h);
			iterList(t, fn);
		}
	}
}

iteriList(l : List<?>, fn : (int, ?) -> void) -> void {
	doIteriList(l, fn, 0);
}

doIteriList(l : List<?>, fn : (int, ?) -> void, i : int) -> void {
	switch (l : List<?>) {
		EmptyList(): {};
		Cons(h, t): {
			fn(i, h);
			doIteriList(t, fn, i + 1);
		}
	}
}

containsList(l : List<?>, v : ?) -> bool {
	existsList(l, \h -> h == v)
}

existsList(l : List<?>, fn : (?) -> bool) -> bool {
	switch (l : List<?>) {
		EmptyList(): false;
		Cons(h, t): if (fn(h)) true else existsList(t, fn);
	}
}

findList(l : List<?>, fn : (?) -> bool) -> Maybe<?> {
	switch (findpList(l, \__, el -> fn(el)) : List<?>) {
		EmptyList(): None();
		Cons(h, t): Some(h);
	}
}

findpList(l : List<?>, fn : (List<?>, ?) -> bool) -> List<?> {
	switch (l : List<?>) {
		EmptyList(): l;
		Cons(h, t): if (fn(l, h)) l else findpList(t, fn);
	}
}

filterList(l : List<?>, fn : (?) -> bool) -> List<?> {
	reverseList(doFilterList(l, fn, EmptyList()));
}

doFilterList(l : List<?>, fn : (?) -> bool, acc : List<?>) -> List<?> {
	switch (l : List<?>) {
		EmptyList(): acc;
		Cons(h, t): doFilterList(t, fn, if (fn(h)) Cons(h, acc) else acc);
	}
}

rlistPush(l : ref List<?>, s : ?) -> void {
	l := Cons(s, ^l);
}

isEmptyList(xs : List<?>) -> bool {
	switch (xs) {
		Cons(__, __): false;
		EmptyList(): true;
	};
}
