import ds/easygraph_utils;
import ds/easygraph_contract;
import ds/easygraph_components;
import ds/easygraph_cut;

export {
	// A simple graph builder where nodes are identified by the data itself
	makeSimpleGraph() -> SimpleGraph<?, ??>;

	isEmptySimpleGraph(g : SimpleGraph<?, ??>) -> bool;

	addSimpleNode(g : SimpleGraph<?, ??>, node : ?) -> SimpleGraph<?, ??>;
	hasSimpleNode(g : SimpleGraph<?, ??>, node : ?) -> bool;

	// The id of this node, in the underlying EasyGraph. -1 when not found
	getSimpleNodeId(g : SimpleGraph<?, ??>, node : ?) -> int;
	lookupSimpleNode(g : SimpleGraph<?, ??>, nodid : int, def : ?) -> ?;

	hasSimpleEdge(g : SimpleGraph<?, ??>, from : ?, to : ?) -> bool;
	addSimpleEdge(g : SimpleGraph<?, ??>, from : ?, to : ?, data : ??, weight : int) -> SimpleGraph<?, ??>;
	// Adds an edge between two nodes only if there isn't already an edge with the same data between them.
	// Returns the modified graph if the edge was added, or the original graph if a matching edge already existed.
	addSimpleUniqueEdge(g : SimpleGraph<?, ??>, from : ?, to : ?, data : ??, weight : int) -> SimpleGraph<?, ??>;

	removeSimpleEdge2(g : SimpleGraph<?, ??>, edge : SimpleEdge2<?, ??>) -> SimpleGraph<?, ??>;
	reverseSimpleEdge2(edge : SimpleEdge2<?, ??>) -> SimpleEdge2<?, ??>;

	getSimpleAdjacentNodes(g : SimpleGraph<?, ??>, from : ?) -> [?];
	getSimpleAdjacentEdges(g : SimpleGraph<?, ??>, from : ?) -> [??];

    // Get all edges with both source and target node data
    getSimpleGraphEdges2(g : SimpleGraph<?, ??>) -> [SimpleEdge2<?, ??>];
	getSimpleGraphEdges(g : SimpleGraph<?, ??>) -> [SimpleEdge<?, ??>];
	getSimpleOutEdges(g : SimpleGraph<?, ??>, node : ?) -> [SimpleEdge2<?, ??>];
	
    SimpleEdge2(
        from : ?, // Source node data
        to : ?,   // Target node data
        data : ??, // Edge data
        weight : int
    );


	reverseSimpleGraph(g : SimpleGraph<?, ??>) -> SimpleGraph<?, ??>;

	reachableClosureFromSimpleNode(g : SimpleGraph<?, ??>, from : ?) -> Set<?>;
	reachableEdgesFromSimpleNode(g : SimpleGraph<?, ??>, from : ?) -> Set<??>;

	// Finds all nodes reachable from a starting node by following edges that match a predicate.
	findSimpleEdgeClosure(graph : SimpleGraph<?, ??>, start : ?, edgePred : (??) -> bool) -> Set<?>;

	// Get distances from the specified node to all reachable nodes in the graph
	getSimpleDijkstra(g : SimpleGraph<?, ??>, from : ?) -> SimpleDijkstra<?, ??>;

		SimpleDijkstra(
			// The link from this node that will give the shortest path out
			spTree : Tree<?, SimpleEdge<?, ??>>, 
			// The cost for this node out
			distanceMap : Tree<?, int>, 
		);
		SimpleEdge(
			target : ?,
			data : ??,
			weight : int,
		);

	simpleDijkstraToGraph(dijkstra : SimpleDijkstra<?, ??>) -> SimpleGraph<?, ??>;

	SimpleGraph(nodes : Tree<?, int>, nnodes : int, graph : EasyGraph<?, ??>);

	// Do a topological sort. Does not visit loop nodes
	topoSortSimpleGraph(graph : SimpleGraph<?, ??>) -> [?];

	simpleGraph2dot(
		graph : SimpleGraph<?, ??>, 
		// This will be highlighted, but does not have to exist
		root : ?,
		graphstyles : [DiGraphStyle<??>], 
		node2styles : (?) -> [GNodeStyle], 
		edge2styles : (??) -> [GEdgeStyle]
	) -> string;

	// Same, but where we get more info on the edges
	simpleGraph2dot2(
		graph : SimpleGraph<?, ??>,
		root : ?,
		graphstyles : [DiGraphStyle<??>],
		node2styles : (?) -> [GNodeStyle],
		edge2styles : (SimpleEdge2<?,??>) -> [GEdgeStyle]
	) -> string;

	simpleLoopFree(g : SimpleGraph<?, ??>, source : ?) -> SimpleGraph<?, ??>;
	// Does not work when there are loops
	simpleTransitiveReduction(g : SimpleGraph<?, ??>) -> SimpleGraph<?, ??>;

	findSimpleGraphCycles(graph : SimpleGraph<?, ??>) -> [[SimpleEdge<?, ??>]];

	// Filter a graph, keeping only nodes that satisfy the keep predicate, but preserving all paths between kept nodes.
	// Also known as graph contraction
	contractSimpleGraph(g : SimpleGraph<?, ??>, keep : (?) -> bool) -> SimpleGraph<?, ??>;

	// Splits a SimpleGraph into multiple separate connected components
	splitSimpleGraphComponents(graph : SimpleGraph<?, ??>) -> [SimpleGraph<?, ??>];

	// Add reverse edges to a graph, so it is effectively undirected
	// If we want to use findSimpleEdgeCut in a undirected graph, you have to remove the
	// reverse edge yourself first.
	makeUndirectedSimpleGraph(graph : SimpleGraph<?, ??>) -> SimpleGraph<?, ??>;

	// Analysis of an edge cut in a SimpleGraph
	SimpleGraphEdgeCutAnalysis(
		edge : SimpleEdge2<?, ??>,        // The edge being analyzed
		fromReachable : Set<?>,           // What nodes are reachable from 'from' when edge is removed
		toReachable : Set<?>             // What nodes are reachable from 'to' when edge is removed
	);

	// Given an edge, find what nodes become unreachable when removing it
	findSimpleEdgeCut(graph : SimpleGraph<?, ??>, edge : SimpleEdge2<?, ??>) -> SimpleGraphEdgeCutAnalysis<?, ??>;

	// Find edge cuts in a graph component. The graph should be strongly connected
	// (i.e., every node can reach every other node)
	// Give it a function to decide which edges to cut
	findSimpleEdgeCuts(graph : SimpleGraph<?, ??>, cutEdge : (SimpleEdge2<?, ??>) -> bool) -> [SimpleGraphEdgeCutAnalysis<?, ??>];

}

makeSimpleGraph() -> SimpleGraph<?, ??> {
	SimpleGraph(makeTree(), 0, makeEasyGraph());
}

isEmptySimpleGraph(g : SimpleGraph<?, ??>) -> bool {
	g.nnodes == 0
}

addSimpleNode(g : SimpleGraph<?, ??>, node : ?) -> SimpleGraph<?, ??> {
	id = lookupTreeDef(g.nodes, node, -1);
	if (id == -1) {
		// New node
		nid = g.nnodes;
		SimpleGraph(
			setTree(g.nodes, node, nid),
			nid + 1,
			addEGNode(g.graph, GNode(nid, node))
		);
	} else {
		// We already have it
		g
	}
}

hasSimpleNode(g : SimpleGraph<?, ??>, node : ?) -> bool {
	containsKeyTree(g.nodes, node)
}

getSimpleNodeId(g : SimpleGraph<?, ??>, node : ?) -> int {
	lookupTreeDef(g.nodes, node, -1);
}

lookupSimpleNode(g : SimpleGraph<?, ??>, nodeid : int, def : ?) -> ? {
	node = lookupTree(g.graph.nodes, nodeid);
	eitherMap(node, \n : GNode<?> -> n.data, def);
}

addSimpleEdge(g : SimpleGraph<?, ??>, from : ?, to : ?, data : ??, weight : int) -> SimpleGraph<?, ??> {
	fromId = lookupTreeDef(g.nodes, from, -1);
	if (fromId == -1) {
		addSimpleEdge(addSimpleNode(g, from), from, to, data, weight);
	} else {
		toId = lookupTreeDef(g.nodes, to, -1);
		if (toId == -1) {
			addSimpleEdge(addSimpleNode(g, to), from, to, data, weight);
		} else {
			SimpleGraph(
				g.nodes,
				g.nnodes,
				addEGEdge(g.graph, GEdge(fromId, toId, data, weight))
			);
		}
	}
}

// Adds an edge between two nodes only if there isn't already an edge with the same data between them.
// Returns the modified graph if the edge was added, or the original graph if a matching edge already existed.
addSimpleUniqueEdge(g : SimpleGraph<?, ??>, from : ?, to : ?, data : ??, weight : int) -> SimpleGraph<?, ??> {
	fromId = lookupTreeDef(g.nodes, from, -1);
	if (fromId == -1) {
		// Source node doesn't exist, add edge with new nodes
		addSimpleEdge(g, from, to, data, weight)
	} else {
		toId = lookupTreeDef(g.nodes, to, -1);
		if (toId == -1) {
			// Target node doesn't exist, add edge with new node
			addSimpleEdge(g, from, to, data, weight)
		} else {
			// Both nodes exist, check edges
			succs = lookupTreeDef(g.graph.edges, fromId, []);
			// Check if there's an edge with matching target and data
			existingEdge = find(succs, \edge ->
				edge.to == toId && edge.data == data
			);

			switch (existingEdge) {
				None(): {
					// No matching edge found, add new edge
					addSimpleEdge(g, from, to, data, weight)
				}
				Some(__): {
					// Matching edge exists, return unchanged graph
					g
				}
			}
		}
	}
}

hasSimpleEdge(g : SimpleGraph<?, ??>, from : ?, to : ?) -> bool {
	fromId = lookupTreeDef(g.nodes, from, -1);
	if (fromId == -1) {
		false;
	} else {
		toId = lookupTreeDef(g.nodes, to, -1);
		if (toId == -1) {
			false;
		} else {
			succs = lookupTreeDef(g.graph.edges, fromId, []);
			exists(succs, \edge : GEdge<??> -> edge.to == toId);
		}
	}
}

removeSimpleEdge2(g : SimpleGraph<?, ??>, edge : SimpleEdge2<?, ??>) -> SimpleGraph<?, ??> {
	fromId = getSimpleNodeId(g, edge.from);
	toId = getSimpleNodeId(g, edge.to);
	if (fromId == -1 || toId == -1) {
		g // Edge doesn't exist, return unchanged graph
	} else {
		SimpleGraph(g with
			graph = removeEGEdge(g.graph, GEdge(fromId, toId, edge.data, edge.weight))
		)
	}
}

reverseSimpleEdge2(edge : SimpleEdge2<?, ??>) -> SimpleEdge2<?, ??> {
	SimpleEdge2(edge.to, edge.from, edge.data, edge.weight)
}


getSimpleAdjacentNodes(g : SimpleGraph<?, ??>, from : ?) -> [?] {
	succs : [GEdge<??>] = lookupTreeDef(g.graph.edges, getSimpleNodeId(g, from), []);

	filtermap(succs, \edge : GEdge<??> -> {
		node = lookupTree(g.graph.nodes, edge.to);
		maybeMap(node, \n : GNode<?> -> n.data);
	})
}

getSimpleAdjacentEdges(g : SimpleGraph<?, ??>, from : ?) -> [??] {
	succs : [GEdge<??>] = lookupTreeDef(g.graph.edges, getSimpleNodeId(g, from), []);
	map(succs, \edge : GEdge<??> -> {
		edge.data;
	})
}

getSimpleGraphEdges(g : SimpleGraph<?, ??>) -> [SimpleEdge<?, ??>] {
	// Get all edges from the underlying EasyGraph
	foldTree(g.graph.edges, [], \fromId, edges : [GEdge<??> ], acc -> {
		// For each edge, we need to look up the source and target nodes to get their data
		edgesWithNodes = filtermap(edges, \edge -> {
			fromNode = lookupTree(g.graph.nodes, fromId);
			toNode = lookupTree(g.graph.nodes, edge.to);
			switch (fromNode) {
				None(): None();
				Some(from): {
					switch (toNode) {
						None(): None();
						Some(to): {
							Some(SimpleEdge(
								from.data, 
								edge.data,
								edge.weight
							))
						}
					}
				}
			}
		});
		concat(acc, edgesWithNodes);
	});
}

reverseSimpleGraph(g : SimpleGraph<?, ??>) -> SimpleGraph<?, ??> {
	SimpleGraph(g with graph = reverseEasyGraph(g.graph))
}

reachableClosureFromSimpleNode(g : SimpleGraph<?, ??>, from : ?) -> Set<?> {
	rc = reachableClosureFromNode(g.graph, getSimpleNodeId(g, from));

	foldSet(rc, makeSet(), \acc, nodeid -> {
		node = lookupTree(g.graph.nodes, nodeid);
		switch (node) {
			None(): acc;
			Some(n): insertSet(acc, n.data);
		}
	})
}

reachableEdgesFromSimpleNode(g : SimpleGraph<?, ??>, from : ?) -> Set<??> {
	// Get the starting node id
	fromId = getSimpleNodeId(g, from);
	
	// Get the set of reachable EasyGraph edges
	easyGraphEdges = reachableEdgesFromNode(g.graph, fromId);
	
	// Convert the EasyGraph edges to SimpleEdge format
	foldSet(easyGraphEdges, makeSet(), \acc, edge -> {
		// Look up the target node data
		toNode = lookupTree(g.graph.nodes, edge.to);
		
		switch (toNode) {
			None(): acc;
			Some(n): {
				insertSet(acc, edge.data);
			}
		}
	});
}

SimpleDijkstraState(
	// The cost for this node out
	distanceMap : Tree<?, int>, 
	// Renaming nodes to visit
	nodesQueue : Set<Pair<int, ?>>, 
	// The shortest link from this node
	spTree : Tree<?, SimpleEdge<?, ??> >, 
	// What nodes have we visited?
	visited : Set<?>, 
	// What nodes remain to be visited?
	targetsLeft : Set<?>
);

getSimpleDijkstra(g : SimpleGraph<?, ??>, from : ?) -> SimpleDijkstra<?, ??> {
	state = SimpleDijkstraState(
		makeTree1(from, 0), 
		makeSet1(Pair(0, from)),
		makeTree(),
		makeSet(),
		foldTree(g.nodes, makeSet(), \node, id, acc -> insertSet(acc, node))
	);

	st = findSimpleDistance(g, state);
	SimpleDijkstra(
		st.spTree,
		st.distanceMap
	);
}

findSimpleDistance(g : SimpleGraph<?, ??>, state : SimpleDijkstraState<?, ??>) -> SimpleDijkstraState<?, ??> {
	popm = popMinSet(state.nodesQueue);

	switch (popm) {
		None(): state;
		Some(pop): {
			node = pop.value.second;
			distance = pop.value.first;
			if (distance == intMax) {
				state;
			} else {
				targetsLeft = removeSet(state.targetsLeft, node);
				if (isEmptySet(targetsLeft)) {
					state;
				} else {
					edges : [GEdge] = lookupTreeDef(g.graph.edges, getSimpleNodeId(g, node), []);
					newState0 = SimpleDijkstraState(state with 
						nodesQueue = pop.rest,
						visited = insertSet(state.visited, node),
						targetsLeft = targetsLeft
					);
					newState = fold(edges, newState0, \acc, edge -> {
						mnode = lookupTree(g.graph.nodes, edge.to);
						nod : Maybe<?> = maybeMap(mnode, \n : GNode<?> -> n.data);
						switch (nod) {
							None(): acc;
							Some(n): {
								updateSimpleDistance(acc, n, node, distance, edge)
							}
						}
					});

					findSimpleDistance(g, newState);
				}
			}
		}
	}
}

updateSimpleDistance(state : SimpleDijkstraState<?, ??>, node : ?, parent : ?, distance : int, edge : GEdge<??>) -> SimpleDijkstraState<?, ??> {
	weight = edge.weight;
	if (containsSet(state.visited, node)) {
		state;
	} else {
		dist = lookupTreeDef(state.distanceMap, node, intMax);
		key = Pair(dist, node);
		newDistance = distance + weight;
		if (newDistance < dist) {
			newKey = Pair(newDistance, node);
			distanceMap = setTree(state.distanceMap, node, newDistance);
			nodesQueue = updateSet(state.nodesQueue, key, newKey);
			simpleEdge = SimpleEdge(parent, edge.data, edge.weight);
			spTree = setTree(state.spTree, node, simpleEdge);
			SimpleDijkstraState(
				state with
				distanceMap = distanceMap,
				nodesQueue = nodesQueue,
				spTree = spTree
			);
		} else {
			state;
		}
	}
}

simpleDijkstraToGraph(dijkstra : SimpleDijkstra<?, ??>) -> SimpleGraph<?, ??> {
	foldTree(dijkstra.spTree, makeSimpleGraph(), \node, edge, acc : SimpleGraph<?, ??> -> {
		addSimpleEdge(acc, node, edge.target, edge.data, edge.weight)
	});
}

simpleGraph2dot(
	graph : SimpleGraph<?, ??>, 
	root : ?,
	graphstyles : [DiGraphStyle<??>], 
	node2styles : (?) -> [GNodeStyle], 
	edge2styles : (??) -> [GEdgeStyle]
) -> string {
	rootId = getSimpleNodeId(graph, root);

	easyGraph2dot(
		graph.graph,
		rootId,
		graphstyles,
		\n -> node2styles(n.data),
		\e -> edge2styles(e.data)
	);
}

simpleGraph2dot2(
	graph : SimpleGraph<?, ??>,
	root : ?,
	graphstyles : [DiGraphStyle<??>],
	node2styles : (?) -> [GNodeStyle],
	edge2styles : (SimpleEdge2<?,??>) -> [GEdgeStyle]
) -> string {
	rootId = getSimpleNodeId(graph, root);

	easyGraph2dot(
		graph.graph,
		rootId,
		graphstyles,
		\n -> node2styles(n.data),
		\e -> {
			// Look up the source and target nodes to get their data
			fromNode = lookupTree(graph.graph.nodes, e.from);
			toNode = lookupTree(graph.graph.nodes, e.to);

			switch (fromNode) {
				None(): [];
				Some(from): {
					switch (toNode) {
						None(): [];
						Some(to): {
							edge2styles(SimpleEdge2(from.data, to.data, e.data, e.weight));
						}
					}
				}
			}
		}
	);
}

topoSortSimpleGraph(graph : SimpleGraph<?, ??>) -> [?] {
	tree = topSortEasyGraph(graph.graph);
	reversed = foldTree(tree, makeTree(), \nodeid, order, acc -> {
		setTree(acc, order, nodeid);
	});
	nodeOrder = getTreeValues(reversed);
	filtermap(nodeOrder, \nodeid -> {
		node = lookupTree(graph.graph.nodes, nodeid);
		maybeMap(node, \n : GNode<?> -> n.data);
	});
}


simpleLoopFree(g : SimpleGraph<?, ??>, source : ?) -> SimpleGraph<?, ??> {
	red = removeBackEGEdges(g.graph, [lookupTreeDef(g.nodes, source, 0)]);
	SimpleGraph(
		g with graph = red
	)
}

simpleTransitiveReduction(g : SimpleGraph<?, ??>) -> SimpleGraph<?, ??> {
	red = transitiveReduction(g.graph);
	SimpleGraph(
		g with graph = red
	)
}

findSimpleGraphCycles(graph : SimpleGraph<?, ??>) -> [[SimpleEdge<?, ??>]] {
	// First find cycles in the EasyGraph
	easyGraphCycles = findEasyGraphCycles(graph.graph);

	// Convert each cycle from EasyGraph edges to SimpleGraph edges
	filtermap(easyGraphCycles, \cycle -> {
		cycleEdges = filtermap(cycle, \edge -> {
			// Look up the target node data
			toNode = lookupTree(graph.graph.nodes, edge.to);
			
			maybeMap(toNode, \n -> 
				SimpleEdge(
					n.data,
					edge.data,
					edge.weight
				)
			);
		});
		
		// Only keep cycles where all edges were successfully converted
		if (length(cycleEdges) == length(cycle)) Some(cycleEdges)
		else None();
	});
}

contractSimpleGraph(g : SimpleGraph<?, ??>, keep : (?) -> bool) -> SimpleGraph<?, ??> {
	// Filter the underlying EasyGraph, converting the node predicate
	filtered = contractEasyGraph(g.graph, \n -> keep(n.data));

	// Create a new mapping from data to node ids for the filtered graph
	nodes = foldTree(filtered.nodes, makeTree(), \id, node, acc -> {
		setTree(acc, node.data, id)
	});

	SimpleGraph(
		nodes,
		sizeTree(nodes),
		filtered
	);
}

// Splits a SimpleGraph into multiple separate connected components
splitSimpleGraphComponents(graph : SimpleGraph<?, ??>) -> [SimpleGraph<?, ??>] {
    // First split the underlying EasyGraph
    easyComponents = splitGraphComponents(graph.graph);
    
    // Convert each EasyGraph component to a SimpleGraph
    map(easyComponents, \component -> {
        // Create a new mapping from data to node ids for this component
        nodes = foldTree(component.nodes, makeTree(), \id, node : GNode<?>, acc -> {
            setTree(acc, node.data, id)
        });
        
        SimpleGraph(
            nodes,
            sizeTree(nodes),
            component
        )
    })
}

getSimpleGraphEdges2(g : SimpleGraph<?, ??>) -> [SimpleEdge2<?, ??>] {
    foldTree(g.graph.edges, [], \fromId, edges : [GEdge<??> ], acc -> {
        // For each edge, we need to look up the source and target nodes to get their data
        edgesWithNodes = filtermap(edges, \edge -> {
            fromNode = lookupTree(g.graph.nodes, fromId);
            toNode = lookupTree(g.graph.nodes, edge.to);
            switch (fromNode) {
                None(): None();
                Some(from): {
                    switch (toNode) {
                        None(): None();
                        Some(to): {
                            Some(SimpleEdge2(
                                from.data,
                                to.data,
                                edge.data,
                                edge.weight
                            ))
                        }
                    }
                }
            }
        });
        concat(acc, edgesWithNodes);
    });
}

getSimpleOutEdges(g : SimpleGraph<?, ??>, node : ?) -> [SimpleEdge2<?, ??>] {
	nodeId = getSimpleNodeId(g, node);
	if (nodeId == -1) [] else {
		edges : [GEdge<??> ] = lookupTreeDef(g.graph.edges, nodeId, []);
		filtermap(edges, \edge -> {
			toNode = lookupTree(g.graph.nodes, edge.to);
			switch (toNode) {
				None(): None();
				Some(to): {
					Some(SimpleEdge2(
						node,
						to.data,
						edge.data,
						edge.weight
					));
				}
			}
		});
	}
}

findSimpleEdgeClosure(graph : SimpleGraph<?, ??>, start : ?, edgePred : (??) -> bool) -> Set<?> {
	removeSet(findSimpleEdgeClosureHelper(graph, [start], makeSet1(start), edgePred), start);
}

findSimpleEdgeClosureHelper(graph : SimpleGraph<?, ??>, pending : [?], visited : Set<?>, edgePred : (??) -> bool) -> Set<?> {
	if (pending == []) visited
	else {
		node = pending[0];
		rest = tail(pending);

		// Get edges matching our predicate
		edges = filter(getSimpleOutEdges(graph, node), \edge -> edgePred(edge.data));
		neighbors = map(edges, \e -> e.to);

		// Find unvisited neighbors
		unvisited = filter(neighbors, \n -> !containsSet(visited, n));

		if (unvisited == []) {
			// No new nodes, continue with rest
			findSimpleEdgeClosureHelper(graph, rest, insertSet(visited, node), edgePred)
		} else {
			// Add new nodes to visit
			newVisited = insertSet(visited, node);
			findSimpleEdgeClosureHelper(graph, concat(rest, unvisited), newVisited, edgePred)
		}
	}
}

makeUndirectedSimpleGraph(graph : SimpleGraph<?, ??>) -> SimpleGraph<?, ??> {
	SimpleGraph(graph with
		graph = makeUndirectedGraph(graph.graph)
	);
}

findSimpleEdgeCut(graph : SimpleGraph<?, ??>, edge : SimpleEdge2<?, ??>) -> SimpleGraphEdgeCutAnalysis<?, ??> {
	fromId = getSimpleNodeId(graph, edge.from);
	toId = getSimpleNodeId(graph, edge.to);

	analysis = findEGEdgeCut(graph.graph, GEdge(fromId, toId, edge.data, edge.weight));

	// Convert the node id sets to node data sets
	SimpleGraphEdgeCutAnalysis(
		edge,
		mapSet(analysis.fromReachable, \id -> lookupSimpleNode(graph, id, edge.from)),
		mapSet(analysis.toReachable, \id -> lookupSimpleNode(graph, id, edge.to))
	)
}

findSimpleEdgeCuts(graph : SimpleGraph<?, ??>, cutEdge : (SimpleEdge2<?, ??>) -> bool) -> [SimpleGraphEdgeCutAnalysis<?, ??>] {
	edges = getSimpleGraphEdges2(graph);
	filtermap(edges, \edge -> {
		if (cutEdge(edge)) {
			Some(findSimpleEdgeCut(graph, edge))
		} else None();
	});
}
