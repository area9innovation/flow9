import ds/egraph;

export {
	egraph2dot(e : EGraph<?>, label : (ENode<?>) -> string) -> string;
}

egraph2dot(egraph : EGraph<?>, label : (ENode<?>) -> string) -> string {
	dot = ref "digraph graphname { compound=true;\n";
	iterEGraph(egraph, \root, nodes : Set<ENode<?>> -> {
		subgraph = "subgraph cluster_" + i2s(root) + " { style=filled; color=lightgrey; label = \"" + i2s(root) + "\"; ";

		cnodes = foldi(set2array(nodes), "", \i, acc, node : ENode<?> -> {
			ty = i2s(i) + ": "+ label(node);
			id = "node_" + i2s(root) + "_" + i2s(i);
			line = id + " [label=\"" + ty + "\"];\n";
			acc + line + "\n";
		});

		clinks = foldi(set2array(nodes), "", \i, acc, node : ENode<?> -> {
			id = "node_" + i2s(root) + "_" + i2s(i);
			links = fold(node.args, "", \acc2, link -> {
				acc2 + id + " -> " + "node_" + i2s(link) + "_0 [lhead=cluster_" + i2s(link) + "];\n";
			});
			acc + links + "\n";
		});

		line = subgraph + cnodes + "}\n" + clinks;

		dot := ^dot + line;
	});
	^dot + "}";
}
