import runtime;

export {
	// Functions for operations with expanding Vector<?> type.
	// Vector<?> may be created with a default value, which is used
	// in case we try to access a vector with index out of bounds.
	//
	//	  CAUTION: Vector type is mutable.
	//
	// Currently vector natives are supported only in java runtime.

	// A structure-wrapper around a native type. 
	Vector : (vect : native, defVal : Maybe<?>);

	// Create a vector with a given capacity. Initially it is empty.
	makeVector(capacity : int) -> Vector<?>;

	//Create a vector with a given capacity and one element v.
	makeVector1(capacity : int, v : ?) -> Vector<?>;

	// Create a vector with a given capacity and default value. Initially it is empty.
	makeVectorDefVal(capacity : int, defVal : ?) -> Vector<?>;

	//Create a vector with a given capacity, one element v and default value.
	makeVectorDefVal1(capacity : int, v : ?, defVal : ?) -> Vector<?>;

	// Safely access vector by index, O(1)
	getVector(vector : Vector<?>, i : int) -> Maybe<?>;

	// Unsafe vector getter, may cause runtime errors. 
	// Use only if you are sure that index is valid.
	getVectorUnsafe(vector : Vector<?>, i : int) -> ?;

	// Vector setter: modifies vector at index i in case i is a valid index, O(1).
	setVector(vector : Vector<?>, i : int, val : ?) -> void;

	// Vector setter: modifies vector at index i, O(1).
	// Use only if you are sure that index is valid.
	setVectorUnsafe(vector : Vector<?>, i : int, val : ?) -> void;

	// Adds a new element to a vector, O(1)
	pushVector(vector : Vector<?>, val : ?) -> void;

	// Remove element at index i from a vector in case i is a valid index, O(n) 
	removeVector(vector : Vector<?>, i : int) -> void;

	// Returns the length of a vector
	sizeVector(vector : Vector<?>) -> int;

	// Clears a vector
	clearVector(vector : Vector<?>) -> void;

	// iter variants for vector
	iterVector(vector : Vector<?>, fn : (?) -> void) -> void;
	iteriVector(vector : Vector<?>, fn : (int, ?) -> void) -> void;

	// map variants for vector
	mapVector(vector : Vector<?>, fn : (?) -> ??) -> Vector<??>;
	mapiVector(vector : Vector<?>, fn : (int, ?) -> ??) -> Vector<??>;

	// fold variants for vector
	foldVector(vector : Vector<?>, init : ??,  fn : (??, ?) -> ??) -> ??;
	foldiVector(vector : Vector<?>, init : ??,  fn : (int, ??, ?) -> ??) -> ??;

	// Converts a vector into array
	vector2array(vector : Vector<?>) -> [?];

	// Converts an array into vector
	array2vector(arr : [?]) -> Vector<?>;

	// Copies a vector. Since vectors are mutable, sometimes we might want to make a copy.
	copyVector(vector : Vector<?>) -> Vector<?>;

	// Append one vector with another. Modifies (expands) the first one.
	appendVector(v1 : Vector<?>, v2 : Vector<?>) -> void;
}

// Set of private natives
native makeNativeVector : (capacity : int) -> native = Native.makeVector;
native getNativeVector : (vector : native, i : int) -> ? = Native.getVector;
native setNativeVector : (vector : native, i : int, val : ?) -> void = Native.setVector;
native addNativeVector : (vector : native, val : ?) -> void = Native.addVector;
native removeNativeVector : (vector : native, i : int) -> void = Native.removeVector;
native sizeNativeVector : (vector : native) -> int = Native.sizeVector;
native clearNativeVector : (vector : native) -> void = Native.clearVector;


// Accessors to natives:

makeVector(capacity : int) -> Vector<?> {
	Vector(makeNativeVector(capacity), None());
}

makeVector1(capacity : int, v : ?) -> Vector<?> {
	ret = Vector(makeNativeVector(capacity), None());
	addNativeVector(ret.vect, v);
	ret;
}

makeVectorDefVal(capacity : int, defVal : ?) -> Vector<?> {
	Vector(makeNativeVector(capacity), Some(defVal));
}

makeVectorDefVal1(capacity : int, v : ?, defVal : ?) -> Vector<?> {
	ret = Vector(makeNativeVector(capacity), Some(defVal));
	addNativeVector(ret.vect, v);
	ret;
}

getVector(vector : Vector<?>, i : int) -> Maybe<?> {
	if (0 <= i && i < sizeNativeVector(vector.vect)) {
		Some(getNativeVector(vector.vect, i));
	} else {
		vector.defVal;
	}
}

getVectorUnsafe(vector : Vector<?>, i : int) -> ? {
	getNativeVector(vector.vect, i);
}

setVector(vector : Vector<?>, i : int, val : ?) -> void {
	if (0 <= i && i < sizeNativeVector(vector.vect)) {
		setNativeVector(vector.vect, i, val);
	}
}

setVectorUnsafe(vector : Vector<?>, i : int, val : ?) -> void {
	setNativeVector(vector.vect, i, val);
}

pushVector(vector : Vector<?>, val : ?) -> void {
	addNativeVector(vector.vect, val);
}

removeVector(vector : Vector<?>, i : int) -> void {
	if (0 <= i && i < sizeNativeVector(vector.vect)) {
		removeNativeVector(vector.vect, i);
	}
}

sizeVector(vector : Vector<?>) -> int {
	sizeNativeVector(vector.vect);
}

clearVector(vector : Vector<?>) -> void {
	clearNativeVector(vector.vect);
}


// Set of flow fallbacks for natives:

makeNativeVector(capacity : int) -> native {
	flow(ref []);
};

getNativeVector(vector : native, i : int) -> ? {
	arr = cast(flow(vector) : flow -> ref [?]);
	^arr[i];
}

setNativeVector(vector : native, i : int, val : ?) -> void {
	arr = cast(flow(vector) : flow -> ref [?]);
	arr := insertArray(^arr, i, val);
}

addNativeVector(vector : native, val : ?) -> void {
	arr = cast(flow(vector) : flow -> ref [?]);
	arr := concat(^arr, [val]);
}

removeNativeVector(vector : native, i : int) -> void {
	arr = cast(flow(vector) : flow -> ref [?]);
	arr := removeIndex(^arr, i);
}

sizeNativeVector(vector : native) -> int {
	arr = cast(flow(vector) : flow -> ref [?]);
	length(^arr);
}

clearNativeVector(vector : native) -> void {
	arr = cast(flow(vector) : flow -> ref [?]);
	arr := [];
}



// Auxiliary vector functions:

iterVector(vector : Vector<?>, fn : (?) -> void) -> void {
	doIterVector(vector, fn, 0);
}

doIterVector(vector : Vector<?>, fn : (?) -> void, i : int) -> void {
	if (i < sizeVector(vector)) {
		fn(getNativeVector(vector.vect, i));
		doIterVector(vector, fn, i + 1);
	}
}

iteriVector(vector : Vector<?>, fn : (int, ?) -> void) -> void {
	doIteriVector(vector, fn, 0);
}

doIteriVector(vector : Vector<?>, fn : (int, ?) -> void, i : int) -> void {
	if (i < sizeVector(vector)) {
		fn(i, getNativeVector(vector.vect, i));
		doIteriVector(vector, fn, i + 1);
	}
}

mapVector(vector : Vector<?>, fn : (?) -> ??) -> Vector<??> {
	ret = makeVector(sizeVector(vector));
	doMapVector(vector, fn, ret, 0);
}

doMapVector(vector : Vector<?>, fn : (?) -> ??, ret : Vector<??>, i : int) -> Vector<??> {
	if (i == sizeVector(vector)) ret else {
		setVector(ret, i, fn(getNativeVector(vector.vect, i)));
		doMapVector(vector, fn, ret, i + 1);
	}
}

mapiVector(vector : Vector<?>, fn : (int, ?) -> ??) -> Vector<??> {
	ret = makeVector(sizeVector(vector));
	doMapiVector(vector, fn, ret, 0);
}

doMapiVector(vector : Vector<?>, fn : (int, ?) -> ??, ret : Vector<??>, i : int) -> Vector<??> {
	if (i == sizeVector(vector)) ret else {
		setVector(ret, i, fn(i, getNativeVector(vector.vect, i)));
		doMapiVector(vector, fn, ret, i + 1);
	}
}

foldVector(vector : Vector<?>, init : ??,  fn : (??, ?) -> ??) -> ?? {
	doFoldVector(vector, init, fn, 0);
}

doFoldVector(vector : Vector<?>, val : ??,  fn : (??, ?) -> ??, i : int) -> ?? {
	if (i == sizeVector(vector)) val else {
		doFoldVector(vector, fn(val, getNativeVector(vector.vect, i)), fn, i + 1);
	}
}

foldiVector(vector : Vector<?>, init : ??,  fn : (int, ??, ?) -> ??) -> ?? {
	doFoldiVector(vector, init, fn, 0);
}

doFoldiVector(vector : Vector<?>, val : ??,  fn : (int, ??, ?) -> ??, i : int) -> ?? {
	if (i == sizeVector(vector)) val else {
		doFoldiVector(vector, fn(i, val, getNativeVector(vector.vect, i)), fn, i + 1);
	}
}

vector2array(vector : Vector<?>) -> [?] {
	foldVector(vector, [], \acc, x -> concat(acc, [x]));
}

array2vector(arr : [?]) -> Vector<?> {
	ret = makeVector(length(arr));
	iter(arr, \x -> pushVector(ret, x));
	ret;
}

copyVector(vector : Vector<?>) -> Vector<?> {
	copy = makeVector(sizeVector(vector));
	iterVector(vector, \x -> pushVector(copy, x));
	copy;
}

appendVector(v1 : Vector<?>, v2 : Vector<?>) -> void {
	iterVector(v2, \x -> pushVector(v1, x));
}
