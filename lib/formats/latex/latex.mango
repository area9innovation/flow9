// Grammar for Lexing LaTeX

@include<list>

latex = latexes Document/1;

latexes = @array0<latex_segment>;

latex_segment =
	commands
	| "$$" ws @array0<math> "$$" ws							DisplayMath/1
	| "$" ws @array0<math> "$" ws							InlineMath/1
	| $(!'\' !'$' !'%' !'}' anychar)+ Text/1
	;

math = 
	math "_" math									Subscript/2
	| math "^" math									Superscript/2
	| commands
	| arg
	| $(!'\' !"_" !"^" !'$' !'%' !'{' !'[' !'}' anychar)+ Text/1
	;

commands = '\' "begin{" ws id "}" ws @array0<arg> latexes '\' "end{" ws id "}" ws Environment/4
	| '\' !"end" $command ws @array0<arg> 			Command/2
	| "%" $(!newline anychar)* newline ws			Comment/1;

arg = "[" ws $(!']' anychar)* "]" ws		OptionalArg/1
	| "{" ws latexes "}" ws					Arg/1
	;

command = ('a'-'z' | 'A'-'Z')+ | nonalpha_command;

nonalpha_command = (nonalpha)+;

id = $bid ws;
bid = ('a'-'z' | 'A'-'Z' | '_') (alnum)*; // Identifiers for environments, etc.
alnum = 'a'-'z' | 'A'-'Z' | '_' | '0'-'9';

ws = (' ' | "\t" | "\r" | "\n")*;
newline = "\n" | "\r\n";

nonalpha = '.' | ',' | ';' | ':' | '!' | '?' | '+' | '-' | '*' | '/' | '&' | '^' | '%' | '$' | '#' | '@' | '~' | '|' | '=' | '[' | ']' | '{' | '}' | '(' | ')' | '\';

anychar = '0x0000'-'0xffff';

latex
