json = ws jsonvalue;

object = "{" ws members?:m "}" ws { JsonObject(unarray(:m)) };

members = member:m ("," ws ~member:ma {:ma})*:ms { consArray(:m, :ms) };

member = singlestring:s ":" ws jsonvalue:v { Pair(:s, :v) };

jsonvalue = object 
	| jsonbool
	| singlestring:s { JsonString(:s) }
	| number:n ws { JsonDouble(:n) }
	| jsonarray:a { JsonArray(:a) }
	| "null" ws { JsonNull() };

jsonarray = "[" ws jsonvalue:v ("," ws ~jsonvalue:va {:va})*:vs "]" ws { consArray(:v, :vs) }
	| "[" ws "]" ws { array() };

jsonbool = "true" ws { JsonBool(true()) }
	| "false" ws { JsonBool(false()) };

number = (int "." unsigned_int?)$s { s2d($s) } 
	| ("." unsigned_int)$s { s2d($s) } 
	| int$i { s2d($i) };

unsigned_int = digit+;
int! = "-" unsigned_int | unsigned_int;
digit = '0'-'9';
hexdigit = digit | 'a'-'f' | 'A' - 'F';

onechar = '\\u' hexdigit hexdigit hexdigit hexdigit | '\\X' hexdigit hexdigit hexdigit hexdigit |'\\x' hexdigit hexdigit | "\\" char | !'"' char;
char = '\u0000' - '\uffff';
singlestring = '"' (onechar*)$s '"'$q ws {strUnescape($s, $q)};
ws = whitespace*;

whitespace = ' ' | '\t' | '\n' | '\u000d';
