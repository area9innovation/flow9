// Â© Copyright 2011 Area9 Technologies.
import algorithms;
import string;
import text/deserialize_string;


export {
	native parseJson : (json : string) -> Json = Native.parseJson;
	// Dummy way to skip js native implementation and use safer flow one which is capable to recover broken json.
	// It is not recommended use it since it is slower than native but can help to debug.
	parseJsonSafe(json : string) -> Json;
	getJsonObjectValue(json : Json, def : [Pair<string, Json>]) -> [Pair<string, Json>];
	getJsonFieldValue(json : Json, field : string, def : Json) -> Json;
	getJsonFieldValueM(json : Json, field : string) -> Maybe<Json>;
	getJsonFieldValueMCustom(json : Json, field : string, options : [JsonFieldOptions]) -> Maybe<Json>;
	getJsonFieldValueCustom(json : Json, field : string, def : Json, options : [JsonFieldOptions]) -> Json;
	getJsonArrayValue(json : Json, def : [Json]) -> [Json];
	getJsonBoolValue(json : Json, def : bool) -> bool;
	getJsonStringValue(json : Json, def : string) -> string;
	getJsonStringValueM(json : Json) -> Maybe<string>;
	getJsonDoubleValue(json : Json, def : double) -> double;
	getJsonDoubleValueM(json : Json) -> Maybe<double>;
	getJsonIntValue(json : Json, def : int) -> int;
	getJsonIntValueM(json : Json) -> Maybe<int>;

	parseJsonObject(json : string, start : int, acc : List<Pair<string, Json>>) -> ParsingAcc<Json>;
	skipJson(json : string, start : int, char : string) -> int;
	parseJsonDouble(s : string, start : int) -> Pair<Json, int>;

	// some shortcuts that seem to be frequently used
	getJsonStringField(json: Json, field: string, def: string) -> string;
	getJsonStringFieldCustom(json : Json, field: string, def : string, options : [JsonFieldOptions]) -> string;
	getJsonDoubleField(json: Json, field: string, def: double) -> double;
	getJsonDoubleFieldCustom(json: Json, field: string, def: double, options : [JsonFieldOptions]) -> double;
	getJsonIntField(json: Json, field: string, def: int) -> int;
	getJsonIntFieldCustom(json: Json, field: string, def: int, options : [JsonFieldOptions]) -> int;
	getJsonBoolField(json: Json, field: string, def: bool) -> bool;
	getJsonBoolFieldCustom(json : Json, field: string, def : bool, options : [JsonFieldOptions]) -> bool;
	// defaults to [] if field not found
	getJsonArrayField(json: Json, field: string) -> [Json];
	getJsonArrayFieldCustom(json: Json, field: string, options : [JsonFieldOptions]) -> [Json];
	// If a field is an object, this dude retrieves it. defaults to JsonObject([])
	getJsonObjectField(json: Json, field: string) -> JsonObject;
	getJsonObjectFieldCustom(json: Json, field: string, options : [JsonFieldOptions]) -> JsonObject;

	getJsonMaybeField(json : Json, getJsonValueFn : (Json, ?) -> ?, field: string, def : ?) -> Maybe<?>;
	getJsonMaybeValue(value : Json, getJsonValueFn : (Json, ?) -> ?, def : ?) -> Maybe<?>;

	// Helpers to work with Json arrays
	getValueFromJsonArray(array: Json, index: int, defaultValue: Json) -> Json;
	getIntValueFromJsonArray(array: Json, index: int, defaultValue: int) -> int;
	getDoubleValueFromJsonArray(array: Json, index: int, defaultValue: double) -> double;
	getStringValueFromJsonArray(array: Json, index: int, defaultValue: string) -> string;
	getBoolValueFromJsonArray(array: Json, index: int, defaultValue: bool) -> bool;

	pushJsonValue(array : Json, value : Json) -> JsonArray;

	setJsonField(json : Json, field : string, value : Json) -> JsonObject;
	setJsonFieldCustom(json : Json, field : string, value : Json, options : [JsonFieldOptions]) -> JsonObject;

		JsonFieldOptions ::= JsonFieldIgnoreCase, JsonFieldTreatAsPath;
			JsonFieldIgnoreCase();
			JsonFieldTreatAsPath(pathDelimiter : string);

	removeJsonObjectField(json : JsonObject, field : string) -> JsonObject;
	// returns `json` if it's not JsonObject
	removeJsonField(json : Json, field : string) -> Json;

	ParsingAcc(data : ?, start : int, finish : int, processed : int);

	Json ::= JsonObject, JsonArray, JsonNull, JsonBool, JsonString, JsonDouble;
		JsonObject(members : [Pair<string, Json>]);
		JsonArray(value : [Json]);
		JsonBool(v : bool);
		JsonString(s : string);
		JsonDouble(value : double);
		JsonNull();

	jsonNull = JsonNull();
	jsonTrue = JsonBool(true);
	jsonFalse = JsonBool(false);
	jsonStringEmpty = JsonString("");

	// Converts back to a single-line JSON string
	json2string(j : Json) -> string;
	json2stringTrimQuotes(j : Json) -> string;

	// Generates formatted JSON string
	// Uses \n and \t to format complex json objects for multiple lines
	json2stringFormatted(j : Json) -> string;

	// rudimentary support for converting things back to json
	array2json(arr: [?], item2json: (?) -> string) -> string;

	//equals Json
	isJsonEqual(first : Json, second : Json) -> bool;
	isJsonEqualIgnoreOrder(first : Json, second : Json, ignoreOrder : bool) -> bool;

	// Returns true if json is an object and has appropriate member.
	jsonObjectHasField(json : Json, member : string) -> bool;
	jsonObjectHasFieldCustom(json : Json, member : string, options : [JsonFieldOptions]) -> bool;
	// Access to a nested field like obj.f1.f2.f3, where fields = [f1, f2, f3]
	getJsonNestedField(json: Json, fields: [string]) -> Json;
	// The setter to the nested field, dual to getJsonNestedField
	setJsonNestedField(json: Json, fields: [string], value : Json) -> Json;

	parseJsonUntilError(json : string) -> JsonParseResult;
		JsonParseResult(
			value : Json,
			errorPosition : int, // -1 if there are no errors
			lastProcessedPosition: int,
		);
}

parseJson(json : string) -> Json {
	parseJsonSafe(json)
}

parseJsonUntilError(json : string) -> JsonParseResult {
	len = strlen(json);
	v = doParseJson(json, len, 0);
	success = v.start != v.finish && v.finish == len;
	JsonParseResult(v.data, if (success) -1 else v.finish, if (success) -1 else v.processed);
}

parseJsonSafe(json : string) -> Json {
	v = doParseJson(json, strlen(json), 0);
	if (v.start == v.finish) JsonDouble(0.0) else v.data;
}

getJsonObjectValue(json : Json, def : [Pair<string, Json>]) -> [Pair<string, Json>] {
	switch (json : Json) {
		JsonObject(members): members;
		default: def;
	}
}

getJsonFieldValue(json : Json, field : string, def : Json) -> Json {
	objects = getJsonObjectValue(json, [Pair(field, def)]);
	findObjectByName(objects, field, false, def);
}

getJsonFieldValueM(json : Json, field : string) -> Maybe<Json> {
	members = getJsonObjectValue(json, []);
	maybeMap(
		find(members, \member -> member.first == field),
		secondOfPair
	);
}

getJsonFieldValueCustom(json : Json, field : string, def : Json, options : [JsonFieldOptions]) -> Json {
	treatAsPath = extractStruct(options, JsonFieldTreatAsPath("")).pathDelimiter;
	pth = if (treatAsPath != "") strSplit(field, treatAsPath) else [field];
	if (length(pth) > 1) {
		child = getJsonFieldValueCustom(json, pth[0], JsonNull(), options);
		if (child == JsonNull()) def else getJsonFieldValueCustom(child, strGlue(tail(pth), treatAsPath), def, options);
	}
	else {
		jsonFieldIgnoreCase = contains(options, JsonFieldIgnoreCase());
		objects = getJsonObjectValue(json, [Pair(field, def)]);
		findObjectByName(objects, field, jsonFieldIgnoreCase, def);
	}
}

findObjectByName(objects : [Pair<string, Json>], key : string, ignoreCase : bool, defaultValue : Json) -> Json {
	key2 = if (ignoreCase) toLowerCase(key) else key;
	findDef(objects, \o -> {
		k = if (ignoreCase) toLowerCase(o.first) else o.first;
		k == key2
	}, Pair(key2, defaultValue)).second;
}

getJsonArrayValue(json : Json, def : [Json]) -> [Json] {
	switch (json : Json) {
		JsonArray(value): value;
		default: def;
	}
}

getJsonBoolValue(json : Json, def : bool) -> bool {
	switch (json : Json) {
		JsonBool(v): v;
		JsonDouble(d): d != 0.0; // For additional robustness
		JsonString(s): s == "1" || s == "true" || s == "TRUE"; // it happens!
		JsonObject(members): def;
		JsonArray(value): def;
		JsonNull(): def;
	}
}

getJsonStringValue(json : Json, def : string) -> string {
	either(getJsonStringValueM(json), def);
}

getJsonStringValueM(json : Json) -> Maybe<string> {
	switch (json : Json) {
		JsonString(s): Some(s);
		JsonDouble(d): Some(d2s(d));
		JsonBool(b): Some(b2s(b));
		default: None();
	}
}

getJsonDoubleValue(json : Json, def : double) -> double {
	either(getJsonDoubleValueM(json), def);
}

getJsonDoubleValueM(json : Json) -> Maybe<double> {
	switch (json : Json) {
		JsonDouble(value): Some(value);
		// Doubles are sometimes taken as strings!
		// we have to check the s, otherwise non digital strings will be converted into Some(0.0)
		JsonString(s): if (isDouble(s)) Some(s2d(s)) else None();
		default: None();
	}
}

getJsonIntValue(json : Json, def : int) -> int {
	either(getJsonIntValueM(json), def);
}

getJsonIntValueM(json : Json) -> Maybe<int> {
	switch (json : Json) {
		JsonDouble(value): Some(floor(value));
		// Ints are sometimes taken as strings!
		// we have to check the s, otherwise non digital strings will be converted into Some(0)
		JsonString(s): if (isDouble(s)) Some(s2i(s)) else None();
		default: None();
	}
}

doParseJson(json : string, jsonLength : int, start : int) -> ParsingAcc<Json> {
	charCode = getCharCodeAt(json, start);
	if (charCode == 32 || charCode == 9 || charCode == 10 || charCode == 13) { // " " || "\t" || "\n" || "\r"
		// Eat white space
		doParseJson(json, jsonLength, start + 1);
	} else {
		if (charCode == 91) {				// "["
			parseJsonArray2(json, jsonLength, start + 1, makeList());
		} else if (charCode == 123) {		// "{"
			parseJsonObject2(json, jsonLength, start + 1, makeList());
		} else if (charCode == 34) {		// "\""
			v = deserializeRestOfString2(json, start + 1);
			ParsingAcc(JsonString(v.first), start + 1, v.second, v.second);
		} else if (charCode == 110 /* "n" */ && substring(json,start, 4) == "null") {
			ParsingAcc(JsonNull(), start, start + 4, start + 4);
		} else if (charCode == 116 /* "t" */ && substring(json,start, 4) == "true") {
			ParsingAcc(JsonBool(true), start, start + 4, start + 4);
		} else if (charCode == 102 /* "f" */ && substring(json,start, 5) == "false") {
			ParsingAcc(JsonBool(false), start, start + 5, start + 5);
		} else {
			v = parseJsonDouble2(json, jsonLength, start);
			ParsingAcc(v.first, start, v.second, v.second);
		}
	}
}

parseJsonObject(json : string, start : int, acc : List<Pair<string, Json>>) -> ParsingAcc<Json> {
	parseJsonObject2(json, strlen(json), start, acc)
}

parseJsonObject2(json : string, jsonLength : int, start : int, acc : List<Pair<string, Json>>) -> ParsingAcc<Json> {
	fin = skipJson2(json, jsonLength, start, " ").second;
	head = switch (acc) {
		EmptyList(): Pair(fin, fin);
		Cons(__, __): skipJson2(json, jsonLength, start, ",");
	};
	charCode = getCharCodeAt(json, head.second);
	if (getCharCodeAt(json, fin) == 125) // "}"
		ParsingAcc(JsonObject(list2array(acc)), start, fin + 1, fin + 1)
	else if (head.second >= jsonLength || head.first < 0) {
		// No comma nor brace - just give what we have so far, to allow partial parsing
		ParsingAcc(JsonObject(list2array(acc)), start, start, fin)
	} else {
		key =
			if (charCode == 34) {// "\""
				v = deserializeRestOfString2(json, head.second + 1);
				if (v.second == head.second + 1) Pair("", head.second + 1) else v
			} else
				Pair("", head.second + 1);

		afterColon = skipJson2(json, jsonLength, key.second, ":");
		value = doParseJson(json, jsonLength, afterColon.second);
		nacc = Cons(Pair(key.first, value.data), acc);
		if (key.second == head.second + 1 || afterColon.first < 0 || value.start == value.finish) {
			// Some problem - just give what we have so far, to allow partial parsing
			ParsingAcc(JsonObject(list2array(nacc)), start, start, value.finish)
		} else {
			parseJsonObject2(json, jsonLength, value.finish, nacc);
		}
	}
}

skipJson(json : string, start : int, char : string) -> int {
	skipJson2(json, strlen(json), start, char).second;
}

skipJson2(json : string, jsonLength : int, start : int, char : string) -> Pair<int, int> {
	if (start >= jsonLength) {
		Pair(-1, start);
	} else {
		headCharCode = getCharCodeAt(json, start);
		if (fromCharCode(headCharCode) == char) {
			v = skipJson2(json, jsonLength, start + 1, " ");
			Pair(start, v.second);
		} else if (headCharCode == 32 || headCharCode == 9 || headCharCode == 10 || headCharCode == 13) {
			skipJson2(json, jsonLength, start + 1, char);
		} else {
			Pair(-1, start);
		}
	}
}

parseJsonArray(json : string, start : int, acc : List<Json>) -> ParsingAcc<Json> {
	parseJsonArray2(json, strlen(json), start, acc);
}

parseJsonArray2(json : string, jsonLength : int, start : int, acc : List<Json>) -> ParsingAcc<Json> {
	fin = skipJson2(json, jsonLength, start, " ").second;
	head = switch (acc) {
		EmptyList(): Pair(fin, fin);
		Cons(__, __): skipJson2(json, jsonLength, start, ",");
	};
	if (getCharCodeAt(json, fin) == 93 || getCharCodeAt(json, head.second) == 93) // "]" or ,]
		ParsingAcc(JsonArray(list2array(acc)), start, fin + 1, fin + 1)
	else if (head.second >= jsonLength || head.first < 0) {
 		// No comma nor bracket - just give what we have so far, to allow partial parsing
		ParsingAcc(JsonArray(list2array(acc)), start, start, fin)
	} else {
		value = doParseJson(json, jsonLength, head.second);
		if (value.start == value.finish) {
			// No progress, so we have some problem - just give what we have so far, to allow partial parsing
			ParsingAcc(JsonArray(list2array(acc)), start, start, value.finish)
		} else {
			parseJsonArray2(json, jsonLength, value.finish, Cons(value.data, acc));
		}
	}
}

parseJsonDouble(s : string, start : int) -> Pair<Json, int> {
	parseJsonDouble2(s, strlen(s), start);
}

parseJsonDouble2(s : string, stringLength: int, start : int) -> Pair<Json, int> {
	end = countUntil(start, stringLength, \i -> {
		c = getCharCodeAt(s, i);
		// "0" <= c && c <= "9") || c == "." || c == "-" || c == "e" || c == "E" || c == "+"
		! ((48 <= c && c <= 57) || c == 46 || c == 45 || c == 101 || c == 69 || c == 43);
	});
	Pair(JsonDouble(s2d(substring(s, start, end - start))), end);
}

getJsonStringField(json: Json, field: string, def: string) -> string {
	getJsonStringValue(getJsonFieldValue(json, field, JsonString(def)), def);
}

getJsonStringFieldCustom(json : Json, field: string, def : string, options : [JsonFieldOptions]) -> string {
	getJsonStringValue(getJsonFieldValueCustom(json, field, JsonString(def), options), def)
}

getJsonDoubleField(json: Json, field: string, def: double) -> double {
	getJsonDoubleValue(getJsonFieldValue(json, field, JsonDouble(def)), def);
}

getJsonIntField(json: Json, field: string, def: int) -> int {
	trunc(getJsonDoubleField(json, field, i2d(def)));
}

getJsonArrayField(json: Json, field: string) -> [Json] {
	// This produces a bit less garbage compared to getJsonFieldValue
	switch (getJsonFieldValueM(json, field) : Maybe<Json>) {
		None(): [];
		Some(fieldValue): getJsonArrayValue(fieldValue, []);
	}
}

array2json(arr: [?], item2json: (?) -> string) -> string {
	"[" + strGlue(map(arr, item2json), ",") + "]";
}

getJsonObjectField(json: Json, field: string) -> JsonObject {
	JsonObject(getJsonObjectValue(getJsonFieldValue(json, field, JsonObject([])), []));
}

getJsonBoolField(json: Json, field: string, def: bool) -> bool {
	getJsonBoolValue(getJsonFieldValue(json, field, JsonBool(def)), def);
}

getJsonBoolFieldCustom(json : Json, field: string, def : bool, options : [JsonFieldOptions]) -> bool {
	getJsonBoolValue(getJsonFieldValueCustom(json, field, JsonBool(def), options), def)
}

getJsonMaybeField(json : Json, getJsonValueFn : (Json, ?) -> ?, field: string, def : ?) -> Maybe<?> {
	value = getJsonFieldValue(json, field, JsonNull());
	getJsonMaybeValue(value, getJsonValueFn, def);
}

getJsonMaybeValue(value : Json, getJsonValueFn : (Json, ?) -> ?, def : ?) -> Maybe<?> {
	switch (value) {
		JsonNull(): None();
		default: Some(getJsonValueFn(value, def));
	}
}

json2string(j : Json) -> string {
	switch (j : Json) {
		JsonNull(): "null";
		JsonBool(v): b2s(v);
		JsonDouble(d): d2s(d);
		JsonString(s): toStringForJson(s);
		JsonArray(arr): "[" + strGlue(map(arr, json2string), ",") + "]";
		JsonObject(m): "{" + strGlue(map(m, \p : Pair<string, Json> -> toStringForJson(p.first) + ":" + json2string(p.second)), ",") + "}";
	}
}

json2stringFormatted(j : Json) -> string {
	switch (j : Json) {
		JsonNull(): "null";
		JsonBool(v): b2s(v);
		JsonDouble(d): d2s(d);
		JsonString(s): toStringForJson(s);
		JsonArray(arr): {
			if (arr == []) "[]" else {
				a = map(arr, \v -> json2stringFormatted(v));
				if (length(arr) <= 1) "[" + a[0] + "]"
				else if (length(arr) == 1) "[" + strGlue(map(strSplit(strGlue(a, ",\n"), "\n"), \l -> "\t" + l),"\n") + "]"
				else "[\n" + strGlue(map(strSplit(strGlue(a, ",\n"), "\n"), \l -> "\t" + l),"\n") + "\n]";
			}
		}
		JsonObject(arr): {
			if (arr == []) {
				"{}";
			} else {
				a = map(arr, \v : Pair<string, Json> -> toStringForJson(v.first) + ": " + json2stringFormatted(v.second));
				if (length(arr) <= 1) {
					"{" + a[0] + "}"
				} else if (length(arr) == 1) {
					"{ " + strGlue(map(strSplit(strGlue(a, ",\n"), "\n"), \l -> "\t" + l),"\n") + "}";
				} else "{\n" + strGlue(map(strSplit(strGlue(a, ",\n"), "\n"), \l -> "\t" + l),"\n") + "\n}";
			}
		}
	}
}


json2stringTrimQuotes(j : Json) -> string {
	trim2(json2string(j), "\"");
}

getValueFromJsonArray(json: Json, index: int, defaultValue: Json) -> Json {
	array = getJsonArrayValue(json, []);
	if (length(array) <= index) {
		defaultValue;
	} else {
		array[index];
	}
}

getIntValueFromJsonArray(json: Json, index: int, defaultValue: int) -> int {
	array = getJsonArrayValue(json, []);
	if (length(array) <= index) {
		defaultValue
	} else {
		value = array[index];
		getJsonIntValue(value, defaultValue)
	}
}

getDoubleValueFromJsonArray(json: Json, index: int, defaultValue: double) -> double {
	array = getJsonArrayValue(json, []);
	if (length(array) <= index) {
		defaultValue;
	} else {
		value = getJsonArrayValue(json, [])[index];
		getJsonDoubleValue(value, defaultValue);
	}
}

getStringValueFromJsonArray(json: Json, index: int, defaultValue: string) -> string {
	array = getJsonArrayValue(json, []);
	if (length(array) <= index) {
		defaultValue;
	} else {
		value = getJsonArrayValue(json, [])[index];
		getJsonStringValue(value, defaultValue);
	}
}

getBoolValueFromJsonArray(json: Json, index: int, defaultValue: bool) -> bool {
	array = getJsonArrayValue(json, []);
	if (length(array) <= index) {
		defaultValue;
	} else {
		value = getJsonArrayValue(json, [])[index];
		getJsonBoolValue(value, defaultValue);
	}
}

isJsonEqual(first : Json, second : Json) -> bool {
	isJsonEqualIgnoreOrder(first, second, false);
}

isJsonEqualIgnoreOrder(first : Json, second : Json, ignoreOrder : bool) -> bool {
	switch (first : Json) {
		JsonBool(v): switch (second : Json) {
			JsonBool(val): v == val;
			default: false;
		}
		JsonDouble(d): switch (second : Json) {
			JsonDouble(val): equalDoubles(d, val);
			default: false;
		}
		JsonString(s): switch (second : Json) {
			JsonString(val): s == val;
			default: false;
		}
		JsonNull(): switch (second : Json) {
			JsonNull(): true;
			default: false;
		}
		JsonArray(value): switch (second : Json) {
			JsonArray(values): {
				if (length(value) == length(values)) {
					if (ignoreOrder) {
						equalMembersIndiecesM : Maybe<Pair<int, int>> = findmapi(
							value,
							\i1, v1 -> maybeMap(
								findi(values, \v2 -> isJsonEqualIgnoreOrder(v1, v2, ignoreOrder)),
								\i2 -> Pair(i1, i2)
							)
						);
						eitherMap(
							equalMembersIndiecesM,
							\equalMembersIndieces -> isJsonEqualIgnoreOrder(
								JsonArray(removeIndex(value, equalMembersIndieces.first)),
								JsonArray(removeIndex(values, equalMembersIndieces.second)),
								ignoreOrder
							),
							length(value) == 0
						);
					} else {
						iteriUntil(value, \id, val -> !isJsonEqual(val, values[id])) == length(value);
					}
				} else {
					false;
				}
			}
			default: false;
		}
		JsonObject(obj): switch (second : Json) {
			JsonObject(objs): {
				if (length(obj) == length(objs)) {
					compareFn = \x : Pair, y : Pair -> if (x.first < y.first) -1 else if (x.first == y.first) 0 else 1;
					sortedObjs = mergeSort(objs, compareFn);
					iteriUntil(
						mergeSort(obj, compareFn),
						\id, val -> val.first != sortedObjs[id].first
							|| !isJsonEqualIgnoreOrder(val.second, sortedObjs[id].second, ignoreOrder)
					) == length(obj);
				} else {
					false;
				}
			}
			default: false;
		}
	}
}

pushJsonValue(array : Json, value : Json) -> JsonArray {
	switch (array) {
		JsonArray(av): JsonArray(arrayPush(av, value));
		default: {
			// Well, we drop whatever we had before
			JsonArray([value]);
		}
	}
}

setJsonField(json : Json, field : string, value : Json) -> JsonObject {
	setJsonFieldCustom(json, field, value, []);
}

setJsonFieldCustom(json : Json, field : string, value : Json, options : [JsonFieldOptions]) -> JsonObject {
	jsonFieldIgnoreCase = contains(options, JsonFieldIgnoreCase());
	fixLetterCase = if (jsonFieldIgnoreCase) toLowerCase else idfn;
	switch (json) {
		JsonObject(ms): {
			hasField = exists(ms, \m -> fixLetterCase(m.first) == fixLetterCase(field));
			if (hasField) {
				JsonObject(map(ms, \m : Pair<string, Json> -> if (fixLetterCase(m.first) == fixLetterCase(field)) Pair(m.first, value) else m));
			} else {
				JsonObject(arrayPush(ms, Pair(field, value)))
			}
		}
		default: JsonObject([Pair(field, value)]);
	}
}

removeJsonObjectField(json : JsonObject, field : string) -> JsonObject {
	JsonObject(eitherMap(
		findi(json.members, \member -> member.first == field),
		\idx -> removeIndex(json.members, idx),
		json.members
	));
}

removeJsonField(json : Json, field : string) -> Json {
	switch (json) {
		JsonObject(members): removeJsonObjectField(json, field);
		default: json;
	}
}

jsonObjectHasField(json : Json, member : string) -> bool {
	switch (json) {
		JsonObject(members): exists(members, \p -> p.first == member);
		default: false;
	}
}

getJsonNestedField(json: Json, fields: [string]) -> Json {
	if (fields == []) json else {
		getJsonNestedField(getJsonFieldValue(json, fields[0], JsonNull()), tail(fields));
	}
}

setJsonNestedField(json: Json, fields: [string], value : Json) -> Json {
	if (fields == []) value else {
		field = fields[0];
		switch (json) {
			JsonObject(members): {
				switch (findi(members, \memb -> memb.first == field)) {
					Some(i): {
						member = members[i].second;
						JsonObject(replace(members, i,
							Pair(field, setJsonNestedField(member, tail(fields), value))
						));
					}
					None(): {
						JsonObject(arrayPush(members,
							Pair(field, setJsonNestedField(JsonNull(), tail(fields), value))
						));
					}
				}
			}
			default: {
				JsonObject([
					Pair(field, setJsonNestedField(JsonNull(), tail(fields), value))
				]);
			}
		}
	}
}

getJsonFieldValueMCustom(json : Json, field : string, options : [JsonFieldOptions]) -> Maybe<Json> {
	treatAsPath = extractStruct(options, JsonFieldTreatAsPath("")).pathDelimiter;
	pth = if (treatAsPath != "") strSplit(field, treatAsPath) else [field];
	if (length(pth) > 1) {
		switch (getJsonFieldValueMCustom(json, pth[0], options)) {
			None(): None();
			Some(child): getJsonFieldValueMCustom(child, strGlue(tail(pth), treatAsPath), options);
		}
	} else {
		members = getJsonObjectValue(json, []);
		ignoreCase = containsStruct(options, JsonFieldIgnoreCase());
		fieldName = if (ignoreCase) toLowerCase(field) else field;
		maybeMap(
			find(members, \member -> (if (ignoreCase) toLowerCase(member.first) else member.first) == fieldName),
			secondOfPair
		);
	}
}

jsonObjectHasFieldCustom(json : Json, member : string, options : [JsonFieldOptions]) -> bool {
	treatAsPath = extractStruct(options, JsonFieldTreatAsPath("")).pathDelimiter;
	pth = if (treatAsPath != "") strCountOf(member, treatAsPath) else 0;
	if (pth > 0) {
		isSome(getJsonFieldValueMCustom(json, member, options));
	} else {
		switch (json) {
			JsonObject(members): {
				ignoreCase = containsStruct(options, JsonFieldIgnoreCase());
				fieldName = if (ignoreCase) toLowerCase(member) else member;
				exists(members, \p -> (if (ignoreCase) toLowerCase(p.first) else p.first) == fieldName);
			}
			default: false;
		}
	}
}

getJsonArrayFieldCustom(json: Json, field: string, options : [JsonFieldOptions]) -> [Json] {
	fieldValue = getJsonFieldValueMCustom(json, field, options);
	fieldValue ?? getJsonArrayValue(fieldValue, []) : [];
}

getJsonDoubleFieldCustom(json: Json, field: string, def: double, options : [JsonFieldOptions]) -> double {
	getJsonDoubleValue(getJsonFieldValueCustom(json, field, JsonDouble(def), options), def);
}

getJsonIntFieldCustom(json: Json, field: string, def: int, options : [JsonFieldOptions]) -> int {
	trunc(getJsonDoubleFieldCustom(json, field, i2d(def), options));
}

getJsonObjectFieldCustom(json: Json, field: string, options : [JsonFieldOptions]) -> JsonObject {
	JsonObject(getJsonObjectValue(getJsonFieldValueCustom(json, field, JsonObject([]), options), []));
}
