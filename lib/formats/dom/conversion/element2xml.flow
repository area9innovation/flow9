import formats/dom/element;
import formats/dom/xml;
import formats/dom/conversion/svgcss2string;
import formats/html/html;
import ds/arrayutils;

export {
	element2xml(e : Element) -> IXmlNode;
}

element2xml(e : Element) -> IXmlNode {
	switch (e) {
		EGeneric(tag, html, children, style, attributes, globalAttributes, events): {
			att = concat(attributes, globalAttributes2attributes(globalAttributes));
			st = cssStyle2string(style);
			allAtt = concat(att, if (st == "") [] else [ElementAttribute("style", st)]);
			ch0 = map(children, element2xml);
			ch = if (html != "") concat(ch0, [IXmlText(html)]) else ch0;
			EIXml(tag, map(allAtt, \a -> IXmlAttribute(a.name, a.value)), ch, map(events, \ev -> IXmlEvent(ev.name, ev.iexp)));
		}
		EGenericSvg(tag, html, children, style, attributes, globalAttributes, events): {
			att = concat(attributes, globalAttributes2attributes(globalAttributes));
			st = svgCssStyle2string(style);
			allAtt = concat(att, if (st == "") [] else [ElementAttribute("style", st)]);
			ch0 = map(children, element2xml);
			ch = if (html != "") concat(ch0, [IXmlText(html)]) else ch0;
			EIXml(tag, map(allAtt, \a -> IXmlAttribute(a.name, a.value)), ch, map(events, \ev -> IXmlEvent(ev.name, ev.iexp)));
		}
	}
}

globalAttributes2attributes(a : GlobalAttributes) -> [ElementAttribute] {
	concatA(
		[
			if (a.accessKey != "") [ElementAttribute("accesskey", a.accessKey)] else [],
			if (a.class != "") [ElementAttribute("class", a.class)] else [],
			if (a.contentEditable) [ElementAttribute("contenteditable", b2s(a.contentEditable))] else [],
			if (a.draggable != "") [ElementAttribute("draggable", a.draggable)] else [],
			if (a.hidden) [ElementAttribute("hidden", b2s(a.hidden))] else [],
			if (a.id != "") [ElementAttribute("id", a.id)] else [],
			if (a.inert) [ElementAttribute("inert", b2s(a.inert))] else [],
			if (a.lang != "") [ElementAttribute("lang", a.lang)] else [],
			if (!a.spellcheck) [ElementAttribute("spellcheck", b2s(a.spellcheck))] else [],
			if (a.tabIndex != -1) [ElementAttribute("tabindex", i2s(a.tabIndex))] else [],
			if (a.title != "") [ElementAttribute("title", a.title)] else []
		]
	);
}
