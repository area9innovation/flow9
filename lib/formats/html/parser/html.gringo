source = $"nil" (node $"cons")* $"source_1";
node = entity | script | tag | comment | text;

script  = '<' ws "script" ws $"nil" (attrib $"cons")* '>' $(!('</' ws "script" ws '>') any)* ('<' ws "script" '>')? $"script_2";
text    = $(!(('<' (id_letter | '/' id_letter | "!--")) | '&' !s) any)+ $"text_1";
comment = "<!--" $(!"-->" any)* "-->" $"comment_1";
entity  = $("&#x" | "&#" | '&') $(hex_digit | letter)+ (ws ';')? $"entity_2";
tag     = $("</" | '<') ws id ws $"nil" (attrib $"cons")* $("/>" | '>') $"tag_4";

attrib = key ws val ws $"attrib_2";
key = $(!(s | '=' | '>' | '/') any)+;
id  = $(id_letter (!(s | '>' | '/') any)*);
val = (
	'=' ws (
		"'" $(!"'" any)* "'" | 
		'"' $(!'"' any)* '"' | 
		$(!(s | '>' | '/' | '=') any)+
	)
) | $$"1";

hex_digit = '0'-'9' | 'A'-'F' | 'a'-'f';
letter = 'a'-'z' | 'A'-'Z';
id_letter = letter | '!' ;

s = ws_+;
ws = ws_*;
ws_ = ' ' | '\t' | '\n' | '\r';

any = '0x0000'-'0xffff';

source
