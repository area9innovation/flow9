source = ws element*;
element =  entity | tag | comment | text;

text = (!(('<' (letter | '/' letter | "!--")) | '&' !s) any)+;
comment = "<!--" comment_text "-->";
	comment_text = (!"-->" any)*;
entity  = entity_prefix entity_body (ws ';')?;
	entity_prefix = ("&#x" | "&#" | '&');
	entity_body = (hex_digit | letter)+;
tag = tag_open id ws attrib* tag_close;
	tag_open = ("</" | '<');
	tag_close = ("/>" | '>');

attrib = attrib1 | attrib2 | attrib3 | attrib4;
attrib1 = id ws '=' ws '\'' attrib1_val '\'' ws;
	attrib1_val = (!'\'' any)*;
attrib2 = id ws '=' ws "\"" attrib2_val "\"" ws;
	attrib2_val = (!"\"" any)*;
attrib3 = id ws '=' ws attrib3_val ws;
	attrib3_val = (letter | dec_digit | '_' | '-')+;
attrib4 = id ws; 

dec_digit = '0'-'9';
hex_digit = dec_digit | 'A'-'F' | 'a'-'f';
letter = 'a'-'z' | 'A'-'Z';
id = (letter (letter | dec_digit)*);

s = ws_+;
ws = ws_*;
ws_ = ' ' | '\t' | '\n' | '\r';

any = '\u0000'-'\uFFFF';
