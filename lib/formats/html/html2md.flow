import formats/html/html_node;
import math/matrix;

export {
	// Converts HTML into markdown. Following markdown constructions are supported:
	// - headings
	// - line breaks
	// - emphasize with bold/itallic font
	// - block code quotations
	// - lists (ordered, unordered)
	// - tables
	ruHtml2md(html : [HtmlNode]) -> string;
}

ruHtml2md(html : [HtmlNode]) -> string {
	output = ruDoHtml2md(html, "*");
	// OK, now clean it up and avoid too many empty lines
	lines = strSplit(output, "\n");
	// Clean out whitespace
	trimmedLines = map(lines, \line -> {
		trim2(line, " \t\r");
	});
	// Reduce two empty lines to one
	simplify = fold(trimmedLines, [], \acc, line -> {
		if (acc != [] && line == "" && lastElement(acc, "") == "") acc else arrayPush(acc, line)
	});
	strGlue(simplify, "\n");
}

ruDoHtml2md(html : [HtmlNode], indent : string) -> string {
	concatStrings(map(html, \elem -> ruHtmlNode2md(elem, indent)))
}

ruHtmlNode2md(html : HtmlNode, indent : string) -> string {
	switch (html) {
		HtmlTag(tag, attribs, body): {
			contents = ruDoHtml2md(body, indent);
			if (strlen(tag) == 2 && getCharAt(tag, 0) == "h") {
				ruHtmlHeading2md(s2i(getCharAt(tag, 1)), contents);
			} else if (tag == "p" || tag == "div" || tag == "body" || tag == "section" || tag == "html") {
				trim2(contents, " \n\t\r") + "\n\n";
			} else if (tag == "br") {
				"\n";
			} else if (tag == "strong" || tag == "b") {
				"**" + contents + "**";
			} else if (tag == "pre" || tag == "tt") {
				"`" + contents + "`";
			} else if (tag == "sup") {
				"^" + contents + "^";
			} else if (tag == "sub") {
				"~" + contents + "~";
			} else if (tag == "em" || tag == "i" || tag == "it") {
				"*" + contents + "*";
			} else if (tag == "blockquote") {
				strGlue(map(strSplit(contents, "\n"), \line -> "> " + line), "\n");
			} else if (tag == "ol" || tag == "ul" || tag == "li") {
				ruHtmlList2md(tag, body, indent);
			} else if (tag == "sub") {
				"_" + contents;
			} else if (tag == "table") {
				ruHtmlTable2md(body);
			} else if (tag == "span") {
				trimmed = trim2(contents, " \n\t\r");
				if (trimmed != contents) trimmed + " " else trimmed
			} else if (tag == "a") {
				href = fold(attribs, "", \acc, attrib -> if (attrib.key == "href") attrib.val else acc);
				if (startsWith(href, "http")) {
					"[" + trim2(contents, " \n\t\r") + "](" + href + ")";
				} else {
					// Internal link. Ignore
					trim2(contents, " \n\t\r");
				}
			} else if (tag == "head" || tag == "header" || tag == "style" || tag == "script" || tag == "link" || tag == "title") {
				// Ignore these
				"";
			} else if (tag == "tr" || tag == "th" || tag == "td") {
				// These tags are processed in `ruHtmlTable2md`
				"";
			} else if (tag == "img") {
				// ![Image](http://example.com/image.png)
				src = fold(attribs, "", \acc, attrib -> if (attrib.key == "src") attrib.val else acc);
				alt = fold(attribs, "Image", \acc, attrib -> if (attrib.key == "alt") attrib.val else acc);
				"![" + alt + "](" + src + ")"
			} else if (tag != "") {
				println("TODO: Support " + tag + " in ruHtmlNode2md");
				contents;
			} else {
				contents;
			}
		}
		HtmlText(text): text;
		HtmlComment(text): "";
		HtmlEntity(prefix, body): unescapeHtmlEntity(html);
	}
}

ruHtmlHeading2md(level : int, body : string) -> string {
	if (level < 1 || level > 5) {
		// Illegal heading level
		body;
	} else if (strContains(body, "\n")) {
		// Heading has new lines
		body;
	} else {
		"\n\n" + strRepeat("#", level) + (if (startsWith(body, " ")) "" else " ") + body + "\n\n";
	}
}

insertPrefix2md(pref : string, s : string) -> string {
	non_space = findiDef(s2a(s), \code -> code > 32, -1);
	if (non_space == -1) {
		s + pref;
	} else {
		spaces = strLeft(s, non_space);
		contents = strRight(s, non_space);
		spaces + pref + contents;
	}
}

ruHtmlList2md(tag : string, body : [HtmlNode], indent : string) -> string {
	i = ref 0;
	"\n" + concatStrings(
		map(body, \elem ->  {
			str = ruHtmlNode2md(elem, indent);
			switch (elem) {
				HtmlTag(tag1, __,__): {
					if (tag1 != "li") str else {
						i := ^i + 1;
						if (tag == "ol") {
							insertPrefix2md(i2s(^i) + ". ", str);
						} else {
							insertPrefix2md(indent + " ", str);
						}
					}
				}
				default: str;
			}
		})
	);
}

ruExtractHtmlTags(body : [HtmlNode]) -> [HtmlTag] {
	filtermap(body, \elem ->
		switch(elem) {
			HtmlTag(tag, attibs, body1): Some(elem);
			default: None();
		}
	);
}

ruFirstTag(body : [HtmlNode]) -> HtmlTag {
	tags = ruExtractHtmlTags(body);
	firstElement(tags, HtmlTag("", [], [])); 
}

ruHtmlTable2md(children : [HtmlNode]) -> string {
	first_row = ruFirstTag(children);
	first_col = ruFirstTag(first_row.children);
	headers = if (first_col.tag != "th") None() else {
		Some(filtermap(first_row.children, \elem ->
			switch(elem) {
				HtmlTag(tag, __, children1): {
					if (tag != "th") None() else Some(ruDoHtml2md(children1, ""));
				}
				default: None();
			}
		));
	}
	tags = filtermap(children, \elem ->
		switch(elem) {
			HtmlTag(__,__,__): Some(elem);
			default: None();
		}
	);
	rows = filtermap(children, \elem ->
		switch(elem) {
			HtmlTag(tag, __, children1): {
				if (tag != "tr") None() else {
					col_tags = ruExtractHtmlTags(children1);
					if (col_tags == []) None() else {
						if (col_tags[0].tag == "th") None() else {
							cols = filtermap(children1, \elem1 -> 
								switch(elem1) {
									HtmlTag(tag1, __, cell): {
										if (tag1 != "td") None() else {
											Some(ruDoHtml2md(cell, ""));
										}
									}
									default: None();
								}
							);
							Some(cols);
						}
					}
				}
			}
			default: None();
		}
	);
	cols = transposeMatrix(rows);
	"\n" + stringMatrix2s(cols, headers, None());
}
