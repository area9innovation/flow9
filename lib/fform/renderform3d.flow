import rendersupport3d;
import fform/fform;

export {
	F3DStage(wh : Transform<WidthHeight>, scene : F3DObject, camera : F3DObject, style : [F3DStageStyle]) -> FForm;
		F3DStageStyle ::= F3DIgnoreMouseEvents, F3DExportScene, F3DTransformControlsEnabled, F3DAttachTransformControls;
			F3DIgnoreMouseEvents();
			F3DExportScene(fn : ref () -> string);
			F3DTransformControlsEnabled(enabled : Transform<bool>); // const(false) by default, doesn't affect F3DTransformControls
			F3DAttachTransformControls(fn : ref (clip : native, attach : bool) -> void);

	F3DObject ::= F3DScene, F3DMutable, F3DVisible, F3DPosition, F3DRotation, F3DScale, F3DLookAt,
		F3DInspectRotation, F3DInspectPosition, F3DInspectScale, F3DInspectBoundingSphereCenter, F3DInspectBoundingSphereMin,
		F3DInspectBoundingSphereMax, F3DCamera, F3DLight, F3DMesh, F3DTransformControls, F3DLoader, F3DGLTFLoader, F3DSceneLoader,
		F3DChildren, F3DNative, F3DJsonObject, F3DAxesHelper, F3DGridHelper, F3DInteractive;

		F3DScene(objects : Transform<[F3DObject]>);
		F3DMutable(object : Transform<F3DObject>);

		F3DVisible(object : F3DObject, visible : Transform<bool>);
		F3DPosition(object : F3DObject, x : Transform<double>, y : Transform<double>, z : Transform<double>);
		F3DRotation(object : F3DObject, x : Transform<double>, y : Transform<double>, z : Transform<double>);
		F3DScale(object : F3DObject, x : Transform<double>, y : Transform<double>, z : Transform<double>);
		F3DLookAt(object : F3DObject, x : Transform<double>, y : Transform<double>, z : Transform<double>, update : DynamicBehaviour<bool>);

		F3DInspectRotation(object : F3DObject, x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, z : DynamicBehaviour<double>);
		F3DInspectPosition(object : F3DObject, x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, z : DynamicBehaviour<double>);
		F3DInspectScale(object : F3DObject, x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, z : DynamicBehaviour<double>);

		F3DInspectBoundingSphereCenter(object : F3DObject, x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, z : DynamicBehaviour<double>);
		F3DInspectBoundingSphereMin(object : F3DObject, x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, z : DynamicBehaviour<double>);
		F3DInspectBoundingSphereMax(object : F3DObject, x : DynamicBehaviour<double>, y : DynamicBehaviour<double>, z : DynamicBehaviour<double>);

		F3DTransformControls(object : F3DObject, enabled : DynamicBehaviour<bool>);

		F3DLoader(objUrl : string, mtlUrl : string);
		F3DGLTFLoader(url : string, onAnimation : ([F3DAnimation]) -> void);
			F3DAnimation(start : () -> void, stop : () -> void, duration : DynamicBehaviour<double>);

		F3DSceneLoader(url : string);
		F3DChildren(object : F3DObject, fn : (F3DNative) -> F3DNative);

		F3DNative ::= F3DNativeObject, F3DNativeScene, F3DNativeMutable;
			F3DNativeObject(object : native);
			F3DNativeScene(scene : native, objects : Transform<[F3DNative]>);
			F3DNativeMutable(object : Transform<F3DNative>);

		F3DJsonObject(json : string);

		F3DAxesHelper(size : double);
		F3DGridHelper(size : double, divisions : int, colorCenterLine : int, colorGrid : int);

		F3DInteractive(object : F3DObject, listeners : [F3DEventHandler]);
			F3DEventHandler ::= F3DMouseDown, F3DMouseUp;
				F3DMouseDown(fn : (handled : bool) -> bool);
				F3DMouseUp(fn : (handled : bool) -> bool);

		F3DLight ::= F3DPointLight;
			F3DPointLight(color : Transform<int>, intensity : Transform<double>, distance : Transform<double>, decayAmount : Transform<double>);

		F3DCamera ::= F3DPerspectiveCamera;
			F3DPerspectiveCamera(fov : Transform<double>, aspect : Transform<double>, near : Transform<double>, far : Transform<double>);

		F3DMesh(geometry : F3DGeometry, material : F3DMaterial);
			F3DGeometry ::= F3DBoxGeometry, F3DCircleGeometry, F3DConeGeometry, F3DCylinderGeometry, F3DNativeGeometry;
				F3DBoxGeometry(width : double, height : double, depth : double, widthSegments : int, heightSegments : int, depthSegments : int);
				F3DCircleGeometry(radius : double, segments : int, thetaStart : double, thetaLength : double);
				F3DConeGeometry(radius : double, height : double, radialSegments : int, heightSegments : int, openEnded : bool, thetaStart : double, thetaLength : double);
				F3DCylinderGeometry(radiusTop : double, radiusBottom : double, height : double, radialSegments : int, heightSegments : int, openEnded : bool, thetaStart : double, thetaLength : double);
				F3DNativeGeometry(geometry : native);

			F3DMaterial ::= F3DMeshBasicMaterial, F3DTextureLoader, F3DNativeMaterials;
				F3DMeshBasicMaterial(color : int, parameters : Tree<string, string>);
				F3DTextureLoader(material : F3DMaterial, url : string);
				F3DNativeMaterials(materials : native);
}

handled3DMouseDown : ref bool = ref false;
handled3DMouseUp : ref bool = ref false;

F3DStage(wh : Transform<WidthHeight>, scene : F3DObject, camera : F3DObject, style : [F3DStageStyle]) -> FForm {
	if (js) {
		container = make(FEmpty());

		load3DLibraries(\ -> {
			metrics = make(FormMetrics(0., 0., 0., 0.));
			clip = make3DStage(fgetValue(fwidth(wh)), fgetValue(fheight(wh)));
			transformControlsEnabled = extractStruct(style, F3DTransformControlsEnabled(const(false))).enabled;
			maybeApply(
				tryExtractStruct(style, F3DAttachTransformControls(ref nop2)),
				\es -> es.fn := \c, b -> if (b) attach3DTransformControls(clip, c) else detach3DTransformControls(clip, c)
			);

			next(
				container,
				FNativeForm(
					FEmpty(),
					metrics,
					\ -> FEmpty(),
					\__, __, __ ->
						NativeRenderResult(
							[clip],
							concat3(
								[
									makeSubscribe(wh, \v -> {
										setClipWidth(clip, v.width);
										setClipHeight(clip, v.height);
										nextDistinct(metrics, FormMetrics(v.width, v.height, v.height, v.height));
									})()
								],
								render3dform(camera, clip, const(false), \c -> {
										set3DCamera(clip, c);

										[];
									}
								),
								render3dform(scene, clip, transformControlsEnabled, \s -> {
									set3DScene(clip, s);

									maybeApply(tryExtractStruct(style, F3DExportScene(ref \ -> "")), \es -> es.fn := \ -> get3DObjectJSON(s));

									addF3DMouseListeners(clip, s, transformControlsEnabled);
								})
							)
							|> (\f -> \ -> applyall(f))
						)
				)
				|> (\f ->
					if (!contains(style, F3DIgnoreMouseEvents())) {
						mouseDown = make(false);

						FGroup2(
							FInteractive(
								[
									MouseDown(\mi -> {
										if (mi.inside()) {
											emit3DMouseEvent(
												clip,
												"mousedown",
												mi.x,
												mi.y
											);
										}

										if (!^handled3DMouseDown) {
											clear3DTransformControls(clip);
										}

										false;
									}),
									MouseUp(\mi -> {
										emit3DMouseEvent(
											clip,
											"mouseup",
											mi().x,
											mi().y
										);

										false;
									}),
									MouseMiddleDown(\mi -> {
										if (mi.inside()) {
											emit3DMouseEvent(
												clip,
												"mousemiddledown",
												mi.x,
												mi.y
											);
										}

										false;
									}),
									MouseMiddleUp(\mi -> {
										emit3DMouseEvent(
											clip,
											"mousemiddleup",
											mi().x,
											mi().y
										);

										false;
									}),
									MouseRightDown(\mi -> {
										if (mi.inside()) {
											emit3DMouseEvent(
												clip,
												"mouserightdown",
												mi.x,
												mi.y
											);
										}

										false;
									}),
									MouseRightUp(\mi -> {
										emit3DMouseEvent(
											clip,
											"mouserightup",
											mi().x,
											mi().y
										);

										false;
									}),
									MouseMove(\mi -> {
										emit3DMouseEvent(
											clip,
											"mousemove",
											mi().x,
											mi().y
										);

										false;
									}),
									KeyDown(\ke -> {
										emit3DKeyEvent(
											clip,
											"keydown",
											ke.utf,
											ke.ctrl,
											ke.shift,
											ke.alt,
											ke.meta,
											ke.keycode
										);
									}),
									KeyUp(\ke -> {
										emit3DKeyEvent(
											clip,
											"keyup",
											ke.utf,
											ke.ctrl,
											ke.shift,
											ke.alt,
											ke.meta,
											ke.keycode
										);
									}),
									FineGrainMouseWheel(\mi -> {
										if (mi.inside) {
											emit3DMouseEvent(
												clip,
												"wheel",
												-mi.dx,
												-mi.dy
											);
										}
									})
								],
								FMutable(fselect(wh, FLift(\v ->
									FGraphics([GRect(0.0, 0.0, v.width, v.height)], [Fill(0x000000), FillOpacity(0.0)])
								))),
							),
							f,
							false
						);
					} else {
						f;
					}
				)
			);
		});

		FMutable(container);
	} else {
		FEmpty();
	}
}

render3dform(object : F3DObject, stage : native, transformControlsEnabled : Transform<bool>, onLoad : (native) -> [() -> void]) -> [() -> void] {
	switch (object : F3DObject) {
		F3DScene(objects) : {
			clip = make3DScene();

			concat(
				onLoad(clip),
				[
					makeSubscribeUns(objects, \ob ->
						map(ob, \o ->
							render3dform(
								o,
								stage,
								transformControlsEnabled,
								\c -> {
									add3DChild(clip, c);

									arrayPush(
										addF3DMouseListeners(stage, c, transformControlsEnabled),
										\ -> remove3DChild(clip, c)
									)
								}
							)
						)
						|> concatA
					)()
				]
			)
		}
		F3DMutable(innerObject) : {
			[
				makeSubscribeUns(innerObject, \obj ->
					render3dform(
						obj,
						stage,
						transformControlsEnabled,
						onLoad
					)
				)()
			]
		}
		F3DNativeMutable(innerObject) : {
			[
				makeSubscribeUns(innerObject, \obj ->
					render3dform(
						obj,
						stage,
						transformControlsEnabled,
						onLoad
					)
				)()
			]
		}
		F3DVisible(innerObject, visible) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip -> {
					concat(
						onLoad(clip),
						[
							makeSubscribe(visible, \v -> set3DObjectVisible(clip, v))(),
						]
					)
				}
			);

			r;
		}
		F3DPosition(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip -> {
					concat(
						onLoad(clip),
						[
							makeSubscribe(x, \v -> set3DObjectX(clip, v))(),
							makeSubscribe(y, \v -> set3DObjectY(clip, v))(),
							makeSubscribe(z, \v -> set3DObjectZ(clip, v))()
						]
					)
				}
			);

			r;
		}
		F3DRotation(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							makeSubscribe(x, \v -> set3DObjectRotationX(clip, v))(),
							makeSubscribe(y, \v -> set3DObjectRotationY(clip, v))(),
							makeSubscribe(z, \v -> set3DObjectRotationZ(clip, v))()
						]
					)
			);

			r;
		}
		F3DScale(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							makeSubscribe(x, \v -> set3DObjectScaleX(clip, v))(),
							makeSubscribe(y, \v -> set3DObjectScaleY(clip, v))(),
							makeSubscribe(z, \v -> set3DObjectScaleZ(clip, v))()
						]
					)
			);

			r;
		}
		F3DLookAt(innerObject, x, y, z, update) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							make4Subscribe(x, y, z, update, \v1, v2, v3, __ -> set3DObjectLookAt(clip, v1, v2, v3))()
						]
					)
			);

			r;
		}
		F3DInspectRotation(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							add3DObjectRotationListener(clip, \nx, ny, nz -> {
								nextDistinct(x, nx);
								nextDistinct(y, ny);
								nextDistinct(z, nz);
							})
						]
					)
			);

			r;
		}
		F3DInspectPosition(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							add3DObjectPositionListener(clip, \nx, ny, nz -> {
								nextDistinct(x, nx);
								nextDistinct(y, ny);
								nextDistinct(z, nz);
							})
						]
					)
			);

			r;
		}
		F3DInspectScale(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							add3DObjectScaleListener(clip, \nx, ny, nz -> {
								nextDistinct(x, nx);
								nextDistinct(y, ny);
								nextDistinct(z, nz);
							})
						]
					)
			);

			r;
		}
		F3DInspectBoundingSphereCenter(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							add3DObjectBoundingBoxListener(clip, \box -> {
								nextDistinct(x, (box[0][0] + box[1][0]) / 2.0);
								nextDistinct(y, (box[0][1] + box[1][1]) / 2.0);
								nextDistinct(z, (box[0][2] + box[1][2]) / 2.0);
							})
						]
					)
			);

			r;
		}
		F3DInspectBoundingSphereMin(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							add3DObjectBoundingBoxListener(clip, \box -> {
								nextDistinct(x, box[0][0]);
								nextDistinct(y, box[0][1]);
								nextDistinct(z, box[0][2]);
							})
						]
					)
			);

			r;
		}
		F3DInspectBoundingSphereMax(innerObject, x, y, z) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							add3DObjectBoundingBoxListener(clip, \box -> {
								nextDistinct(x, box[1][0]);
								nextDistinct(y, box[1][1]);
								nextDistinct(z, box[1][2]);
							})
						]
					)
			);

			r;
		}
		F3DTransformControls(innerObject, enabled) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						[
							add3DEventListener(clip, "detached", \ -> nextDistinct(enabled, false)),
							add3DEventListener(clip, "attached", \ -> nextDistinct(enabled, true)),
							makeSubscribe(enabled, \en ->
								if (en)
									attach3DTransformControls(stage, clip)
								else
									detach3DTransformControls(stage, clip)
							)()
						]
					)
			);

			r;
		}
		F3DLight() : {
			renderF3DLight(object, onLoad);
		}
		F3DCamera() : {
			renderF3DCamera(object, onLoad);
		}
		F3DMesh(geometry, material) : {
			onLoad(make3DMesh(renderF3DGeometry(geometry), renderF3DMaterial(material)));
		}
		F3DLoader(objUrl, mtlUrl) : {
			uns = ref [];

			load3DObject(objUrl, mtlUrl, \obj -> {
				uns := onLoad(obj);
			});

			[
				\ -> dispUnsA(uns)
			]
		}
		F3DGLTFLoader(url, onAnimation) : {
			uns = ref [];

			load3DGLTFObject(url, \animations, scene, scenes, cameras, assets -> {
				uns := onLoad(scene);

				if (length(animations) > 0) {
					mixer = create3DAnimationMixer(scene);

					onAnimation(
						map(animations, \a -> {
							aUns = ref None();
							duration = make(get3DAnimationDuration(a));

							uns :=
								concat(
									^uns,
									[
										\ -> dispUns2(aUns),
										makeSubscribe2(duration, \d -> set3DAnimationDuration(a, d))()
									]
								);

							F3DAnimation(
								\ -> {
									aUns := Some(start3DAnimationMixer(mixer, a));
								},
								\ -> {
									dispUns2(aUns);
								},
								duration
							)
						})
					)
				}
			});

			[
				\ -> dispUnsA(uns)
			]
		}
		F3DSceneLoader(url) : {
			uns = ref [];

			load3DScene(url, \obj -> {
				uns := onLoad(obj);
			});

			[
				\ -> dispUnsA(uns)
			]
		}
		F3DChildren(innerObject, fn) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip -> {
					parsedClip = parseF3DNativeObject(clip);

					concat(
						parsedClip.second,
						render3dform(
							fn(parsedClip.first),
							stage,
							transformControlsEnabled,
							onLoad
						)
					)
				}
			);

			r;
		}
		F3DNativeObject(nativeObject) : {
			onLoad(nativeObject);
		}
		F3DNativeScene(scene, objects) : {
			concat(
				onLoad(scene),
				[
					makeSubscribeUns(objects, \ob ->
						map(ob, \o ->
							render3dform(
								o,
								stage,
								transformControlsEnabled,
								\c -> {
									if (!contains(get3DObjectChildren(scene), c)) {
										add3DChild(scene, c);

										arrayPush(
											addF3DMouseListeners(stage, c, transformControlsEnabled),
											\ -> remove3DChild(scene, c)
										)
									} else {
										[]
									}
								}
							)
						)
						|> concatA
					)()
				]
			)
		}
		F3DJsonObject(json) : {
			onLoad(make3DObjectFromJSON(json));
		}
		F3DAxesHelper(size) : {
			onLoad(make3DAxesHelper(size));
		}
		F3DGridHelper(size, divisions, colorCenterLine, colorGrid) : {
			onLoad(make3DGridHelper(size, divisions, colorCenterLine, colorGrid));
		}
		F3DInteractive(innerObject, listeners) : {
			r = render3dform(
				innerObject,
				stage,
				transformControlsEnabled,
				\clip ->
					concat(
						onLoad(clip),
						map(listeners, \l -> {
							switch (l : F3DEventHandler) {
								F3DMouseDown(fn): {
									add3DEventListener(clip, "mousedown", \ -> {
										handled3DMouseDown := fn(^handled3DMouseDown);

										if (^handled3DMouseDown) {
											deferred(\ -> handled3DMouseDown := false);
										}

									});
								}
								F3DMouseUp(fn): {
									add3DEventListener(clip, "mouseup", \ -> {
										handled3DMouseUp := fn(^handled3DMouseUp);

										if (^handled3DMouseUp) {
											deferred(\ -> handled3DMouseUp := false);
										}
									});
								}
							}
						})
					)
			);

			r;
		}
	}
}

renderF3DCamera(camera : F3DCamera, onLoad : (native) -> [() -> void]) -> [() -> void] {
	switch (camera : F3DCamera) {
		F3DPerspectiveCamera(fov, aspect, near, far): {
			clip = make3DPerspectiveCamera(fgetValue(fov), fgetValue(aspect), fgetValue(near), fgetValue(far));

			concat(
				onLoad(clip),
				[
					makeSubscribe2(fov, \v -> set3DCameraFov(clip, v))(),
					makeSubscribe2(aspect, \v -> set3DCameraAspect(clip, v))(),
					makeSubscribe2(near, \v -> set3DCameraNear(clip, v))(),
					makeSubscribe2(far, \v -> set3DCameraFar(clip, v))()
				]
			)
		}
	}
}

renderF3DLight(light : F3DLight, onLoad : (native) -> [() -> void]) -> [() -> void] {
	switch (light : F3DLight) {
		F3DPointLight(color, intensity, distance, decayAmount): {
			clip = make3DPointLight(fgetValue(color), fgetValue(intensity), fgetValue(distance), fgetValue(decayAmount));

			concat(
				onLoad(clip),
				[
					makeSubscribe2(color, \v -> set3DLightColor(clip, v))(),
					makeSubscribe2(intensity, \v -> set3DLightIntensity(clip, v))(),
					makeSubscribe2(distance, \v -> set3DLightDistance(clip, v))(),
					makeSubscribe2(decayAmount, \v -> set3DLightDecay(clip, v))()
				]
			)
		}
	}
}

renderF3DGeometry(geometry : F3DGeometry) -> native {
	switch (geometry : F3DGeometry) {
		F3DBoxGeometry(width, height, depth, widthSegments, heightSegments, depthSegments) : {
			make3DBoxGeometry(width, height, depth, widthSegments, heightSegments, depthSegments);
		}
		F3DCircleGeometry(radius, segments, thetaStart, thetaLength) : {
			make3DCircleGeometry(radius, segments, thetaStart, thetaLength);
		}
		F3DConeGeometry(radius, height, radialSegments, heightSegments, openEnded, thetaStart, thetaLength) : {
			make3DConeGeometry(radius, height, radialSegments, heightSegments, openEnded, thetaStart, thetaLength);
		}
		F3DCylinderGeometry(radiusTop, radiusBottom, height, radialSegments, heightSegments, openEnded, thetaStart, thetaLength) : {
			make3DCylinderGeometry(radiusTop, radiusBottom, height, radialSegments, heightSegments, openEnded, thetaStart, thetaLength);
		}
		F3DNativeGeometry(innerGeometry) : {
			innerGeometry;
		}
	}
}

renderF3DMaterial(material : F3DMaterial) -> native {
	switch (material : F3DMaterial) {
		F3DMeshBasicMaterial(color, parameters) : {
			make3DMeshBasicMaterial(color, parameters |> tree2pairs |> (\p -> map(p, \v -> [v.first, v.second])));
		}
		F3DTextureLoader(innerMaterial, url) : {
			load3DTexture(renderF3DMaterial(innerMaterial), url);
		}
		F3DNativeMaterials(innerMaterials) : {
			innerMaterials;
		}
	}
}

parseF3DNativeObject(object : native) -> Pair<F3DNative, [() -> void]> {
	children = make(get3DObjectChildren(object));
	uns = ref [];

	Pair(
		F3DNativeMutable(fselect(children, FLift(\ch -> {
			dispUnsA(uns);

			if (length(ch) > 0) {
				parsedChildren =
					fold(ch, Pair([], []), \acc, c -> {
						cc = parseF3DNativeObject(c);

						Pair(arrayPush(acc.first, cc.first), concat(acc.second, cc.second));
					});

				uns := parsedChildren.second;

				F3DNativeScene(object, const(parsedChildren.first));
			} else {
				F3DNativeObject(object);
			}
		}))),
		[
			add3DEventListener(object, "childrenchanged", \ -> {
				next(children, get3DObjectChildren(object));
			}),
			\ -> dispUnsA(uns)
		]
	)
}

addF3DMouseListeners(stage : native, object : native, transformControlsEnabled : Transform<bool>) -> [() -> void] {
	if (transformControlsEnabled != const(false)) {
		children = get3DObjectChildren(object);

		if (length(children) > 0) {
			concatA(map(children, \c -> addF3DMouseListeners(stage, c, transformControlsEnabled)))
		} else {
			[
				makeSubscribeUns(transformControlsEnabled, \en ->
					if (en)
						[
							add3DEventListener(object, "mousedown", \ -> {
								if (!^handled3DMouseDown) {
									handled3DMouseDown := true;
									deferred(\ -> handled3DMouseDown := false);

									attach3DTransformControls(stage, object);
								}
							})
						]
					else
						[]
				)()
			]
		}
	} else {
		[]
	}
}