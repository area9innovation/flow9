import fform/fformutils;
import form/formtransforms;
import loop;

export {
	findTaggedFFormPositionScale(f : FForm, tag : int) -> Maybe<PositionScale>;
}

findTaggedFFormPositionScale(f : FForm, tag : int) -> Maybe<PositionScale> {
	ftfps = \ff -> findTaggedFFormPositionScale(ff, tag);
	cps = \ps, ff -> eitherMap(ftfps(ff), \p -> Some(concatPositionScale(ps, p)), None());

	switch (f) {
		FInspect(i, form) : if (extractStruct(i, ITag(tag - 1)).i == tag) Some(zeroPositionScale) else ftfps(form);

		FEmpty() : None();
		FText(__, __) : None();
		FParagraph(__, __) : None();
		FGraphics(__, __) : None();
		FPicture(__, __, __) : None();
		FRealHTML(__, __, __) : None();
		FVideo(__, __, __) : None();
		FTextInput(__, __, __) : None();

		FTranslate(x, y, form) : cps(PositionScale(Point(fgetValue(x), fgetValue(y)), zeroScale), form);
		FCrop2(left, top, width, height, enabled, form, stack) : cps(PositionScale(Point(-fgetValue(left), -fgetValue(top)), zeroScale), form);
		FBorder(left, top, right, bottom, form) : cps(PositionScale(Point(left, top), zeroScale), form);
		FScale(x, y, form) : cps(PositionScale(zeroPoint, zeroScale), form);

		FGroup(layers, __) : foldWhile2(layers, None(), \__, l -> eitherMap(ftfps(l), \ps -> Pair(Some(ps), true), Pair(None(), false)));
		FGroup2(layer1, layer2, __) : eitherFn(ftfps(layer1), \ps -> Some(ps), \ -> ftfps(layer2));
		FDecorator2(layer1, decorators, __, __) : eitherFn(ftfps(layer1), \ps -> Some(ps), \ ->
			foldWhile2(decorators, None(), \__, d -> eitherMap(ftfps(d.second()), \ps -> Pair(Some(ps), true), Pair(None(), false))));
		FDynamicGroup2(__, currentStack, __) :
			foldWhile2(getValue(currentStack), None(), \__, l -> eitherMap(ftfps(l), \ps -> Pair(Some(ps), true), Pair(None(), false)));

		FRotate(__, form) : ftfps(form); // TODO:
		FAlpha(__, form) : ftfps(form);
		FVisible(__, form) : ftfps(form);
		FRenderable(__, form) : ftfps(form);
		FMask2(form, __, __) : ftfps(form);
		FInteractive(__, form) : ftfps(form);
		FFilter2(__, form, __) : ftfps(form);
		FCursor(__, form) : ftfps(form);
		FMutable2(form, __) : ftfps(fgetValue(form));
		FControlFocus(__, form) : ftfps(form);
		FConstructor(form, __) : ftfps(form);
		FCreate2(current, fn) : ftfps(if (^current != FEmpty()) ^current else fn());
		FFullWindow(__, __, form) : ftfps(form);
		FFullScreen2(__, __, form, __) : ftfps(form);
		FBaseline(__, form) : ftfps(form);
		FSize2(__, form) : ftfps(form);
		FAvailable2(__, form) : ftfps(form);
		FAccess(__, form) : ftfps(form);
		FSetPending(__, form) : ftfps(form);
		FNativeForm(__, __, form, __) : ftfps(form());

		FMForm(form) : {
			if (isConvertableForm(form))
				ftfps(form2fform(form))
			else if (hasTaggedForm(form, tag))
				Some(PositionScale(
					findTaggedFormPosition(form, tag),
					findTaggedFormScale(form, tag)
				))
			else
				None()
		}
		FCanvas(content) : ftfps(content);
		FAnimation(content, __, __) : ftfps(content);
		FOrigin(origin, form) : ftfps(form); // TODO:
	}
}