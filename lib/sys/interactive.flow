import zorder;

export {
	native getMouseX : io (clip : native) -> double = RenderSupport.getMouseX;
	native getMouseY : io (clip : native) -> double = RenderSupport.getMouseY;
	native getTouchPoints : io (clip : native) -> [[double]] = RenderSupport.getTouchPoints;

	panCapture : ref Maybe<(GestureState, double, double, double, double) -> bool> = ref None();
	pinchCapture : ref Maybe<(GestureState, double, double, double, double) -> bool> = ref None();
	swipeCapture : ref Maybe<(double, double, double, double) -> bool> = ref None();

	handleRealEvents(l : EventHandler, clip : native, clipalive : ref bool, stage : native, zorder : [int], doHittest : bool, doRespectHandled : bool) -> () -> void;

	emptyMouseInfo = MouseInfo(0.0, 0.0, false);
	emptyMouseWheelInfo = MouseWheelInfo(0.0, 0.0, false);
	emptyTouchInfo = TouchInfo([], []);
}

getTouchPoints(clip : native) -> [[double]] {
	[[getMouseX(clip), getMouseY(clip)]]
}

native hittest : io (clip : native, x : double, y : double) -> bool = RenderSupport.hittest;
native addMouseWheelEventListener : io (clip : native, cb : (delta : double) -> void) -> () -> void = RenderSupport.addMouseWheelEventListener;

handleRealEvents(l : EventHandler, clip : native, clipalive : ref bool, stage : native, zorder : [int], doHittest : bool, doRespectHandled : bool) -> () -> void {
	getMouseInfo : () -> MouseInfo = \ -> {
		if (^clipalive && getClipRenderable(clip)) {
			MouseInfo(
				getMouseX(clip), getMouseY(clip),
				doHittest && hittest(clip, getMouseX(stage), getMouseY(stage)));
		} else {
			emptyMouseInfo
		}
	};

	getMouseUpInfo : () -> MouseInfo = \ -> {
		if (^clipalive && getClipVisible(clip)) {
			MouseInfo(
				getMouseX(clip), getMouseY(clip),
				doHittest && hittest(clip, getMouseX(stage), getMouseY(stage)));
		} else {
			emptyMouseInfo
		}
	};

	getTouchInfo : () -> TouchInfo = \ -> {
		if (^clipalive && getClipRenderable(clip)) {
			TouchInfo(
				map(getTouchPoints(clip), \tp -> {
					Point(tp[0], tp[1]);
				}),
				map(getTouchPoints(stage), \tp -> {
					\ -> {
						^clipalive && doHittest && getClipRenderable(clip) && hittest(clip, tp[0], tp[1]);
					}
				})
			);
		} else {
			emptyTouchInfo
		}
	};

	getTouchEndInfo : () -> TouchInfo = \ -> {
		if (^clipalive && getClipVisible(clip)) {
			TouchInfo(
				map(getTouchPoints(clip), \tp -> {
					Point(tp[0], tp[1]);
				}),
				map(getTouchPoints(stage), \tp -> {
					\ -> {
						^clipalive && doHittest && getClipRenderable(clip) && hittest(clip, tp[0], tp[1]);
					}
				})
			);
		} else {
			emptyTouchInfo
		}
	};

	addKeyVisFilter = \fn : (handled : bool, event : KeyEvent) -> bool -> {
		\handled : bool, kfn : KeyEvent -> {
			if (^clipalive && getClipVisible(clip)) fn(handled, kfn) else handled
		}
	};

	miDownHandler = \fn : (handled : bool, mi : MouseDownInfo) -> bool -> {
		\handled : bool, mi : GetMouseInfo -> {
			if (^clipalive && !(handled && doRespectHandled) && getClipRenderable(clip)) {
				mdi = MouseDownInfo(getMouseX(clip), getMouseY(clip), \ -> {
					^clipalive && doHittest && getClipRenderable(clip) && hittest(clip, getMouseX(stage), getMouseY(stage))
				});
				fn(handled, mdi)
			} else handled
		}
	};

	miHandler = \fn : (handled : bool, info : () -> MouseInfo) -> bool -> {
		\handled : bool, gm : GetMouseInfo -> {
			if (^clipalive && !(handled && doRespectHandled)) {
				fn(handled, gm.fn)
			} else handled
		}
	};

	miTouchHandler = \fn : (handled : bool, info : TouchInfo) -> bool -> {
		\handled : bool, gm : GetTouchInfo -> {
			if (^clipalive && !(handled && doRespectHandled)) {
				fn(handled, gm.fn())
			} else handled
		}
	};

	switch (l : EventHandler) {
		MouseDown2(fn): handleZorderedDownEvent(stage, zorder, GetMouseInfo(getMouseInfo), miDownHandler(fn));
		MouseUp2(fn): handleZorderedUpEvent(stage, zorder, GetMouseInfo(getMouseUpInfo), miHandler(fn));
		MouseRightDown2(fn): handleZorderedRightDownEvent(stage, zorder, GetMouseInfo(getMouseInfo), miDownHandler(fn));
		MouseRightUp2(fn): handleZorderedRightUpEvent(stage, zorder, GetMouseInfo(getMouseUpInfo), miHandler(fn));
		MouseMiddleDown2(fn): handleZorderedMiddleDownEvent(stage, zorder, GetMouseInfo(getMouseInfo), miDownHandler(fn));
		MouseMiddleUp2(fn): handleZorderedMiddleUpEvent(stage, zorder, GetMouseInfo(getMouseUpInfo), miHandler(fn));
		MouseMove2(fn): handleZorderedMoveEvent(stage, zorder, GetMouseInfo(getMouseInfo), miHandler(fn));
		RollOver(fn): {
			addEventListener(clip, "rollover", \ -> if (^clipalive && getClipVisible(clip)) fn(getMouseInfo));
		}
		RollOut(fn): {
			addEventListener(clip, "rollout", \ -> if (^clipalive) fn(getMouseInfo));
		}
		FileDrop(maxFilesCount, mimeTypeFilter, onDone): {
			addFileDropListener(clip, maxFilesCount, mimeTypeFilter, onDone);
		}
		MouseWheel(fn): {
			addMouseWheelEventListener(clip,
				\deltay -> {
					if (^clipalive && getClipRenderable(clip))
						fn(\ -> MouseWheelInfo(
							0.0, deltay,
							doHittest && hittest(clip, getMouseX(stage), getMouseY(stage))));
				}
			);
		}
		FineGrainMouseWheel2(fn): handleZorderedFineGrainWheelEvent(
			stage, zorder, GetMouseWheelInfo(\ -> {
				dx = getValue(fineGrainMouseWheelDx);
				dy = getValue(fineGrainMouseWheelDy);
				MouseWheelInfo(
					dx, dy,
					doHittest && hittest(clip, getMouseX(stage), getMouseY(stage)))
			}),
			\handled : bool, gm : GetMouseWheelInfo -> {
				if (^clipalive && getClipRenderable(clip)) fn(handled, gm.fn) else handled
			}
		);
		KeyDown2(fn): handleZorderedKeyDownEvent(stage, zorder, addKeyVisFilter(fn));
		KeyUp2(fn): handleZorderedKeyUpEvent(stage, zorder, addKeyVisFilter(fn));
		TouchStart2(fn): handleZorderedTouchStartEvent(stage, zorder, GetTouchInfo(getTouchInfo), miTouchHandler(fn));
		TouchMove2(fn): handleZorderedTouchMoveEvent(stage, zorder, GetTouchInfo(getTouchInfo), miTouchHandler(fn));
		TouchEnd2(fn): handleZorderedTouchEndEvent(stage, zorder, GetTouchInfo(getTouchEndInfo), miTouchHandler(fn));
		PinchGesture(fn): {
			// Does not support IgnoreHitTest so far
			prevX = ref 0.0;
			prevY = ref 0.0;

			cb = \state, focusX, focusY, scale, p4 -> {
				switch (state : GestureState) {
					GestureStateBegin(): { prevX := focusX; prevY := focusY; }
					GestureStateProgress(): {}
					GestureStateEnd(): {}
				}

				ret = fn(state, scale, focusX - ^prevX, focusY - ^prevY);
				prevX := focusX; prevY := focusY;
				ret
			};

			handleZorderedDownEvent(stage, zorder, GetMouseInfo(getMouseInfo), \handled, gm -> {
				if (^clipalive && isNone(^pinchCapture) && getClipRenderable(clip) && gm.fn().inside) pinchCapture := Some(cb);
				handled
			});
		}
		SwipeGesture(fn): {
			// Does not support IgnoreHitTest so far
			cb = \x, y, vx, vy -> { fn(vx, vy) };
			handleZorderedDownEvent(stage, zorder, GetMouseInfo(getMouseInfo), \handled, gm -> {
				if (^clipalive && isNone(^swipeCapture) && getClipRenderable(clip) && gm.fn().inside) swipeCapture := Some(cb);
				handled
			});
		}
		PanGesture(fn): {
			// Does not support IgnoreHitTest so far
			cb = \state, x, y, dx, dy -> { fn(state, dx, dy) };
			handleZorderedDownEvent(stage, zorder, GetMouseInfo(getMouseInfo), \handled, gm -> {
				// Capture pan event
				if (^clipalive && isNone(^panCapture) && getClipRenderable(clip) && gm.fn().inside && fn(GestureStateBegin(), 0.0, 0.0) /* can handle now */)
					panCapture := Some(cb);
				handled
			});
		}
		IgnoreHitTest(e): handleRealEvents(e, clip, clipalive, stage, zorder, false, doRespectHandled);
		RespectHandled(e): handleRealEvents(e, clip, clipalive, stage, zorder, doHittest, true);
	}
}

// Fallbacks for natives.
getMouseX(clip : native) -> double {
	println("TODO: implement 'getMouseX' native - a native fallback stub is used");
	0.0
}

getMouseY(clip : native) -> double {
	println("TODO: implement 'getMouseY' native - a native fallback stub is used");
	0.0
}
hittest(clip : native, x : double, y : double) -> bool {
	println("TODO: implement 'hittest' native - a native fallback stub is used");
	false;
}
addMouseWheelEventListener(clip : native, cb : (delta : double) -> void) -> () -> void {
	println("TODO: implement 'addMouseWheelEventListener' native - a native fallback stub is used");
	nop;
}
