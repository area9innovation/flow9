export {
	PlatformEvent ::= ApplicationUserIdle, ApplicationUserActive, ApplicationSuspended, ApplicationResumed, NetworkOnline, NetworkOffline, FontsReady, LowMemory, DeviceBackButton;

		ApplicationUserIdle : (); // No activity for more than 1 min or any other amount defined with setUserIdleLimit
		ApplicationUserActive : (); // First user action after idle
		// On non-mobile platforms ApplicationSuspended and ApplicationResumed correspond to window focus
		// I.e. if browser or cpp window looses focus ApplicationSuspended called and vice versa
		ApplicationSuspended : ();
		ApplicationResumed : ();
		NetworkOnline : ();
		NetworkOffline : ();
		FontsReady : ();
		LowMemory : ();
		DeviceBackButton : ();

	// Here listener should return true when it wants to cancel default action and pretends to handle situation itself
	// Only DeviceBackButton() on Android makes sence now
	addPlatformEventCancellingListener(event : PlatformEvent, cb : () -> bool) -> () -> void;
	addPlatformEventListener(event : PlatformEvent, cb : () -> void) -> () -> void;
	// Set how much time has to pass after last user action before ApplicationUserIdle event is triggered
	// 60000 ms by default (1 minute)
	native setUserIdleLimit : io (ms : int) -> void = RenderSupport.setUserIdleLimit;

	NetworkState ::= NetworkOnline, NetworkOffline;

	getNetworkState : () -> NetworkState;
	globalNetworkState : Behaviour<NetworkState>;

	// This works on mobiles when some file type (PDF for example) is associated with the flow app
	// The app could upload the file to the server for instance
	native addCustomFileTypeHandler : io (cb : (string) -> void) -> () -> void = Native.addCustomFileTypeHandler;

	// Counts user active time in ms from the moment of calling initActiveTimeCounter
	// Uses ApplicationUserIdle, ApplicationUserActive to determine activity
	// (i.e. user has to be inactive at least for 1 min for counter to stop)
	initActiveTimeCounter() -> ActiveTimeCounter;
		ActiveTimeCounter(getActiveTime : () -> double, disposer : () -> void);
}

import transforms;


// Listen to platform events.
// event values : "suspend" (App suspened), "resume" (App resumed), "online" (network online), "offline" (netwoek offline)
// Returns disposer
native addPlatformEventListenerNative : (event : string, cb : () -> bool) -> () -> void = Native.addPlatformEventListener;

addPlatformEventCancellingListener(event : PlatformEvent, cb : () -> bool) {
	e = switch(event) {
		ApplicationSuspended(): "suspend";
		ApplicationUserIdle(): "idle";
		ApplicationUserActive(): "active";
		ApplicationResumed(): "resume";
		NetworkOnline(): "online";
		NetworkOffline(): "offline";
		FontsReady(): "fontsready";
		LowMemory(): "lowmemory";
		DeviceBackButton() : "devicebackbutton";
	};

	addPlatformEventListenerNative(e, cb)
}

addPlatformEventListener(event : PlatformEvent, cb : () -> void)
	addPlatformEventCancellingListener(event, \ -> { cb(); false });

_globalNetworkState : DynamicBehaviour<NetworkState> = make(NetworkOnline());
globalNetworkState = select(_globalNetworkState, idfn);

NetworkOnlineListenerDisposer = addPlatformEventListener(NetworkOnline(), \ -> { next(_globalNetworkState, NetworkOnline()); });
NetworkOfflineListenerDisposer = addPlatformEventListener(NetworkOffline(), \ -> { next(_globalNetworkState, NetworkOffline()); });

getNetworkState() {
	getValue(globalNetworkState);
}

// Default implementation for other targets
addCustomFileTypeHandler(cb : (path : string) -> void) -> () -> void {
	nop
}

initActiveTimeCounter() -> ActiveTimeCounter {
	activationTime = ref timestamp();
	isActive = ref false;
	totalTime = ref 0.0;

	d1 = addPlatformEventListener(ApplicationUserIdle(), \ -> {
		isActive := false;
		totalTime := ^totalTime + timestamp() - ^activationTime;
	});

	d2 = addPlatformEventListener(ApplicationUserActive(), \ -> {
		isActive := true;
		activationTime := timestamp();
	});

	ActiveTimeCounter(
		\ -> {
			^totalTime + if (^isActive) timestamp() - ^activationTime else 0.0
		},
		\ -> {
			d1();
			d2();
		}
	);
}

setUserIdleLimit(ms : int) { }