import form/paragraphtypes;
import tropic/tropic_metrics;

export {
	TParaWord(
		word : GeneralWrapElement,
		inspector : ParaElementInspector,
		ghostView : Tropic,  // This MUST be TSelect that depend on rtl Behaviour field below, if we talk about text.
		id : DynamicBehaviour<int>,
		style : [CharacterStyle],
		rtl : DynamicBehaviour<bool>,
		clickB : DynamicBehaviour<bool>,
		scriptM : Maybe<ParaElementScriptType>
	);
	dummyInspectorFn = \ -> ParaElementInspector(make(-1), make(0.0), make(0.0), makeWH(), make(0.0), make(0.0), make(-1), make(false), make(0.0), make(0.0), make(WordPositionInLineFirst()));

	getWordInspector(paraWord : TParaWord) -> ParaElementInspector;
	dummyTParaWordFn = \-> TParaWord(
		nonTextElementDontKeep,
		dummyInspectorFn(),
		TEmpty(),
		make(-1),
		[],
		make(false),
		make(false),
		None()
	);
	dummyTParaWord = dummyTParaWordFn();
	getTropicTextAndStyle(tr : Tropic) -> Pair<string, [TCharacterStyle]>;
	getTropicTextStyle(tr : Tropic) -> [TCharacterStyle];
	getTropicText(tr : Tropic) -> string;
}

getWordInspector(paraWord : TParaWord) -> ParaElementInspector {
	paraWord.inspector
}

getTropicText(tr : Tropic) -> string {
	getTropicTextAndStyle(tr).first
}

getTropicTextStyle(tr : Tropic) -> [TCharacterStyle] {
	getTropicTextAndStyle(tr).second;
}

getTropicTextAndStyle(tr : Tropic) -> Pair<string, [TCharacterStyle]> {
	switch(tr) {
		TText(txt, st) : Pair(txt, st);
		TBorder(__, __, __, __, t) : getTropicTextAndStyle(t);
		TPositionScale(__, t) : getTropicTextAndStyle(t);
		TTranslate(__, t) : getTropicTextAndStyle(t);
		TScale(__, t) : getTropicTextAndStyle(t);
		TRotate(__, t) : getTropicTextAndStyle(t);
		TAlpha(__, t) : getTropicTextAndStyle(t);
		TMask(__, t) : getTropicTextAndStyle(t);
		TFilter(__, t) : getTropicTextAndStyle(t);
		TCursor(__, t) : getTropicTextAndStyle(t);
		TAccess(__, t) : getTropicTextAndStyle(t);
		TCrop2(__, __, __, t) : getTropicTextAndStyle(t);
		TVisible(__, t) : getTropicTextAndStyle(t);
		TInteractive(__, t) : getTropicTextAndStyle(t);
		TBaselineOffset(__, t) : getTropicTextAndStyle(t);
		TFullWindow(__, t) : getTropicTextAndStyle(t);
		TConstruct(__, t) : getTropicTextAndStyle(t);
		TDispose(__, t) : getTropicTextAndStyle(t);
		TAvailable(t, __) : getTropicTextAndStyle(t);
		TInspect(__, t) : getTropicTextAndStyle(t);
		TFormAvailable(f, __) : Pair(getFormText(f), getFormTextStyle(f));
		TForm(f) : Pair(getFormText(f), getFormTextStyle(f));
		TFormIn(f, __) : Pair(getFormText(f), getFormTextStyle(f));
		default : Pair("", []);
	}
}
