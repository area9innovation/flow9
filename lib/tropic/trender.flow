import sys/target;
import formats/css/stylesheet;

import tropic/tropic2form;
import tropic/tropictransforms;
import fform/renderfform;
import fform/fformmetrics;
import text/serialize;

export {
	// Renders tropic, taking DPI and zooming for accessibility using hot-keys into account
	// Returns a disposer. This is the recommended way to render a Tropic.
	trender(t : Tropic, style : [TRenderStyle]) -> () -> void;

	// Same as above, except that also returns a function which can tell where TTag items are
	trender2(t : Tropic, style : [TRenderStyle]) -> Pair<() -> void, (int) -> PositionScale>;

	// In difference to trender renders content over all
	// iframes and textinputs of previous render calls
	trenderStage(t : Tropic, style : [TRenderStyle]) -> () -> void;

	// Same as above, except that also returns a function which can tell where TTag items are
	trender2Stage(t : Tropic, style : [TRenderStyle], toStage : bool) -> Pair<() -> void, (int) -> PositionScale>;

	// Render the given tropic, setting up the drag/tooltips handling. Drag/tooltips support does not work unless you use this!
	trenderManager(manager : TManager, t : Tropic) -> () -> void;

	// The current accessibility zoom
	accessibilityZoom = make(s2d(getKeyValue("zoom", "1.0")));
}


trender(t : Tropic, style : [TRenderStyle]) -> () -> void {
	trender2(t, style).first;
}

trender2(t : Tropic, style : [TRenderStyle]) -> Pair<() -> void, (int) -> PositionScale> {
	trender2Stage(t, style, false);
}

trenderStage(t : Tropic, style : [TRenderStyle]) -> () -> void {
	trender2Stage(t, style, true).first;
}

trender2Stage(t : Tropic, style : [TRenderStyle], toStage : bool) -> Pair<() -> void, (int) -> PositionScale> {

	// Scale to density-independend pixels (dp).
	// See https://material.io/guidelines/layout/units-measurements.html
	//
	// In js, dp = px, since px refers to css pixels and it's the browser's job to make them
	// correspond to the same physical dimensions.
	// See https://www.w3.org/TR/css3-values/#reference-pixel

	dpif = if (js) 1.0 else extractStruct(style, TDpif(screenDensity)).dpif ;	// There is also getPixelsPerCm which might be a useful alternative

	awh = extractStruct(style, TInspectStageWH(make(WidthHeight(getStageWidth(), getStageHeight()))));

	stageWH = extractStruct(style, TStageWH(getStageWidth(), getStageHeight()));
	sf = if (!js && dpif == 1.0) {
		ws = max(1.0, stageWH.width / 1536.0);
		hs = max(1.0, stageWH.height / 1536.0);

		// Desktop where we do not have accurate DPI: Just use 1536 as the max useful resolution in each direction
		min(ws, hs)
	} else dpif;

	scale = select2u(accessibilityZoom, awh.wh, \as, wh -> {
		if (js) {
			Factor(as, as)
		} else {
			ws = max(1.0, wh.width / 1536.0);
			hs = max(1.0, wh.height / 1536.0);
			s = as * max(sf, min(ws, hs));
			Factor(s, s);
		}
	});

	dpiFactor = extractStruct(style, TInspectDpiFactor(make(Factor(1.0, 1.0)))).factor;

	u1 = connect(scale.first, dpiFactor);

	sheet = extractStruct(style, emptyStylesheet);

	form =
		FDisposer(
			tropic2fform(TScale(scale.first, t), sheet, awh.wh),
			\ -> {
				scale.second();
				u1()
			}
		)
		|> monitorZooming;

	grab : TGrabSnapshot = extractStruct(style, TGrabSnapshot(\fn -> nop));
	disp1 = grab.grabFn(\ -> {
		TSFForm(fform2sfform(form, false), TFormMetrics(const(fgetValue(awh.wh).width), const(fgetValue(awh.wh).height), const(fgetValue(awh.wh).height)))
	});

	Pair(
		{
			disp2 = if (toStage) frenderStage(form, awh.wh) else frender(form, awh.wh);

			\ -> {
				disp1();
				disp2();
			}
		},
		\id ->
			eitherMap(
				findTaggedFFormPositionScale(form, id),
				\ps -> PositionScale(Point(ps.pos.x / fgetValue(dpiFactor).x, ps.pos.y / fgetValue(dpiFactor).y), ps.scale), // Convert to trender coordinates!
				zeroPositionScale
			)
	);
}

accessibilityZoomFactors = [0.25, 0.33, 0.5, 0.66, 0.75, 0.8, 0.9, 1.0, 1.1, 1.25, 1.5, 1.75, 2.0, 2.5, 3.0, 4.0, 5.0];

monitorZooming(f : FForm) -> FForm {
	if (!js || getRendererType() == "html") {
		ctrlDown = ref false;
		zoomTooltipVisible = make(false);

		FConstruct(
			[
				\ -> addEventListener(getStage(), "resize", \ -> if (getBrowserZoom() != 1.0) next(accessibilityZoom, 1.0)),
				makeSubscribeUns(zoomTooltipVisible, \vis -> {
					if (vis)
						[
							interruptibleTimer(2000, \ -> nextDistinct(zoomTooltipVisible, false)),
							frender(
								tropic2fform(
									TBorder4(
										8.0,
										TFrame(8.0, 4.0, [Fill(0x424242)],
											TDynamicText(fselect(accessibilityZoom, FLift(\az -> "Zoom: " + i2s(floor(az * 100.0)) + " %")), [Fill(white)])
										)
									)
									|> TCenterX,
									emptyStylesheet,
									const(WidthHeight(getStageWidth(), getStageHeight()))
								),
								make(WidthHeight(getStageWidth(), getStageHeight()))
							)
						]
					else
						[]
				}),
				\ -> {
					if (getValue(accessibilityZoom) != 1.0) {
						timer(100, \ -> nextDistinct(zoomTooltipVisible, true));
					}

					\ -> nextDistinct(zoomTooltipVisible, false);
				}
			],
			FInteractive(
				[
					KeyDown2(\handled, ki -> {
						ctrlDown := ki.ctrl;

						if (!handled && ki.ctrl && getBrowserZoom() == 1.0) {
							if (ki.keycode  == 187 || ki.utf == "=") {
								ki.preventDefault();
								v = getValue(accessibilityZoom);
								next(accessibilityZoom, fold(accessibilityZoomFactors, 5.0, \acc, sc -> if (sc < acc && sc > v) sc else acc));
								deferred(\ -> nextDistinct(zoomTooltipVisible, true));
								setKeyValue("zoom", d2s(getValue(accessibilityZoom)));
								true
							} else if (ki.keycode  == 95 || ki.utf == "-") {
								ki.preventDefault();
								v = getValue(accessibilityZoom);
								next(accessibilityZoom, fold(accessibilityZoomFactors, 0.25, \acc, sc -> if (sc > acc && sc < v) sc else acc));
								deferred(\ -> nextDistinct(zoomTooltipVisible, true));
								setKeyValue("zoom", d2s(getValue(accessibilityZoom)));
								true
							} else {
								false
							}
						} else
							handled
					}),
					KeyUp2(\handled, ki -> {
						ctrlDown := ki.ctrl;
						handled;
					}),
					// FineGrainMouseWheel2(\handled, mi ->
					// 	if (!handled && ^ctrlDown && getBrowserZoom() == 1.0) {
					// 		m = mi();

					// 		if (m.dy > 0.1) {
					// 			m.preventDefault();
					// 			v = getValue(accessibilityZoom);
					// 			next(accessibilityZoom, fold(accessibilityZoomFactors, 5.0, \acc, sc -> if (sc < acc && sc > v) sc else acc));
					// 			setKeyValue("zoom", d2s(getValue(accessibilityZoom)));
					// 			true
					// 		} else if (m.dy < -0.1) {
					// 			m.preventDefault();
					// 			v = getValue(accessibilityZoom);
					// 			next(accessibilityZoom, fold(accessibilityZoomFactors, 0.25, \acc, sc -> if (sc > acc && sc < v) sc else acc));
					// 			setKeyValue("zoom", d2s(getValue(accessibilityZoom)));
					// 			true
					// 		} else {
					// 			false
					// 		}
					// 	} else
					// 		handled
					// )
				],
				f
			)
		)
	} else {
		f
	}
}

trenderManager(manager : TManager, t : Tropic) -> () -> void {
	r = trender2(t, manager.renderStyle);
	if (^(manager.findPositionScaleFn) == dummyTFinder) {
		manager.findPositionScaleFn := r.second;
	} else {
		println("Warning: New call of trenderManager would overwrite findPositionScaleFn");
	}
	r.first;
}