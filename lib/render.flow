import ui/fontmapping; // getMappedFont
import net/picutil;
import form/graphicstransforms;
import fform/fform;
import fonts_repo;
import dynamic;

export {
	ClipCapabilities(move : bool, filters : bool, interactive : bool, scale : bool, alpha : bool);

	DefinedTextStyle(
		face : FontFace,
		fontSize : double,
		fillColor : int,
		fillOpacity : double,
		letterSpacing : double,
		lineHeightPercent : double,
		backgroundColor : int,
		backgroundOpacity : double,
		advancedSharpness : int,
		needBaseline : bool,
		antiAlias : int,
		gridFit : int
	);

	RenderResult(
		clips : [native],
		widthHeight : Behaviour<WidthHeight>,
		baseline : Behaviour<double>,
		pending : Behaviour<int>,
		disposers :[() -> void],
		capabilities : ClipCapabilities
	);

	// Behaviour-based styles (i.e. FDynamicColor) aren't applied.
	makeStyledTextfield(text : string, style : [FTextStyle]) -> native;

	calculateDefaultTextBackgroundColorOpacity(style : [CharacterStyle]) -> double;
	getDefinedTextStyle(style : [CharacterStyle]) -> DefinedTextStyle;
	defineTextStyle : (textfield : native, text : string, style : [CharacterStyle]) -> void;

	userDefinedLetterSpacing = ref getUserDefinedLetterSpacing();
	userDefinedLetterSpacingPercent = ref getUserDefinedLetterSpacingPercent();

	// The methods to control the cache of pictures size.
	// Allmethods uses the applyMediaPathMapping method.
	setCachedPictureSize(url : string, size : WidthHeight) -> void;
	getCachedPictureSize(url : string) -> Maybe<WidthHeight>;
	removeCachedPictureSize(url : string) -> void;

	// key is url processed with applyMediaPathMapping
	CachedPicturesSizes : ref Tree<string, WidthHeight> = ref makeTree();

	renderGraphics(clip : native, path : [StaticGraphicOp], style : [GraphicsStyle]) -> WidthHeight;

	addKeyboardShortcut(shortcut : string, description : string) -> () -> void;
	getKeyboardShortcuts() -> string;
}

makeStyledTextfield(text : string, style : [FTextStyle]) -> native {
	cs = style2characterStyle(style);
	r = makeTextfield(getMappedFontFace(cs));
	rtl = extractStruct(style, SetRTL(getDefaultRtl())).rtl;
	maybeApply(tryExtractStruct(style, TagName("")), \tn -> setClipTagName(r, tn.tagName));
	setEscapeHTML(r, extractStruct(style, EscapeHTML(false)).escape);
	setTextDirection(r, if (rtl) "rtl" else "ltr");
	defineTextStyle(r, text, cs);
	r
}

setCachedPictureSize(url : string, size : WidthHeight) -> void {
	CachedPicturesSizes := setTree(^CachedPicturesSizes, applyMediaPathMapping(url), size)
}

getCachedPictureSize(url : string) -> Maybe<WidthHeight> {
	lookupTree(^CachedPicturesSizes, applyMediaPathMapping(url))
}

removeCachedPictureSize(url : string) -> void {
	CachedPicturesSizes := removeFromTree(^CachedPicturesSizes, applyMediaPathMapping(url));
}

calculateDefaultTextBackgroundColorOpacity(style : [CharacterStyle]) -> double {
	b2d(containsStruct(style, BackgroundFill(0)));
}

getDefinedTextStyle(style : [CharacterStyle]) -> DefinedTextStyle {
	// Set up the style
	fontfamily = ref "";  // Mapping will override it to the default.
	fontweight = ref 400;
	fontslope = ref FONT_SLOPE_NORMAL;
	fontsize = ref 11.0;
	fillcolour = ref 0x000000;
	fillopacity = ref 1.0;
	backgroundcolour = ref 0xffffff;
	backgroundopacity = ref calculateDefaultTextBackgroundColorOpacity(style);
	letterspacingDefined = ref false;
	letterspacing = ref ^userDefinedLetterSpacing;
	lineHeightPercent = ref -1.0;
	advancedSharpness = ref -400;
	advancedAntiAliasType = ref FontAntiAliasAdvanced();
	advancedGridFitType = ref FontGridFitNone();
	specifiedSharpness = ref false;
	needBaseline = ref true;
	iter(style, \s -> {
		switch (s : CharacterStyle) {
			FontFamily(name) : fontfamily := ^fontfamily + (if (^fontfamily!="") "," else "") + name;
			FontSize(size) : {
				fontsize := size;

				if (!^letterspacingDefined && ^userDefinedLetterSpacingPercent != 0.0) {
					letterspacing := size * ^userDefinedLetterSpacingPercent;
				}
			}
			Fill(col): fillcolour := col;
			FillOpacity(op): fillopacity := op;
			LetterSpacing(op) : {
				letterspacingDefined := true;
				letterspacing := op;
			}
			LineHeightPercent(prc) : lineHeightPercent := prc;
			Sharpness(sharpness, antiAliasType, gridFitType) : {
				advancedSharpness := sharpness;
				advancedAntiAliasType := antiAliasType;
				advancedGridFitType := gridFitType;
				specifiedSharpness := true;
			}
			BackgroundFill(col): backgroundcolour := col;
			BackgroundFillOpacity(opacity): backgroundopacity := opacity;
			NeedBaseline(need): needBaseline := need; 
			default : {}
		}
	});

	mappedFont = getMappedFont(^fontfamily, ^fontsize);
	fontFace = fontName2fontFace(mappedFont.first);
	if (js) {
		fontweight := fontFace2weight(fontFace);
		fontslope := fontFace2slope(fontFace);
	}
	fontsize := mappedFont.second;

	// 21273
	if (!^specifiedSharpness && flash && (^fontsize > 20.0)) {
		advancedSharpness :=
			if (^fontsize < 40.0) {
				delta : double = 1.0-dpow(1.0-((^fontsize-20.0) / 20.0), 2.0);
				round(-400.0 + 300.0 * delta);
			}
			else
				-100;
	}

	antialiasint = switch(^advancedAntiAliasType : FontAntiAliasType) {
		FontAntiAliasNormal() : 0;
		FontAntiAliasAdvanced() : 1;
	};
	gridfitint = switch(^advancedGridFitType : FontGridFitType) {
		FontGridFitNone() : 0;
		FontGridFitPixel() : 1;
		FontGridFitSubpixel() : 2;
	};

	DefinedTextStyle(
		FontFace(fontFace with weight=^fontweight, slope=^fontslope),
		^fontsize,
		^fillcolour,
		^fillopacity,
		^letterspacing,
		^lineHeightPercent,
		^backgroundcolour,
		^backgroundopacity,
		^advancedSharpness,
		^needBaseline,
		antialiasint,
		gridfitint
	);
}

defineTextStyle(textfield, text, style) {
	definedTextStyle = getDefinedTextStyle(style);

	if (definedTextStyle.lineHeightPercent >= 0.0) {
		setLineHeightPercent(textfield, definedTextStyle.lineHeightPercent);
	}

	if (!definedTextStyle.needBaseline) {
		setTextNeedBaseline(textfield, false);
	}

	setTextAndStyle(
		textfield,
		text,
		if (js && length(definedTextStyle.face.families) > 1 && getRendererType() == "html")
			strGlue(definedTextStyle.face.families, ",")
		else
			definedTextStyle.face.family,
		min(1024.0, definedTextStyle.fontSize),
		definedTextStyle.face.weight,
		definedTextStyle.face.slope,
		definedTextStyle.fillColor,
		definedTextStyle.fillOpacity,
		definedTextStyle.letterSpacing,
		definedTextStyle.backgroundColor,
		definedTextStyle.backgroundOpacity
	);

	setAdvancedText(textfield, definedTextStyle.advancedSharpness, definedTextStyle.antiAlias, definedTextStyle.gridFit);
}

renderGraphics(clip : native, path : [StaticGraphicOp], style : [GraphicsStyle]) -> WidthHeight {
	g = getGraphics(clip);
	clearGraphics(g);

	// Set up the style
	fillcolour = ref 0xdeaddead; // A hack
	fillopacity = ref 1.0;
	gradientFill : ref Maybe<GradientFill> = ref None();
	gradientType = ref "linear";
	strokecolour = ref 0xdeaddead;
	strokeopacity = ref 1.0;
	strokewidth = ref 1.0;
	strokeLineGradient : ref Maybe<StrokeLineGradient> = ref None();
	iter(style, \s -> {
		switch (s : GraphicsStyle) {
			Fill(col): fillcolour := col;
			FillOpacity(op): fillopacity := op;
			GradientFill(c, rotation): gradientFill := Some(s);
			RadialGradient(): gradientType := "radial";
			Stroke(colour): strokecolour := colour;
			StrokeOpacity(opacity): strokeopacity := opacity;
			StrokeWidth(width): strokewidth := width;
			StrokeLineGradient(c, rotation): strokeLineGradient := Some(s);
			UseSvg() : {}
		}
	});
	if (0xdeaddead != (^fillcolour)) {
		beginFill(g, ^fillcolour, ^fillopacity);
	}
	if (0xdeaddead != (^strokecolour) || isSome(^strokeLineGradient)) {
		colour = if (0xdeaddead == (^strokecolour)) 0xffffff else ^strokecolour;
		setLineStyle(g, ^strokewidth, colour, ^strokeopacity);
	}
	// Determine dimensions
	minX = ref 99999.0;
	maxX = ref -99999.0;
	minY = ref 99999.0;
	maxY = ref -99999.0;

	updateRange = \x, y -> {
		minX := min(x, ^minX);
		maxX := max(x, ^maxX);
		minY := min(y, ^minY);
		maxY := max(y, ^maxY);
	}

	iter(path, \op ->
		switch (op : StaticGraphicOp) {
			MoveTo(x, y) : updateRange(x,y);
			LineTo(x, y) : updateRange(x,y);
			CubicBezierTo(x, y, cx, cy): updateRange(x,y);
			QuadraticBezierTo(x, y, cx, cy): updateRange(x,y);
			ClosePath(): {}
			GRect(x, y, w, h): {
				updateRange(x, y);
				updateRange(x + w, y + h);
			}
			GRoundedRect(x, y, w, h, __): {
				updateRange(x, y);
				updateRange(x + w, y + h);
			}
			GEllipse(x, y, w, h): {
				updateRange(x - w, y - h);
				updateRange(x + w, y + h);
			}
			GCircle(x, y, r): {
				updateRange(x - r, y - r);
				updateRange(x + r, y + r);
			}
		});

	w = abs(^maxX - ^minX);
	h = abs(^maxY - ^minY);

	switch (^gradientFill : Maybe<GradientFill>) {
		Some(gf): {
			colors = map(gf.points, \c -> c.color);
			alphas = map(gf.points, \c -> c.alpha);
			offsets = map(gf.points, \c -> c.offset);
			matrix = makeMatrix(w, h, gf.rotation, ^minX, ^minY);
			beginGradientFill(g, colors, alphas, offsets, matrix, ^gradientType);
		}
		None(): {}
	}

	switch (^strokeLineGradient : Maybe<StrokeLineGradient>) {
		Some(sg): {
			colors = map(sg.points, \c -> c.color);
			alphas = map(sg.points, \c -> c.alpha);
			offsets = map(sg.points, \c -> c.offset);
			matrix = makeMatrix(w, h, sg.rotation, ^minX, ^minY);
			setLineGradientStroke(g, colors, alphas, offsets, matrix);
		}
		None() : {}
	}

	if (contains(style, UseSvg()) && !isUrlParameterFalse("useSvg")) {
		useSvg(clip);
	}

	// Render the path
	needClose = ref false;
	moveTo(g, 0.0, 0.0); // Make sure we start from (0.0, 0.0). JS works incorrectly without this
	path2 = if (js && length(path) == 1) path else deconstructGraphicsPath(path);
	iter(path2, \op ->
		switch (op : StaticGraphicOp) {
			MoveTo(x, y) : {
				moveTo(g, x, y);
			}
			LineTo(x, y) : {
				needClose := true;
				lineTo(g, x, y);
			}
			CubicBezierTo(x, y, cx, cy): {
				needClose := true;
				curveTo(g, cx, cy, x, y);
			}
			QuadraticBezierTo(x, y, cx, cy): {
				needClose := true;
				curveTo(g, cx, cy, x, y);
			}
			ClosePath(): {
				needClose := false;
				endFill(g);
			}
			GRect(x, y, width, height): {
				needClose := false;
				drawRect(g, x, y, width, height);
			}
			GRoundedRect(x, y, width, height, radius): {
				needClose := false;
				drawRoundedRect(g, x, y, width, height, radius);
			}
			GEllipse(x, y, width, height): {
				needClose := false;
				drawEllipse(g, x, y, width, height);
			}
			GCircle(x, y, radius): {
				needClose := false;
				drawCircle(g, x, y, radius);
			}
		}
	);
	// Workaround for cpp : single LineTo with Fill can disappear.
	if (cpp) {
		lines = fold(path2, Pair(true, 0), \acc : Pair<bool, int>, p ->
			if (acc.first) switch(p) {
				LineTo(__, __) : Pair(acc with second = acc.second + 1);
				CubicBezierTo(__, __, __, __) : Pair(acc with first = false);
				QuadraticBezierTo(__, __, __, __) : Pair(acc with first = false);
				default : acc
			} else acc
		);
		if (lines.first && lines.second == 1) {
			needClose := false;
		}
	}
	if (^needClose) {
		endFill(g);
	}

	WidthHeight(^maxX, ^maxY);
}

KeyboardShortcut(keys : string, description : string);
keyboardShortcuts : ref [KeyboardShortcut] = ref [];

addKeyboardShortcut(shortcut : string, description : string) -> () -> void {
	sc = KeyboardShortcut(shortcut, description);
	keyboardShortcuts := arrayPush(^keyboardShortcuts, sc);
	\ -> keyboardShortcuts := removeFirst(^keyboardShortcuts, sc);
}

getKeyboardShortcuts() {
	default_shortcuts = if (js) "Ctrl+Up : Zoom in\nCtrl+Dn : Zoom out\n" else "";
	fold(^keyboardShortcuts, default_shortcuts, \in, sc -> in + sc.keys + " : " + sc.description + "\n");
}
