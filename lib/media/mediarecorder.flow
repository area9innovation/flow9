import string;
import media/mediastream;

export {
	makeMediaRecorderFromStream(destination : [MediaRecorderDestination], stream : MediaStream,
		onReady : (MediaRecorderControls) -> void, onError: (string) -> void, styles : [MediaRecorderStyle]) -> void;

	MediaRecorderDestination ::= MediaRecorderWebsocketURI, MediaRecorderFilePath, MediaRecorderDataCallback;

		MediaRecorderWebsocketURI : (uri : string);
		MediaRecorderFilePath : (uri : string);
		MediaRecorderDataCallback : (callback : (string) -> void);

	MediaRecorderStyle ::= MediaRecorderTimeslice;

	MediaRecorderTimeslice(timeslice : int);

	MediaRecorderControls(start : () -> void, resume : () -> void, pause : () -> void, stop : () -> void);

	// Using https://github.com/Kagami/vmsg
	// Works for all browsers with WASM support
	// makeWasmAudioRecorder()
}

makeMediaRecorderFromStream(destination : [MediaRecorderDestination], stream : MediaStream,
		onReady : (MediaRecorderControls) -> void, onError: (string) -> void, styles : [MediaRecorderStyle]) -> void {
	websocketUri = extractStruct(destination, MediaRecorderWebsocketURI("")).uri;
	filePath = extractStruct(destination, MediaRecorderFilePath("")).uri;
	dataCb = extractStruct(destination, MediaRecorderDataCallback(nop1)).callback;

	timeslice = extractStruct(styles, MediaRecorderTimeslice(-1)).timeslice;

	onRecorderReady = \recorder -> {
		onReady(MediaRecorderControls(
			\ -> startRecording(recorder, timeslice),
			\ -> resumeRecording(recorder),
			\ -> pauseRecording(recorder),
			\ -> stopRecording(recorder)
		));
	};

	makeMediaRecorderFromStreamNative(websocketUri, filePath, dataCb, stream.stream, timeslice, onRecorderReady, onError);
}

native startRecording : io(recorder : native, timeslice : int) -> void = MediaRecorderSupport.startRecording;
native resumeRecording : io(recorder : native) -> void = MediaRecorderSupport.resumeRecording;
native pauseRecording : io(recorder : native) -> void = MediaRecorderSupport.pauseRecording;
native stopRecording : io(recorder : native) -> void = MediaRecorderSupport.stopRecording;

native makeMediaRecorderFromStreamNative : io(websocketUri : string, filePath : string, dataCallback : (string) -> void, stream : native, timeslice : int,
	onRecorderReady : (native) -> void,	onRecorderError : (string) -> void) -> void = MediaRecorderSupport.makeMediaRecorderFromStream;

// default fallbacks - do nothing

startRecording(recorder : native, timeslice : int) -> void { }
resumeRecording(recorder : native) -> void { }
pauseRecording(recorder : native) -> void { }
stopRecording(recorder : native) -> void { }
makeMediaRecorderFromStreamNative(websocketUri : string, filePath : string, dataCallback : (string) -> void, stream : native, timeslice : int,
	onRecorderReady : (native) -> void,	onRecorderError : (string) -> void) -> void { }