import pebbles/pebble_parameters;

export {
	// Tab name is reflected in the pebble parameter "t",
	// i.e URL parameter will be changed every time user switches a tab
	// and vice versa changing URL parameter "t=<tab-name>" will switch tab in the UI
	//
	// NOTE: it will clean last pebble parameters except "t" on every tab switch and
	// restore them from history once user opens the same tab again (RecordURLRestoreOrChange).
	// If you want to preserve parameters across different tabs call `buildCustomPebbledTabs` with `RecordURLChange`
	buildPebbledTabs(
		controller : PebbleController,
		tabs : [Pair<string, Material>],
		style : [MTabsStyle]
	) -> Material;

	// By default use RecordURLRestoreOrChange() as `action`, this will make last path part parameters of the pebble to
	// - be reflected in the URL
	// - be cleaned up when user switches the tab
	// - be restored from the history if user switches to the previously visited tab
	// 	 if you need to spread some parameters from tab to tab use RecordURLRestoreOrChangeParameters(shared)
	// 	 where `shared` - list of parameter's keys which won't be cleaned during switching tabs
	// Also you can simply switch tabs by changing tabIndexB
	buildCustomPebbledTabs(
		controller : PebbleController,
		parameterName : string,
		tabIndexB : DynamicBehaviour<int>,
		action : PebbleAction,
		tabs : Transform<[PebbleTabDynamic]>,
		style : [MTabsStyle]
	) -> Material;

	PebbleTabDynamic : (uid : string, name : Transform<string>, content : Material, style : [MTabStyle]);
	PebbleTab(uid : string, name : string, content : Material, style : [MTabStyle]) -> PebbleTabDynamic {
		PebbleTabDynamic(uid, const(name), content, style);
	}

	makePebbleTabName(s : string) -> string;
	tabs2PebbleTabs(tabs : [Pair<string, Material>]) -> [PebbleTabDynamic];
}

buildPebbledTabs(
	controller : PebbleController,
	tabs : [Pair<string, Material>],
	style : [MTabsStyle]
) -> Material {

	buildCustomPebbledTabs(
		controller,
		"t",
		make(0),
		RecordURLRestoreOrChange(),
		const(tabs2PebbleTabs(tabs)),
		style
	)
}

buildCustomPebbledTabs(
	controller : PebbleController,
	parameterName : string,
	tabIndexB : DynamicBehaviour<int>,
	action : PebbleAction,
	tabs : Transform<[PebbleTabDynamic]>,
	style : [MTabsStyle]
) -> Material {
	tabUids = fmap(tabs, \t -> t.uid);
	findTabIndexByUid = \uid -> findiDef(fgetValue(tabUids), eq(uid), 0);
	findTabUidByIndex = \index -> elementAt(fgetValue(tabUids), index, "");

	zeroTabUidT = fselect(tabUids, FLift(\__ -> findTabUidByIndex(getValue(tabIndexB))));
	tabUidB = make(getLastPathPartParameter(getCurrentPebble(controller), parameterName, fgetValue(zeroTabUidT)));

	fixHistoryAndStackForTab = \tabUid -> {
		pebbleStack = getValue(controller.pebbleStackB);
		currentURL = getBrowserURL();
		correctURL = takeBefore(currentURL, "#", currentURL) +
			pebble2UrlHash(extendCurrentPebbleWithParameter(controller, parameterName, tabUid));
		last = length(pebbleStack) - 1;
		devtrace("pebbles: adding parameter for default tab: " + parameterName + " = " + tabUid);
		lastPebble = pebbleStack[last];
		fixedPebble = setLastPathPartParameter(lastPebble, parameterName, tabUid);
		if (fixedPebble != lastPebble) {
			head = if (last > 0 && equalPebbles(pebbleStack[last - 1], fixedPebble)) {
				take(pebbleStack, last - 1);
			} else {
				take(pebbleStack, last);
			}

			devtrace("pebbles: added parameter for default tab: " + parameterName + " = " + tabUid + ", pebble = " + toString(fixedPebble));
			historyReplaceState(JsonNull(), "", Some(correctURL));

			// let's finish current event chain, then set fixed pebble
			deferred(\ -> {
				// fix current pebble only if stack is the same
				if (getValue(controller.pebbleStackB) == pebbleStack) {
					next(controller.pebbleStackB, arrayPush(head, fixedPebble));
					nextDistinct(controller.currentPebbleB, fixedPebble);
				}
			});
		}
	}

	MConstruct(
		[	makeSubscribe2(zeroTabUidT, \ztuid -> {
				devtrace("pebbles: new zeroTabUid: " + parameterName + " : " + ztuid);
				nextDistinct(tabUidB, ztuid);
			}),
			\ -> {
				fixHistoryAndStackForTab(getValue(tabUidB));
				nop;
			}
		],
		MLinkPebbleParameters(controller,
			[
				PebbleStringLink(
					parameterName,
					tabUidB,
					\tabUid -> {
						// It's for lost filters in case of opening tabs without preset "t" parameter
						pebbleStack = getValue(controller.pebbleStackB);
						if (!contains(fgetValue(tabUids), tabUid) && pebbleStack != [] && length(fgetValue(tabs)) > 1) {
							zeroTabUid = fgetValue(zeroTabUidT);
							fixHistoryAndStackForTab(zeroTabUid);
						}
					},
					action
				)
			],
			// Can we move this subscriber to the MConstruct above?
			MConstruct([
					\-> bidirectionalLink(tabUidB, tabIndexB, findTabIndexByUid, findTabUidByIndex)
				], MDynamicTabs(
					fmapi(tabs, \index, tab -> {
						switch (tryExtractStruct(tab.style, MTabConfirmSwitch(nop1))) {
							Some(confirmStyle): {
								MTab(
									MConfirmPebbleParameterSwitch(
										controller,
										[KeyValue(
											parameterName,
											findTabUidByIndex(index)
										)],
										\callback -> {
											if (getValue(tabIndexB) == index) {
												// Tab switch is triggered by pebble changes (UP/BACK buttons, URL modification, etc)
												confirmStyle.confirm(callback);
											} else {
												// Tab switch is triggered by UI and already confirmed by MTab itself
												callback(true)
											}
										},
										Some(\-> true),
										tab.content
									),
									tab.name,
									tab.style
								)
							}
							None(): {
								MTab(tab.content, tab.name, tab.style)
							}
						}
					}),
					tabIndexB,
					style
				)
			)
		)
	)
}

makePebbleTabName(s : string) -> string {
	humanizeName(s)
}

tabs2PebbleTabs(tabs : [Pair<string, Material>]) -> [PebbleTabDynamic] {
	map(tabs, \tab -> {
		PebbleTab(
			makePebbleTabUid(tab.first),
			makePebbleTabName(tab.first),
			tab.second,
			[MTabIcon(MIcon("view_list", []))]
		)
	})
}