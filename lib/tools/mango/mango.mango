term = 
	term "|>" ws <term 						Precedence/2
	|> term "|" ws <term 					Choice/2
	|> term <term							Sequence/2
	|> "$" ws term							PushMatch/1
	|> "<" ws term							Lower/1
	|> "#" ws term							Error/1
	|> term "*" ws							Star/1
	|> term "+" ws							Plus/1
	|> term "?" ws							Optional/1
	|> "!" ws term							Negate/1
	|> 
		"(" ws term ")" ws							
		| uid "/" ws $int ws				Construct/2
		| string		 					String/1
		| char "-" char						Range/2	
		| stringq 							String/1
		| id "=" ws term ";" ws term		Rule/3
		| id 								Variable/1
		| "@" ws id "<" ws term ">" ws "=" ws term ";" ws term	GrammarFn/4
		| "@" ws id "<" ws term ">" ws		GrammarCall/2
		| "@" ws id !"<"					StackOp/1
		| "@" ws stringq					StackOp/1
		| "%" $(alnum+) @switch
	;

id = $bid ws;
bid = ('a'-'z' | '_') (alnum)*; // Initial lower case only
uid = $('A'-'Z' alnum*) ws; 	// Initial upper case only
alnum = 'a'-'z' | 'A'-'Z' | '_' | '0'-'9';

int = '0'-'9'+;

string = '"' $(!'"' anychar)* '"' ws;
stringq = "'" $(!"'" anychar)* "'" ws;

char = "'" $("0x" hexdigit+ | anychar) "'" ws;

hexdigit = '0'-'9' | 'a'-'f' | 'A'-'F';

@include<whitespace>

@include<highlight>
@highlight<id "variable.parameter">
@highlight<uid "entity.name.function">
@highlight<int "constant.numeric">
@highlight<string "string.quoted.double">
@highlight<stringq "string.quoted.single">
@highlight<char "constant.character">
@highlight<ws "comment.block">
@bracket<"(" ")">
@linecomment<"//">
@blockcomment<"/*" "*/">
@vscommand<"Mango check" "mango grammar=${relativeFile}" "F7">

grammar = ws term;
grammar
