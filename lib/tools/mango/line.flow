import string;

export {
	getLinePos(c : string, pos : int, line : int, i : int) -> string;
}

getLinePos(c : string, pos : int, line : int, i : int) -> string {
	if (i >= strlen(c)) {
		i2s(line) + ":\n";
	} else {
		eol = strRangeIndexOf(c, "\n", i, strlen(c));
		prline = \text -> {
			column = pos - i;
			i2s(line) + ":" + i2s(column + 1) + ":\n" + text + "\n"
			+ strLeft(concatStrings(map(s2a(text), \a -> if (a == 9) "\t" else " ")), column) + "^";
		}
		if (eol == -1) {
			text = substring(c, i, strlen(c));
			prline(text);
		} else if (eol >= pos) {
			text = substring(c, i, eol - i);
			prline(text);
		} else {
			// Not found yet, keep going
			getLinePos(c, pos, line + 1, eol + 1);
		}
	}
}
