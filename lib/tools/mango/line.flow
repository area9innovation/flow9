import string;

export {
	// Given a file at path with content c, and a position pos in that content,
	// return a string that represents the path, line and column of that position
	// including the text.
	getLinePos(path : string, c : string, error : string, pos : int, line : int, i : int) -> string;
}

getLinePos(path : string, c : string, error : string, pos : int, line : int, i : int) -> string {
	if (i >= strlen(c)) {
		(if (path != "") path + ":" else "") + i2s(line) + ": " + error + "\n";
	} else {
		eol = strRangeIndexOf(c, "\n", i, strlen(c));
		prline = \text -> {
			column = pos - i;
			(if (path != "") path + ":" else "") + i2s(line) + ":" + i2s(column + 1) + ": " + error + "\n" + text + "\n"
			+ strLeft(concatStrings(map(s2a(text), \a -> if (a == 9) "\t" else " ")), column) + "^";
		}
		if (eol == -1) {
			text = substring(c, i, strlen(c));
			prline(text);
		} else if (eol >= pos) {
			text = substring(c, i, eol - i);
			prline(text);
		} else {
			// Not found yet, keep going
			getLinePos(path, c, error, pos, line + 1, eol + 1);
		}
	}
}
