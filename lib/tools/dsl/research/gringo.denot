syntax Gringo {
	term = 
		term "|>" ws term 							Precedence/2
		|> term "|" ws term 						Choice/2
		|> term term								Seq/2
		|> "$" ws term								Command/1
		|> "<" ws term								Lower/1
		|> "#" ws term								Error/1
		|> term "*" ws								Star/1
		|> term "+" ws								Plus/1
		|> term "?" ws								Opt/1
		|> "!" ws term								Negate/1
		|> 
			"(" ws term ")" ws							
			| "'" char "'" ws "-" ws "'" char "'" ws	Range/2
			| '"' string '"' ws							String/1
			| "'" stringq "'" ws						String/1
			| id ws "=" ws term ";" ws term				Rule/3
			| id ws										Var/1
			| uid ws "/" ws int ws						Construct/2
		;

	id = $('a'-'z' | '0'-'9' | '_')+;
	uid = $('A'-'Z' | '0'-'9' | '_')+;

	int = $'0'-'9'+;

	string = $(!'"' '0x0'-'0xFFFF')*;
	stringq = $(!"'" '0x0'-'0xFFFF')*;
	char = $("0x" hexdigit+) | $anychar;
	anychar = '0x0'-'0xFFFF';
	hexdigit = 'a'-'f' | 'A'-'F' | '0'-'9';
	ws = s*;
	s = " " | "\t" | "\n" | "\r" | "//" (!"\n" anychar)* "\n" | "/*" (!"*/" anychar)* "*/";
	ws term
}
