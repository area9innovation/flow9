import ds/list;
import runtime;
import text/deserialize_string;
import math/math;

export {
	PEnv(
		stack : ref List<flow>,
	);

	pdebug(env : PEnv) -> void;
	pushPEnv(env : PEnv, value : flow) -> bool;
	popPEnv(env : PEnv) -> flow;
	// Expect exactly one element on the stack
	getSinglePEnv(env : PEnv, def : flow) -> flow;
	pushPArray(env : PEnv, top : List<flow>) -> bool;
	pprintln(env : PEnv) -> void;
	pifte(env : PEnv) -> void;
	peval(env : PEnv) -> void;
	pdrop(env : PEnv) -> void;
	pdup(env : PEnv) -> void;
	pswap(env : PEnv) -> bool;
	pcons(env : PEnv) -> bool;
	pfn1(env : PEnv, fn : (flow) -> flow) -> bool;
	pfn2(env : PEnv, fn : (flow, flow) -> flow) -> bool;
	pconstruct0(env : PEnv, id : string) -> bool;
	pconstruct1(env : PEnv, id : string) -> bool;
	pconstruct2(env : PEnv, id : string) -> bool;
	pconstruct3(env : PEnv, id : string) -> bool;
	pconstruct4(env : PEnv, id : string) -> bool;
	pconstruct5(env : PEnv, id : string) -> bool;
}

pdebug(env : PEnv) -> void {
	println("\nDump of Poppy stack:");
	iter(list2array(^(env.stack)), \p -> {
		println(p)
	});
}

pushPEnv(env : PEnv, value : flow) -> bool {
	env.stack := Cons(value, ^(env.stack));
	true;
}

popPEnv(env : PEnv) -> flow {
	st = ^(env.stack);
	switch (st) {
		EmptyList(): {
			println("Error: Empty stack");
			0;
		}
		Cons(head, t): {
			env.stack := t;
			head;
		}
	}
}

getSinglePEnv(env : PEnv, def : flow) -> flow {
	st = ^(env.stack);
	switch (st) {
		EmptyList(): def;
		Cons(head, t): {
			if (t != EmptyList()) def
			else head
		}
	}
}

pushPArray(env : PEnv, top : List<flow>) -> bool {
	pushPEnv(env, collectPArray(env, top, []));
}

collectPArray(env : PEnv, top : List<flow>, acc : [flow]) -> [flow] {
	if (^(env.stack) == top) acc
	else {
		el = popPEnv(env);
		collectPArray(env, top, arrayPush(acc, el));
	}
}

pprintln(env : PEnv) -> void {
	val = popPEnv(env);
	println(val);
}

pifte(env : PEnv) -> void {
	else_ = popPEnv(env);
	then_ = popPEnv(env);
	cond = popPEnv(env);
	if (cond) {
		fn = cast(then_ : flow -> (PEnv) -> void);
		fn(env)
	} else {
		fn = cast(else_ : flow -> (PEnv) -> void);
		fn(env)
	}
}

peval(env : PEnv) -> void {
	flowfn = popPEnv(env);
	fn = cast(flowfn : flow -> (PEnv) -> void);
	fn(env);
}

pequal(env : PEnv) -> bool {
	e1 = popPEnv(env);
	e2 = popPEnv(env);
	pushPEnv(env, e1 == e2)
}

pdrop(env : PEnv) -> void {
	st = ^(env.stack);
	switch (st) {
		EmptyList(): {}
		Cons(__, t): env.stack := t;
	}
}
pdup(env : PEnv) -> void {
	st = ^(env.stack);
	switch (st) {
		EmptyList(): {}
		Cons(h, __): env.stack := Cons(h, st);
	}
}
pswap(env : PEnv) -> bool {
	e1 = popPEnv(env);
	e2 = popPEnv(env);
	pushPEnv(env, e1);
	pushPEnv(env, e2);
}

pconstruct0(env : PEnv, id : string) -> bool {
	pushPEnv(env, makeStructValue(id, [], IllegalStruct()));
}

pconstruct1(env : PEnv, id : string) -> bool {
	arg = popPEnv(env);
	pushPEnv(env, makeStructValue(id, [arg], IllegalStruct()));
}

pconstruct2(env : PEnv, id : string) -> bool {
	arg2 = popPEnv(env);
	arg1 = popPEnv(env);
	pushPEnv(env, makeStructValue(id, [arg1, arg2], IllegalStruct()));
}

pconstruct3(env : PEnv, id : string) -> bool {
	arg3 = popPEnv(env);
	arg2 = popPEnv(env);
	arg1 = popPEnv(env);
	pushPEnv(env, makeStructValue(id, [arg1, arg2, arg3], IllegalStruct()));
}

pconstruct4(env : PEnv, id : string) -> bool {
	arg4 = popPEnv(env);
	arg3 = popPEnv(env);
	arg2 = popPEnv(env);
	arg1 = popPEnv(env);
	pushPEnv(env, makeStructValue(id, [arg1, arg2, arg3, arg4], IllegalStruct()));
}

pconstruct5(env : PEnv, id : string) -> bool {
	arg5 = popPEnv(env);
	arg4 = popPEnv(env);
	arg3 = popPEnv(env);
	arg2 = popPEnv(env);
	arg1 = popPEnv(env);
	pushPEnv(env, makeStructValue(id, [arg1, arg2, arg3, arg4, arg5], IllegalStruct()));
}

pcons(env : PEnv) -> bool {
	h = popPEnv(env);
	t = popPEnv(env);
	pushPEnv(env, Cons(h, t));
}

pfn1(env : PEnv, fn : (flow) -> flow) -> bool {
	pushPEnv(env, fn(popPEnv(env)));
}

pfn2(env : PEnv, fn : (flow, flow) -> flow) -> bool {
	e2 = popPEnv(env);
	e1 = popPEnv(env);
	pushPEnv(env, fn(e1, e2));
}
