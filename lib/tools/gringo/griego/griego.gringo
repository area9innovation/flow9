term = 
	term "|>" ws term 							$"precedence_2"
	|> term "|" ws term 						$"choice_2"
	|> term term								$"seq_2"
	|> "$" ws term								$"unquote_1"
	|> "<" ws term								$"lower_1"
	|> "#" ws term								$"error_1"
	|> term "*" ws								$"star_1"
	|> term "+" ws								$"plus_1"
	|> term "?" ws								$"opt_1"
	|> "!" ws term								$"negate_1"
	|> 
		"(" ws term ")" ws							
		| '"' string '"' ws							$"string_1"
		| "'" char "'" ws "-" ws "'" char "'" ws	$"range_2"
		| "'" stringq "'" ws						$"string_1"
		| id ws "=" ws term ";" ws term				$"rule_3"
		| id ws										$"var_1"
	;

id = $('a'-'z' | 'A'-'Z' | '0'-'9' | '_')+;

string = $(!'"' '0x0'-'0xFFFF')*;
stringq = $(!"'" '0x0'-'0xFFFF')*;
char = $("0x" hexdigit+) | $anychar;
anychar = '0x0'-'0xFFFF';
hexdigit = 'a'-'f' | 'A'-'F' | '0'-'9';
ws = s*;
s = " " | "\t" | "\n" | "\r" | "//" (!"\n" anychar)* "\n" | "/*" (!"*/" anychar)* "*/";
grammar = ws term;
grammar
