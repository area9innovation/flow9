import tools/gringo/gringo_action;
import tools/gringo/gringo_ast;
import tools/gringo/jingo/jingo_ast;
import tools/gringo/jingo/utils;
import ds/list;
import math/math;

export {
	jingoAstAction = GringoAction(
		\acc : JingoStack<bool>, v -> {
			if (v == "nil") {
				pushJingoStack(acc, JArray([]));
			} else if (v == "operand") {
				op = popJingoStack(acc);
				pushJingoStack(op.second, JOperand(getJString(op.first)));
			} else if (v == "cons") {
				elm = popJingoStack(acc);
				array = popJingoStack(elm.second);
				pushJingoStack(array.second, JArray(arrayPush(getJArray(array.first), elm.first)));
			} else if (v == "int") {
				op = popJingoStack(acc);
				pushJingoStack(op.second, JInt(s2i(getJString(op.first))));
			} else if (v == "string") {
				// This is probably just acc, but to be safe, we do this
				op = popJingoStack(acc);
				pushJingoStack(op.second, JString(getJString(op.first)));
			} else if (v == "def") {
				pushJingoStack(acc, JOperand("define"));
			} else if (v == "defs") {
				pushJingoStack(acc, JOperand("defines"));
			} else {
				println("TODO: Handle "+ v);
				acc;
			}
		},
		\acc : JingoStack<bool>, m -> {
			pushJingoStack(acc, JString(m));
		}
	);

	JingoStack(
		stack : List<Jingo<?>>,
	);

	pushJingoStack(a : JingoStack<?>, e : Jingo<?>) -> JingoStack<?>;
	popJingoStack(a : JingoStack<?>) -> Pair<Jingo<?>, JingoStack<?>>;
}

pushJingoStack(a : JingoStack<?>, e : Jingo<?>) -> JingoStack<?> {
	JingoStack(
		a with stack = Cons(e, a.stack),
	);
}

popJingoStack(a : JingoStack<?>) -> Pair<Jingo<?>, JingoStack<?>> {
	switch (a.stack) {
		EmptyList(): {
			println("Stack underflow");
			Pair(JOperand(""), a);
		}
		Cons(head, tailL): {
			Pair(head, JingoStack(a with stack = tailL))
		}
	}
}
