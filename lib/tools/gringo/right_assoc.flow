import text/gringo/gringo_ast;

export {
	// Turns ((a | b) | c)  into (a | (b | c))
	rightAssociateGTerm(g : GTerm) -> GTerm;
}

rightAssociateGTerm(g : GTerm) -> GTerm {
	switch (g) {
		GPrecedence(a, b): {
			ra = rightAssociateGTerm(a);
			rb = rightAssociateGTerm(b);
			switch (ra) {
				GPrecedence(ra0, ra1): {
					// If these survived, we did something wrong earlier
					GChoice(ra0, rightAssociateGTerm(GChoice(ra1, rb)));
				}
				default: {
					// If these survived, we did something wrong earlier
					GChoice(ra, rb);
				}
			}
		}
		GChoice(a, b): {
			ra = rightAssociateGTerm(a);
			rb = rightAssociateGTerm(b);
			switch (ra) {
				GChoice(ra0, ra1): {
					GChoice(ra0, rightAssociateGTerm(GChoice(ra1, rb)));
				}
				default: {
					GChoice(ra, rb);
				}
			}
		}
		GRule(id, term, body): GRule(id, rightAssociateGTerm(term), rightAssociateGTerm(body));
		GSeq(a, b): {
			ra = rightAssociateGTerm(a);
			rb = rightAssociateGTerm(b);
			switch (rb) {
				GSeq(rb0, rb1): {
					GSeq(rightAssociateGTerm(GSeq(ra, rb0)), rb1);
				}
				default: {
					GSeq(ra, rb);
				}
			}
		}
		GStar(term): GStar(rightAssociateGTerm(term));
		GPlus(term): GPlus(rightAssociateGTerm(term));
		GOpt(term): GOpt(rightAssociateGTerm(term));
		GNegate(term): GNegate(rightAssociateGTerm(term));
		GError(term): GError(rightAssociateGTerm(term));
		GLower(term): GLower(rightAssociateGTerm(term));
		GUnquote(term): GUnquote(rightAssociateGTerm(term));
		GString(text): g;
		GRange(lower, upper): g;
		GVar(id): g;
		GEpsilon(): g;
	}
}
