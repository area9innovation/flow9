exp = exp "||" ws exp $"||"
	|> exp "&&" ws exp $"&&"
	|> exp "==" ws exp $"==" | exp "!=" ws exp $"!="
	|> exp ("<=" ws exp $"<=" | "<" ws exp $"<" | ">=" ws exp $">=" | ">" ws exp $">")
	|> exp < ("+" ws exp $"+" | "-" ws exp ws $"-")*
	|> exp ("*" ws exp $"*" | "/" ws exp $"/" | "%" ws exp $"%")*

	|> exp ("[" ws exp "]" ws $"index")+ 
	|> exp ("." ws exp $"dot")+
	|> exp "?" ws exp ":" ws exp $"ifelse"

	|> "-" ws exp $"negate"

	|> (
		"if" ws "(" ws exp ")" ws exp "else" ws exp $"ifelse" 
		| "if" ws "(" ws exp ")" ws exp $"if"
		| "(" ws exp ")" ws 
		| "true" $"true"
		| "false" $"false"
		| string ws $"unescape"
		| int ws $"s2i"
		| "[" ws exps "]" ws
	);

string = $('"' onechar_* '"') ws;

onechar_ = backslash escapedchar_
	| !'"' !backslash anychar;

escapedchar_ = backslash | '"' | "n" | "t" | "r";
backslash = '\';
anychar = '0x0000'-'0xffff';

int = $'0'-'9'+;
exps = $"list" exp $"cons" ("," ws exp $"cons")* ("," ws)? 
	| $"list";

ws = s*;
s = " " | "\t" | "\n";
exp
