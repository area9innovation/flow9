exp = 
	exp ("||" ws exp $"||")*
	|> exp ("&&" ws exp $"&&")*
	|> exp ("==" ws exp $"==" | "!=" ws exp $"!=")*
	|> exp ("<=" ws exp $"<=" | "<" ws exp $"<" | ">=" ws exp $">=" | ">" ws exp $">")*
	|> exp <("+" ws exp $"+" | "-" ws exp $"-")*
	|> exp ("*" ws exp $"*" | "/" ws exp $"/" | "%" ws exp $"%")*
	|> exp ("(" ws exps ")" ws $"call")+
	|> exp ("[" ws exp "]" ws $"index")+
	|> (
		'-' ws exp $"negate"
		| '!' ws exp $"not"
	)
	|> (
		"if" !firstid ws "(" ws exp ")" ws exp "else" ws exp $"if"
		| backslash ws args "->" ws exp $"lambda"
		| id "=" ws exp ";" ws expsemis $"sequence" $"let"
		| "{" ws expsemis "}" ws $"sequence"
		| "(" ws exp ")" ws
		| "[" ws exps "]" ws
		| $int ws $"s2i"
		| $('"' stringchar* '"') ws $"unescape"
		| "true" ws $"true"
		| "false" ws $"false"
		| id $"var"
	)
	;

expsemis = $"list" exp $"cons" (";" ws exp $"cons")* | $"list";

exps = $"list" exp $"cons" ("," ws exp $"cons")* | $"list";

args = $"list" id $"cons" ("," ws id $"cons")* | $"list";

id = $(firstid restid*) ws;

firstid = 'a' - 'z' | 'A' - 'Z' | '_';
restid = firstid | '0'-'9';

stringchar = !'"' anychar;
anychar = '0x0000'-'0xffff';

int = '0'-'9'+;

ws = cs*;

cs = ' ' | '\t' | '\n';
backslash = '\';

exp
