import sys/concurrent;
import fs/filesystem;
import ds/tree;
import net/url_parameter;
import lingo/flow/javascript_cli_params;

// Use compilerconfig.flow to get a useful, easy-to-use configuration,
// and then functions here to work with it
export {
	// Parses config file as a string for options
	parseFlowConfig(config : string) -> Tree<string, string>;

	// Finds directory, containing 'flow.config' file upwards from the given directory. Empty string means no such directory.
	findFlowConfigDir(dir : string) -> string;

	// Finds 'flow.config' file upwards from the given directory and returns its content. Returns empty string if there's no such file.
	findFlowConfig(dir : string) -> string;

	// Read config parameter.
	// Priority (increasing):
	// 1) Take default value
	// 2) Read from flow.config
	// 3) Get from command line/url
	getConfigParameterDef(config : Tree<string, string>, paramName : string, defValue : string) -> string;
	getConfigParameter(config : Tree<string, string>, paramName : string) -> string;

    isConfigParameterTrue : (config : Tree<string, string>, param : string)->bool;
    isConfigParameterFalse : (config : Tree<string, string>, param : string)->bool;
    isConfigParameterSet : (config : Tree<string, string>, param : string)->bool;
    isAnyConfigParametersTrue : (config : Tree<string, string>, params : [string])->bool;
    areAllConfigParametersTrue : (config : Tree<string, string>, params : [string])->bool;
    isAnyConfigParametersSet : (config : Tree<string, string>, params : [string])->bool;
    areAllConfigParametersSet : (config : Tree<string, string>, params : [string])->bool;

    getConfigParameterInt(config : Tree<string, string>, paramName : string, defValue : int) -> int;

    // Sets (overrides) the given config.
    setConfigParameter : (conf : CompilerConfig, key : string, value : string)->CompilerConfig;
    // Sets the given config if it is not already set.
    underrideConfigParameter : (conf : CompilerConfig, key : string, value : string)->CompilerConfig;

    getConstConfig : (config : [Pair<string, string>])->Tree<string, string>;

    // Gets config from select url parameters
    getUrlConfig : (params : [string])->Tree<string, string>;

    // Gets config from all url parameters
    getAllUrlConfig() -> Tree<string, string>;

    // Gets config from select url parameters given by first of params and 
    // stores them under second of params.
    getUrlConfigMapNames : (params : [Pair<string, string>])->Tree<string, string>;

    // Combines two configs. Settings in over overrides settings in under.
    combineConfigsOverride : (
        over : Tree<string, string>, 
        under : Tree<string, string>
    )->Tree<string, string>;
    combineConfigs : (
        left : Tree<string, string>, 
        right : Tree<string, string>,
        combine : (param : string, fromLeft : string, fromRight : string)->string
    )->Tree<string, string>;

    // The structure to contain the configuration for a compiler invocation
    CompilerConfig(
        flowfile : string, 
        includes : [string],
        objectFolders : [string],
        verbose : int,
        jsParams : JSCliParams,

        config : Tree<string, string>,
        threadId : int, // stores the id of a thread, responsible for a task (in parallel mode)
    );
    compilerConfigFromConfig : (config : Tree<string, string>) -> CompilerConfig;
    compilerConfigToTree : (config : CompilerConfig) -> Tree<string, string>;
    compilerConfigToString(config : CompilerConfig) -> string;
	jsCliParamsToTree(params : JSCliParams) -> Tree<string, string>;

    // Finds the directory of flow distribution.
	// In case of failure prints the warning message and returns "../flow9/"
	getFlowDir() -> string;

	// Tries to find the directory of flow distribution.
	// In case of failure returns "" and doens't report anything.
	findFlowDir() -> string;

	fcDirName(path : string) -> string;

	setConfigFlowfile(config : CompilerConfig, flowfile : string) -> CompilerConfig;

	patchRemoteConfig(conf : CompilerConfig) -> CompilerConfig;

	// Set (add or modify) a config field(s) in conf struct.
	setConfigOption(conf : CompilerConfig, opt : string, val : string) -> CompilerConfig;
	setConfigOptions(conf : CompilerConfig, opt_pairs : [Pair<string, string>]) -> CompilerConfig;
}

getFlowDir() -> string {
	flowdir = findFlowDir();
	if (flowdir != "") flowdir else {
		println("WARNING: Could not determine the flow directory.");
		println("Defaults to ../flow9/. Use bin-dir=<path> to specify where the flow folder is");
		"../flow9/";
	} 
}

fcFlowDir : ref Maybe<string> = ref None();

findFlowDir() -> string {
	init_dir = \-> {
		// Use the 'print-flow-dir=1' option of flowc
		flowDirByFlowc = \-> {
			if (strContains(getApplicationPath(), "flowc1")) {
				// flowc1 must not use itsef in this function, 
				// otherwise there's a fork bomb.
				"" 
			} else {
				dir_lines = ref "";
				err_lines = ref "";
				code = execSystemProcess("flowc1", ["print-flow-dir=1"], ".", 
					\out -> dir_lines := ^dir_lines + out, 
					\err -> {
						println(err);
						err_lines := err;
					}
				);
				filtered = filtermap(strSplit(^dir_lines, "\n"), \line -> {
					line1 = trim2(line, " \n\t\r");
					if (line1 == "") None() else Some(line1);
				});
				if (code != 0 || ^err_lines != "" || length(filtered) != 1) "" else {
					filtered[0];
				}
			}
		}
		// Use the path of the executable
		flowDirByAppPath = \-> {
			appPath = strReplace(getApplicationPath(), "\\", "/");
			if (strContains(appPath, "/platforms/qt")) {
				takeBefore(appPath, "/platforms/qt", appPath) + "/"
			} else if (strContains(appPath, "flow9/platforms/java")) {
				takeBefore(appPath, "platforms/java", appPath)
			} else if (strContains(appPath, "tools/flowc/")) {
				takeBefore(appPath, "tools/flowc/", appPath)
			} else if (strContains(appPath, "tools/flowc_lsp/")) {
				takeBefore(appPath, "tools/flowc_lsp/", appPath)
			} else if (strContains(appPath, "flow9/bin/")) {
				takeBefore(appPath, "bin/", appPath)
			} else if (strContains(appPath, "bin/")) {
				takeBefore(appPath, "bin/", appPath)
			} else {
				flowDirByFlowc();
			}
		}
		binDir = getUrlParameter("bin-dir");
		flowdir = if (binDir == "") {
			flowDirByAppPath()
		} else {
			if (endsWith(binDir, "bin/") || endsWith(binDir, "bin\\"))
				substring(binDir, 0, strlen(binDir)-4)
			else
				substring(binDir, 0, strlen(binDir)-3);
		}
		strReplace(flowdir, "\\", "/");
    }
    onlyOnce(fcFlowDir, init_dir);
}

findFlowConfig(dir : string) -> string {
	conf_dir = findFlowConfigDir(dir);
	if (conf_dir == "") "" else  getFileContent(conf_dir + "/flow.config");
}

fcDirName(path : string) -> string {
	path1 = strReplace(path, "\\", "/");
	if (!strContains(path1, "/")) "" else {
		strLeft(path1, strLastIndexOf(path1, "/"));
	}
}

findFlowConfigDir(d : string) -> string {
	dir = rtrim2(d, "/");
	if (fileExists(dir + "/flow.config")) dir else {
		// Check for root directory or c:/ in windows
		if (dir == "" || (strlen(dir) <= 3 && isLetter(getCharAt(dir, 0)) && getCharAt(dir, 1) == ":")) "" else {
			findFlowConfigDir(fcDirName(dir));
		}
	}
}

parseFlowConfig(config : string) -> Tree<string, string> {
	lines = strSplit(config, "\n");
	fold(lines, makeTree(), \acc, l -> {
		// Skip comment lines in flow.config
		if (!startsWith(ltrim2(l, " \t"), "#")) {
			opts = strSplit(l, "=");
			if (length(opts) == 2) {
				opt = trim2(opts[0], " \t\u000d");
				val = trim2(opts[1], " \t\u000d");
				val1 = if (opt == "resource-file") {
						val2 = if (endsWith(val, ".swf")) val
							else val + ".swf";
						if (strFindFirstOf(val2, "/\\") != -1) val2
						else "resources/" + val2
					} else val;
				setTree(acc, opt, val1)
			} else acc;
		} else acc;
	});
}

getConfigParameterDef(config : Tree<string, string>, paramName : string, defValue : string) -> string {
	getUrlParameterDef(paramName, lookupTreeDef(config, paramName, defValue))
}

getConfigParameter(config : Tree<string, string>, paramName : string) -> string {
	getConfigParameterDef(config, paramName, "");
}

getConfigParameterInt(config : Tree<string, string>, paramName : string, defValue : int) -> int {
    raw = getConfigParameterDef(config, paramName, "");
    if (raw == "") defValue else s2i(raw); 
}

isConfigParameterSet(config, param)
    getConfigParameter(config, param) != "";

isConfigParameterTrue(config, param) 
    isParameterTrue(getConfigParameter(config, param));

isConfigParameterFalse(config, param) 
    isParameterFalse(getConfigParameter(config, param));

isAnyConfigParametersTrue(config : Tree<string, string>, params : [string]) -> bool {
	exists(params, \param -> isConfigParameterTrue(config, param));
}

areAllConfigParametersTrue(config : Tree<string, string>, params : [string]) -> bool {
	forall(params, \param -> isConfigParameterTrue(config, param));
}

isAnyConfigParametersSet(config : Tree<string, string>, params : [string]) -> bool {
	exists(params, \param -> isConfigParameterSet(config, param));
}

areAllConfigParametersSet(config : Tree<string, string>, params : [string]) -> bool {
	forall(params, \param -> isConfigParameterSet(config, param));
}

setConfigParameter(conf, key, value)
    compilerConfigFromConfig(setTree(conf.config, key, value));

underrideConfigParameter(conf, key, value)
    if (containsKeyTree(conf.config, key)) conf
    else setConfigParameter(conf, key, value);

getConstConfig(params) 
    fold(params, makeTree(), \t, pair->
        if (pair.second != "") setTree(t, pair.first, pair.second)
        else t
    );

getUrlConfig(params)
    getUrlConfigMapNames(map(params, \param->Pair(param, param)));

getUrlConfigMapNames(params)
    fold(params, makeTree(), \config, param->
        eitherGetUrlParameter(param.first,
            \value->setTree(config, param.second, value),
            config
        )
    );

getAllUrlConfig() -> Tree<string, string> {
	fold(getAllUrlParametersArray(), makeTree(), \acc, param -> setTree(acc, param[0], param[1]))
}

combineConfigsOverride(over, under) 
    mergeTreeCustom(over, under, \__, upper, __ -> upper);

combineConfigs(left, right, combine)
    mergeTreeCustom(left, right, combine);

compilerConfigFromConfig(config) {
	v = getConfigParameterDef(config, "verbose", "0");
	verboseLevel = if (isDigits(v)) s2i(v) else 0;

	dpn = getConfigParameterDef(config, "dce-preserved-names", "");
	dcePreservedNames = fold(strSplit(dpn, ","), makeSet(), \a, i -> {
		if (i != "") insertSet(a, i) else a
	});
	CompilerConfig(
		getConfigParameter(config, "flowfile"),
		//includes : [string]
		strSplit(getConfigParameter(config, "include"), ","),
		//objectFolders : [string]
		strSplit(getConfigParameter(config, "obj"), ","),
		//verbose : int
		verboseLevel,
		//jsParam
		JSCliParams(
			//jsfile : string,
			getConfigParameter(config, "js"),
			//esfile : string,
			getConfigParameter(config, "es6"),
			//typescriptFile : string,
			getConfigParameter(config, "ts"),
			//debug : bool,
			isConfigParameterTrue(config, "debug"),
			//code : bool,
			isConfigParameterTrue(config, "jscode"),
			//mutable modules : string,
			getConfigParameter(config, "jsmodules"),
			//comments : bool,
			isConfigParameterTrue(config, "jscomments"),
			//node : bool,
			isConfigParameterTrue(config, "nodejs"),
			// webmodule : bool,
			isConfigParameterTrue(config, "webmodule"),
			//isES6 : bool,
			false,
			//isTypeScript : bool,
			false,
			//tsProcess : TypeScriptProcessing,
			tsProcessing_dummy,
			//readable : bool,
			isConfigParameterTrue(config, "readable"),
			//beautify : bool,
			isConfigParameterTrue(config, "readable"),
			//sourcemap : bool,
			isConfigParameterTrue(config, "js-source-map"),
			//verbose : int,
			verboseLevel,
			//nwjs : bool
			isConfigParameterTrue(config, "nwjsmode"),
			// fontconfig : string
			getConfigParameter(config, "fontconfig"),
			// dcePreservedNames : Set<string>,
			dcePreservedNames,
			isConfigParameterTrue(config, "dce-remove-main"),
			getConfigParameter(config, "jslibraryobject"),
			isConfigParameterTrue(config, "call-main")
		),
		//config : Tree<string, string>
		config,
		s2i(getThreadId()) // current thread id
	);
}

compilerConfigToTree(config : CompilerConfig) -> Tree<string, string> {
	mergeTree(
		mergeTree(
			config.config,
			jsCliParamsToTree(config.jsParams),
		),
		getConstConfig([
			Pair("flowfile", config.flowfile),
			Pair("include", strGlue(config.includes, ",")),
			Pair("obj", strGlue(config.objectFolders, ",")),
			Pair("verbose", i2s(config.verbose)),
			Pair("threadId", i2s(config.threadId)),
		])
	)
}

compilerConfigToString(config : CompilerConfig) -> string {
	"flowfile=" + config.flowfile + ", " +
	"verbose=" + i2s(config.verbose) + ", " +
	"includes=[" + strGlue(config.includes, ",") + "], " +
	"objectFolders=[" + strGlue(config.objectFolders, ",") + "], " +
	"threadId=" + i2s(config.threadId) + ", " +
	strGlue(map(tree2pairs(config.config), \p -> p.first + "=" + p.second), ", ");
}

jsCliParamsToTree(params : JSCliParams) -> Tree<string, string> {
	getConstConfig([
		Pair("js", params.jsfile),
		Pair("es6", params.esfile),
		Pair("ts", params.typescriptFile),
		Pair("debug", b2s(params.debug)),
		Pair("jscode", b2s(params.code)),
		Pair("jsmodules", params.modules),
		Pair("jscomments", b2s(params.comments)),
		Pair("nodejs", b2s(params.node)),
		Pair("webmodule", b2s(params.webmodule)),
		Pair("isES6", b2s(params.isES6)),
		Pair("isTypeScript", b2s(params.isTypeScript)),
		Pair("readable", b2s(params.readable)),
		Pair("jsbeautify", b2s(params.beautify)),
		Pair("js-source-map", b2s(params.sourcemap)),
		Pair("jsverbose", i2s(params.verbose)),
		Pair("nwjs", b2s(params.nwjs)),
		Pair("fontconfig", params.fontconfig),
		Pair("dce-preserved-names", strGlue(set2array(params.dcePreservedNames), ",")),
		Pair("dce-remove-main", b2s(params.dceRemoveMain)),
		Pair("jslibraryobject", params.jslibraryObject),
		Pair("js-call-main", b2s(params.callMain))
	])
}

setConfigFlowfile(conf : CompilerConfig, file : string) -> CompilerConfig {
	CompilerConfig(conf with flowfile = file)
}

patchRemoteConfig(conf : CompilerConfig) -> CompilerConfig {
	cwd = strReplace(resolveRelativePath("."), "\\", "/");

	// Setup the 'output-dir' in case it is not set explicitly.
	conf1 = switch (lookupTree(conf.config, "output-dir")) {
		Some(__): conf.config;
		None():   setTree(conf.config, "output-dir", cwd);
	}
	// Add a flowfile as a config tree option.
	conf2 = setTree(conf1, "file", conf.flowfile);

	// Setup the 'working-dir' option
	conf3 = setTree(conf2, "working-dir", lookupTreeDef(conf2, "working-dir", cwd));

	// Resolve relative path for 'fontconfig-file', because server may run in a different place.
	conf4 = switch (lookupTree(conf3, "fontconfig-file")) {
		Some(file): setTree(conf3, "fontconfig-file", resolveRelativePath(file));
		None():     conf3;
	}

	// Update 'fontconfig' field in JSCliParams
	fontconfig = lookupTreeDef(conf4, "fontconfig-file", conf.jsParams.fontconfig);
	jsParams = JSCliParams(conf.jsParams with fontconfig = fontconfig);

	// Output the result
	CompilerConfig(conf with config = conf4, jsParams = jsParams);
}

setConfigOption(conf : CompilerConfig, opt : string, val : string) -> CompilerConfig {
	CompilerConfig(conf with 
		flowfile = if (opt == "file" || opt == "flowfile") val else conf.flowfile,
		includes = if (opt == "I" || opt == "include") strSplit(val, ",") else conf.includes,
		objectFolders = if (opt == "obj" || opt == "objectFolders") strSplit(val, ",") else conf.objectFolders,
		verbose = if (opt == "verbose") s2i(val) else conf.verbose,
		jsParams = JSCliParams(conf.jsParams with
			jsfile = if (opt == "js") val else if (opt == "html") val + ".js" else conf.jsParams.jsfile,
			esfile = if (opt == "es6") val else conf.jsParams.esfile,
			typescriptFile = if (opt == "ts") val else conf.jsParams.typescriptFile,
			debug = if (opt == "debug") s2b(val) else conf.jsParams.debug,
			code = if (opt == "jscode") s2b(val) else conf.jsParams.code,
			modules = if (opt == "jsmodules") val else conf.jsParams.modules,
			comments = if (opt == "jscomments") s2b(val) else conf.jsParams.comments,
			node = if (opt == "nodejs") s2b(val) else conf.jsParams.node,
			webmodule = if (opt == "webmodule") s2b(val) else conf.jsParams.webmodule,
			readable = if (opt == "readable") s2b(val) else conf.jsParams.readable,
			beautify = if (opt == "jsbeautify") s2b(val) else conf.jsParams.beautify,
			sourcemap = if (opt == "js-source-map") s2b(val) else conf.jsParams.sourcemap,
			verbose = if (opt == "jsverbose") s2i(val) else conf.jsParams.verbose,
			nwjs = if (opt == "nwjs" || opt == "nwjsmode") s2b(val) else conf.jsParams.nwjs,
			fontconfig = if (opt == "fontconfig-file" || opt == "fontconfig") val else conf.jsParams.fontconfig,
			dcePreservedNames = if (opt == "dce-preserved-names") buildSet(strSplit(val, ",")) else conf.jsParams.dcePreservedNames,
			dceRemoveMain = if (opt == "dce-remove-main") s2b(val) else conf.jsParams.dceRemoveMain,
			jslibraryObject = if (opt == "jslibraryobject") val else conf.jsParams.jslibraryObject,
			callMain = if (opt == "js-call-main") s2b(val) else conf.jsParams.callMain,
		),
		config = setTree(conf.config, opt, val)
	);
}

setConfigOptions(conf : CompilerConfig, opt_pairs : [Pair<string, string>]) -> CompilerConfig {
	fold(opt_pairs, conf, \acc, pair -> setConfigOption(acc, pair.first, pair.second));
}
