import fs/filesystem;
import sys/target;

export {
	// Trims the filename to be a flow path, i.e. no .flow suffix, and no lib/ prefix or anything like that
	trimFlowfile : (flowfile : string, imports : [string], workingDir : string)->string;

	// Find the given flow-file, using imports. Returns a full filesystem path
	findFlowPath(flowfile : string, flowsuffix : bool, imports : [string], workingDir : string) -> string;

	// Find the given flow-file, using imports. Returns a relative filesystem path
	findFlowRelativePath(flowfile : string, flowsuffix : bool, imports : [string], workingDir : string) -> string;

	// Returns current working directory with slashes (/) and no slash at the end
	fcCurrentWorkingDir() -> string;

	// Convert \ to / and C:-like prefix to c: (lowercase) in path.
	fcNormalizeWindowsPath(path : string) -> string;


	// When URI parser parses something like:
	//	file:///c:/Users/dmitr/area9/flow9/tools/flowc
	// it outputs path as:
	//	/c:/Users/dmitr/area9/flow9/tools/flowc
	// which is wrong in windows, first slash must be removed.
	//
	fcCorrectUriPath(path : string) -> string;
}

trimFlowfile(flowfile : string, imports : [string], workingDir : string) {
	noflow0 = if (endsWith(flowfile, ".flow")) strsubsmart(flowfile, 0, -5) 
	else flowfile;
	shift = if (workingDir == "") "" else relatingPath(workingDir, resolveRelativePath("."));
	pathShift = if (shift == "") shift else shift + "/";
	noflow = strReplace(noflow0, "\\", "/");

	processInculde = \acc, inc -> {
		resolved0 = resolveRelativePath(inc);
		resolved = strReplace(resolved0, "\\", "/");
		if (startsWith(toLowerCase(acc), toLowerCase(resolved))) {
			strRight(acc, strlen(resolved))
		} else if (startsWith(toLowerCase(acc), toLowerCase(inc))) {
			strRight(acc, strlen(inc))
		} else acc
	};

	// Find the path-less path - the flowpath
	trim2(fold(imports, noflow, \acc, inc -> {
		acc1 = if (pathShift != "") {
			processInculde(acc, pathShift + inc)
		} else acc;
		processInculde(acc1, inc)
	}), "/")
}

findFlowPath(flowpath : string, flowsuffix : bool, imports : [string], workingDir : string) -> string {
	withFlow = if (endsWith(flowpath, ".flow") || !flowsuffix) flowpath else flowpath + ".flow";
	shift = if (workingDir == "") "" else relatingPath(workingDir, resolveRelativePath("."));
	pathShift = if (shift == "") shift else shift + "/";
	path = if (fileExists(withFlow)) {
		resolveRelativePath(withFlow);
	} else {
		doFindFlowPath(withFlow, imports, true, pathShift);
	}
	fcNormalizeWindowsPath(path);
}

findFlowRelativePath(flowpath : string, flowsuffix : bool, imports : [string], workingDir : string) -> string {
	withFlow = if (endsWith(flowpath, ".flow") || !flowsuffix) flowpath else flowpath + ".flow";
	shift = if (workingDir == "") ""  else relatingPath(workingDir, resolveRelativePath("."));
	pathShift = if (shift == "") shift else shift + "/";
	if (fileExists(withFlow)) {
		withFlow;
	} else {
		doFindFlowPath(withFlow, imports, false, pathShift);
	}
}

doFindFlowPath(withFlow : string, imports : [string], doResolve : bool, pathShift : string) -> string {
	if (imports == []) withFlow
	else {
		head = imports[0];
		directPath = head + (if (endsWith(head, "/") || endsWith(head, "\\")) "" else "/") + withFlow;
		if (fileExists(directPath)) {
			if (!doResolve) directPath
			else resolveRelativePath(directPath);
		} else {
			shiftedPath = pathShift + directPath;
			if (fileExists(shiftedPath)) {
				if (!doResolve) shiftedPath
				else resolveRelativePath(shiftedPath);
			} else {
				rest = tail(imports);
				doFindFlowPath(withFlow, rest, doResolve, pathShift);
			}
		}
	}
}

fcCurrentWorkingDir() -> string {
	rtrim2(fcNormalizeWindowsPath(resolveRelativePath(".")), "/");
}

fcNormalizeWindowsPath(path : string) -> string {
	path1 = strReplace(path, "\\", "/");
	if (hasTargetName("windows")) {
		drive = getCharAt(path1, 0);
		// Detect C: windows prefix
		if (isLetter(drive) && getCharAt(path1, 1) == ":") {
			// Convert C:, D:, ... to lowercase c:, d:, ...
			toLowerCase(drive) + ":" + strRight(path1, 2);
		} else {
			path1
		}
	} else {
		path1;
	}
}

fcCorrectUriPath(path : string) -> string {
	if (!(startsWith(path, "/") || startsWith(path, "\\")) || !hasTargetName("windows")) path else {
		// Detect c:, d:, C:, D:, etc.
		drive = getCharAt(path, 1);
		if (isLetter(drive) && getCharAt(path, 2) == ":") {
			// Convert C: to c:
			toLowerCase(drive) + ":" + strRight(path, 3);
		} else {
			// keep original path
			path
		}
	}
}
