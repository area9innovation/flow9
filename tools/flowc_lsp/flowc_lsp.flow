import math/stringmath;
import tools/flowc_lsp/lsp_protocol;

runLspServer(lsp_conf : Tree<string, string>) -> void {
	out_cb = \log_request, st, msg -> {
		if (log_request) {
			lspServerLog("Response[" + d2st((timestamp() - st) / 1000.0, 2) + " s.]: " + escapeStr(msg), lsp_conf);
		}
		print(msg);
	}
	exit_cb = \-> {
		lspServerLog("LSP server stopped.", lsp_conf);
		quit(0);
	}
	switch (lspReadRequest()) {
		Some(req): {
			// Special watermark for non-logging
			log_request = !strContains(req.request, "do_not_log_this");
			if (log_request) {
				lspServerLog("Request: " + escapeStr(req.request), lsp_conf);
			}
			start_time = timestamp();
			json = lspDecodeJsonRequest(req.json);
			if (json == JsonNull()) {
				lspServerLog("WARNING: Illegal request:\n" + req.request, lsp_conf);
			} else {
				// Run a request processing asynchronious, because the main loop - 
				// runLspServer must be tail-recursive to avoid stack overflow.
				deferred(\ -> lspProcessRequest(lsp_conf, json, 
					\msg -> out_cb(log_request, start_time, msg),
					exit_cb
				));
			}
		}
		None(): {
			lspServerLog("Failed to read a request.", lsp_conf);
		}
	}
	runLspServer(lsp_conf);
}

main() {
	lsp_conf = getAllUrlConfig();
	lsp_conf_str = strGlue(lspConf2ConsoleArgs(lsp_conf), " ");
	lspServerLog("LSP server started in: " + resolveRelativePath(".") + " directory, lsp_conf: '" + lsp_conf_str + "'", lsp_conf);
	runLspServer(lsp_conf);
}
