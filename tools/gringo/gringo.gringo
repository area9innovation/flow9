term = 
	term "|>" ws term 							$"GPrecedence"
	|> term "|" ws term 						$"GChoice"
	|> term term								$"GSeq"
	|> "$" ws term								$"GUnquote"
	|> "<" ws term								$"GLower"
	|> "#" ws term								$"GError"
	|> term "*" ws								$"GStar"
	|> term "+" ws								$"GPlus"
	|> term "?" ws								$"GOpt"
	|> "!" ws term								$"GNegate"
	|> 
		"(" ws term ")" ws							
		| '"' string '"' ws							$"GString"
		| "'" char "'" ws "-" ws "'" char "'" ws	$"GRange"
		| "'" stringq "'" ws						$"GString"
		| id ws "=" ws term ";" ws term				$"GRule"
		| id ws										$"GVar"
	;

id = $('a'-'z' | 'A'-'Z' | '0'-'9' | '_')+;
int = $'0'-'9'+;

string = $(!'"' '0x0'-'0xFFFF')*;
stringq = $(!"'" '0x0'-'0xFFFF')*;
char = $("0x" hexdigit+) | $anychar;
anychar = '0x0'-'0xFFFF';
hexdigit = 'a'-'f' | 'A'-'F' | '0'-'9';
ws = s*;
// We do not have \n and \t and such, so these are here verbatim
s = " " | "	" | "
" | "//" (!"
" anychar)* "
" | "/*" (!"*/" anychar)* "*/";
grammar = ws term;
grammar
