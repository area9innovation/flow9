import tools/dsl/dsl_parse;
import tools/dsl/dsl_rewrite;
import tools/dsl/dsl_compiler;
import tools/dsl/dsl_eval;
import tools/dsl/dsl_language;

export {
	// Parse and desugar a program in this language
	parseDsl(file : string, language : DslLanguage, program : string) -> DslAst;

	// Optimize this program
	optimizeDsl(language : DslLanguage, program : DslAst) -> DslAst;

	runDsl(file : string, language : DslLanguage, program : string) -> DslAst;
}

parseDsl(file : string, language : DslLanguage, program : string) -> DslAst {
	parsed = parseProgram(file, language.grammar, program);
	desugarDsl(language, parsed)
}

desugarDsl(language : DslLanguage, parsed : DslAst) -> DslAst {
	desugar = language.desugaring;
	desugared = desugar ?? rewriteDsl(parsed, desugar, 2) : parsed;
	base = language.baseLanguage;
	base ?? desugarDsl(base, desugared) : desugared
}

optimizeDsl(language : DslLanguage, program : DslAst) -> DslAst {
	opt = language.optimization;
	optimized = opt ?? {
		rewriteDsl(program, opt, 2);
	} : program;
	base = language.baseLanguage;
	base ?? optimizeDsl(base, optimized) : optimized;
}

runDsl(file : string, language : DslLanguage, program : string) -> DslAst {
	p = parseDsl(file, language, program);
	env = prepareDslEnv(makeDslEnv(), language);
	evaluateDsl(env, p);
}

prepareDslEnv(env : DslEnv, language : DslLanguage) -> DslEnv {
	base = language.baseLanguage;
	nenv = base ?? {
		prepareDslEnv(env, base);
	} : env;
	DslEnv(foldTree(language.runtime, nenv.env, \name, val, acc -> {
		setTree(acc, name, val)
	}))
}
