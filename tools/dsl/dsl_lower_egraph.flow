import tools/dsl/registry/dsl_registry;

export {
	// Does a bottom-up visit of the e-graph, visiting the first node that
	// match the filter, and then applying those nodes with the
	// result of their children
	transformDslEGraph(e : DslEGraph, eclass : int,
		fromFilter : (DslENode) -> bool,
		toFilter : (DslENode) -> bool,
		// Given this node, from the "fromFilter" world,
		// with children from the "toFilter" world,
		// convert the node to the toFilter
		applyFn : (node : DslENode, children : [DslENode]) -> Maybe<DslENode>
	) -> Maybe<DslENode>;
}

transformDslEGraph(e : DslEGraph, eclass : int, 
	fromFilter : (DslENode) -> bool,
	toFilter : (DslENode) -> bool,
	applyFn : (node : DslENode, children : [DslENode]) -> Maybe<DslENode>
) -> Maybe<DslENode> {
	root = getDslEClassRoot(e, eclass);
	nodes = getDslEClassNodes(e, root);
	matching = set2array(filterSet(nodes, fromFilter));
	if (matching != []) {
		// We just pick the first match
		preferred : DslENode = matching[0];
		children = filtermap(preferred.args, \childClass -> {
			transformDslEGraph(e, childClass, fromFilter, toFilter, applyFn)
		});
		if (length(children) == length(preferred.args)) {
			applyFn(preferred, children);
		} else {
			println("Invariant broken: We expected to be able to lower " + i2s(eclass));
			None();
		}
	} else {
		existing = set2array(filterSet(nodes, toFilter));
		if (existing != []) {
			Some(existing[0])
		} else None();
	}
}
