import text/gringo/gringo_ast;
import tools/dsl/dsl_parse;
import ds/set;

export {
	extractGringoActions(acc : Set<string>, a : GTerm) -> Set<string>;
}

extractGringoActions(acc : Set<string>, a : GTerm) -> Set<string> {
	switch (a) {
		GRule(id, term, body): extractGringosActions(acc, [term, body]);
		GPrecedence(left, right): extractGringosActions(acc, [left, right]);
		GChoice(left, right): extractGringosActions(acc, [left, right]);
		GSeq(left, right): extractGringosActions(acc, [left, right]);
		GStar(term): extractGringoActions(acc, term);
		GPlus(term): extractGringoActions(acc, term);
		GOpt(term): extractGringoActions(acc, term);
		GNegate(term): extractGringoActions(acc, term);
		GError(term): extractGringoActions(acc, term);
		GLower(term): extractGringoActions(acc, term);
		GUnquote(term): {
			switch (term) {
				GString(act): {
					parts = splitDslAction(act);
					insertSet(acc, parts.first);
				}
				default: acc;
			}
		}
		GString(text): acc;
		GRange(lower, upper): acc;
		GVar(id): acc;
		GEpsilon(): acc;
	}
}

extractGringosActions(acc : Set<string>, ts : [GTerm]) -> Set<string> {
	fold(ts, acc, extractGringoActions)
}
