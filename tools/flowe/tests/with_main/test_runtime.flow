native println2 : io (flow) -> void = Native.println;
native quit : io (code : int) -> void = Native.quit;
native toString2 : (flow) -> string = Native.toString;
native map : ([?], (?) -> ??) -> [??] = Native.map;
native iter : ([?], (?)->void) -> void = Native.iter;

// Runtime/dynaimc natives
native getStructName : (flow) -> string = Native.getStructName;
native extractStructArguments : (value : flow) -> [flow] = Native.extractStructArguments;
native isArray : (value : flow) -> bool = Native.isArray;
native isSameStructType : (value1 : flow, value2 : flow) -> bool = Native.isSameStructType;
IllegalStruct(); // Common default value for makeStructValue
native makeStructValue : (structname : string, args : [flow], default_value : flow) -> flow = Native.makeStructValue;
native isSameObj : (value1 : ?, value2 : ?) -> bool = Native.isSameObj;

StrA(a : int);
StrB(a : StrA, b : string);
StrC(
	x : StrA,
	y : StrB
);

main() {
	println2("Test runtime ...");
	a = StrA(5);
	b = StrB(a, "7");
	c = StrC(StrA(-1), b);

	println2("getStructName(" + toString2(a) + "): " + getStructName(a));
	println2("getStructName(" + toString2(b) + "): " + getStructName(b));
	println2("getStructName(" + toString2(c) + "): " + getStructName(c));

	println2("extractStructArguments(" + toString2(a) + "):");
	iter(extractStructArguments(a), \x -> println2("\t" + toString2(x)));

	println2("extractStructArguments(" + toString2(b) + "):");
	iter(extractStructArguments(b), \x -> println2("\t" + toString2(x)));

	println2("extractStructArguments(" + toString2(c) + "):");
	iter(extractStructArguments(c), \x -> println2("\t" + toString2(x)));

	arr0 : [int] = [];
	println2("isArray(" + toString2(arr0) + "): " + toString2(isArray(arr0)));
	arr1 = [1, 2, 3];
	println2("isArray(" + toString2(arr1) + "): " + toString2(isArray(arr1)));
	println2("isArray(" + toString2(a) + "): " + toString2(isArray(a)));
	println2("isArray(" + toString2(b) + "): " + toString2(isArray(b)));
	println2("isArray(" + toString2(c) + "): " + toString2(isArray(c)));


	objs : [flow] = [cast(a : StrA -> flow) , b, c, arr0, arr1];
	iter(objs, \o1 ->
		iter(objs, \o2 -> 
			println2("isSameStructType(" + toString2(o1) + ", " + toString2(o2) + "): " + toString2(isSameStructType(o1, o2)))
		)
	);

	c1 = makeStructValue("StrC", [cast(StrA(-1) : StrA -> flow), b], IllegalStruct());
	println2("c1: " + toString2(c1));
	println2("getStructName(" + toString2(c1) + "): " + getStructName(c1));
	println2("c1.x: " + toString2(c1.x));
	println2("c1.y: " + toString2(c1.y));

	println2("c and c are the same: " + toString2(isSameObj(c, c)));
	println2("c and c1 are the same: " + toString2(isSameObj(c, c1)));

	quit(0);
}