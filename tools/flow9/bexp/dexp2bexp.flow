import tools/flow9/dexp/dexp;
import tools/flow9/dexp/pretty;
import tools/flow9/types/typeenv;
import tools/flow9/types/builtin;
import tools/flow9/bexp/bexp;
import tools/flow9/types/util;

export {
	dexp2bexp(env : BExpEnv, d : DExp) -> BExp;

	BExpEnv(
		onError : (int, string) -> void,
		tmap : TMap,
		eclassType : Tree<int, TType>,
		resolveStructName : (string) -> Maybe<DStruct>,
	);

	ttype2btype(env : BExpEnv, type : TType) -> BType;

	resolveBType(env : BExpEnv, pos : int, eclass : int) -> BType;
}

resolveBType(env : BExpEnv, pos : int, eclass : int) -> BType {
	root = rootTTypeEClass(env.tmap, eclass);
	mtype = lookupTree(env.eclassType, root);
	mtype ?? {
		if (unresolvedTType(mtype)) {
			if (reportErrorOnType(env, mtype)) {
				env.onError(pos, "Add type parameter. Implicit polymorphism in " + prettyTType(mtype));
			}
		}
		ttype2btype(env, mtype)
	} : {
		// These are already reported
		env.onError(pos, "Add type annotation. Unresolved type (equivalence class e" + i2s(root) + ")");
		BTypeName("Error!", []);
	}
}

reportErrorOnType(env : BExpEnv, type : TType) -> bool {
	rec = \tt -> reportErrorOnType(env, tt);
	switch (type) {
		TTypeName(id, typars): {
			mstruct = env.resolveStructName(id);
			mstruct ?? {
				// OK, if this struct has implicit type-pars, we do not care
				!mstruct.implicitTypars;
			} : {
				// Check recursively
				forall(typars, rec)
			}
		}
		TTypeFunction(args, returnType): forall(args, rec) && rec(returnType);
		default: true;
	}
}

dexp2bexp(env : BExpEnv, d : DExp) -> BExp {
	todo = \ -> {
		println("TODO: dexp2bexp for " + d.structname);
		// prettyDExp("", "", d);
		BString("ERROR!");
	}
	rec = \ee -> {
		dexp2bexp(env, ee);
	}
	type = \eclass : int -> resolveBType(env, d.pos, eclass);
	switch (d) {
		DVoid(pos): BVoid();
		DBool(v, pos): BBool(v);
		DInt(v, pos): BInt(v);
		DDouble(v, pos): BDouble(v);
		DString(v, pos): BString(v);
		DStringInclude(path, pos): {
			BString(getFileContent(path));
		}
		DVar(id, pos, eclass): {
			BVar(id, type(eclass));
		}
		DLet(id, value, body, pos, eclass): BLet(id, rec(value), rec(body), type(eclass));
		DIf(cond, then, else_, pos, eclass): BIf(rec(cond), rec(then), rec(else_), type(eclass));
		DCall(fn, args, pos, eclass): {
			BCall(rec(fn), map(args, rec), type(eclass))
		}
		DConstruct(structid, args, pos, eclass): {
			BConstruct(structid, map(args, rec), type(eclass))
		}
		DLambda(args, body, pos, eclass): {
			BLambda(map(args, \a -> a.id), rec(body), type(eclass));
		}
		DRef(value, pos, eclass): {
			typ = type(eclass);
			// The type of __ref is not right, but should not matter. It is really (typar_0_of(typ)) -> typ
			BCall(BVar("__ref", typ), [rec(value)], typ);
		}
		DField(value, field, pos, eclass): BField(rec(value), field, type(eclass));	
		DSetMutable(lhs, field, value, pos, eclass): {
			BSetMutable(rec(lhs), field, rec(value), type(eclass))
		}
		DArray(exps, pos, eclass): {
			typ = type(eclass);
			// The type of array is not right, but should not matter. It is really (typar_0_of(typ)) -> typ
			BArray(map(exps, rec), typ);
		}
		DSequence(exps, pos, eclass): BSequence(map(exps, rec), type(eclass));
		DSwitch(value, stype, cases, defaultCase, pos, eclass): {
			swtype = type(value.eclass);
			BSwitch(
				BVar(value.id, swtype), swtype,
				map(cases, \case : DCase -> {
					BCase(case.id, rec(case.body))
				}),
				defaultCase ?? {
					Some(rec(defaultCase))
				} : None(),
				type(eclass)
			);
		}
		DCast(value, target, pos): {
			// We probably do not need this in the backends. It is purely a type checking thing
			rec(value);
		}
		DDowncast(oldid, newid, subtype, supereclass, scope, pos, eclass): {
			BLet(
				newid, BVar(oldid, type(supereclass)),
				rec(scope),
				type(eclass)
			);
		}
		DTyped(value, subtype, pos, eclass): rec(value);
	}
}

ttype2btype(env : BExpEnv, type : TType) -> BType {
	switch (type) {
		TTypeName(id, typars): {
			if (trim2(id, "?") == "" && typars == []) {
				BTypePar(id);
			} else {
				BTypeName(id, map(typars, \tp -> ttype2btype(env, tp)));
			}
		}
		TTypeEClass(eclass): {
			// println("ERROR: Tyvar");
			BTypeName("bool", []);
		}
		TTypeFunction(args, returnType): {
			BTypeFunction(map(args, \a -> ttype2btype(env, a)), ttype2btype(env, returnType));
		}
		TTypeOverload(tt, overloads): {
			println("ERROR: Overload");
			BTypeName("ERROR overload " + toString(tt), []);
		}
		TTypeSupertype(__, subtypes, maxed): {
			println("ERROR: Supertype");
			BTypeName("ERROR supertype", []);
		}
	}
}
