import tools/flow9/backends/link;
import tools/flow9/backends/back_eval;
import tools/flow9/backends/pretty;
import tools/flow9/backends/cpp/cpp_backend;
import tools/flow9/backends/type_hierarchy;
import tools/flow9/backends/utils;
import tools/flow9/backends/struct_ids;

/*

flowcpp flow9\flow9.flow -- file=demos/euler/euler1.flow cpp=euler1.cpp
g++ -std=c++2a euler1.cpp 2>out.flow
-I for path to flow9
g++ -std=c++2a /mnt/d/a9/flow9/demos/euler/euler8.cpp -o euler8 -I/mnt/d/a9/flow9/
*/

// Known issues:
//
// 1) Aliases
// Example :
// 1: Struct* a = new Struct();
// 2: Struct* b = a;
// -> drop(a); drop(b); If we miss the additional dup(a), there will be an attempt to double free the memory.
//
// 2) Struct inside Struct
// Example :
// 1: Struct* a = new Struct();
// 2: Struct2* b = new Struct2(a);
// -> drop(a); drop(b); If we miss the additional dup(a), there will be 1)wrong data in b; 2) an attempt to double free the memory.

export {
	// Compiles the given flow path to C++ as a single string
	bmodule2cpp(cache : FlowCache, flowpath : string, shortenIds : bool) -> string;
}

bmodule2cpp(cache : FlowCache, flowpath : string, shortenIds : bool) -> string {
	bprogram2cpp(linkBProgram(cache, flowpath, shortenIds, cppBackendSpec()), flowpath);
}

debugFlowMode = false;

bprogram2cpp(b : BProgram, flowpath : string) -> string {
	typeHierarchy = resolveBTypeHierarchy(b.structs, b.unions);
	unionTypes = flatUnionTypes(typeHierarchy.unionTypes);
	unionedStructs = splitUnionedStructs(unionTypes, typeHierarchy.unionedStructs, b.structs, b.unions);
	if (debugFlowMode) println(toString(b) + "\n");

	info = CppInfo(
		fold(b.declarations, makeSet(), \acc, d -> {
			insertSet(acc, d.id)
		}),
		makeStructIds(b.structs, b.unions),
		unionTypes,
		getStatFunctionTypes(b.declarations),
		values2treeEx(unionedStructs.second, \struct -> struct.struct.id, recursiveDStruct2recursiveStruct),
		makeUsedStructTypars(b.structs)
	);

	backend = cppBackend(info);
	// Structs in unions
	simpleUnionStructs = fold(unionedStructs.first, "", \acc, struct -> {
		acc + dstruct2cpp(backend, struct) + "\n"
	});
	unionStructs = fold(unionedStructs.second, simpleUnionStructs, \acc, struct -> {
		acc + recursiveDstruct2cpp(backend, struct, false) + "\n"
	});
	recursiveStructs = fold(unionedStructs.second, "", \acc, struct -> {
		acc + recursiveDstruct2cpp(backend, struct, true) + "\n"
	});
	methodRecursiveStructs = fold(unionedStructs.second, "", \acc, struct -> {
		acc 
		+ makeStructConstructors(backend, struct, true) + "\n" 
		+ makeStructDestructor(backend, struct, true) + "\n"
		+ makeStructOperators(backend, struct) + "\n"
	});
	unions = superglue(typeHierarchy.unions, \u : DUnion -> dunion2cpp(backend, u, info.unionTypes), "\n");
	structs = superglue(typeHierarchy.structs, \s : DStruct -> dstruct2cpp(backend, s), "\n");
	functions = map(b.declarations, \d : BDeclaration -> bdeclaration2cpp(backend, d));
	body = superglue(functions, firstOfPair, "\n");
	declarations = superglue(filter(functions, \p -> p.second != ""), secondOfPair, ";\n");

	// The backend keeps track of what requirements are used by the various
	// constructs used
	strReplace(
		"#inc" + "lude <cstdint>\n#include <functional>\n#include <iostream>\n#include <memory>\n#include <type_traits>\n"
		// TODO: fix in the future path to flow9
		+ "#inc" + "lude \"" + (if (memoryManagement) "tools/flow9/backends/cpp/flow_type.hpp\" \n" else "tools/flow9/backends/cpp/flow_type_old.hpp\" \n")
		+ getBackendRequirements(backend) + "\n"
	//	+ "// RUNTIME\n"
	//	+ runtime + "\n"
		+ "// Structs for unions\n"
		+ unionStructs
		+ "// UNIONS\n"
		+ unions + "\n"
		+ "// RECURSIVE STRUCTURES\n"
		+ recursiveStructs + "\n"
		+ "// METHODS OF RECURSIVE STRUCTURES\n"
		+ methodRecursiveStructs + "\n"
		+ "// STRUCTS\n"
		+ structs + "\n"
		+ "// DECLARATIONS\n"
		+ declarations + ";\n"
		+ "// BODY\n"
		+ body
		// TODO: If flow_main returns int, do something else
		+ "int main() { flow_main(); return 0; }",
		"\t",
		"  "
	)
}

// TODO: filter fns in the correct order ?
// result : body, declaration
bdeclaration2cpp(backend : Backend<CppInfo>, d : BDeclaration) -> Pair<string, string> {
	// TODO: calculate form 'flow_natives.hpp' or cppBackendSpec()
	implementedNatives = getImplementedNatives();
	switch (d) {
		BGlobalVar(id, body, type): {
			Pair(btype2cpp(backend, type) + " " + id + " = " + bexp2cpp(backend, body, true) + ";", "");
		}
		BStatFunction(id, typars, args, body, tailCall, type): {
			if (id == "flow_i2s") {
				declaration = "std::u16string flow_i2s(int32_t flow_i)";
				Pair(
					blueprint(
						"
					%declaration% {
					  std::wstring_convert<std::codecvt_utf8_utf16<char16_t>,char16_t> convert;
					  return convert.from_bytes(std::to_string(flow_i));
					}
				",
						[ "declaration", declaration,]
					),
					declaration
				)
			} else if (id == "flow_i2d") {
				declaration = "double flow_i2d(int32_t flow_i)";
				Pair(
					blueprint(
						"
					%declaration% {
						return double(flow_i);
					}
				",
						[ "declaration", declaration,]
					),
					declaration
				)
			} else {
				polyvars = uniq(map(typars, \pv -> btype2cpp(backend, BTypePar(pv))));
				argtypes = getBArgTypes(type);
				breturnType = getBReturnType(type);
				isSimpleReturnType = isSimpleBType(breturnType) || !memoryManagement;
				returnType = btype2cpp(backend, breturnType);

				declaration = blueprint(
					"%polyvars%%returnType% %id%(%args%)",
					[
						"polyvars", {
							if (polyvars == []) ""
							else "template <" + superglue(polyvars, \pv -> "typename " + pv, ", ") + "> "
						},
						"id", id,
						// TODO: if (tailCall) byValue (as it is now) else byRef (add &)
						"args", supergluei(args, \i, arg -> {
							argType = argtypes[i];
							btype2cpp(backend, argType) +(if (!isSimpleBType(argType) && memoryManagement) "* " else " ") + arg
						}, ", "),
						"returnType", returnType + (if (isSimpleReturnType) "" else "*"),
					]
				);

				argIds = buildSet(args);
				fnBody = if (tailCall) {
					tailCallBody2cpp(backend, body, returnType, isSimpleReturnType, argIds);
				} else {
					bstatement2cpp(backend, body, argIds, true);
				}

				Pair(
					blueprint(
						"
					%declaration% {
						%body%
					}
				",
						[
							"declaration", declaration,
							"body", fnBody,
						]
					),
					declaration
				)
			}
		}
		BExpNative(id, isio, type, nativeName): {
			if (containsSet(implementedNatives, id)) {
				Pair("", "")
			} else {
				Pair("// TODO: Native " + id, "");
			}
		}
	}
}

tailCallBody2cpp(backend : Backend<CppInfo>, body : BStatement, type : string, simpleType : bool, fnArgs : Set<string>) -> string {
	defaultReturn = if (type == bvoidType.id) {
		"";
	} else if (simpleType) {
		blueprint(
			"%type% _defReturn;
return _defReturn;",
			["type", type,]
		);
	} else {
		blueprint(
			"%type%* _defReturn;
return *_defReturn;",
			["type", type,]
		);
	}

	recursiveBody = bstatement2cpp(backend, body, fnArgs, true);
	if (recursiveBody == "") {
		defaultReturn
	} else {
		blueprint(
			"bool _end;
do {
	_end = true;
	%body%
} while (!_end);
%return%",
			[
				"body", recursiveBody,
				"return", defaultReturn,
			]
		)
	}
}

bstatement2cpp(backend : Backend<CppInfo>, s : BStatement, fnArgs : Set<string>, withReturn : bool) -> string {
	isLocalVar = \val: BExp -> {
		isBConstruct(val)
		|| {
			varId = getBVarId(val);
			varId != "" && !containsSet(fnArgs, varId)
		}
	}
	todo = \ -> {
		println("TODO C++ statement: " + toString(s));
		toString(s)
	}
	rec = \ss -> bstatement2cpp(backend, ss, fnArgs, withReturn);
	rece = \ee -> bexp2cpp(backend, ee, false);
	switch (s) {
		BIfStatement(cond, then, else_): {
			blueprint("
					if (%c%) {
						%then%
					} else {
						%else%
					}",
				[
					"c", rece(cond),
					"then", rec(then),
					"else", rec(else_),
			]);

		}
		BLetStatement(id, value, body): {
			valueType = getBType(value);
			varType = if (memoryManagement && !isSimpleBType(valueType)) (btype2cpp(backend, valueType) + "*") else "auto";
			varType + " " + id + " = " + bexp2cpp(backend, value, false) + ";\n"
			+ (if (memoryManagement) ("std::cout<<\"FN : &" + id + " = \"<< &" + id + " << \" : value=\" << " + id + " <<std::endl;\n") else "")
			+ bstatement2cpp(backend, body, fnArgs, withReturn);
		}
		BSetMutableStatement(left, field, value): todo();
		BReturnVoid(): "return;";
		BReturn(val): {
			// TODO: replace reuse with drop , extract real value (isBConstruct)
			type = getBType(val);
			value = bexp2cpp(backend, val, false) + ";";
			if (type == bvoidType || !withReturn) {
				value
			} else if (isSimpleBType(type) || !isLocalVar(val) && !memoryManagement) {
				"return " + value;
			} else {
				// TODO: think about. this is not recommended
				//    Effective C++, Scott Meyers:
				//    Never return a pointer or a reference to a local stack object or a reference to a heap-allocated object
				if (memoryManagement) {
					"return " + value;
				} else {
					if (isBConstruct(val) || isBCast(val)) {
						blueprint(
							"auto* _tmp_return = new %value%
"
							+ "return *_tmp_return;",
							["value", value,]
						);
					} else {
						blueprint(
							"auto* _tmp_return = &%value%;
	"
							+ "return *_tmp_return;",
							["value", value,]
						);
					}
				}
			}
		}
		BTailCall(fn, args, values, type): {
			"_end = false;\n"
			+ supergluei(args, \i, arg -> "auto __" + arg + " = " + rece(values[i]), ";\n") + ";\n"
			+ supergluei(args, \i, arg -> arg + " = reuse(" + "__" + arg + ")", ";\n") + ";"
		}
		BSequenceStatement(statements): {
			blueprint("
					{
						%exps%
					}",
				[
					"exps", supergluei(statements, \i, v -> bstatement2cpp(backend, v, fnArgs, i == length(statements) - 1), "\n"),
				]
			);

		}
		BSwitchStatement(expr, cases, type): {
			switchType = getBType(expr);
			caseIndexes = switchType2caseIndexes(switchType, backend);
			// cases should not be empty. otherwise let there be a crash.
			// cpp needs a default case in the 'return' case
			defaultCase = cases[0].body;
			blueprint("
					switch (%value%.index()) {
						%cases%
						%default%
					}",
				[
					"value", if (memoryManagement) ("(*" + rece(expr) + ")") else rece(expr),
					"cases", superglue(cases, \case : BCaseStatement -> {
						blueprint("
							case %id%: {
								%body%
								break;
							}",
							[
								"id", eitherMap(lookupTree(caseIndexes, case.id), i2s, ""),
								"body", rec(case.body),
							]);
					}, ";\n"),
					"default", "default: {" + (if (type == bvoidType) "" else rec(defaultCase)) + ";break;}",
			]);
		}
	}
}


bexp2cpp(backend : Backend<CppInfo>, bexp : BExp, isGlobalExp : bool) -> string {
	rec = \ee -> bexp2cpp(backend, ee, false);
	switch (bexp) {
		BVoid(): "";
		BBool(v): if (v) "true" else "false";
		BInt(v): "int32_t(" + i2s(v) + ")";
		BDouble(v): double2string(v);
		BString(v): "std::u16string(u" + toString(v) + ")";
		BVar(id, type): bvar2cpp(bexp, backend);
		BLet(id, value, body, type): {
			// TODO: don't create a lambda if BLet(BLet( ... BLet(BIf))) // ref_counting.flow
			blueprint("
				[%capture%]() {
					auto %id% = %value%;
					%debug%
					return %body%;
				}()", [
				"capture", if (isGlobalExp) "" else "&",
				"id", id,
				"value", rec(value),
				"body", rec(body),
				"debug", if (memoryManagement) ("std::cout<<\"FN : &" + id + " = \"<< &" + id + " <<std::endl;") else "",
			])
		}
		BIf(cond, then, else_, type): {
			blueprint("
					(%c%) ? (
						%then%
					) : (
						%else%
					)",
				[
					"c", rec(cond),
					"then", rec(then),
					"else", rec(else_),
			]);
		}
		BCall(fn, args, type): {
			unpair(createTmpVars(backend, args, isGlobalExp), \newStatements, newArgs -> {
				createdTmpVars = newStatements != [];
				newBCall = BCall(fn, newArgs, type);
				// TODO: find a better solution!!
				// and we only need lambda for BLetStatement
				// TODO: drop tmp vars
				if (createdTmpVars) {
					blueprint(" ([%capture%]() {
	%tmpVars%return %body%;
})()",
					[
						"capture", if (isGlobalExp) "" else "&",
						"tmpVars", concatStrings(newStatements),
						"body", produceBackendCall(backend, newBCall, \newBackend, newExp -> bexp2cpp(newBackend, newExp, isGlobalExp)),
				]);
				} else {
					produceBackendCall(backend, newBCall, \newBackend, newExp -> bexp2cpp(newBackend, newExp, isGlobalExp))
				}
				// it works with linux, but not with VS
				/*(if (createdTmpVars) "({\n" else "")
				+ concatStrings(newStatements)
				+ produceBackendCall(backend, BCall(fn, newArgs, type), \newBackend, newExp -> bexp2cpp(newBackend, newExp, isGlobalExp))
				+ (if (createdTmpVars) ";\n})" else "")*/
			});
		}
		BLambda(args, body, type): {
			argtypes = getBArgTypes(type);
			returnBType = getBReturnType(type);
			returnType = btype2cpp(backend, returnBType);
			blueprint("
					%type%([%capture%](%args%)%returnType% {
						%return% %body%;
					})",
				[
					"capture", if (isGlobalExp) "" else "&",
					"args", supergluei(args, \i, arg -> {
						btype2cpp(backend, argtypes[i]) + (if (!isSimpleBType(argtypes[i]) && memoryManagement) "& " else " ") + arg
					}, ", "),
					"body", rec(body),
					"type", btype2cpp(backend, type),
					"return", if (returnType == bvoidType.id) "" else "return",
					"returnType", if (returnType == bvoidType.id) "" else (" -> " + returnType + (if (!isSimpleBType(returnBType) && memoryManagement) "& " else " ")),
			]);
		}
		BSequence(exps, type): {
			lastInd = length(exps) - 1;
			blueprint("
					(%exps%)",
				[
					"exps", superglue(exps, rec, ", "),
			]);
		}
		BConstruct(struct, args, type): {
			if (memoryManagement) {
				blueprint(
					"new %struct%{%args%}",
					[
						"struct", if (args == []) struct else addTyparsToStruct(backend, type, struct),
						"args", superglue(args, rec, ", "),
					]
				);
			} else {
				blueprint(
					"(struct %struct%){%args%}",
					[
						"struct", if (args == []) struct else addTyparsToStruct(backend, type, struct),
						"args", superglue(args, rec, ", "),
					]
				);
			}
		}
		BField(value, field, type): {
			structType = getBType(value);
			isSimpleStructType = memoryManagement && !isSimpleBType(structType);
			isRecursiveField = eitherMap(
				lookupTree(backend.info.recursiveStructs, bType2id(structType)),
				\s -> containsSet(s.recursiveFields, field),
				false
			);
			blueprint("
				%ptr%%value%.%field%",
				[
					"value", rec(value) + (if (isSimpleStructType) ")" else ""),
					"field", field,
					"ptr",  (if (isSimpleStructType) "(*" else "") + (if (isRecursiveField && !memoryManagement) "*" else "")
			]);
		}
		BSetMutable(left, field, value, type): "TODO-set-mutable(" + rec(left) + "." + field + ", " + rec(value);
		BSwitch(value, switchType, allCases, defaultCaseM, type): makeBSwitchType(bexp, backend);
		BArray(args, type): {
			if (memoryManagement) {
				blueprint("new %type%({ %args% })",
					[
						"type", btype2cpp(backend, type),
						"args", superglue(args, rec, ", "),
				]);
				} else {
					blueprint("%type%({ %args% })",
						[
							"type", btype2cpp(backend, type),
							"args", superglue(args, rec, ", "),
					]);
				}
		}
		BCast(value, from, to): castBexp2cpp(backend, bexp);
		BDupValue(value) : bCounter2cpp(backend, bexp);
		BDropValue(value) : bCounter2cpp(backend, bexp);
		BReuseValue(value) : bCounter2cpp(backend, bexp);
	}
}

dunion2cpp(backend : Backend<CppInfo>, u : DUnion, unionTypes : Tree<string, [BType]>,) -> string {
	ptr = if (memoryManagement) "*" else "";
	addTemplatesToStruct(
		dtypes2parameters(mapConcat(u.types, \t -> t.typars)),
		if (memoryManagement) {
			blueprint("
				using %name% = _FlowUnion<%members%>;
				std::ostream& operator<<(std::ostream& os, const %name%& a){
					\tos << \"%name% [union type]\";
					\treturn os;
				}",
				[
					"name", u.id,
					"members", superglue(lookupTreeDef(unionTypes, u.id, []), \t : BType -> btype2cpp(backend, t) + ptr, ", ")
				]
			)
		} else {
			blueprint("
				using %name% = std::variant<%members%>;", [
					"name", u.id,
					"members", superglue(lookupTreeDef(unionTypes, u.id, []), \t : BType -> btype2cpp(backend, t) + ptr, ", ")
				]
			)
		}
	);
}

dstruct2cpp(backend : Backend<CppInfo>, s : DStruct) -> string {
	argTypes = lookupTreeDef(backend.info.usedTypars, s.id, []);
	lastInd = length(s.structArgs) - 1;
	operatorPrint = addTemplatesToStruct(
		argTypes,
		blueprint(
			"std::ostream& operator<<(std::ostream& os, const %type%%typar%& a){
	os << \"%type%{\"; %fields% os <<\"}\";
	return os;
}\n",
			[
				"type", s.id,
				"typar", makeTyparsForConstructorHeader(argTypes),
				"fields", supergluei(
					s.structArgs,
					\i, a ->  "flow_print2(a." + a.id + "); " + (if (i == lastInd) "" else "os<<\", \";"),
					""
				)
			]
		)
	);

	recStruct = RecursiveDStruct(s, map(s.structArgs, \__ ->false));

	dstruct2cppEx(
		backend,
		s,
		\__, type -> dtype2cpp(backend, type),
		blueprint(
			"
			bool operator==(const %type%& a) const {return %equalFields%;}
			bool operator!=(const %type%& a) const {return %nequalFields%;}
			bool operator>(const %type%& a) const {return %gFields%;}
			bool operator<(const %type%& a) const {return %lFields%;}
			bool operator>=(const %type%& a) const {return %geFields%;}
			bool operator<=(const %type%& a) const {return %leFields%;}
			%destructor%
			%constructors%\n",
			[
				"type", s.id,
				"equalFields", makeEqualStructArgsBody(backend, s.structArgs, []),
				"nequalFields", makeNotEqualStructArgsBody(backend, s.structArgs, []),
				"gFields", makeGreaterStructArgsBody(backend, s.structArgs, []),
				"lFields", makeLessStructArgsBody(backend, s.structArgs, []),
				"leFields", makeLessEqStructArgsBody(backend, s.structArgs, []),
				"geFields", makeGreaterEqStructArgsBody(backend, s.structArgs, []),
				"constructors", makeStructConstructors(backend, recStruct, false),
				"destructor", makeStructDestructor(backend, recStruct, false),
			]
		)
	) + "\n"
	+ operatorPrint
	+ makeStructConstructors(backend, recStruct, true)
	+ makeStructDestructor(backend, recStruct, true)
	+ "\n";
}

dstruct2cppEx(backend : Backend<CppInfo>, s : DStruct, argType2cpp : (int, DType) -> string, dstructFns : string) -> string {
	addTemplatesToStruct(
		lookupTreeDef(backend.info.usedTypars, s.id, []),
		"struct " + s.id + "{"
		+ supergluei(s.structArgs, \i, sa -> {
			argType2cpp(i, sa.type) + " " + sa.id + ";"
		}, "; ")
		+ dstruct2id(backend, s)
		+ dstruct2counter(backend, s) + "\n"
		+ dstructFns
		+ dstruct2DupDropFields(s)
		+ "};\n"
	);
}

recursiveDstruct2cpp(backend : Backend<CppInfo>, s : RecursiveDStruct, withBody : bool) -> string {
	if (withBody) {
		overloadedOperators = blueprint(
			"%type%& operator=(const %type%& a);
	bool operator==(const %type%& a) const;
	bool operator!=(const %type%& a) const;
	bool operator>(const %type%& a) const;
	bool operator<(const %type%& a) const;
	bool operator>=(const %type%& a) const;
	bool operator<=(const %type%& a) const;
	",
			["type", s.struct.id,]
		);
		dstruct2cppEx(
			backend,
			s.struct,
			\ind, type -> {
				isRecursiveField = elementAt(s.recursiveArgs, ind, false);
				cppType = dtype2cpp(backend, type);
				if (isRecursiveField && !memoryManagement) "std::unique_ptr<" + cppType + ">" else cppType;
			},
			makeStructConstructors(backend, s, false) + "\n	"
			+ makeStructDestructor(backend, s, false) + "\n"
			+ overloadedOperators + "\n"
		);
	} else {
		addTemplatesToStruct(
			lookupTreeDef(backend.info.usedTypars, s.struct.id, []),
			"struct " + s.struct.id + ";"
		);
	}
}

addTemplatesToStruct(types : [string], body : string) -> string {
	templates = makeStructTemplates(types);
	if (templates == "") body else templates + "\n" + body;
}

dtypes2parameters(types : [DType]) -> [string] {
	uniq(filtermap(extractDTypes(types, []), \type -> {
		parametr = bTypePar2cpp(dtype2btype(type));
		if (parametr == "") None() else Some(parametr) 
	}));
}

makeStructTemplates(parameters : [string]) -> string {
	if (parameters == []) {
		""
	} else {
		blueprint(
			"template <typename %types%>",
			["types", strGlue(parameters, ", typename ")]
		);
	}
}

extractDTypes(types : [DType], acc : [DType]) -> [DType] {
	if (types == []) {
		acc
	} else {
		children = mapConcat(types, \type -> 
			switch (type : DType) {
				DTypeName(__, typars, __): typars;
				DTypePar(__, __) : [];
				DTypeFunction(args, returnType, __): arrayPush(args, returnType);
			}
		);
		extractDTypes(children, concat(acc, types));
	}
}
addTyparsToStruct(backend : Backend<CppInfo>, type : BType, body : string) -> string {
	switch (type : BType) {
		BTypePar(id) : body;
		BTypeName(id, typars) : {
			if (typars == []) {
				body
			} else {
				blueprint(
					"%struct%<%types%>",
					[
						"struct", body,
						"types", superglue(typars, \tp -> btype2cpp(backend, tp), ", ")
					]
				);
			}
		}
		BTypeFunction(args, returnType) : body;
	}
}

dtype2cpp(backend : Backend<CppInfo>, type : DType) -> string {
	btype = dtype2btype(type);
	btype2cpp(backend, btype)
	+ (if (isSimpleBType(btype)) "" else "*");
}

btype2cpp(backend : Backend<CppInfo>, btype : BType) -> string {
	btype2cppEx(backend, btype, \type -> typars2cpp(backend, type.typars));
}

typars2cpp(backend : Backend<CppInfo>, typars : [BType]) -> string {
	if (typars != []) {
		"<" + superglue(typars, \tp -> btype2cpp(backend, tp), ", ") + ">"
	} else {
		""
	}
}

btype2cppEx(backend : Backend<CppInfo>, btype : BType, structTypars2cpp : (BTypeName) -> string) -> string {
	switch (btype) {
		BTypePar(id): bTypeParId2cpp(id);
		BTypeName(id, typars): {
			if (id == "void") "void"
			else if (id == "bool" || id == "double") id
			else if (id == "int") { "int32_t" }
			else if (id == "string") { "std::u16string" }
			else if (id == "flow") "_FlowType"
			else if (id == "array") {
				if (memoryManagement) {
					"_FlowArray<" + btype2cpp(backend, typars[0]) + (if (isSimpleBType(typars[0])) "" else "*") + ">"
				} else {
					"std::vector<" + btype2cpp(backend, typars[0]) + ">"
				}
			} else if (id == "ref") {
				blueprint(
					"std::shared_ptr<%valueType%>",
					["valueType", btype2cpp(backend, typars[0]),]
				);
			} else if (trim2(id, "?") == "") {
				btype2cpp(backend, BTypePar(id));
			} else {
				id + structTypars2cpp(btype)
			}
		}
		BTypeFunction(args, returnType): {
			fnType2cpp = \type -> {
				btype2cpp(backend, type)
				+ (if (!isSimpleBType(type) && memoryManagement) "&" else "")
			}
			"std::function<" + fnType2cpp(returnType)
			+ "(" + superglue(args, fnType2cpp, ", ") + ")>"
		}
	}
}

dstruct2id(backend : Backend<CppInfo>, s : DStruct) -> string {
	eitherMap(
		lookupTree(backend.info.structIds, s.id),
		\id -> " static const unsigned int _id = " + i2s(id) + ";",
		""
	);
}

dstruct2counter(backend : Backend<CppInfo>, s : DStruct) -> string {
	if (memoryManagement) " int _counter = 1;"
	else "";
}

dstruct2DupDropFields(s : DStruct) -> string {
	if (memoryManagement) {
		makeFn = \name, sa -> if (isSimpleBType(dtype2btype(sa.type))) "" else ("::" + name + "(" + sa.id + ");");
		blueprint(
			"void dupFields() {%dupFields%}
			void dropFields() {%dropFields%}
",
			[
				"type", s.id,
				"dupFields", superglue(s.structArgs, \sa -> makeFn("dup", sa), " "),
				"dropFields", superglue(s.structArgs, \sa -> makeFn("drop", sa), " "),
			]
		)
	} else {
		""
	}
}

makeBSwitchType(bexp : BSwitch, backend : Backend<CppInfo>) -> string {
	rec = \ee -> bexp2cpp(backend, ee, false);
	len = length(bexp.cases);
	expDefaultCase = bexp.defaultCase;

	if (len < 1) { // error
		""
	} else if (len < 6) { // if
		caseIndexes = switchType2caseIndexes(bexp.switchType, backend);
		// cases should not be empty. otherwise let there be a crash.
		defaultCase = expDefaultCase ?? expDefaultCase : bexp.cases[0].body;
		cases = expDefaultCase ?? bexp.cases : tail(bexp.cases);
		blueprint("
				%cases%(
					%default%
				)",
			[
				"cases", superglue(cases, \case -> {
					blueprint("
						(%value%.index() == %id%) ? (
							%body%
						) : ",
						[
							"value", if (memoryManagement) ("(*" + rec(bexp.value) + ")") else rec(bexp.value),
							"id", eitherMap(lookupTree(caseIndexes, case.id), i2s, ""),
							"body", rec(case.body)
						]
					);
				}, ""),
				"default", " " + rec(defaultCase),
		]);
	} else { // switch + lambda
		caseIndexes = switchType2caseIndexes(bexp.switchType, backend);
		// cases should not be empty. otherwise let there be a crash.
		// cpp needs a default case in the 'return' case
		defaultCase = expDefaultCase ?? expDefaultCase : bexp.cases[0].body;
		blueprint("
				[&](int32_t ind){switch (ind) {
					%cases%
					%default%
				}}(%value%.index())",
			[
				"value", if (memoryManagement) ("(*" + rec(bexp.value) + ")") else rec(bexp.value),
				"cases", superglue(bexp.cases, \case -> {
					blueprint("
						case %id%: {
							return %body%;
						}",
						[
							"id", eitherMap(lookupTree(caseIndexes, case.id), i2s, ""),
							"body", rec(case.body)
						]);
				}, ";\n"),
				"default", "default: return " + rec(defaultCase) + ";",
		]);
	}
}

switchType2caseIndexes(switchType : BType, backend : Backend<CppInfo>) -> Tree<string, int> {
	switch (switchType : BType) {
		BTypePar(__) : makeTree(); // error
		BTypeName(id, __) : {
			foldi(
				lookupTreeDef(backend.info.unionTypes, id, []),
				makeTree(),
				\i, acc, item : BType -> setTree(acc, bType2id(item), i)
			);
		}
		BTypeFunction(__, __) : makeTree(); // error
	}
}

// only for structs
castBexp2cpp(backend : Backend<CppInfo>, bcast : BCast) -> string {
	liftType = \value -> {
		 blueprint(
			if (memoryManagement) "new %type%{%value%}" else "(%type%)(%value%)",
			[
				"type", btype2cpp(backend, bcast.to),
				"value", value,
			]
		);
	}

	downType = \value -> {
		type = btype2cppEx(backend, bcast.to, \sType -> {
			usedTypars = lookupTreeDef(backend.info.usedTypars, sType.id, []);
			if (usedTypars == []) "" else typars2cpp(backend, sType.typars);
		});
		blueprint(
			"std::get<%type%>(%id%)",
			[
				"type", type + (if (memoryManagement) "*" else ""),
				"id", (if (memoryManagement) "*" else "") + value,
			]
		);
	}

	value = bexp2cpp(backend, bcast.value, false);
	switch (getBCastType(backend, bcast) : BCastType) {
		BCastUp() : liftType(value);
		BCastDown() : downType(value);
		BCastNone() : value;

	}
}

// only for structs
BCastType ::= BCastUp, BCastDown, BCastNone;
	BCastUp();
	BCastDown();
	BCastNone();

getBCastType(backend : Backend<CppInfo>, bcast : BCast) -> BCastType {
	getStructTypeId = \type : BType -> 
		switch (type : BType) {
			BTypePar(__) : "";
			BTypeName(typeId, __) : if (containsKeyTree(backend.info.structIds, typeId)) typeId else "";
			BTypeFunction(__, returnType) : "";
		}

	isSubStruct = \unionId : string, subStructId : string -> {
		exists(lookupTreeDef(backend.info.unionTypes, unionId, []), \t : BType -> bType2id(t) == subStructId);
	}

	fromId = getStructTypeId(bcast.from);
	toId =  getStructTypeId(bcast.to);

	if (fromId == "" || toId == "") {
		BCastNone();
	} else if (isSubStruct(fromId, toId)) {
		BCastDown();
	} else if (isSubStruct(toId, fromId)) {
		BCastUp();
	} else {
		BCastNone();
	}
}

bTypePar2cpp(btype : BType) -> string {
	switch (btype) {
		BTypePar(id): bTypeParId2cpp(id);
		default: "";
	}
}

bTypeParId2cpp(id : string) -> string {
	// In C++, we use a, b, c, ...
	len = strlen(id) - 1;
	fromCharCode(getCharCodeAt("A", 0) + len);
}

flatUnionTypes(unionTypes : Tree<string, [DTypeName]>) -> Tree<string, [BType]> {
	mapTree(unionTypes, \subTypes -> flatUnions(subTypes, unionTypes, []));
}

flatUnions(types : [DTypeName], unionTypes : Tree<string, [DTypeName]>, acc : [BType]) -> [BType] {
	if (types == []) {
		acc
	} else {
		splittedTypes = fold2(types, acc, [], \structTypes, unionSubTypes, type -> {
			eitherFn(
				lookupTree(unionTypes, dType2id(type)),
				\subTypes -> Pair(structTypes, concat(unionSubTypes, subTypes)),
				\-> Pair(arrayPush(structTypes, dtype2btype(type)), unionSubTypes)
			)
		});
		flatUnions(splittedTypes.second, unionTypes, splittedTypes.first);
	}
}

bType2id(type : BType) -> string {
	switch (type : BType) {
		BTypePar(id) : id;
		BTypeName(id, __) : id;
		BTypeFunction(__, __) : "";
	}
}

dType2id(type : DType) -> string {
	switch (type : DType) {
		DTypeName(id, __, __) : id;
		DTypePar(id, __) : id;
		DTypeFunction(__, __, __) : "";
	}
}

getStatFunctionTypes(declarations : [BDeclaration]) -> Tree<string, Pair<Maybe<[string]>, BType>> {
	nativesWhiteList = getPolimorpicNatives();
	fold(declarations, makeTree(), \acc, decl -> {
		switch (decl) {
			BGlobalVar(id, body, type): acc;
			BStatFunction(id, typars, args, body, tailCall, type): setTree(acc, id, Pair(Some(typars), type));
			BExpNative(id, isio, type, nativeName): if (containsSet(nativesWhiteList, id)) setTree(acc, id, Pair(None(), type)) else acc;
		}
	});
}

bvar2cpp(bexp : BVar, backend : Backend<CppInfo>) -> string {
	if (bexp.id == "__cast") {
		// From : __cast(flow_var)
		// To : flow_cast<TypeName>(flow_var)
		fromToType = switch (bexp.type : BType) {
			BTypePar(__) : Triple("", false, false);
			BTypeName(__, __) : Triple("", false, false);
			BTypeFunction(args, returnType) : Triple(
				btype2cpp(backend, returnType),
				containsKeyTree(backend.info.unionTypes, bType2id(returnType)),
				args != [] && containsKeyTree(backend.info.unionTypes, bType2id(args[0]))
			)
		}
		if (fromToType.first == "") {
			// something is wrong
			bexp.id
		} else {
			untriple(fromToType, \toType, isToUnion, isFromUnion -> blueprint(
				"%castFn%<%type%>",
				[
					"type", toType,
					"castFn", if (!isToUnion && isFromUnion) "flow_cast_variant" else "flow_cast",
				]
			))
		}
	} else {
		// add function argument types to help the с++ compiler with polymorphism
		// before : typeA2typeB
		// after : typeA2typeB<TypeA,TypeB>
		bexp.id + bVar2realFuncTypes(bexp, backend);
	}
}

// find the type of function arguments
bVar2realFuncTypes(var : BVar, backend : Backend<CppInfo>) -> string {
	switch (var.type : BType) {
		BTypePar(__) : "";
		BTypeName(__, __) : "";
		BTypeFunction(args, returnType) : {
			eitherMap(
				lookupTree(backend.info.statFunctions, var.id),
				unpairC(\polyvarsM, statFuncType -> {
					switch (statFuncType : BType) {
						BTypePar(__) : "";
						BTypeName(__, __) : "";
						BTypeFunction(sfArgs, sfReturnType) : {
							polyvars = getTreeValues(extractPolyTypes(makeTree(), BTypeFunction(args, returnType), statFuncType));
							if (polyvars == []) {
								""
							} else {
								"<" + strGlue(uniq(map(polyvars, \pv -> btype2cpp(backend, pv))), ", ") + "> "
							}
						}
					}
				}),
				""
			)
		}
	}
}

RecursiveDStruct(
	struct : DStruct,
	recursiveArgs : [bool], // len(recursiveArgs) = len(structArgs)
);

recursiveDStruct2recursiveStruct(struct : RecursiveDStruct) -> RecursiveStruct {
	RecursiveStruct(
		foldi(struct.struct.structArgs, makeSet(), \i, acc, arg -> {
			isRecursiveField = elementAt(struct.recursiveArgs, i, false);
			if (isRecursiveField) insertSet(acc, arg.id) else acc
			
		})
	)
}

splitUnionedStructs(unionTypes : Tree<string, [BType]>, unionedStructs : Tree<string, DStruct>, structs : Tree<string, DStruct>, unions : Tree<string, DUnion>,) -> Pair<[DStruct], [RecursiveDStruct]> {
	foldTree(unionedStructs, Pair([], []), \id, struct, acc -> {
		recFields = getRecursiveDStructureArgs(struct, structs, unions);
		if (exists(recFields, idfn)) {
			Pair(acc.first, arrayPush(acc.second, RecursiveDStruct(struct, recFields)))
		} else {
			Pair(arrayPush(acc.first, struct), acc.second)
		}
	});
}

getRecursiveDStructureArgs(
	struct : DStruct,
	allStructs : Tree<string, DStruct>,
	allUnions : Tree<string, DUnion>,
) -> [bool] {
	map(struct.structArgs, \arg -> 
		switch (arg.type : DType) {
			DTypeName(id, __, __) : {
				eitherFn(
					lookupTree(allStructs, id),
					\st -> isRecursiveDStructure(st.id, [st], [], allStructs, allUnions),
					\-> eitherMap(
						lookupTree(allUnions, id),
						\un -> isRecursiveDStructure(un.id, [], [un], allStructs, allUnions),
						false
					)
				)
			}
			DTypePar(id, __) : false;
			DTypeFunction(__, __, __) : false;
		}
	);
}

isRecursiveDStructure(
	structId : string,
	structs : [DStruct],
	unions : [DUnion],
	allStructs : Tree<string, DStruct>,
	allUnions : Tree<string, DUnion>,
) -> bool {
	structIds : [string] = mapConcat(structs, \struct -> filtermap(struct.structArgs, \arg -> 
		switch (arg.type : DType) {
			DTypeName(id, __, __) : Some(id);
			DTypePar(id, __) : None();
			DTypeFunction(__, __, __) : None();
		}
	));
	unionIds : [string] = mapConcat(unions, \union -> map(union.types, \arg -> arg.id));
	argIds = concat(structIds, unionIds);

	if (argIds == []) {
		false
	} else if (exists(argIds, \id -> id == structId)) {
		true
	} else {
		argStructs = filtermap(argIds, \id -> lookupTree(allStructs, id));
		argUnions = filtermap(argIds, \id -> lookupTree(allUnions, id));
		isRecursiveDStructure(structId, argStructs, argUnions, allStructs, allUnions)
	}
}

makeStructCopyConstructorBody(backend : Backend<CppInfo>, simpleArgs : [DStructArg], recArgs : [DStructArg]) -> string {
	blueprint(
			"%simpleTypes%
	%recTypes%",
			[
				"simpleTypes", strGlue(map(if (memoryManagement) concat(simpleArgs, recArgs) else simpleArgs, \arg -> 
					blueprint(
						"%field% = a.%field%;",
						["field", arg.id]
					)
				), "\n"),
				"recTypes", strGlue(map(if (memoryManagement) [] else recArgs, \arg -> 
					blueprint(
						"%field% = std::make_unique<%ptype%>(*a.%field%);",
						[
							"field", arg.id,
							"ptype", dtype2cpp(backend, arg.type)
						]
					)
				), "\n")
			]
		);
}

makeOperatorStructArgsBody(
	backend : Backend<CppInfo>,
	simpleArgs : [DStructArg],
	recArgs : [DStructArg],
	operator : string,
	function : string,
	anyField : bool, // true = OR, false = AND
) -> string {
	if (recArgs == [] && simpleArgs == []) {
		"true"
	} else {
		makeCompareValue = \id : string, withPrefix : bool -> {
			if (operator != "") {
				blueprint(
					"%prefix%%id% %operator% %prefix%a.%id%",
					[
						"id", id,
						"operator", operator,
						"prefix", if (withPrefix) "*" else "",
					]
				)
			} else {
				blueprint(
					"%fn%(%prefix%%id%, %prefix%a.%id%)",
					[
						"id", id,
						"fn", function,
						"prefix", if (withPrefix) "*" else "",
					]
				)
			}
		} 
		join = if (anyField) " || " else " && ";
		recursiveArgs = superglue(recArgs, \arg -> makeCompareValue(arg.id, true), join);
		superglue(simpleArgs, \arg -> makeCompareValue(arg.id, false), join)
		+ (if (recursiveArgs == "" || simpleArgs == []) "" else join)
		+ recursiveArgs;
	}
}

makeEqualStructArgsBody(backend : Backend<CppInfo>, simpleArgs : [DStructArg], recArgs : [DStructArg]) -> string {
	makeOperatorStructArgsBody(backend, simpleArgs, recArgs, "", "areValuesEqual", false);
}

makeNotEqualStructArgsBody(backend : Backend<CppInfo>, simpleArgs : [DStructArg], recArgs : [DStructArg]) -> string {
	makeOperatorStructArgsBody(backend, simpleArgs, recArgs, "!=", "", true);
}

makeGreaterStructArgsBody(backend : Backend<CppInfo>, simpleArgs : [DStructArg], recArgs : [DStructArg]) -> string {
	makeOperatorStructArgsBody(backend, simpleArgs, recArgs, ">", "", true);
}
makeLessStructArgsBody(backend : Backend<CppInfo>, simpleArgs : [DStructArg], recArgs : [DStructArg]) -> string {
	makeOperatorStructArgsBody(backend, simpleArgs, recArgs, "<", "", true);
}
makeLessEqStructArgsBody(backend : Backend<CppInfo>, simpleArgs : [DStructArg], recArgs : [DStructArg]) -> string {
	"(" + makeEqualStructArgsBody(backend, simpleArgs, recArgs)
	+ ") || (" + makeLessStructArgsBody(backend, simpleArgs, recArgs) + ")"
}
makeGreaterEqStructArgsBody(backend : Backend<CppInfo>, simpleArgs : [DStructArg], recArgs : [DStructArg]) -> string {
	"(" + makeEqualStructArgsBody(backend, simpleArgs, recArgs)
	+ ") || (" + makeGreaterStructArgsBody(backend, simpleArgs, recArgs) + ")"
}

makeTyparsForConstructorHeader(argTypes : [string]) -> string {
	typeParams = strGlue(argTypes, ", ");
	if (typeParams == "") "" else "<" + typeParams + ">";
}

// TODO
makeStructDestructor(backend : Backend<CppInfo>, struct : RecursiveDStruct, withBody : bool) -> string {
	if (memoryManagement) {
	if (withBody) {
		makeDropField = \field : DStructArg, simple : bool -> "::drop(" + (if (simple || memoryManagement) "" else "*") + field.id + ");";
		makeReleasePtr = \field : DStructArg -> if (memoryManagement) "" else ("delete " + field.id + ".release();");

		printField = \id, isRecursive -> {
			if (isRecursive && !memoryManagement) {
				"if (" + id + " != nullptr) flow_print2(*" + id + "); else std::cout <<\"DELETED\""
			} else {
				"flow_print2(" + id + ")"
			}
		}
		isRecursiveField = \i -> elementAt(struct.recursiveArgs, i, false);
		structArgs = unzipi(struct.struct.structArgs, \i, arg -> isRecursiveField(i));
		isNotSimpleArgType = \arg -> !isSimpleBType(dtype2btype(arg.type));
		simpleArgs = filter(structArgs.second, isNotSimpleArgType);
		recArgs = filter(structArgs.first, isNotSimpleArgType);

		blueprint(
			"%type%::~%type%(){\n"
			+ ( if (memoryManagement) "  std::cout << (_counter == 0 ? \"\" : \" !!ERROR!! \") << \" ~ destroy %type%(\"; %printFields%; std::cout<<\") counter=\"<<_counter<<\" &=\" << this <<\" ~ \"<<std::endl;\n" else "")
			+"%fields%
}",
			[
				"type", struct.struct.id,
				"printFields", supergluei(struct.struct.structArgs, \i, a -> printField(a.id, isRecursiveField(i)), "; "),
				"fields", superglue(simpleArgs, \f -> makeDropField(f, true), "\n")
						+ (if (simpleArgs == []) "" else "\n")
						+ superglue(recArgs, \f -> makeDropField(f, false) + "\n" + makeReleasePtr(f), "\n")
						+ (if (recArgs == []) "" else "\n")
				// TODO: free vectors, etc
			]
		);
	} else {
		blueprint(
			"~%type%();",
			[
				"type", struct.struct.id,
			]
		)
	}
	} else "";
}

makeStructConstructors(backend : Backend<CppInfo>, struct : RecursiveDStruct, withBody : bool) -> string {
	if (withBody) {
		args = unzipi(struct.struct.structArgs, \i, arg -> elementAt(struct.recursiveArgs, i, false));
		simpleArgs = args.second;
		recArgs = args.first;
		initDeclaration = makeStructInitCtorDeclaration(backend, struct);
		copyDeclaration = makeStructCopyCtorDeclaration(struct);
		argTypes = lookupTreeDef(backend.info.usedTypars, struct.struct.id, []);
		typars = makeTyparsForConstructorHeader(argTypes);
		
		copyConstructor = blueprint(
			"%type%%typars%::%declaration% {
	%body%
}",
			[
				"type", struct.struct.id,
				"typars", typars,
				"declaration", copyDeclaration,
				"body", makeStructCopyConstructorBody(backend, simpleArgs, recArgs)
			]
		);
		initConstructor = if (initDeclaration == "") {
			""
		} else {
			blueprint(
				"%type%%typars%::%declaration% {
	%simpleTypes%
	%recTypes%
}",
				[
					"type", struct.struct.id,
					"typars", typars,
					"declaration", initDeclaration,
					"simpleTypes", strGlue(map(if (memoryManagement) struct.struct.structArgs else simpleArgs, \arg -> 
						blueprint(
							"%field% = _%field%;",
							["field", arg.id]
						)
					), "\n"),
					"recTypes", strGlue(map(if (memoryManagement) [] else recArgs, \arg -> 
						blueprint(
							"%field% = std::make_unique<%ptype%>(_%field%);",
							[
								"field", arg.id,
								"ptype", dtype2cpp(backend, arg.type)
							]
						)
					), "\n")
				]
			);
		}

		addTemplatesToStruct(argTypes, initConstructor) + "\n" + addTemplatesToStruct(argTypes, copyConstructor) + "\n";
	} else {
		"\n	" + makeStructInitCtorDeclaration(backend, struct)
		+ ";\n	" + makeStructCopyCtorDeclaration(struct)
		+ ";\n	" + makeStructDefConstructor(struct)
		+ ";\n" ;
	}
}

makeStructOperators(backend : Backend<CppInfo>, s : RecursiveDStruct) -> string {
	argTypes = lookupTreeDef(backend.info.usedTypars, s.struct.id, []);
	splittedArgs = unzipi(s.struct.structArgs, \i, arg -> elementAt(s.recursiveArgs, i, false));
	typars = makeTyparsForConstructorHeader(argTypes);

	copyOp = blueprint(
			"%type%%typar%& %type%%typar%::operator=(const %type%& a) {
	if (this != &a) {
		%copyFields%
	}
	return *this;
}",
		[
			"type", s.struct.id,
			"typar", typars,
			"copyFields", unpair(splittedArgs, \recArgs, simpleArgs -> makeStructCopyConstructorBody(backend, simpleArgs, recArgs)),
		]
	);
	// TODO: order
	printOperator = blueprint(
		"std::ostream& operator<<(std::ostream& os, const %type%%typar%& a){
    os << \"%type%{\"; %fields% os <<\"}\";
    return os;
}",
		[
			"type", s.struct.id,
			"typar", typars,
			"fields", unpair(splittedArgs, \recArgs, simpleArgs -> 
				superglue(simpleArgs, \a -> "flow_print2(a." + a.id + ");", "")
				+ superglue(recArgs, \a -> "flow_print2(*a." + a.id + ");", "")
			)
		]
	);

	makeOperator = \op : string, fn -> blueprint(
			"bool %type%%typar%::operator%op%(const %type%& a) const {
  return %cmpFields%;
}",
		[
			"op", op,
			"type", s.struct.id,
			"typar", typars,
			"cmpFields", unpair(splittedArgs, \recArgs, simpleArgs -> fn(backend, simpleArgs, recArgs)),
		]
	);

    addTemplatesToStruct(argTypes, copyOp) + "\n"
    + addTemplatesToStruct(argTypes, makeOperator("==", makeEqualStructArgsBody)) + "\n"
    + addTemplatesToStruct(argTypes, makeOperator("!=", makeNotEqualStructArgsBody)) + "\n"
    + addTemplatesToStruct(argTypes, makeOperator(">", makeGreaterStructArgsBody)) + "\n"
    + addTemplatesToStruct(argTypes, makeOperator("<", makeLessStructArgsBody)) + "\n"
    + addTemplatesToStruct(argTypes, makeOperator("<=", makeLessEqStructArgsBody)) + "\n"
    + addTemplatesToStruct(argTypes, makeOperator(">=", makeGreaterEqStructArgsBody)) + "\n"
    + addTemplatesToStruct(argTypes, printOperator) + "\n";
}

makeStructInitCtorDeclaration(backend : Backend<CppInfo>, struct : RecursiveDStruct) -> string {
	if (struct.struct.structArgs == []) {
		// we have the default constructor
		""
	} else {
		blueprint(
			"%type%(%fields%)",
			[
				"type", struct.struct.id,
				"fields", superglue(
					struct.struct.structArgs,
					\arg -> blueprint(
						"%type% _%field%",
						[
							"field", arg.id,
							"type", dtype2cpp(backend, arg.type),
						]
					),
					", "
				),
			]
		);
	}
}

makeStructCopyCtorDeclaration(struct : RecursiveDStruct) -> string {
	blueprint(
		"%type%(const %type%& a)",
		[
			"type", struct.struct.id,
		]
	);
}

makeStructDefConstructor(struct : RecursiveDStruct) -> string {
	blueprint(
		"%type%() = default",
		["type", struct.struct.id,]
	);
}

// supertype (union) can have templates, but struct can't
// TreeEmpty has no types in the following example:
//     Tree<?, ??> ::= TreeNode<?, ??>, TreeEmpty;
makeUsedStructTypars(structs : Tree<string, DStruct>) -> Tree<string, [string]> {
	mapTree(structs, \struct -> dtypes2parameters(map(struct.structArgs, \arg -> arg.type)))
}

// TODO: 
isSimpleBType(type : BType) -> bool {
	type == bvoidType || type  == bboolType || type  == bintType || type  == bdoubleType || type  == bstringType;
}

// TODO: ignore simple types (remove all dup/drop/reuse) ?
bCounter2cpp(backend : Backend<CppInfo>, exp : BCounter) -> string {
	rec = \ee -> bexp2cpp(backend, ee, false);
	extractValueFromBDup = \e -> switch (e : BCounter) {
		BDupValue(value) : value;
		BDropValue(__) : exp;
		BReuseValue(__) : exp;
	}
	extractCastUpFromExp = \e -> switch (e : BExp) {
		BCast(__, __, __) : if (getBCastType(backend, e) == BCastUp()) Some(e) else None();
		default : None();
	}
	bcounter2fnName = \-> switch (exp : BCounter) {
		BDupValue(__) : "dup";
		BDropValue(__) : "drop";
		BReuseValue(__) : "reuse";
	}

	value = exp.value;
	type = getBReturnType(getBType(value));
	makeCppCounter = \-> blueprint(
		"%fn%(%val%)",
		[
			"fn", bcounter2fnName(),
			"val", rec(value),
		]
	);

	if (isSimpleBType(type) || !memoryManagement) {
		rec(value)
	// TODO: move to ref_counting ?
	// replace dup(liftType(val)) with liftType(dup(val)) // swap BCast and BDup
	} else if (memoryManagement) {
		newVal = extractValueFromBDup(exp);
		if (newVal == exp) {
			makeCppCounter()
		} else {
			eitherFn(
				extractCastUpFromExp(newVal),
				\bcast -> bexp2cpp(backend, BCast(BDupValue(bcast.value), bcast.from, bcast.to), false),
				makeCppCounter
			)
		}
	} else {
		makeCppCounter()
	}
}

// TODO: drop tmp vars! or replace this fn with ? (* with unique_ptr ?)
// Test case #1: refcnt12.flow : test5() 
// fn3(Struct(v), end)
// tmp_var = Struct(v)
// Test case #1: refcnt17.flow : test8() 
// useVars8 = \v, v2 -> v;
// a = useVars8(Struct(8), Struct(11));
// tmp_var_1 = Struct(8);
tmpVarCounter = ref 0;
createTmpVars(backend : Backend<CppInfo>, args : [BExp], isGlobalExp : bool) -> Pair<[string], [BExp]> {
	if (memoryManagement) {
	newArgs = map(args, \arg -> {
		if (isBLet(arg) || isBConstruct(arg)) {
			varName = "_tmp" + i2s(atomicRefIntAddition(tmpVarCounter, 1));
			tmpVar = blueprint(
				"auto %varName% = %exp%;\n"
				+ (if (memoryManagement) ("std::cout<<\"FN : &%varName% = \"<< &%varName% <<std::endl;\n") else ""),
				[
					"varName", varName,
					"exp", bexp2cpp(backend, arg, isGlobalExp),
				]
			);
			Pair(Some(tmpVar), BVar(varName, getBType(arg)));
		} else {
			Pair(None(), arg);
		}
	});

	Pair(filtermap(newArgs, firstOfPair), map(newArgs, secondOfPair));
	} else {
		Pair([], args)
	}
}

isBConstruct(exp : BExp) -> bool {
	switch (exp : BExp) {
		BConstruct(__, __, __) : true;
		BCast(value, __, __) : isBConstruct(value);
		default : false;
	}
}

isBLet(exp : BExp) -> bool {
	switch (exp : BExp) {
		BLet(__, __, __, __) : true;
		BCast(value, __, __) : isBLet(value);
		default : false;
	}
}

getBVarId(exp : BExp) -> string {
	switch (exp : BExp) {
		BVar(id, __) : id;
		BCast(value, __, __) : getBVarId(value);
		BReuseValue(value) : getBVarId(value);
		BDropValue(value) : getBVarId(value);
		BField(value, __, __) : getBVarId(value);
		default : "";
	}
}

isBCast(exp : BExp) -> bool {
	switch (exp : BExp) {
		BCast(value, __, __) : true;
		default : false;
	}
}
