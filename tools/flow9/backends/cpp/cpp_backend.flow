import tools/flow9/backends/backend;
import text/blueprint;
import tools/flow9/backends/cpp/cpp_natives;

export {
	CppInfo(
		globals : Set<string>,
		structIds : Tree<string, int>, // id - globalIndex
		unionTypes : Tree<string, [BType]>,
		statFunctions : Tree<string, Pair<Maybe<[string]>, BType>>, // id - (polyvars, type)
		recursiveStructs : Tree<string, RecursiveStruct>, // typeId - fieldId
		usedTypars : Tree<string, Pair<[string], [bool]>>, // structId - (used_typars, is_used)
	);
	cppBackend(info : CppInfo) -> Backend<CppInfo>;
	cppBackendSpec() -> BackendSpec;
	cppGenerators() -> [BackGen];

	RecursiveStruct(
		recursiveFields : Set<string>,
	);
}

cppBackend(info : CppInfo) -> Backend<CppInfo> {
	makeBackend(
		"cpp", 
	[
		// Keywords
		"and_eq", "And", "asm", "auto", "bitand", "bitor", "bool", "break", "case", "catch", 
		"char", "class", "compl", "const_cast", "const", "continue", "default", "delete", "do", 
		"double", "dynamic_cast", "else", "enum", "explicit", "export", "extern", "false", "float", 
		"for", "friend", "goto", "if", "inline", "int", "long", "mutable", "namespace", "new", 
		"not_eq", "not", "operator", "or_eq", "or", "private", "protected", "public", "register", 
		"reinterpret_cast", "return", "short", "signed", "sizeof", "static_cast", "static", "struct", 
		"switch", "template", "this", "throw", "true", "try", "typedef", "typeid", "typename", 
		"union", "unsigned", "using", "virtual", "void", "volatile", "wchar_t", "while", "xor_eq", 
		"xor", 	
	], 
		// generators
		cppGenerators(),
		info
	);
}

cppBackendSpec() -> BackendSpec {
	makeBackendSpec(
		cppBackend(CppInfo(makeSet(), makeTree(), makeTree(), makeTree(), makeTree(), makeTree())),
		"", "flow_", "",
		getNativeNames()
	)
}

cppGenerators() -> [BackGen] {
	[
		BackGen("||", bboolType, BackBinOp(" || ", 10, BackArg(0), BackArg(1))),
		BackGen("&&", bboolType, BackBinOp(" && ", 20, BackArg(0), BackArg(1))),
		BackGen("==", bvoidType, BackBinOp(" == ", 30, BackArg(0), BackArg(1))),
		BackGen("!=", bvoidType, BackBinOp(" != ", 30, BackArg(0), BackArg(1))),

		BackGen("<=", bvoidType, BackBinOp(" <= ", 35, BackArg(0), BackArg(1))),
		BackGen("<", bvoidType, BackBinOp(" < ", 35, BackArg(0), BackArg(1))),
		BackGen(">=", bvoidType, BackBinOp(" >= ", 35, BackArg(0), BackArg(1))),
		BackGen(">", bvoidType, BackBinOp(" > ", 35, BackArg(0), BackArg(1))),

		BackGen("+", bintType, BackBinOp(" + ", 40, BackArg(0), BackArg(1))),
		BackGen("+", bdoubleType, BackBinOp(" + ", 40, BackArg(0), BackArg(1))),
		BackGen("+", bstringType, BackCall("concatFlowStrings", [BackArg(0), BackArg(1)])),

		BackGen("-", bintType, BackBinOp(" - ", 40, BackArg(0), BackArg(1))),
		BackGen("-", bdoubleType, BackBinOp(" - ", 40, BackArg(0), BackArg(1))),

		BackGen("*", bintType, BackBinOp(" * ", 50, BackArg(0), BackArg(1))),
		BackGen("*", bdoubleType, BackBinOp(" * ", 50, BackArg(0), BackArg(1))),

		BackGen("/", bintType, BackBinOp(" / ", 50, BackArg(0), BackArg(1))),
		BackGen("/", bdoubleType, BackBinOp(" / ", 50, BackArg(0), BackArg(1))),

		BackGen("%", bintType, BackBinOp(" % ", 50, BackArg(0), BackArg(1))),
		BackGen("%", bdoubleType, BackCall("std::fmod", [BackArg(0), BackArg(1)])),

		BackGen("__negate", BTypeFunction([bintType], bintType), BackPreOp(" -", 60, BackArg(0))),
		BackGen("__negate", BTypeFunction([bdoubleType], bdoubleType), BackPreOp(" -", 60, BackArg(0))),
		BackGen("__not", bvoidType, BackPreOp(" !", 60, BackArg(0))),

		BackGen("__ref", bvoidType, BackCall("makeFlowRef", [BackArg(0)])),
		BackGen("__deref", bvoidType, BackConcat([BackText("("), BackCall("*", [BackArg(0)]), BackText(")"), ])),
		BackGen(":=", bvoidType, BackBinOp(" = ", 70, BackConcat([BackText("*"), BackArg(0)]), BackArg(1))),

		BackGen("__index", bvoidType, BackCall("getFlowArrayItem", [BackArg(0), BackArg(1)])),
	]
}