import tools/flow9/backends/js/js;
import tools/flow9/backends/cpp/cpp;
import tools/flow9/backends/flowc/flowc;
import tools/flow9/backends/flix/flix;
import tools/flow9/pexp/parse;
import tools/flow9/pexp/pretty;
import tools/flow9/dexp/desugar;
import tools/flow9/dexp/pretty;
import tools/flow9/driver;
import fs/filesystem;
import math/stringmath;
import tools/flowc/backends/build;

main() {
	test = getUrlParameter("test");
	file = if (test != "") {
		if (test == "1") "tools/flow9/tests/"
		else if (strContains(test, "/")) {
			test
		} else "tools/flow9/tests/" + test;
	} else "";

	config = switch (getCompilerConfig(fcCurrentWorkingDir())) {
		Failed(msg): {
			println(msg + "\nUsing a default config.");
			includes = strSplit(getUrlParameter("I"), ",");
			allincludes = uniq(filter(concat([".", getFlowDirectory() + "/lib", getFlowDirectory()], includes), neq("")));
			verbose = s2i(getUrlParameterDef("verbose", "0"));
			CompilerConfig(file, allincludes, [], verbose, JSCliParams_dummy, makeTree(), s2i(getThreadId()));
		}
		Some(config): {
			config;
		}
	}

	allincludes = config.includes;
	verbose = config.verbose > 0;

	errors = ref 0;
	t = timestamp();

	// OK, set up tracking of pending asynchronous stuff
	pending = ref 0;
	startPending = \ -> {
		p = ^pending;
		pending := p + 1;
	};
	completePending = \ec -> {
		p = ^pending;
		pending := p - 1;
		if (p <= 0) {
			println(d2st((timestamp() - t) / 1000.0, 2) + "s");
			quit(ec);
		} else {
			println("pending: " + i2s(^pending) + " (must be 0), done in " + d2st((timestamp() - t) / 1000.0, 2) + "s");
			quit(ec);
		}
	};

	// This is how we compile a file
	process_file = \src_file, ver, onDone : () -> void -> {
		compileFlowe(allincludes, startPending, completePending, errors, src_file, verbose || ver, onDone);
	};

	// If we do not have a file=<file>, but just <file>, find it
	files = filtermap(getAllUrlParametersArray(), \a -> if (a[1] == "") Some(a[0]) else None());
	if (length(files) > 1) {
		println("Only expected one file, not: " + strGlue(files, " & "));
	}
	filepar = getUrlParameterDef("file", file);
	thefile = if (filepar == "" && files != []) files[0] else filepar;

	if (thefile == "") {
		printFlowUsage();
		quit(1);
	} else if (!isDirectory(thefile)) {
		process_file(thefile, false, \ -> {
			completePending(0);
		});
	} else {
		files0 = if (isUrlParameterTrue("rec")) readDirectoryRecursively(thefile) else map(readDirectory(thefile), \f -> pathCombine(thefile, f));
		cfiles = sort(filter(files0, \f -> endsWith(f, ".flow")));
		applyAllSync(
			map(cfiles, \f -> {
				\onDone -> {
					process_file(f, true, onDone)
				}
			}),
			\ -> {
				completePending(0);
			}
		);
	};
}


// This is how we compile a file.
// TODO: Consolidate completePending and onDone
compileFlowe(allincludes : [string], startPending : () -> void, completePending : (int) -> void, errors : ref int, src_file : string, verbose : bool, onDone : () -> void) -> void {
	flow_file = path2flowPath(allincludes, src_file);
	if (verbose) {
		println("compiling file '" + src_file + "' (" + flow_file + ")");
	}
	cache = makeFlowCache(\e -> {
		errors := ^errors + 1;
		println(e);
	}, allincludes);

	b = compileFlow(cache, flow_file);

	promises = allP(concatA([
		if (isUrlParameterTrue("js")) [buildJsTarget(cache, b, src_file, STANDARD_JS)] else [],
		if (isUrlParameterTrue("es6")) [buildJsTarget(cache, b, src_file, STANDARD_ES6)] else [],
		if (isUrlParameterTrue("node")) [buildJsTarget(cache, b, src_file, NODEJS_MODE)] else [],
		if (isUrlParameterTrue("nw")) [buildJsTarget(cache, b, src_file, NWJS_MODE)] else [],
		if (isUrlParameterTrue("ts")) [buildJsTarget(cache, b, src_file, TS_MODE)] else [],
	]));
	doneP(promises, \ok -> {
		if (isUrlParameterTrue("cpp")) {
			buildCppTarget(cache, b, src_file);
		}
		if (isUrlParameterTrue("flix")) {
			buildFlixTarget(cache, b, src_file);
		}
		if (isUrlParameterTrue("flowc") || getUrlParameter("bytecode") != "") {
			mfiprogram = bmodule2fiprogram(cache, b.flowpath);
			switch (mfiprogram) {
				None(): {
					onDone();
				}
				Some(fiprogram): {
					buildFiProgram(fiprogram, initFcTypeEnvGlobal(), \done -> onDone());
				}
			}
		} else {
			onDone();
		}
	}, \error -> {
		println("Failed");
		onDone();
	})
}

buildJsTarget(cache : FlowCache, b : BModule, src_file : string, jsmode : int) -> Promise<string, string> {
	verbose = isUrlParameterTrue("verbose");
	run_tests = isUrlParameterTrue("run-tests");
	if (verbose) {
		println("building JS target");
	}

	thenP(
		bmodule2js(cache, b.flowpath, isUrlParameterTrue("shorten-ids"), jsmode), 
		\js_src -> {
		js_file = changeFileExt(src_file, ".js"); // TODO: If ts mode, use .ts
		setFileContent(js_file, js_src);
		if (verbose) {
			println("JS target '" + js_file + "' is built.");
		}
		if (run_tests) {
			if (verbose) {
				println("Running test '" + js_file + "'");
			}
			exit_code = execSystemProcess("node", [js_file], ".", println, println);
			if (exit_code != 0 || verbose) {
				println("Test '" + js_file + "' " +
					(if (exit_code == 0) "COMPLETED" else  "FAILED with code: " + i2s(exit_code))
				);
			}
			if (!isUrlParameterTrue("leave-js")) {
				del_err = deleteFile(js_file);
				if (del_err != "") {
					println(del_err);
				}
			}
		}
		if (verbose) {
			println("\n-------------------\n");
		};
		js_file
	});
}

buildCppTarget(cache : FlowCache, b : BModule, src_file : string) -> void {
	verbose = isUrlParameterTrue("verbose");
	if (verbose) {
		println("building Cpp target");
	}
	cpp_src = bmodule2cpp(cache, b.flowpath, isUrlParameterTrue("shorten-ids"));
	cppArg = getUrlParameter("cpp");
	cpp_file = if (strlen(cppArg) > 1) cppArg else changeFileExt(src_file, ".cpp");
	if (setFileContent(cpp_file, cpp_src)) {
		println("C++ target '" + cpp_file + "' is built.");
	} else {
		println("Error: Could NOT save '" + cpp_file + "'");
	}
	if (verbose) {
		println("\n-------------------\n");
	}
}

buildFlixTarget(cache : FlowCache, b : BModule, src_file : string) -> void {
	verbose = isUrlParameterTrue("verbose");
	if (verbose) {
		println("building Flix target");
	}
	flix_src = bmodule2flix(cache, b.flowpath, isUrlParameterTrue("shorten-ids"));
	flixArg = getUrlParameter("cpp");
	flix_file = if (strlen(flixArg) > 1) flixArg else changeFileExt(src_file, ".flix");
	if (setFileContent(flix_file, flix_src)) {
		println("Flix target '" + flix_file + "' is built.");
	} else {
		println("Error: Could NOT save '" + flix_file + "'");
	}
	if (verbose) {
		println("\n-------------------\n");
	}
}


printFlowUsage() -> void {
	printl2 = \s -> {
		if (isUrlParameterTrue("help")) {
			println(s);
		}
	}
	println("Usage:");
	println("flow9 <args>");
	printl2("java -jar flow9.jar -- <args>");
	println("");
	println("Args:");
	println("<file>                File to compile");
	println("file=<path>,<folder>  File (or folder) to compile");
	printl2("  rec=1               Compile all files recursively in the folder given");
	printl2("test=<file/folder>    Compile file(s) from tools/flow9/tests/<file>");
	println("");

	println("I=<path>,<path>       Include paths");
	println("js=1 or <path>        Compile to JS for web. Combine with flowc=1 to get full support");
	printl2("  shorten-ids=1       Whether to shorten the ids in the output");
	printl2("node=1 or <path>      Compile to JS for nodejs.");
	printl2("  run-tests=1         Execute the resulting JS with node");
	println("cpp=1 or <path>       Compile to C++");
	printl2("flowc=1 or <path>     Use the flowc backends for code generation. Java and more comes into play");
	println("");
	printl2("incremental=0         Turn off incremental compilation");
	printl2("debug=1,id,path       Debug compilation of the relevant piece");
	printl2("verbose=0,1,2         Amount of debugging information");
	printl2("stages=parse,type,resolve,lower  What stages to debug");
	printl2("dead=1 or <path>      Report what names are dead.");
	println("");

	if (!isUrlParameterTrue("help")) {
		println("\nAdd help=1 for complete usage");
	}
}
