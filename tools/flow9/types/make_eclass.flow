import tools/flow9/dexp/dexp;
import ds/treeutils;

export {
	populateEClass(mkEClass : () -> int, e : DExp) -> void;
}

populateEClass(mkEClassFn : () -> int, e : DExp) -> void {
	mkEClass = \ee, pos : int -> {
		if (ee != -1) {
			println("TODO: Expected a null-eclass " + toString(e));
			ee;
		} else {
			mkEClassFn();
		}
	}
	todo = \ -> {
		println("Add eclass");
	}
	rec = \ee -> populateEClass(mkEClassFn, ee);
	switch (e) {
		DVoid(pos): {};
		DBool(v, pos): {};
		DInt(v, pos): {};
		DDouble(v, pos): {};
		DString(v, pos): {};
		DStringInclude(path, pos): {};
		DVar(id, pos, eclass): e.eclass ::= mkEClass(eclass, pos);
		DLet(id, value, body, pos, eclass): {
			rec(value);
			rec(body);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DIf(cond, then, else_, pos, eclass): {
			rec(cond);
			rec(then);
			rec(else_);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DCall(fn, args, pos, eclass): {
			rec(fn);
			iter(args, rec);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DConstruct(structid, args, pos, eclass): {
			iter(args, rec);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DLambda(args, body, pos, eclass): {
			rec(body);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DRef(value, pos, eclass): {
			rec(value);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DField(value, field, pos, eclass): {
			rec(value);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DSetMutable(lhs, field, value, pos, eclass): {
			rec(lhs);
			rec(value);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DArray(exps, pos, eclass): {
			iter(exps, rec);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DSequence(exps, pos, eclass): {
			iter(exps, rec);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DSwitch(value, type, cases, defaultCase, pos, eclass): {
			rec(value);
			iter(cases, \c -> rec(c.body));
			defaultCase ?? rec(defaultCase) : {};
			e.eclass ::= mkEClass(eclass, pos);
		}
		DCast(value, target, pos):  {
			rec(value);
		}
		DDowncast(id, newid, subtype, subeclass, supereclass, scope, pos, eclass): {
			rec(scope);
			e.eclass ::= mkEClass(eclass, pos);
		}
		DTyped(value, supertype, pos, eclass): {
			rec(value);
			e.eclass ::= mkEClass(eclass, pos);
		}
	}
}
