import algorithms;

export {
	ErrorReason(
		// Lookup from position to reasons
		reasons : Tree<int, [string]>,
	);

	makeErrorReason(pos : [int], error : string) -> ErrorReason;

	elaborateReason(e : ErrorReason, extra : string) -> ErrorReason;

	reportErrors(e : ErrorReason, onError : (int, string) -> void) -> void;

	debugErrorReason(er : ErrorReason, prefix : string) -> void;
}

makeErrorReason(pos : [int], error : string) -> ErrorReason {
	if (pos == []) {
		println("We do not have a position for this error: " + error);
		printCallstack();
	}
	ErrorReason(fold(pos, makeTree(), \acc, p -> treePushToArrayUnique(acc, p, error)));
}

elaborateReason(e : ErrorReason, extra : string) -> ErrorReason {
	ErrorReason(
		mapTree(e.reasons, \rs -> {
			if (contains(rs, extra)) rs
			else arrayPush(rs, extra)
		})
	)
}

reportErrors(er : ErrorReason, onError : (int, string) -> void) -> void {
	cnt = ref 0;
	traverseInOrder(er.reasons, \pos, errors -> {
		onError(pos, 
			(if (^cnt == 0) "ERROR: " else "")
			+ strGlue(errors, ", ")
		);
		cnt := ^cnt + 1;
	});
}

debugErrorReason(er : ErrorReason, prefix : string) -> void {
	if (isEmptyTree(er.reasons)) {
		println(prefix);
	} else {
		first = ref true;
		spaces = stringFold(prefix, "", \acc, ch -> {
			if (ch == 32) acc + " " else acc;
		});
		traverseInOrder(er.reasons, \pos, errors -> {
			if (^first) {
				println(prefix + strGlue(errors, ", "));
				first := false;
			} else {
				println(spaces + "also from: " + strGlue(errors, ", "));
			}
		});
	}
}
