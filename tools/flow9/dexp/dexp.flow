import ds/list;
import ds/set;

export {
	DModule(
		// This is stable across computers
		flowpath : string, 
		// This is different from computer to computer
		fullpath : string,
		imports : [DImport], 
		forbids : [DForbid],
		exported : DExport, 
		structs : Tree<string, DStruct>,
		unions : Tree<string, DUnion>,
		natives : Tree<string, DNative>,
		globals : Tree<string, DGlobal>, 
		// The order of globals in the source code
		order : [string],
		// TODO: To help track dependencies, consider to add this
		// so we can propagate into BModule
		// stringincludes : [DStringInclude],
		pos : int
	);

	DImport(path : string, pos : int);
	DForbid(path : string, pos : int);

	DExport(
		// Structs and unions exported by name, but not by structure
		exportedTypeNames : Set<string>, 
		// Structs and unions where the structure is exported.
		// We allow .field on these, but not on exportedTypeNames
		exportedTypes : Set<string>, 
		// Globals and functions that are exported
		exportedGlobals : Set<string>, 
		// Placement of the export section in the file
		pos : int
	);

	// Typars is defined if this name is polymorphic.
	DGlobal(id : string, typars : [string], recursive : bool, value : DExp, mutable eclass : int, pos : int);
	DNative(id : string, isIo : bool, type : DType, name : string, fallback : bool, pos : int);

	// Defining a union. Typars are ?, ??.
	DUnion(id : string, typars : [DTypePar], types : [DTypeName], pos : int);
	// Defining a struct.
	DStruct(
		id : string, 
		// If this struct appears in a parametric union, we implicitly make it polymorphic.
		// When that happens, we set this to true
		implicitTypars : bool, 
		//  The typars are ?, ??, but since they are implicit, we do not have positions
		typars : [string], 
		structArgs : [DStructArg], 
		pos : int
	);
		DStructArg(id : string, ismutable : bool, type : DType, pos : int);
		
	DValue ::= DVoid, DBool, DInt, DDouble, DString;
		DVoid(pos : int);
		DBool(v : bool, pos : int);
		DInt(v : int, pos : int);
		DDouble(v : double, pos : int);
		DString(v : string, pos : int);

	DExp ::= DValue, DStringInclude,
		DVar, DLet, DIf, DCall, DConstruct, DLambda,
		DRef, DField, DSetMutable, DArray, DSequence, DSwitch, 
		DCast, DDowncast, DTyped;

		DStringInclude(path : string, pos : int);

		DVar(id : string, pos : int, mutable eclass : int);
		DLet(id : string, value : DExp, body : DExp, pos : int, mutable eclass : int);
		DIf(cond : DExp, then : DExp, else_ : DExp, pos : int, mutable eclass : int);
		// The eclass is the type of the entire lambda
		DLambda(args : [DArgType], body : DExp, pos : int, mutable eclass : int);
			DArgType(id : string, type : DType, pos : int);

		// Array construction
		DArray(exps : [DExp], pos : int, mutable eclass : int);
		DSequence(exps : [DExp], pos : int, mutable eclass : int);
		DCall(fn : DExp, args : [DExp], pos : int, mutable eclass : int);
		// Struct constructor
		DConstruct(structid : string, args : [DExp], pos : int, mutable eclass : int);

		DRef(value : DExp, pos : int, mutable eclass : int);

		DField(value : DExp, field : string, pos : int, mutable eclass : int);
		// The eclass is always void, so not that useful
		DSetMutable(lhs : DExp, field : string, value : DExp, pos : int, mutable eclass : int);

		DSwitch(value : DVar, type : DType, cases : [DCase], defaultCase : Maybe<DExp>, pos : int, mutable eclass : int);
			DCase(id : string, body : DExp, pos : int);
		DCast(value : DExp, target : DType, pos : int);
		// This does a downcast from a supertype to a subtype - a la let-binding of newid : subtype = downcast(id); scope
		DDowncast(id : string, newid : string, subtype : DType, mutable subeclass : int, mutable supereclass : int, scope : DExp, pos : int, mutable eclass : int);
		// We declare that value is a subtype of the type
		DTyped(value : DExp, supertype : DType, pos : int, mutable eclass : int);

	DType ::= DTypePar, DTypeName, DTypeFunction;
		// This is used for void, int, Maybe, etc. Also, "auto" is used for implicit types
		DTypeName(id : string, typars : [DType], pos : int);
		// This is ?, ??, ...
		DTypePar(id : string, pos : int);
		DTypeFunction(args : [DType], returnType : DType, pos : int);
}
