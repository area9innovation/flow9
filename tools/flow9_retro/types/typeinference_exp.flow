import tools/flow9/types/builtin;
import tools/flow9/dexp/pretty;
import tools/flow9/types/polymorphism;
import tools/flow9/types/cgraph/constraint;

export {
	ttypeInferenceDExp(env : TTypeEnv, id : string, d : DExp) -> TTypeEClass;
}

inferenceIndent : ref string = ref "  ";

ttypeInferenceDExp(env : TTypeEnv, topid : string, d : DExp) -> TTypeEClass {
	if (true) {
		doTTtypeInferenceDExp(env, topid, d);
	} else {
		indent = ^inferenceIndent;
		if (true) {
			locals = getTreeKeys(env.localTypes);
			println(indent + "Locals " + toString(locals) + " for inference of " + summarizeDExp(d));
			// prettyDExp(env, "", indent, d);
		}

		inferenceIndent := indent + "  ";
		t = doTTtypeInferenceDExp(env, topid, d);
		inferenceIndent := indent;

		println(indent + "gave " + prettyTType(t));
		t;
	}
}

doTTtypeInferenceDExp(env : TTypeEnv, topid : string, d : DExp) -> TTypeEClass {
	restype = \reason, tv : int, type : TTypeEClass -> {
		unifyCType(env.tmap, type, TTypeEClass(tv), makeErrorReason([d.pos], reason));
		TTypeEClass(tv);
	};
	asEClass = \reason, type -> {
		resultCType(env.tmap, type, makeErrorReason([d.pos], reason));
	}

	switch (d) {
		DVoid(pos): asEClass("void", TTypeName("void", []));
		DBool(v, pos): asEClass("bool", TTypeName("bool", []));
		DInt(v, pos): asEClass("int", TTypeName("int", [])); 
		DDouble(v, pos): asEClass("double", TTypeName("double", []));
		DString(v, pos): asEClass("string", TTypeName("string", []));
		DStringInclude(path, pos): asEClass("stringinclude", TTypeName("string", []));
		DVar(id, pos, eclass): {
			mtype = lookupTree(env.localTypes, id);
			type : TTypeEClass = mtype ?? {
				// Local types should NOT be instantiated
				mtype
			} : {
				moptype = getBuiltinTType(id);
				externalType = switch (moptype) {
					None(): {
						modtype : Maybe<TType> = lookupTree(env.moduleTypes, id);
						switch (modtype) {
							None(): env.resolveId(pos, id);
							Some(mdtype): {
								mdtype;
							}
						}
					}
					Some(optype): optype;
				};
				if (isTracingId(env.tracing, StageTypeInference(), 3, topid)) {
					println("    var " + id + " : " + prettyTType(externalType));
				}

				// Any other type needs instantiation
				itype = instantiateTTypePar(env, externalType);
				asEClass("var " + id, itype);
			};

			if (isTracingId(env.tracing, StageTypeInference(), 2, topid)) {
				println("    var " + id + " : " + prettyTType(type));
			}
			
			restype("var " + id, eclass, type);
		}
		DTyped(value, type, pos, eclass): {
			valtype = ttypeInferenceDExp(env, topid, value);
			if (isDTypeAuto(type)) {
				restype("autotyped", eclass, valtype);
			} else {
				reason = makeErrorReason([pos], "typed");
				ttype = dtype2CType(env.tmap, type, reason);
				subtypeTTypes(env, valtype, ttype, "typed", pos);
				restype("typed", eclass, ttype);
			}
		}
		DLet(id, value, body, pos, eclass): {
			tvalue = ttypeInferenceDExp(env, topid, value);
			nenv = TTypeEnv(env with localTypes = setTree(env.localTypes, id, tvalue));
			tbody = ttypeInferenceDExp(nenv, topid, body);
			restype("let " +id, eclass, tbody);
		}
		DIf(cond, then, else_, pos, eclass): {
			tcond = ttypeInferenceDExp(env, topid, cond);
			tthen = ttypeInferenceDExp(env, topid, then);
			telse = ttypeInferenceDExp(env, topid, else_);

			unifyCType(env.tmap, tcond, TTypeName("bool", []), makeErrorReason([pos], "if-cond"));
			res = TTypeEClass(eclass);
			reason = makeErrorReason([pos], "if-body");
			subtypeCType(env.tmap, tthen, res, false, reason);
			subtypeCType(env.tmap, telse, res, false, reason);
			res;
		}
		DCall(fn, args, pos, eclass): {
			fntype1 = ttypeInferenceDExp(env, topid, fn);
			id = switch (fn) {
				DVar(fnid, __, __): " " + fnid;
				default: "";
			};

			argtypes = map(args, \a -> {
				ttypeInferenceDExp(env, topid, a);
			});
			rettype = TTypeEClass(eclass);
			fntype2 = TTypeFunction(argtypes, rettype);
			reason = makeErrorReason([pos], "call" + id);
			subtypeCType(env.tmap, fntype1, fntype2, false, reason);
			rettype;
		}
		DConstruct(structid, args, pos, eclass): {
			mstruct = env.resolveStructName(structid);
			mstruct ?? {
				argTypes = map(args, \arg -> {
					ttypeInferenceDExp(env, topid, arg)
				});
				itype = instantiateTTypeNameForStruct(env, mstruct);
				if (length(argTypes) == length(itype.second)) {
					iteri(itype.second, \i, fields : Triple<string, bool, TType> -> {
						subtypeTTypes(env, argTypes[i], fields.third, "construct " + structid + " arg #" + i2s(i), pos);
					});
				} else {
					env.onError(pos, "Struct " + structid + " expects " + i2s(length(itype.second)) + " arguments, got " + i2s(length(argTypes)));
				}

				restype("construct " + structid, eclass, asEClass("construct " + structid, itype.first));
			} : {
				env.onError(pos, "Unknown struct " + structid);
				TTypeEClass(env.mkEClass());
			}
		}
		DLambda(args, body, pos, eclass): {
			argTypes : [TTypeEClass] = map(args, \arg -> {
				reason = makeErrorReason([pos], "lambda arg");
				dtype2CType(env.tmap, arg.type, reason);
			});
			nenv = foldi(args, env, \i, acc, arg -> {
				TTypeEnv(acc with localTypes = setTree(acc.localTypes, arg.id, argTypes[i]))
			});
			retType = ttypeInferenceDExp(nenv, topid, body);
			resType = TTypeEClass(env.mkEClass());
			reason = makeErrorReason([pos], "lambda-type");
			subtypeCType(env.tmap, retType, resType, false, reason);
			type = TTypeFunction(argTypes, resType);
			restype("lambda", eclass, asEClass("lambda", type));
		}
		DRef(value, pos, eclass): {
			vtype = ttypeInferenceDExp(env, topid, value);
			reftype = TTypeEClass(env.mkEClass());
			subtypeCType(env.tmap, vtype, reftype, false, makeErrorReason([pos], "ref type"));
			unifyCType(env.tmap, TTypeEClass(eclass), TTypeName("ref", [reftype]), makeErrorReason([pos], "ref"));
			TTypeEClass(eclass);
		}
		DField(value, field, pos, eclass): {
			// OK, this is an overload of all structs with a field
			tvalue = ttypeInferenceDExp(env, topid, value);

			fntype = getDFieldType(env, field, pos, eclass);

			rtype = TTypeEClass(eclass);
			calltype = TTypeFunction([tvalue], rtype);

			unifyCType(env.tmap, calltype, fntype, makeErrorReason([pos], "field " + field));
			rtype;
		}
		DSetMutable(left, field, value, pos, eclass): {
			ltype = ttypeInferenceDExp(env, topid, left);
			valtype = ttypeInferenceDExp(env, topid, value);

			fntype = getDFieldType(env, field, pos, eclass);

			valueType = TTypeEClass(env.mkEClass());
			reason = makeErrorReason([pos], "set field " + field);
			subtypeCType(env.tmap, valtype, valueType, false, reason);

			calltype = TTypeFunction([ltype], valueType);
			unifyCType(env.tmap, calltype, fntype, reason);
			restype("setmutable", eclass, asEClass("setmutable", voidTType));
		}
		DArray(exps, pos, eclass): {
			vtypes = map(exps, \e -> {
				ttypeInferenceDExp(env, topid, e);
			});
			arraytype = TTypeEClass(env.mkEClass());
			iteri(vtypes, \i, vtype -> {
				subtypeCType(env.tmap, vtype, arraytype, false, makeErrorReason([pos], "array element #" + i2s(i)));
				{}
			});

			unifyCType(env.tmap, TTypeEClass(eclass), TTypeName("array", [arraytype]), makeErrorReason([pos], "constant array"));
			TTypeEClass(eclass);
		}
		DSequence(exps, pos, eclass): {
			stype = fold(exps, voidTType, \acc, e -> {
				ttypeInferenceDExp(env, topid, e);
			});
			restype("seq", eclass, asEClass("seq", stype));
		}
		DSwitch(value, type, cases, defaultCase, pos, eclass): {
			tvalue = ttypeInferenceDExp(env, topid, value);
			// Relate the type of the value with all structs mentioned
			caseTypes = map(cases, \c -> instantiateTTypeName(env, c.id));

			reason = makeErrorReason([pos], "switch cases");
			iter(caseTypes, \ct -> {
				subtypeCType(env.tmap, ct, tvalue, false, reason);
				{}
			});

			if (!isDTypeAuto(type)) {
				reason2 = makeErrorReason([pos], "switch type");
				ttype = dtype2CType(env.tmap, type, reason2);
				unifyCType(env.tmap, tvalue, ttype, reason2);
				{}
			}

			// Find the resulting type of the bodies
			caseBodyTypes = map(cases, \c -> ttypeInferenceDExp(env, topid, c.body));
			bodytypes = defaultCase ?? {
				arrayPush(caseBodyTypes, ttypeInferenceDExp(env, topid, defaultCase));
			} : {
				caseBodyTypes
			};
			resultType = TTypeEClass(eclass);
			reason3 = makeErrorReason([pos], "switch result");
			iter(bodytypes, \ct -> {
				subtypeCType(env.tmap, ct, resultType, false, reason3);
				{}
			});
			resultType;
		}
		DCast(value, target, pos): {
			tvalue = ttypeInferenceDExp(env, topid, value);
			super = TTypeEClass(env.mkEClass());
			reason = makeErrorReason([pos], "cast");
			subtypeCType(env.tmap, tvalue, super, false, reason);

			ttype = dtype2CType(env.tmap, target, reason);
			subtypeCType(env.tmap, ttype, super, false, reason);
			asEClass("cast", ttype)
		}
		DDowncast(oldid, newid, subtype, __, __, scope, pos, eclass): {
			moldtype = lookupTree(env.localTypes, oldid);
			moldtype ?? {
				d.supereclass ::= moldtype.eclass;
			} : {
				env.onError(pos, oldid + " in downcast unknown");
			};

			tsubtype = {
				reason = makeErrorReason([pos], "downcast");
				dtype2CType(env.tmap, subtype, reason);
			}
			teclass = asEClass("downcast", tsubtype);

			d.subeclass ::= teclass.eclass;

			nenv = TTypeEnv(env with 
				localTypes = setTree(env.localTypes, newid, teclass)
			);
			dtype = ttypeInferenceDExp(nenv, topid, scope);

			// Relate the subtype with the supertype
			etype = TTypeEClass(d.supereclass);
			reason = makeErrorReason([pos], "downcast");
			subtypeCType(env.tmap, teclass, etype, false, reason);
			unifyCType(env.tmap, TTypeEClass(eclass), dtype, reason);
			TTypeEClass(eclass)
		}
	}
}

// OK, find the type for this field (resolving to an overload if ambigious)
getDFieldType(env : TTypeEnv, field : string, pos : int, eclass : int) -> TType {
	fieldStructs : [DStruct] = env.resolveField(field);
	// Types for overload functions
	fieldFnTypes : [TTypeFunction] = filtermap(fieldStructs, \struct : DStruct -> {
		getDStructFieldType(env, struct, field);
	});

	fieldUnions : Set<string> = env.resolveUnionField(field);
	unionFns = map(set2array(fieldUnions), \un -> {
		utype = instantiateTTypeName(env, un);

		subtypes : [string] = env.resolveSubtypes(un);

		// Find the type of this field
		fieldType : Maybe<TTypeFunction> = fold(subtypes, None(), \acc : Maybe<TTypeFunction>, type -> {
			mstruct = env.resolveStructName(type);
			mstruct ?? {
				getDStructFieldType(env, mstruct, field)
			} : {
				acc;
			}
		});
		rt = switch (fieldType) {
			None(): {
				env.onError(pos, "Unknown field " + field + " type from union");
				TTypeEClass(env.mkEClass());
			}
			Some(ft): {
				ft.returnType
			}
		};
		TTypeFunction([utype], rt);
	});

	fnTypes = concat(fieldFnTypes, unionFns);
	// println("Field " + field + " has fns " + toString(fnTypes));

	if (fnTypes == []) {
		env.onError(pos, "Unknown field " + field);
		TTypeEClass(env.mkEClass());
	} else if (length(fnTypes) == 1) {
		fnTypes[0]
	} else TTypeOverload(TTypeEClass(eclass), sort(fnTypes));
}

getDStructFieldType(env : TTypeEnv, struct : DStruct, field : string) -> Maybe<TTypeFunction> {
	stypenameFields = instantiateTTypeNameForStruct(env, struct);
	stypename = stypenameFields.first;
	fields = stypenameFields.second;

	mft : Maybe<Triple<string, bool, TType>> = find(fields, \sa -> sa.first == field);
	fieldType : Maybe<TType> = maybeMap(mft, \ft -> {
		// OK, the typars are wrong here, since we should use the type
		// of the field
		ft.third
	});
	fieldType ?? {
		Some(TTypeFunction([stypename], fieldType));
	} : None();
}

subtypeTTypes(env : TTypeEnv, sub : TType, super : TType, reason : string, pos : int) -> void {
	subtypeCType(env.tmap, sub, super, false, makeErrorReason([pos], reason));
	{}
}
