import tools/flow9_retro/types/type;
import tools/flow9_retro/types/typeenv;
import tools/flow9_retro/dexp/dexp;

export {
	// What is the TType of this expression? This instantiates polymorphism
	dexpType(env : TTypeEnv, e : DExp) -> TType;

	// Convert this to a ttype, instantiating polymorphism
	dtype2ttype(env : TTypeEnv, instantiate : bool, d : DType) -> TType;
}

dexpType(env : TTypeEnv, e : DExp) -> TType {
	n = \s -> TTypeName(s, []);
	switch (e) {
		DVoid(pos): n("void");
		DBool(v, pos): n("bool");
		DInt(v, pos): n("int"); 
		DDouble(v, pos): n("double");
		DString(v, pos): n("string");
		DStringInclude(path, pos): n("string");
		DVar(id, pos, eclass): TTypeEClass(eclass);
		DLet(id, value, body, pos, eclass): TTypeEClass(eclass);
		DIf(cond, then, else_, pos, eclass): TTypeEClass(eclass);
		DCall(fn, args, pos, eclass): TTypeEClass(eclass);
		DConstruct(structid, args, pos, eclass): TTypeEClass(eclass);
		DLambda(args, body, pos, eclass): TTypeEClass(eclass);
		DRef(value, pos, eclass): TTypeEClass(eclass);
		DField(value, field, pos, eclass): TTypeEClass(eclass);
		DSetMutable(lhs, field, value, pos, eclass): TTypeEClass(eclass);
		DArray(exps, pos, eclass): TTypeEClass(eclass);
		DSequence(exps, pos, eclass): TTypeEClass(eclass);
		DSwitch(value, type, cases, defaultCase, pos, eclass): TTypeEClass(eclass);
		
		DCast(value, target, pos): {
			dtype2ttype(env, true, target); // TODO: This is wrong. We should have an eclass
		}
		DDowncast(oldid, newid, subtype, subeclass, supereclass, scope, pos, eclass): TTypeEClass(eclass);
		DTyped(value, subtype, pos, eclass): TTypeEClass(eclass);
	}
}

dtype2ttype(env : TTypeEnv, instantiate : bool, d : DType) -> TType {
	switch (d) {
		DTypePar(id, pos): {
			if (instantiate) {
				meclass = lookupTree(env.typars, id);
				meclass ?? {
					TTypeEClass(meclass);
				} : {
	// 				println("Unresolved type par!");
					TTypeEClass(env.mkEClass());
				}
			} else TTypeName(id, []);
		}
		DTypeName(id, typars, pos): {
			if (id == "auto") {
				TTypeEClass(env.mkEClass())
			} else {
				TTypeName(id, map(typars, \tv -> dtype2ttype(env, instantiate, tv)));
			}
		}
		DTypeFunction(args, returnType, pos): {
			TTypeFunction(
				map(args, \tv -> dtype2ttype(env, instantiate, tv)), 
				dtype2ttype(env, instantiate, returnType)
			);
		}
	}
}
