import tools/flow9/types/type;
import maybe;
import ds/tree;
import algorithms;

export {
	voidTType : TType = TTypeName("void", []);
	boolTType : TType = TTypeName("bool", []);
	intTType : TType= TTypeName("int", []);
	doubleTType : TType = TTypeName("double", []);
	stringTType : TType = TTypeName("string", []);
	refTType : (TType) -> TType;
	arrayTType : (TType) -> TType;
	getBuiltinTType(op : string) -> Maybe<TType>;
}

builtinTOps : ref Maybe<Tree<string, TType>> = ref None();

refTType(rt : TType) -> TType {
	TTypeName("ref", [rt]);
}

arrayTType(rt : TType) -> TType {
	TTypeName("array", [rt]);
}

getBuiltinTType(op : string) -> Maybe<TType> {
	ot = onlyOnce(builtinTOps, \ -> {
		typar1 = TTypeName("?", []);
		pairs2tree([
			Pair("||", TTypeFunction([boolTType, boolTType], boolTType)),
			Pair("&&", TTypeFunction([boolTType, boolTType], boolTType)),
			Pair("__not", TTypeFunction([boolTType], boolTType)),
			Pair("==", TTypeFunction([typar1, typar1], boolTType)),
			Pair("!=", TTypeFunction([typar1, typar1], boolTType)),
			Pair("<=", TTypeFunction([typar1, typar1], boolTType)),
			Pair("<", TTypeFunction([typar1, typar1], boolTType)),
			Pair(">=", TTypeFunction([typar1, typar1], boolTType)),
			Pair(">", TTypeFunction([typar1, typar1], boolTType)),
			Pair("+", TTypeOverload(typar1, sort([
				TTypeFunction([intTType, intTType], intTType),
				TTypeFunction([doubleTType, doubleTType], doubleTType),
				TTypeFunction([stringTType, stringTType], stringTType),
			]))),
			Pair("-", TTypeOverload(typar1, sort([
				TTypeFunction([intTType, intTType], intTType),
				TTypeFunction([doubleTType, doubleTType], doubleTType),
			]))),
			Pair("__negate", TTypeOverload(typar1, sort([
				TTypeFunction([intTType], intTType),
				TTypeFunction([doubleTType], doubleTType),
			]))),
			Pair("*", TTypeOverload(typar1, sort([
				TTypeFunction([intTType, intTType], intTType),
				TTypeFunction([doubleTType, doubleTType], doubleTType),
			]))),
			Pair("/", TTypeOverload(typar1, sort([
				TTypeFunction([intTType, intTType], intTType),
				TTypeFunction([doubleTType, doubleTType], doubleTType),
			]))),
			Pair("%", TTypeOverload(typar1, sort([
				TTypeFunction([intTType, intTType], intTType),
				TTypeFunction([doubleTType, doubleTType], doubleTType),
			]))),
			Pair(":=", TTypeFunction([refTType(typar1), typar1], voidTType)), // TODO: Add supertype?
			Pair("__deref", TTypeFunction([refTType(typar1)], typar1)), // TODO: Add supertype?
			Pair("__index", TTypeFunction([arrayTType(typar1), intTType], typar1)),
			Pair("__structname", TTypeFunction([typar1], stringTType)),
		])
	});
	lookupTree(ot, op);
}
