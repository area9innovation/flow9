import tools/flow9_retro/backends/js/js_utils;
import formats/json/json_parse;
import tools/flow9_retro/backends/utils;

export {
	prepareWebFonts(cfg : JsConfig) -> Pair<[string], [string]>;
}

prepareWebFonts(cfg : JsConfig) {
	fontSetup = [
		Pair("styles", "@fontstyles"),
		Pair("webfontconfig", "@webfontconfig")
	];

	resourcesuffix = "_tmp.json";
	resourceprefix = "_";

	res = if (!fileExists(cfg.fontconfig)) {
		println("WARNING: font config file: " + cfg.fontconfig + " doesn't exist");
		[];
	} else {
		if (cfg.verbose > 0) {
			println("Using fontconfig file '" + cfg.fontconfig + "'");
		}
		jf = getFileContent(cfg.fontconfig);
		switch (parseJsonSafer(jf)) {
			Some(jsn): {
				fold(fontSetup, [], \acc, e -> {
					v = getJsonFieldValue(jsn, e.first, JsonNull());
					if (v != JsonNull()) {
						fl_name = resourceprefix + e.first + resourcesuffix;
						plSaveFileContent(fl_name, json2string(v));
						arrayPush(acc, Pair(fl_name, e.second));
					} else acc
				});
			}
			None(): {
				println("Error: Config file: " + cfg.fontconfig + " contains non-valid JSON format");
				[]
			}
		}
	}
	tmpfiles = map(res, \p -> p.first);
	args = map(res, \p -> "-resource " + p.first + p.second);

	Pair(tmpfiles, args);
}
