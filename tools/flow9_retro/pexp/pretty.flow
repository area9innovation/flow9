import tools/flow9_retro/pexp/pexp;
import string;

export {
	prettyPExp(input : string, i : string, p : PExp) -> void;
}

prettyPExp(input : string, i : string, p : PExp) -> void {
	rec = \e -> prettyPExp(input, i + " ", e);
	pr = \s : string -> {
		line = if (p.pos == -1) "MISSING POS " + p.structname else substring(input, p.pos, 20);
		println(i + s + ": \"" + takeBefore(line, "\n", line) + "\"");
	}
	switch (p) {
		PVoid(pos): pr("void");
		PBool(v, pos): pr("bool");
		PInt(v, pos): pr("int");
		PDouble(v, pos): pr("double");
		PString(v, pos): pr("string");
		PLambda(args, body, pos): {
			pr("lambda");
			rec(body);
		}
		PModule(tl, pos): {
			pr("module");
			iter(tl, rec);
		}
		PImport(path, pos): pr("import " + path);
		PForbid(path, pos): pr("forbid " + path);
		PExport(exports, pos): {
			pr("export");
			iter(exports, rec);
		}
		PGlobal(id, value, pos): {
			pr("global " + id);
			rec(value);
		}
		PNative(id, isIo, type, name, pos): {
			pr("native def");
			rec(type);
		}
		PUnion(typename, types, pos): {
			pr("union");
			rec(typename);
			iter(types, rec);
		}
		PStruct(id, structArgs, pos): {
			pr("struct " + id);
			iter(structArgs, rec);
		}
		PStructArg(id, ismutable, type, pos): {
			pr("struct arg" + id);
			rec(type);
		}
		PTypeDef(id, type, pos): {
			pr("typedef " + id);
			rec(type);
		}
		PTypePar(id, pos): pr(id);
		PTypeName(id, typars, pos): {
			pr("typename " + id);
			iter(typars, rec);
		}
		PTypeFunction(args, returnType, pos): {
			pr("function type");
			iter(args, rec);
			rec(returnType);
		}
		PArgType(id, type, pos): {
			pr("arg " + id);
			rec(type);
		}
		PStringInclude(path, pos): {
			pr("String include " + path);
		}
		PVar(var, pos): {
			pr("var " + var);
		}
		PLet(var, value, body, pos): {
			pr("let " + var);
			rec(value);
			rec(body);
		}
		PIf(cond, then, else_, pos): {
			pr("if");
			rec(cond);
			rec(then);
			rec(else_);
		}
		PCall(fn, args, pos): {
			pr("call");
			rec(fn);
			iter(args, rec);
		}
		PCallOp(op, args, pos): {
			pr("op: " + op);
			iter(args, rec);
		}
		PRef(value, pos): {
			pr("ref");
			rec(value);
		}
		PField(value, field, pos): {
			pr("field ." + field);
			rec(value);
		}
		PSetMutable(lhs, field, value, pos): {
			pr("set mutable field" + field);
			rec(lhs);
			rec(value);
		}
		PArray(exps, pos): {
			pr("array");
			iter(exps, rec);
		}
		PSequence(exps, pos): {
			pr("sequence");
			iter(exps, rec);
		}
		PSwitch(value, cases, pos): {
			pr("switch");
			rec(value);
			iter(cases, rec);
		}
		PCase(id, args, body, pos): {
			pr("case " + id + "(" + strGlue(args, ", ") + ")");
			rec(body);
		}
		PCast(value, target, pos): {
			pr("cast");
			rec(value);
			rec(target);
		}
		PTyped(value, type, pos): {
			pr("typed");
			rec(value);
			rec(type);
		}
		PWith(struct, value, fields, pos): {
			pr("with " + struct);
			rec(value);
			iter(fields, rec);
		}
		PFieldAssign(id, value, pos): {
			pr("field " + id);
			rec(value);
		}
		PMaybe(value, match, otherwise, pos): {
			pr("maybe");
			rec(value);
			rec(match);
			rec(otherwise);
		}
		PList(l, pos): {
			pr("list");
			iterList(l, rec)
		}
	}
}
