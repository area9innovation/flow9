import tools/flowc/backends/cpp2/fi2cpp_compile;
import tools/flowc/backends/cpp2/fi2exe;
import tools/flowc/backends/common;

export {
	fi2cpp2(program : FiProgram, cfg : FcCpp2Config, callback : () -> void) -> void;
}

fi2cpp2(program : FiProgram, cfg : FcCpp2Config, callback : () -> void) -> void {
	if (cfg.verbose > 0) {
		fcPrintln("Making cpp2 target for '" + cfg.flowfile + "'...", cfg.threadId);
		if (cfg.verbose > 1) {
			fcPrintln("outputFile: " + cfg.outputFile, cfg.threadId);
			fcPrintln("outputDir: " + cfg.outputDir, cfg.threadId);
		}
	}
	// Translate global functions
	switch (lookupTree(program.names.toplevel, "main")) {
		None(): cpp2_error("No main() function.");
		Some(amain): {
			switch (amain) {
				FiFunctionDec(__,__,__,__,__): {
					writer = cpp2Writer(cfg, program, amain);
					cpp2WriterTranslateGlobalFunctions(writer);
					cpp2WriterTranslateGlobalVariables(writer);
					cpp2WriterEmitCommonAndTables(writer);
					cpp2writeSources(writer);
					fi2cpp2renameMainFile(cfg);
				}
				default: cpp2_error("main must be a function.");
			}
		}
	}
	if (cfg.buildExe) {
		buildFiExeFile(cfg, callback);
	} else {
		callback();
	}
}

fi2cpp2renameMainFile(cfg : FcCpp2Config) {
	make_path = \s -> {
		normalized = strReplace(strReplace(strReplace(s, "/", "_"), ".flow", ""), ".", "_");
		cpp2MakePath("flow_" + normalized + ".cpp", cfg);
	}
	trimmed = fold(cfg.includes, cfg.flowfile, \acc, incl ->
		strReplace(strReplace(acc, incl, ""), incl + "/", "")
	);
	slash_ind = strLastIndexOf(trimmed, "/");
	reduced = if (slash_ind == -1) trimmed else strRight(trimmed, slash_ind + 1);
	if (trimmed != reduced) {
		err = renameFile(make_path(trimmed), make_path(reduced));
		if (err != "") {
			fcPrintln(err, cfg.threadId);
		}
	}
}
