import sys/system;
import sys/target;
import math/stringmath;
import tools/flowc/backends/common;

export {
	buildFiExeFile(cfg : FcCpp2Config, callback : (int) -> void) -> void;
}

ProcDescr(
	header: string,
	prog  : string,
	args  : [string],
	dir   : string,
	stdin : string,
	verbose : int,
	cout : (string) -> void,
	msg  : (string) -> void
);

chainOfProcesses(procs : [ProcDescr]) -> ((int) -> void) -> () -> void {
	\callback -> 
		fold(reverseA(procs), 
			\-> callback(0), 
			\acc, proc : ProcDescr -> {
				\-> {
					if (proc.verbose > 1) {
						proc.msg("running: " + proc.prog + " " + strGlue(proc.args, " "));
					}
					if (proc.verbose > 0 && proc.header != "") {
						proc.msg(proc.header);
					}
					start = timestamp();
					has_errors = ref false;
					process = runSystemProcess(proc.prog, proc.args, proc.dir,
						\out -> {
							if (proc.verbose > 0 && out != "") {
								proc.cout(out);
							}
						},
						\err -> {
							if (err != "") {
								has_errors := true;
								if (proc.verbose > 0) {
									proc.msg("errors:\n" + err);
								}
							}
						},
						\code -> {
							if (^has_errors) {
								callback(5);
							} else if (code != 0) {
								if (proc.verbose > 0) {
									proc.msg("return code: " + i2s(code));
								}
								callback(5 + code);
							} else {
								if (proc.verbose > 1) {
									proc.msg("finished in: " + d2st((timestamp() - start) / 1000.0, 2) + "s");
								}
								acc();
							}
						}
					);
					writeProcessStdin(process, proc.stdin);
				}
			}
		);
}

buildFiExeFile(cfg : FcCpp2Config, callback : (int) -> void) -> void {
	exe = if (hasTargetName("windows")) changeFileExt(cfg.outputFile, ".exe") else cfg.outputFile;
	target = changeFileExt(fileNameOnly(exe), "");
	flowdir = rtrim2(getFlowDir(), "/");
	outdir = rtrim2(cfg.outputDir, "/");
	gendir = rtrim2(cfg.generateDir, "/");
	builddir = pathCombine(gendir, "build");
	if (cfg.verbose > 0) {
		fcPrintln("Going to build '" + exe + "' executable", cfg.threadId);
	}
	if (isDirectory(builddir)) {
		// Clean up previous builds
		err = deleteRecursively(builddir);
		if (err != "") {
			fcPrintln(err, cfg.threadId);
		}
	}
	ensureDirectoryExists(builddir);
	qmake_template = "#include tools/flowc/backends/cpp2/qmake_template.pro";
	qmake_file = strReplaces(qmake_template, ["%TARGET", target, "%FLOWDIR", flowdir]);
	defout = \msg -> {
		lines = filter(strSplit(msg, "\n"), \line -> line != "");
		fcPrintln(strGlue(lines, "\n"), cfg.threadId);
	}
	buildout = \msg -> {
		sources = filtermap(
			strSplit(msg, "\n"), 
			\line -> {
				words = strSplit(line, " ");
				findmap(words, \word -> 
					if (!endsWith(word, ".cpp")) None() else {
						Some("Compiled c++ source: " + word)
					}
				)
			}
		);
		fcPrintln(strGlue(sources, "\n"), cfg.threadId);
	}
	if (setFileContent(pathCombine(gendir, target + ".pro"), qmake_file)) {
		chainOfProcesses([
			ProcDescr(
				"Configuring build for " + cfg.outputFile, 
				"qmake", 
				concat3(
					["CONFIG+=native_build", "../" + target + ".pro"], 
					if (cfg.useGui) [] else ["CONFIG+=no_gui"],
					if (!cfg.debug) ["CONFIG+=release"] else ["CONFIG+=debug"],
				), 
				builddir, "", cfg.verbose, defout, defout
			),
			ProcDescr(
				"Building " + cfg.outputFile + " ...", 
				"make", 
				["-j" + i2s(availableProcessors())], 
				builddir, "", cfg.verbose, buildout, defout
			),
			ProcDescr(
				"Copying executable " + exe, "cp", 
				[pathCombine(builddir, target), exe], 
				outdir, "", cfg.verbose, defout, defout
			)
		])(callback)();
	} else {
		fcPrintln("coundn't write a qmake project file '" + pathCombine(gendir, target) + "'' target", cfg.threadId);
		callback(5);
	}
}
