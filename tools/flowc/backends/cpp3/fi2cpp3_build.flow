import tools/flowc/backends/cpp3/fi2cpp3_util;
import string_utils;
import sys/process;

export {
	fi2cpp3Build(gctx: Cpp3GlobalContext, program: Cpp3CompiledProgram, callback : (int) -> void) -> void;
}

fi2cpp3Build(gctx: Cpp3GlobalContext, program: Cpp3CompiledProgram, callback : (int) -> void) -> void {
	qt_deps = fi2cpp3QmakeDeps(program);
	if (length(qt_deps) == 0) {
		fi2cpp3BuildDirectly(gctx, program, callback);
	} else {
		fi2cpp3BuildWithQmake(gctx, program, qt_deps, callback);
	}
}

fi2cpp3ExecutableName(gctx: Cpp3GlobalContext) -> string {
	if (windows()) {
		changeFileExt(gctx.config.outfile, ".exe");
	} else {
		gctx.config.outfile;
	}
}

fi2cpp3BuildDirectly(gctx: Cpp3GlobalContext, program: Cpp3CompiledProgram, callback : (int) -> void) -> void {
	cpp_sources = filter(program.sources, \path -> endsWith(path, ".cpp"));
	opts = concat(fi2cpp3BuildOpts(gctx), ["-std=c++20"]);
	args = concat3(cpp_sources, opts, ["-o", fi2cpp3ExecutableName(gctx)]);
	if (gctx.config.config.verbose > 0) {
		fcPrintln("Compiling with cpp3: 'g++ " + strGlue(args, " ") + "', dir: " + gctx.packagePath + "...", gctx.config.config.threadId);
	}
	result_exit_code = execSystemProcess("g++", args, gctx.packagePath,
		\out -> if (gctx.config.config.verbose > 0 && !isSpace(out)) {
				fcPrintln(out, gctx.config.config.threadId);
		},
		\err -> if (!isSpace(err)) {
			fcPrintln(err, gctx.config.config.threadId);
		}
	);
	if (result_exit_code == 0) callback(0) else callback(5);
}

fi2cpp3BuildOpts(gctx: Cpp3GlobalContext) -> [string] {
	debug_param = getConfigParameterDef(gctx.program.config.config, "cpp-debug", "");
	debug = if (debug_param == "") [] else {
		["-O0", "-ggdb3"]
	}
	opt_param = getConfigParameterDef(gctx.program.config.config, "cpp-optimize", "");
	optimize = if (opt_param == "" || debug_param != "") [] else {
		["-O" + opt_param]
	}
	concatA([debug, optimize, ["-Wno-unused-value", "-Wno-unused-parameter", "-Wno-unused-variable", "-Wno-return-type"]]);
}

fi2cpp3QmakeDeps(prog: Cpp3CompiledProgram) -> [Cpp3Qt] {
	list2array(foldTree(prog.modules, makeList(), \__,m, acc -> 
		fold(m.natives, acc, \ac, nat -> 
			fold(nat.dependencies, ac, \a, dep ->
				switch (dep) {
					Cpp3Qt(__,__): Cons(dep, a);
					default: a;
				}
			)
		)
	));
}

fi2cpp3BuildWithQmake(gctx: Cpp3GlobalContext, program: Cpp3CompiledProgram, qt_deps: [Cpp3Qt], callback : (int) -> void) -> void {

	qt_conf = fi2cpp3QmakeConf(qt_deps, program.sources, gctx);
	qt_conf_file = gctx.config.outfile + ".pro";
	fi2cpp3SaveFile(qt_conf_file, qt_conf, gctx);
	qmake_args = ["-o", "Makefile", qt_conf_file];
	if (gctx.config.config.verbose > 0) {
		fcPrintln("Preparing Makefile with cpp3: 'qmake " + strGlue(qmake_args, " ") + "', dir: " + gctx.packagePath + "...", gctx.config.config.threadId);
	}
	qmake_result = execSystemProcessOutput("qmake", qmake_args, gctx.packagePath);
	if (gctx.config.config.verbose > 0 || qmake_result.exitCode != 0) {
		if (!isSpace(qmake_result.stdall)) {
			fcPrintln(qmake_result.stdall, gctx.program.config.threadId);
		}
	}
	if (qmake_result.exitCode != 0) callback(5) else {
		build_concurrency = getConfigParameterDef(
			gctx.program.config.config, 
			"cpp-build-jobs", 
			i2s(availableProcessors())
		);
		make_args = ["-j" + build_concurrency];
		if (gctx.config.config.verbose > 0) {
			fcPrintln("Building Makefile with cpp3: 'make " + strGlue(make_args, " ") + "', dir: " + gctx.packagePath + "...", gctx.config.config.threadId);
		}
		make_exit_code = execSystemProcess("make", make_args, gctx.packagePath,
			\out -> if (gctx.config.config.verbose > 0 && !isSpace(out)) {
				fcPrintln(out, gctx.config.config.threadId);
			},
			\err -> if (!isSpace(err)) {
				fcPrintln(err, gctx.config.config.threadId);
			}
		);
		if (make_exit_code != 0) callback(5) else callback(0);
	}
}

fi2cpp3QmakeConf(qt_deps: [Cpp3Qt], sources: [string], gctx: Cpp3GlobalContext) -> string {
	qt_opts = concatA(map(qt_deps, \dep -> dep.opts));
	unconditional = fold(qt_opts, makeTree(), \acc, opt -> 
		if (opt.condition != "") acc else 
		setTree(acc, opt.name, concat(lookupTreeDef(acc, opt.name, []), [opt]))
	);
	folded = fold(qt_opts, makeTree(), \acc, opt -> {
		opt_cond = lookupTreeDef(acc, opt.condition, makeTree());
		opt_name = lookupTreeDef(opt_cond, opt.name, makeTree());
		opt_sign = lookupTreeDef(opt_name, opt.sign, []);
		setTree(acc, opt.condition, 
			setTree(opt_cond, opt.name, 
				setTree(opt_name, opt.sign, 
					concat(opt_sign, opt.value)
				)
			)
		)
	});
	opts2qt = \opts -> {
		foldTree(opts, "", \name, opt, acc ->
			foldTree(opt, acc, \sign, vals, ac ->
				name + " " + sign + " " + strGlue(vals, " ") + "\n"
			)
		)
	}
	cpp_sources = filter(sources, \path -> endsWith(path, ".cpp"));
	hpp_sources = filter(sources, \path -> endsWith(path, ".hpp"));
	cxx_opts = superglue(concat(["-std=c++2a"], fi2cpp3BuildOpts(gctx)), \opt -> "QMAKE_CXXFLAGS += " + opt, "\n");
	conf_opts = strGlue(gctx.config.qtOpts, "\n");
	"QT += core\n" +
	//"QMAKE_CXXFLAGS_RELEASE -= -O2\n" +
	//"QMAKE_CXXFLAGS_RELEASE += -O3\n" +
	"CONFIG += -std=c++2a\n" +
	//"QMAKE_CXXFLAGS += -Werror\n" +
	//"WARNINGS += -Wall\n" +
	"QMAKE_CXXFLAGS += -Wno-unused-value -Wno-unused-parameter -Wno-unused-variable -Wno-return-type\n" +
	(if (isConfigParameterTrue(gctx.config.config.config, "cpp-debug") || isConfigParameterTrue(gctx.config.config.config, "cpp3-debug")) "CONFIG+=debug\n" else "") +
	(if (isConfigParameterTrue(gctx.config.config.config, "cpp-optimize") || isConfigParameterTrue(gctx.config.config.config, "cpp3-optimize")) "CONFIG+=release\n" else "") +
	cxx_opts + "\n" +
	(if (conf_opts == "") "" else conf_opts + "\n") +
	foldTree(folded, "", \cond, opts, acc ->
		if (cond != "") {
			cond + " {\n" + strIndent(opts2qt(opts)) + "\n}\n";
		} else {
			opts2qt(opts)
		}
	) + "\n" +
	concatStrings(map(hpp_sources, \hpp -> 
		"HEADERS += " + hpp + "\n"
	)) + "\n" +
	concatStrings(map(cpp_sources, \cpp ->
		"SOURCES += " + cpp + "\n"
	)) + "\n";
}

