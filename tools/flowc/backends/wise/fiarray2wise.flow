import string;
import tools/flowc/backends/wise/flow_wise_types;

// TODO: think about
// makeWiArrayConstruction()
// create runtime fns : createArray_N - where n = array_length
// -> we'll replace arr = {tmp=; ...  tmp} with arr = createArray_N(1,2,3);

export {
	makeStringType() -> FlowWiType; // string = array of chars

	makeWiArrayIndex(e1 : FlowWiExp, e2 : FlowWiExp, type : FlowWiType) -> FlowWiExp;
	makeWiArrayConstruction(exps : [FlowWiExp], tmpIndex : int, type : FlowWiType) -> Maybe<FlowWiExp>;
	isWiArrayConstruction(exp : FlowWiExp) -> bool;
}

makeStringType() -> FlowWiType {
	FlowWiBasictype("string");
}

// TODO: type
makeWiArrayIndex(e1 : FlowWiExp, e2 : FlowWiExp, type : FlowWiType) -> FlowWiExp {
	FlowWiCall(
		FlowWiPolyCall(FlowWiVar("getValueArray"), [type]),
		[e1, e2, makeDefaultTypeValue(type)]
	);
}

makeDefaultTypeValue(type : FlowWiType) -> FlowWiExp {
	switch (type : FlowWiType) {
		FlowWiBasictype(s) : 
			if (s == "i32") FlowWiInt(0)
			else if (s == "i64") FlowWiInt(0)
			else if (s == "f32") FlowWiDouble(0.0)
			else if (s == "f64") FlowWiDouble(0.0)
			else if (s == "bool") FlowWiBool(false)
			else { // auto, v128, templates
				println("ERROR: Can not determine the default value for '" + s + "'");
				FlowWiInt(0);
			};
		FlowWiFntype(__, __) : FlowWiInt(0); // pointer
		FlowWiTupletype(types) : FlowWiTuple(map(types, makeDefaultTypeValue));
		FlowWiArrayType(t) : FlowWiInt(0); // pointer
	}
}

makeWiArrayConstruction(exps : [FlowWiExp], tmpIndex : int, type : FlowWiType) -> Maybe<FlowWiExp> {
	if (exps == []) {
		Some(makeCreateArrayCall(0, "i32"));
	} else {
		basicType = extractWiseArrayType(type);
		if (basicType == "string") {
			makeWiArrayConstructionByType(
				exps,
				FlowWiVar("setCharString"),
				tmpIndex,
				\e -> switch (e : FlowWiExp) {
					FlowWiInt(__): Some(e);
					default : None();
				},
				basicType
			);
		} else if (basicType == "") {
			println("array of " + toString(type) + " is not implemented");
			None();
		} else {
			makeWiArrayConstructionByType(
				exps,
				FlowWiPolyCall(FlowWiVar("setValueArray"), [FlowWiBasictype(basicType)]),
				tmpIndex,
				\e -> switch (e : FlowWiExp) {
					FlowWiInt(__): Some(e);
					FlowWiDouble(__): Some(e);
					FlowWiBool(__): Some(e);
					FlowWiTuple(__): Some(e);
					// array ?
					default : None();
				},
				basicType
			);
		}
	} 
}

extractWiseArrayType(type : FlowWiType) -> string {
	switch (type : FlowWiType) {
		FlowWiBasictype(t) : t;
		FlowWiFntype(__, __) : "";
		FlowWiTupletype(__) : "";
		FlowWiArrayType(t) : extractWiseArrayType(t);
	}
}

makeWiArrayConstructionByType(exps : [FlowWiExp], setValFn : FlowWiExp, tmpIndex : int, getVal : (FlowWiExp) -> Maybe<FlowWiExp>, type : string) -> Maybe<FlowWiExp> {
	values = filtermap(exps, getVal);
	if (length(values) == length(exps)) {
		tmpVar = "tmp" + i2s(tmpIndex);
		Some(FlowWiBrace([
			FlowWiInt(0), // not to erase '{}' in the result string // TODO: fix it
			FlowWiBrace([FlowWiLet(
				tmpVar,
				0,
				FlowWiBasictype("i32"), // pointer
				makeCreateArrayCall(length(values), type),
				FlowWiBrace(arrayPush(
					mapi(values, \i, value -> FlowWiCall(setValFn, [FlowWiVar(tmpVar), FlowWiInt(i), value])),
					FlowWiVar(tmpVar)
				)),
			)])
		]));
	} else {
		None();
	}
}

makeCreateArrayCall(len : int, type : string) -> FlowWiCall {
	if (type == "string") {
		FlowWiCall(FlowWiVar("createString"), [FlowWiInt(len)])
	} else if (type == "") {
		println("create array of " +  type + " is not implemented");
		FlowWiCall(FlowWiVar("createArray"), [FlowWiInt(len)]);
	} else {
		FlowWiCall(
			FlowWiPolyCall(FlowWiVar("createArray"), [FlowWiBasictype(type)]),
			[FlowWiInt(len)]
		)
	}
}

isWiArrayConstruction(exp : FlowWiExp) -> bool {
	isCreateFn = \e -> {
		switch (e : FlowWiExp) {
			FlowWiCall(v, __) : {
				v == makeCreateArrayCall(0, "i32").exp
				|| v == makeCreateArrayCall(0, "string").exp;
			}
			default : false;
		}
	}
	isCreateFn(exp)
	|| {
		switch (exp : FlowWiExp) {
			FlowWiBrace(es): length(es) > 1
							&& switch (es[1] : FlowWiExp) {
								FlowWiBrace(es1): es1 != []
													&& switch (es1[0] : FlowWiExp) {
														FlowWiLet(__, __, __, e, __) : isCreateFn(e);
														default : false;
													};
								default : false;
							};
			default : false;
		}
	}

}
