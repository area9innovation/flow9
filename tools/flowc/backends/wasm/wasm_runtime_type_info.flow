import tools/flowc/backends/wasm/wasm_opcodes;
import tools/flowc/backends/wasm/wasm_runtime;

export {
	wasm_calc_struct_size(typedesc : int) -> int;
	wasm_set_struct_values(struct : int, typedesc : int, args : int) -> void;
	wasm_calc_struct_nfields(v : int) -> int;
	wasm_struct_nfields_address(v : int) -> int;

	// helpers to parse type info at runtime

	// next two function do almost the same except first one returns type description (i.e. address where type located)
	// and second one returns explicit type
	// both functions can return 'return type' when arg_idx == -1
	wasm_get_arg_td(funcDescr : int, arg_idx : int) -> int;
	wasm_get_arg_type(funcDescr : int, arg_idx : int) -> int;

	isHeap(type : int) -> bool;
	type2size(elemType : int) -> int;
}

wasm_calc_struct_size(typedesc : int) -> int {
	snamelen = wasm_i32_load8_u(typedesc + 1);
	nfields = wasm_i32_load8_u(typedesc + 2 + snamelen);
	wasm_do_calc_struct_size(typedesc + 3 + snamelen, 0, nfields) + 8
}

wasm_do_calc_struct_size(typedesc : int, acc : int, count : int) -> int {
	if (count == 0) {
		acc 
	} else {
		size = wasm_type_size(typedesc);
		wasm_do_calc_struct_size(typedesc + size, acc + size, count - 1)
	}
}

wasm_set_struct_values(struct : int, typedesc : int, args : int) -> void {
	snamelen = wasm_i32_load8_u(typedesc + 1);
	nfields = wasm_i32_load8_u(typedesc + 2 + snamelen);
	firstField = typedesc + 3 + snamelen;
	wasm_do_set_struct_values(struct + 8, firstField, args, 0, nfields)
}

wasm_do_set_struct_values(dst : int, typedesc0 : int, arr : int, idx : int, nfields : int) -> void {
	typedesc = typedesc0 + 2;	// skipping field id
	if (idx < nfields) {
		type = wasm_i32_load8_u(typedesc);
		if (type == 2) { // int
			// add refcnt before invoking to compensate for automatic decrease
			f = wasm_get_array_i32_ref(wasm_inc_refcnt_passthru(arr), idx, wasm_array_flow_type_id());
			v = wasm_i32_load(f + 8);
			wasm_i32_store(dst, v);
		} else {
			printi(type);
			wasm_unreachable();	// not supported yet
		}
		wasm_do_set_struct_values(
			dst + 4,
			wasm_next_typedescriptor(typedesc),
			arr,
			idx + 1,
			nfields
		)
	}
}

wasm_struct_nfields_address(v : int) {
	typedesc = wasm_i32_load(v + 4);
	snamelen = wasm_i32_load8_u(typedesc + 1);
	typedesc + 2 + snamelen
}

wasm_calc_struct_nfields(v : int) {
	wasm_i32_load8_u(wasm_struct_nfields_address(v));
}

wasm_find_arg_address(arg_pnt : int, arg_idx : int) -> int {
	if (arg_idx == 0) {
		arg_pnt
	} else {
		next = wasm_next_typedescriptor(arg_pnt);

		wasm_find_arg_address(next, arg_idx - 1)
	}
}

// if arg_idx == -1 then we'll return 'returnType' for passed function description
wasm_get_arg_td(funcDescr : int, arg_idx : int) -> int {
	if (wasm_i32_load8_u(funcDescr + 0) != 12) {
		prints("not a function");
		printi(wasm_i32_load8_u(funcDescr + 0));
		wasm_unreachable();
	}
	
	wasm_find_arg_address(funcDescr + 1 + 1, if (arg_idx == -1) /*nun of args*/wasm_i32_load8_u(funcDescr + 1) else arg_idx);
}

wasm_get_arg_type(funcDescr : int, arg_idx : int) -> int {
	elemType_td = wasm_get_arg_td(funcDescr, arg_idx);
	wasm_i32_load8_u(elemType_td);
}

isHeap(type : int) -> bool {
	//type is not bool-int-double
	!(type == 1 || type == 2 || type == 3)
}

type2size(elemType : int) -> int {
	if (elemType == 3) 8 else 4;
}
