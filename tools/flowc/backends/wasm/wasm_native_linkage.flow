import tools/flowc/incremental/fitype;
import tools/flowc/incremental/fiprogram;
import tools/flowc/incremental/fimodule;
import tools/flowc/backends/wasm/wasm_type_descriptor;
import tools/flowc/backends/wasm/f2wasm_utils;
import tools/flowc/backends/common;
import ds/tree;

export {
	wasmLinkNative(cfg : FiWasmConfig, program : FiProgram, nativ : FiNativeDec) -> FiNativeDec;
    // this filters out native declarations that match functions defined in top level
    // used to substitute natives with functions in runtime
    wasmOverrideNatives(modules : Tree<string, FiModule>, toplevels : Tree<string, FiDeclaration>) -> Tree<string, FiModule>;
}

unpackArrayArgumentType(t : WasmTypeFunction) -> WasmTypeDescriptor {
	argType = t.args[0].type;
	cast(argType : WasmTypeDescriptor -> WasmTypeArray).type;
}

unpackArgumentType(t : WasmTypeFunction, argidx : int) -> WasmTypeDescriptor {
	t.args[argidx].type;
}


link3way(prefix : string, wasmType : WasmTypeDescriptor) -> string {
	switch (wasmType) {
		WasmTypeDouble(): prefix + "_f64";
		default: if (wasmTypeUsesWasmHeap(wasmType)) prefix + "_i32_ref"
			else prefix + "_i32";
	}
}


buildLinkageMap() {
	linkages : [Pair<string, (FiNativeDec, WasmTypeFunction) -> string>] = [
		Pair("Native.concat", \x, wt -> 
			link3way("wasm_typed_concat", unpackArrayArgumentType(wt))),
		Pair("Native.replace", \x, wt -> 
			link3way("wasm_typed_replace", unpackArrayArgumentType(wt))),
		Pair("Native.subrange", \x, wt -> 
			link3way("wasm_typed_subrange", unpackArrayArgumentType(wt))),
		Pair("Native.length", \x, wt -> "wasm_typed_length"),
		Pair("Native.iter", \x, wt -> "wasm_typed_iter"),
		Pair("Native.iteri", \x, wt -> "wasm_typed_iteri"),
		Pair("Native.iteriUntil", \x, wt -> "wasm_typed_iteriUntil"),
		Pair("Native.map", \x, wt -> "wasm_typed_map"),
		Pair("Native.mapi", \x, wt -> "wasm_typed_mapi"),
		Pair("Native.filter", \x, wt -> "wasm_typed_filter"),
		Pair("Native.s2a", \x, wt -> "wasm_s2a"),
		Pair("Native.string2utf8", \x, wt -> "wasm_string2utf8"),
		Pair("Native.fold", \x, wt -> 
			link3way("wasm_typed_fold", unpackArgumentType(wt, 1))),
		Pair("Native.foldi", \x, wt -> 
			link3way("wasm_typed_foldi", unpackArgumentType(wt, 1))),
	];
	pairs2tree(linkages);
}


wasmLinkNative(cfg : FiWasmConfig, program : FiProgram, n : FiNativeDec) {
	converter = lookupTreeDef(linkageMap, n.nativeName, \x, wt -> x.nativeName);
	wt = cast(fitype2wasmtype(program.names, n.type, cfg.threadId) : WasmTypeDescriptor -> WasmTypeFunction);
	newNativeName = converter(n, wt);
	if (n.nativeName == newNativeName) 
		n 
	else
		FiNativeDec(newNativeName, n.io, n.type, newNativeName, n.fallbackLambda, n.declStart, n.defiStart)
}

linkageMap : Tree<string, (FiNativeDec, WasmTypeFunction) -> string> = buildLinkageMap();

// This grabs flow-based implementations of natives to run in the Wasm side, and uses that as the implementation
wasmOverrideNatives(modules : Tree<string, FiModule>, toplevels : Tree<string, FiDeclaration>) -> Tree<string, FiModule> {
    filterTopLevels = \name -> {
			eitherMap(lookupTree(toplevels, name), \fb -> {
				switch (fb) {
					FiFunctionDec(__, __, __, __, __): false;
					FiGlobalVar(__, __, __, __, __): true;
					FiNativeDec(__, __, __, __, __, __, __): true;
				}
			}, true)
		};

	mapTree(modules, \module -> {
		natives2 = filter(module.natives, \n -> filterTopLevels(n.name));
        functions2 = filter(module.functions, \f -> filterTopLevels(f.name));
        initOrder2 = filter(module.initOrder, filterTopLevels);

		FiModule(module with natives = natives2, functions = functions2, initOrder = initOrder2);
	});
}
