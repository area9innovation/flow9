import text/blueprint;
import runtime;
import sys/system;
import formats/base64;
import fs/filesystem;
import tools/common/pathutil;
import ds/list;
import tools/flowc/backends/common;
import tools/flowc/backends/html/fi2html_defines;

export {
	fi2MonolithicHTML(
		cfg0 : FiHTMLConfig,
		callback : (int) -> void) -> void;
}

makeHtml(title, lnFavicon, scripts, lnCss, predefinedParams, manifestLink, noTranslate: bool) {
// ****************************************************************
// Please pay attention!
// It is very important to have variable 'htmlBundle' defined
// in the <script>.
// This variable is used to determine if we run html bundle
// or regular js file. Decision is made in www/js/jscommon.js
// ****************************************************************

Cons("<!doctype html><html xmlns='http://www.w3.org/1999/xhtml' xml:lang='en' lang='en'", Cons(if (noTranslate) " translate='no'" else "", Cons(">
  <head>
    <title>", Cons(title, Cons("</title>

", Cons((if (noTranslate)
"    <meta name='google' content='notranslate' />" else ""),
Cons("<meta http-equiv='X-UA-Compatible' content='IE=11' />
    <meta http-equiv='content-type' content='application/xhtml+xml; charset=utf-8' />
    <meta http-equiv='cache-control' content='no-cache'/>
    <!-- disable user scaling on mobile devices -->
    <meta name='viewport' content='initial-scale=1.0,maximum-scale=1.0,user-scalable=no' />
    <!-- Suppress browser request for favicon.ico -->
    <!-- Inline load of fonts.css -->
	<link rel='stylesheet' href='fonts/fonts.css' media='all'>
	<script>
    var htmlBundle = true;", Cons(predefinedParams, Cons("
    var favIcon ='", Cons(lnFavicon, Cons("';
    var docHead = document.getElementsByTagName('head')[0];
    var newLink = document.createElement('link');
    newLink.id = 'app-favicon';
    newLink.rel = 'shortcut icon';
    newLink.href = 'data:image/png;base64,'+favIcon;
    docHead.appendChild(newLink);",	Cons(manifestLink, Cons("
    </script>
    <link rel='apple-touch-icon' sizes='120x120' href='icons/icon-120.png' />
    <link rel='apple-touch-icon' sizes='152x152' href='icons/icon-152.png' />
    <link rel='apple-touch-icon' sizes='167x167' href='icons/icon-167.png' />
    <link rel='apple-touch-icon' sizes='180x180' href='icons/icon-180.png' />",
Cons(lnCss, Cons(
"  </head>
  <body>

",
Cons(scripts, Cons("

</body>
</html>
", makeList()))))))))))))))))) |> reverseList |> list2string;
}


includeScript(cfg : FiHTMLConfig, name: string)  {
	fcVerbose(cfg, 1, "Linker: reading " + name);
	content = getFileContent(findFlowPath(name, false, cfg.includes, cfg.workingDir));
	fileNameComment = if (cfg.debug) "<!-- " + name + " -->" else "";

	fcVerbose(cfg, 1, "Linker: blueprinting " + name + "...");
	"
" + fileNameComment + "
<script type='text/javascript'>
  <!--//--><![CDATA[//><!--
" + content + "
//--><!]]>
</script>
";
}

includeCss(cfg : FiHTMLConfig, name: string) -> string {
	fcVerbose(cfg, 1, "Linker: reading " + name + "...");
	content = getFileContent(findFlowPath(name, false, cfg.includes, cfg.workingDir));

	fcVerbose(cfg, 1, "Linker: blueprinting " + name + "...");
"
<style type='text/css'>
	<!--/*--><![CDATA[/*><!--*/
" + content + "
	/*]]>*/-->
</style>
";
}

includeImage(cfg : FiHTMLConfig, name: string) {
	fcVerbose(cfg, 1, "Linker: reading " + name + "...");
	// Pay attention, getFileContentBinary() used instead of getFileContent()
	strGlue(map(strSplit(encodeBase64(getFileContentBinary(findFlowPath(name, false, cfg.includes, cfg.workingDir))), fromCharCode(10)), \e -> {trim2(e, "\u000d")}), "");
}

filesAvailable(cfg : FiHTMLConfig) {
	fcVerbose(cfg, 0, "Checking files availability");
	fileList = arrayPush(concat3(cfg.scripts, cfg.additionalScripts, [fiHTMLFavIcon, cfg.splash, fiHTMLCss]), cfg.jsfile);
	r = fold(fileList, true, \acc, e -> {
		fe = findFlowPath(e, false, cfg.includes, cfg.workingDir);
		fe1 = fileExists(fe);
		fcVerbose(cfg, 0, "Found '" + e + "' at: " + fe);
		if (!fe1) {
			fcPrintln("!Not found '" + e + "' during linking...", cfg.threadId);
		}
		acc && fe1;
	});
	fcVerbose(cfg, 0, "All files available: " + toString(r));
	r;
}

fi2MonolithicHTML(cfg0 : FiHTMLConfig, callback) {

	cfg = FiHTMLConfig(cfg0 with scripts = if (cfg0.scripts == []) fiHTMLScriptList else cfg0.scripts);

	doLink = \ -> {
		fcVerbose(cfg, 0, "Start linking " + cfg.outputfile);
		if (filesAvailable(cfg)) {
			scripts = strGlue(map(arrayPush(concat(cfg.scripts, cfg.additionalScripts), cfg.jsfile), \e -> {
				fcVerbose(cfg, 0, "Preparing file '" + e + "' for linking.");
				includeScript(cfg, e)
			}), "");

			manifestLink = if (cfg.manifestFileName != "") {
				"\n    var pwaLink = document.createElement('link');
    pwaLink.rel = 'manifest';
    var _s = window.location.search.substring(1).split('&').filter(_v=>(_v1=>_v1!='dev'&&_v1!='devtrace')(_v.split('=')[0])).join('&');
    pwaLink.href = '" + cfg.manifestFileName + "' + (_s==''?_s:'" + (if (strContains(cfg.manifestFileName, "?")) "&" else "?") +
		"'+_s);
	document.head.appendChild(pwaLink);
    var pwaScript = document.createElement('script');
    pwaScript.src = 'js/pwacompat.min.js';
    document.head.appendChild(pwaScript);"
			} else {
				""
			};

			predefinedParams =
				if (cfg.predefinedParams == []) {
					""
				} else {
					"\n    var predefinedBundleParams=[" +
					strGlue(map(cfg.predefinedParams, \e -> "[\"" + e.first + "\", \"" + e.second + "\"]"), ",") +
					"];"
				}

			fcVerbose(cfg, 0, "Assembling...");
			res = makeHtml(
				cfg.title,
				includeImage(cfg, fiHTMLFavIcon),
				scripts,
				includeCss(cfg, fiHTMLCss),
				predefinedParams,
				manifestLink,
				cfg.noTranslate
			);

			fcVerbose(cfg, 1, "Saving...");
			success = setFileContent(cfg.outputfile, res);
			fcVerbose(cfg, 0, "File " + cfg.outputfile + " linked.");
			success
		} else {
			fcPrintln("Linking monolithic file " + cfg.outputfile + " failed.", cfg.threadId);
			// If we already in verbose mode, do not show this help line.
			if (cfg.verbose == 0) {
				fcPrintln("Run once again with verbose=1 or verbose=2 to see which file missed.", cfg.threadId);
			}
			false;
		}
	}

	file = cfg.outputfile;

	if (cfg.minify) {
		// This branch for Unix-based systems
		minify_params = [cfg.jsfile, "-o", cfg.jsfile];
		startProcess("uglifyjs", minify_params, ".", "",
			\c, o, e -> {
				fcVerbose(cfg, 0, "Running 'uglifyjs' for " + cfg.jsfile);
				if (c != 0) {
					// Windows version of js-beautify utility named differently
					startProcess("uglifyjs.cmd", minify_params, ".", "",
						\c1, o1, e1 -> {
							fcVerbose(cfg, 0, "Trying 'uglifyjs.cmd' for " + cfg.jsfile);
							success = doLink();
							if (c1 != 0) {
								fcPrintln(
									"uglifyjs.cmd failed.\n" +
									"err:\n" + e1 + "\n" +
									"out:\n" + o1 + "\n" +
									"exit code: " + i2s(c1), 
									cfg.threadId
								);
							}
							callback(if (c1 == 0 && success) 0 else 5 + c1);
						}
					);
				} else {
					success = doLink();
					callback(if (success) 0 else 5);
				}
			}
		);
	} else {
		success = doLink();
		callback(if (success) 0 else 5);
	}
}
