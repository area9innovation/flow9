import tools/flowc/backends/nim/fi2nim_utils;
import tools/flowc/manipulation/tail_call;

export {
	fiNimCompileModule(env : FiNimEnv, module: FiModule) -> NimCompiledSource;
	fiNimCompileToplevel(env : FiNimEnv, decl: FiDeclaration) -> Maybe<NimCompiledFunc>;
}

fiNimCompileModule(env : FiNimEnv, module: FiModule) -> NimCompiledSource {
	missing_predefined = ref buildSet(map(nimPredefinedNatives, \p -> p.name));
	module_natives = filtermap(module.natives, \n -> 
		if (containsKeyTree(env.natives, n.name)) Some(n) else None()
	);
	module_natives_names = buildSet(map(module_natives, \n -> n.name));
	notNatives = filter(module.initOrder, \mn -> !containsSet(module_natives_names, mn));
	module_decls = concat(
		module_natives,
		filtermap(notNatives, \name -> lookupTree(env.prog.names.toplevel, name))
	);
	fcVerbose(env.cfg, 0, "Generating Nim code for module: '" + module.fileinfo.flowfile + "'");
	module_comment = NimLine(0, "# Module: " + module.fileinfo.flowfile);
	module_code = list2array(fold(module_decls, Cons(NimCompiledFunc(module_comment, module_comment), EmptyList()), \acc, decl ->
		eitherMap(fiNimCompileToplevel(env, decl), \compiled -> 
			Cons(NimCompiledFunc(compiled with def = fi2nimAddEmptyLine(compiled.def)), acc), 
			acc
		)
	));
	imported = concat(
		["flow_runtime", "flow_types", "osproc", "asyncdispatch", "strutils", "sequtils", "math", "flow_lib/httpServer_type"],
		map(set2array(module.allImports), \imp -> fiNimRenameFile(imp))
	);
	imports = fi2nimJoinNimCodeMany(concat(
		map(imported, \imp -> NimLine(0, "import " + imp)),
		concatA(filtermap(module_natives, \n ->
			maybeMap(lookupTree(env.natives, n.name), \nat ->
				map(nat.imports, \imp -> NimLine(0, imp))
			)
		))
	));
	forward_decls = fi2nimJoinNimCodeMany(map(module_code, \c -> c.decl));
	definitions = fi2nimJoinNimCodeMany(map(module_code, \c -> c.def));

	code = fi2nimJoinNimCodeMany([
		imports,
		NimLine(0, "# Forward declarations"),
		forward_decls,
		fi2nimEmptyLine(),
		NimLine(0, "# Source code of functions / global variables"),
		definitions,
		fi2nimEmptyLine(),
		if (exists(module.functions, \f -> f.name == "main")) NimLine(0, "main()") else NimSnippet("")
	]);
	NimCompiledSource(fiNimRenameFile(module.fileinfo.flowfile), code);
}

fiNimCompileToplevel(env0 : FiNimEnv, decl : FiDeclaration) -> Maybe<NimCompiledFunc> {
	env = FiNimEnv(env0 with currentToplevel = decl.name);
	if (env.cfg.verbose > 1) {
		println("\tcompiling decl: " + decl.name + " ... ");
	}
	compiled = switch (decl) {
		FiNativeDec(name, io, type, nativeName, fallback, __, __): {
			maybeMap(lookupTree(env.natives, name), \nat-> {
				// native is implemented in runtime
				env.native_imports := mergeSets(^(env.native_imports), buildSet(nat.imports));
				nat.code;
			});
		}
		FiFunctionDec(name, lambda,__,__, __): {
			fiNimCompileFunction(env, decl);
		}
		FiGlobalVar(name, __,__,__,__): {
			Some(fiNimCompileGlobalVar(env, decl));
		}
	};
	if (env.cfg.verbose > 1) {
		println("\t" + decl.name + " is COMPILED");
	}
	compiled;
}

fiNimNormalizeMain(func: FiFunctionDec) -> FiFunctionDec {
	if (func.name != "main" || func.type.returnType == FiTypeVoid()) func else {
		// In case 'main' is returning non-void value force it not to return.
		FiFunctionDec(func with
			type = FiTypeFunction(func.lambda.type with returnType = FiTypeVoid()),
			lambda = FiLambda(func.lambda with
				type = FiTypeFunction(func.lambda.type with returnType = FiTypeVoid()),
				body = FiSeq(concat(unFiSeq(func.lambda.body), [FiVoid(0)]), FiTypeVoid(), 0)
			)
		)
	}
}

fiNimCompileFunction(env : FiNimEnv, func0: FiFunctionDec) -> Maybe<NimCompiledFunc> {
	if (containsSet(nimForbiddenFunctions, func0.name)) {
		fcPrintln("Program uses the forbidden function: " + func0.name, env.cfg.threadId);
		None();
	} else {
		func = fiNimNormalizeMain(func0);
		type = FiTypeFunction(func.type with args = func.lambda.args);

		lambda = func.lambda;

		fn_typars = fiNimTypeParamsList(env, func.type);
		decl = "proc " + fiNimRenameGlobal(func.name) + "*" + fn_typars + fiNimCompileFuncType(env, type);
		has_tailcall = fcDetectTailCall(func);

		locals = fold(lambda.args, makeSet(), \acc, arg -> insertSet(acc, arg.name));
		fn_code = if (!fcDetectTailCall(func)) {
			fiNimCompile(env, lambda.body, FiNimReturnOut(), locals);
		} else {
			tailcall_args = map(lambda.args, \arg -> 
				FiFunArg(arg with name = fiNimRenameKeyword("tailcall_" + arg.name))
			);
			body_type = fiExpType(lambda.body);
			body = foldri(lambda.args, lambda.body, \i, acc, arg -> 
				fiRenameVar(acc, arg.name, tailcall_args[i].name)
			);
			env1 = FiNimEnv(env with 
				tailcall_fn = func.name,
				tailcall_args = tailcall_args
			);
			body_code = fiNimCompile(env1, body, FiNimReturnOut(), 
				fold(tailcall_args, locals, \acc, arg -> insertSet(acc, arg.name))
			);
			fi2nimJoinNimCode(
				fi2nimMakeNimBlock(0, mapi(lambda.args, \i, arg -> 
					"var " + fiNimRenameKeyword(tailcall_args[i].name) + ": " + 
						fiNimCompileType(env, arg.type) + " = " + 
						fiNimRenameKeyword(arg.name)
				)),
				fi2nimJoinNimCode(
					NimLine(0, "while true:"),
					fi2nimSureIndent(body_code)
				)
			);
		}
		//we cannot terminate the program, if there is no quit(). maybe quit() in some callback.
		waitMain = if (func.name == "main") {
			fi2nimMakeNimBlock(0, [
				"# Waiting code",
				"while true: waitFor(sleepAsync(2000000000))"
			]);
		} else {
			NimSnippet("")
		}
		Some(NimCompiledFunc(
			if (func.name == "main") NimSnippet("") else NimLine(0, decl),
			fi2nimJoinNimCode(
				NimLine(0, decl + (if (func.name == "main") " {.discardable.}" else "") + " ="), 
				fi2nimDecideToIndent(lambda.body, fi2nimJoinNimCode(fn_code, waitMain))
			)
		));
	}
}

fiNimCompileGlobalVar(env : FiNimEnv, var: FiGlobalVar) -> NimCompiledFunc {
	value = if (fiNimIsExp(var.value)) var.value else {
		FiCall(FiLambda([], var.value, FiTypeFunction([], var.type), 0), [], var.type, 0);
	}
	NimCompiledFunc(
		NimSnippet(""), 
		fi2nimJoinNimCode(
			NimLine(0, "var " + fiNimRenameGlobal(var.name) + "*: " + fiNimCompileType(env, var.type) + " ="),
			fi2nimDecideToIndent(value, fiNimCompile(env, value, FiNimReturnValue(), makeSet()))
		)
	);
}

fi2nimDecideToIndent(x: FiExp, c: NimCode) -> NimCode {
	if (fiNimIsExp(x)) fi2nimMaybeIndent(c) else fi2nimSureIndent(c);
}

fiNimIsExp(e: FiExp) -> bool {
	switch (e) {
		FiConst(): true;
		FiVar(__,__,__): true;
		FiLet(__,__,__,__,__,__): false;
		FiLambda(args, body, type, __): true;
		FiCall(fn, es, __, __): fiNimIsExp(fn) && forall(es, fiNimIsExp);
		FiSeq(es, __, __): {
			if (length(es) == 0) true else
			if (length(es) == 1) fiNimIsExp(es[0]) else false;
		}
		FiIf(e1, e2, e3, __, __):
			fiNimIsExp(e1) && fiNimIsExp(e2) && fiNimIsExp(e3);
		FiSwitch(x, __, cs, __, __): {
			false;
		}
		FiCallPrim(op, es, tp, __): {
			forall(es, fiNimIsExp) && 
			switch (op : FcPrim) {
				FcAssignPrim():	false;
				FcSetMutablePrim(name): false;
				default: true;
			};
		}
		FiCast(ex,__,__,__,__): fiNimIsExp(ex);
		default: false;
	}
}

FiNimReturn ::= FiNimReturnOut, FiNimReturnLocal, FiNimReturnValue, FiNimDiscardValue;
	FiNimReturnOut();
	FiNimReturnLocal(var: string);
	FiNimReturnValue();
	FiNimDiscardValue();

fiNimCompile(env : FiNimEnv, expr : FiExp, returns: FiNimReturn, locals: Set<string>) -> NimCode {
	wrap_ret = \c -> {
		make_ret = \-> switch (returns) {
			FiNimReturnOut(): fi2nimJoinNimCode(NimSnippet("return "), c);
			FiNimDiscardValue(): fi2nimJoinNimCode(NimSnippet("discard "), c)
			FiNimReturnLocal(var): fi2nimJoinNimCode(NimSnippet(fiNimRenameKeyword(var) + " = "), c);
			FiNimReturnValue(): c;
		}
		tp = fiExpType(expr);
		switch (tp) {
			FiTypeVoid(): c;
			FiTypeParameter(nm): {
				if (returns == FiNimReturnOut() || returns == FiNimDiscardValue()) {
					NimBlock([
						NimLine(0, "when " + fiNimCompileType(env, tp) + " is void:"),
						fi2nimSureIndent(c),
						NimLine(0, "else:"),
						fi2nimSureIndent(make_ret())
					]);
				} else {
					make_ret();
				}
			}
			default: make_ret();
		}
	}
	switch (expr) {
		FiVoid(__):    if (returns == FiNimReturnOut()) NimLine(0, "return") else NimLine(0, "discard");
		FiBool(b, __): wrap_ret(NimSnippet(if (b) "true" else "false"));
		FiInt(v, __):  wrap_ret(NimSnippet(i2s(v) + "i32"));
		FiDouble(d, __): {
			s = d2s(d);
			wrap_ret(NimSnippet(if (strIndexOf(s, ".") < 0 && strIndexOf(s, "e") < 0) s + ".0" else s));
		}
		FiString(s,__): {
			s1 = concatStrings(mapi(s2a(s), \i, code ->
				if (code == 0x09) "\\t" else
				if (code == 0x0a) "\\n" else
				if (code == 0x0d) "\\r" else
				if (code == 0x22) "\\\"" else
				if (code == 0x5c) "\\\\" else
				if (0 < code && code < 0x80) fromCharCode(code) else
				"\\u" + lpad(formatHex(code), "0", 4)
			));
			wrap_ret(NimSnippet("rt_utf8_to_string(\"" + s1 + "\")"));
		}
		FiVar(x, type,__): {
			x_code = if (containsSet(locals, x)) fiNimRenameKeyword(x) else fiNimRenameGlobal(x);
			typars = switch (type) {
				FiTypeFunction(args, rt): {
					n: Maybe<FiDeclaration> = lookupTree(env.prog.names.toplevel, x);
					switch (n) {
						Some(decl): {
							tps = fiMatchTyparsOrdered(decl.type, type);
							if (length(tps) == 0) "" else {
								"[" + superglue(tps, \tp -> fiNimCompileType(env, tp), ", ") + "]";
							}
						}
						None(): "";
					}
				}
				default: "";
			}
			wrap_ret(NimSnippet(x_code + typars));
		}
		FiLet(x, tp, e1, e2, __, __): {
			direct = \-> fi2nimJoinNimCode(
				fi2nimJoinNimCode(
					NimLine(0, "var " + fiNimRenameKeyword(x) + ": " + fiNimCompileType(env, tp) + " ="),
					fi2nimMaybeIndent(fiNimCompile(env, e1, FiNimReturnValue(), locals))
				),
				fi2nimCode2Block(fiNimCompile(env, e2, returns, insertSet(locals, x)))
			);
			indirect = \-> fi2nimJoinNimCode(
				fi2nimJoinNimCode(
					NimLine(0, "var " + fiNimRenameKeyword(x) + ": " + fiNimCompileType(env, tp)),
					fiNimCompile(env, e1, FiNimReturnLocal(x), locals)
				),
				fi2nimCode2Block(fiNimCompile(env, e2, returns, insertSet(locals, x)))
			);
			switch (e1) {
				FiSwitch(__,__,__,__,__): indirect();
				FiLet(__,__,__,__,__,__): indirect();
				default: direct();
			}
		}
		FiLambda(args, body, type, __): {
			wrap_ret(fi2nimJoinNimCode(
				NimLine(0, fiNimCompileType(env, FiTypeFunction(type with args = args)) + " ="), 
				fi2nimSureIndent(fiNimCompile(
					FiNimEnv(env with tailcall_fn = ""), body, FiNimReturnOut(), 
					fold(args, locals, \acc, arg -> insertSet(acc, arg.name))
				))
			));
		}
		FiCall(fn, es, __, __): {
			args_code: [NimCode] = map(es, \e -> fiNimCompile(env, e, FiNimReturnValue(), locals));
			switch (fn) {
				FiVar(x, __, __): {
					args: NimCode = fi2nimBracketNimCode(fi2nimGlueNimCode(args_code, ", "));
					switch (lookupTree(nimInlinedFuncs, x)) {
						Some(inline): {
							wrap_ret(fi2nimJoinNimCode(NimSnippet(inline), args));
						}
						None(): {
							if (!(returns == FiNimReturnOut() && x == env.tailcall_fn)) {
								fn_code = fiNimCompile(env, fn, FiNimReturnValue(), locals);
								wrap_ret(fi2nimJoinNimCode(fn_code, args));
							} else {
								// Tail call detected
								arg_is_not_changed = \y, i -> {
									switch (y) {
										FiVar(v,__,__): v == env.tailcall_args[i].name;
										default: false;
									}
								}
								tmp_arg = \i -> fiNimRenameKeyword("tmp__" + env.tailcall_args[i].name);
								fi2nimJoinNimCodeMany(concat3(
									filtermapi(args_code, \i, arg_code -> 
										if (arg_is_not_changed(es[i], i)) None() else {
											Some(fi2nimCode2Block(fi2nimJoinNimCode(
												NimLine(0, "let " + tmp_arg(i) + ": " + fiNimCompileType(env, env.tailcall_args[i].type) + " ="),
												fi2nimMaybeIndent(args_code[i])
											)));
										}
									),
									filtermapi(args_code, \i, arg_code -> 
										if (arg_is_not_changed(es[i], i)) None() else {
											Some(NimLine(0, fiNimRenameKeyword(env.tailcall_args[i].name) + " = " + tmp_arg(i)));
										}
									),
									[NimLine(0, "continue")]
								));
							}
						}
					}
				}
				default: {
					wrap_ret(fi2nimJoinNimCode(
						fi2nimBracketNimCode(fiNimCompile(env, fn, FiNimReturnValue(), locals)),
						fi2nimBracketNimCode(fi2nimGlueNimCode(args_code, ", "))
					));
				}
			}
		}
		FiSeq(es, __, __): {
			if (length(es) == 0) {
				if (returns == FiNimReturnOut()) NimLine(0, "return") else NimLine(0, "discard");
			} else if (length(es) == 1) {
				fiNimCompile(env, es[0], returns, locals);
			} else {
				fi2nimJoinNimCodeMany(mapi(es, \i, x -> {
					needs_discard = switch(x) {
						FiLet(__, __, __, __, __, __) : false;
						FiCallPrim(op, __, __, __) : {
							switch (op) {
								FcAssignPrim() : false;
								FcSetMutablePrim(__) : false;
								default: true;
							}
						}
						FiCall(__, __, tp, __): {
							switch (tp) {
								FiTypeVoid() : false;
								default : true;
							}
						}
						default: fiExpType(x) != FiTypeVoid();
					}
					x_returns = if (i + 1 == length(es)) returns else 
						if (needs_discard) FiNimDiscardValue() else FiNimReturnValue();
					fi2nimCode2Line(fiNimCompile(env, x, x_returns, locals));
				}))
			}
		}
		FiIf(e1, e2, e3, __, __): {
			fi2nimJoinNimCode(
				fi2nimJoinNimCode(
					fi2nimCode2Block(fi2nimJoinNimCode3(
						NimSnippet("if "), fiNimCompile(env, e1, FiNimReturnValue(), locals), NimSnippet(":")
					)), 
					fi2nimSureIndent(fiNimCompile(env, e2, returns, locals))
				),
				fi2nimJoinNimCode(
					NimLine(0, "else: "), 
					fi2nimSureIndent(fiNimCompile(env, e3, returns, locals))
				)
			);
		}
		FiSwitch(x, x_type, cs, __, __): {
			x_code = fiNimCompile(env, x, FiNimReturnValue(), locals);
			def_case = switch (find(cs, \c -> c.struct == "default")) {
				Some(dc): fi2nimMaybeIndent(fiNimCompile(env, dc.body, returns, locals));
				None():   fi2nimJoinNimCode3(
					NimSnippet("rt_runtime_error(\"illegal struct id in switch: \" & $"), x_code, NimSnippet(".str_id)")
				);
			}
			fi2nimJoinNimCodeMany(concat3(
				[fi2nimCode2Block(fi2nimJoinNimCodeMany([NimSnippet("case "), x_code, NimSnippet(".str_id"), NimSnippet(":")]))],
				map(filter(cs, \c -> c.struct != "default"), \c ->
					fi2nimJoinNimCode(
						NimLine(0, "of int32(" + fi2nimStructId(c.struct) + "): #[" + c.struct + "]#"), 
						fi2nimMaybeIndent(fiNimCompile(env, c.body, returns, locals))
					)
				),
				[fi2nimJoinNimCode(NimLine(0, "else: "), def_case)]
			));
		}
		FiCallPrim(op, es, tp, __): {
			args = map(es, \arg : FiExp -> fiNimCompile(env, arg, FiNimReturnValue(), locals));
			arg_type = if (length(es) > 0) fiExpType(es[0]) else FiTypeVoid();
			opt = switch (op : FcPrim) {
				FcOrPrim():     fi2nimBinOpNimCode(args[0], " or ", args[1]);
				FcAndPrim():    fi2nimBinOpNimCode(args[0], " and ", args[1]);
				FcNotPrim():    fi2nimJoinNimCode(NimSnippet("not "), args[0]);
				FcEqPrim():     fiNimCompileComparison(args[0], args[1], " == ");
				FcNePrim():     fiNimCompileComparison(args[0], args[1], " != ");
				FcLePrim():     fiNimCompileComparison(args[0], args[1], " <= ");
				FcGePrim():     fiNimCompileComparison(args[0], args[1], " >= ");
				FcLtPrim():     fiNimCompileComparison(args[0], args[1], " < ");
				FcGtPrim():     fiNimCompileComparison(args[0], args[1], " > ");
				FcPlusPrim():   fi2nimBinOpNimCode(args[0], if (arg_type == FiTypeString()) " & " else " + ", args[1]);
				FcMinusPrim():  fi2nimBinOpNimCode(args[0], " - ", args[1]);
				FcMulPrim():    fi2nimBinOpNimCode(args[0], " * ", args[1]);
				FcDivPrim():    fi2nimBinOpNimCode(args[0], if (arg_type == FiTypeInt()) " div " else " / ", args[1]);
				FcModPrim():    fi2nimBinOpNimCode(args[0], " mod ", args[1]);
				FcNegPrim():    fi2nimJoinNimCode(NimSnippet("-"), args[0]);
				FcArrayPrim():  fi2nimJoinNimCode3(NimSnippet("@["), fi2nimGlueNimCode(args, ", "), NimSnippet("]"));
				FcIndexPrim():  fi2nimJoinNimCodeMany([args[0], NimSnippet("["), args[1],  NimSnippet("]")]);
				FcDerefPrim():  fi2nimJoinNimCode(args[0], NimSnippet(".val"));
				FcAssignPrim():	fi2nimJoinNimCodeMany([NimSnippet("("), args[0], NimSnippet(".val = "), args[1], NimSnippet(")")]);
				FcRefPrim():    fi2nimJoinNimCode3(NimSnippet("Ref[" + fiNimCompileType(env, arg_type) + "](val: "), args[0], NimSnippet(")"));
				FcStructPrim(struct_name): {
					tn = cast(tp: FiType -> FiTypeName);
					fiMapTypeName(tn, env.prog.names, 
						\struct -> fi2nimJoinNimCode(
							NimSnippet(fiNimCompileType(env, tn)),
							fi2nimBracketNimCode(
								fi2nimGlueNimCode(
									concat(
										[NimSnippet("str_id: int32(" + fi2nimStructId(struct_name) + ")")],
										mapi(args, \i, arg -> fi2nimJoinNimCode(NimSnippet(fiNimRenameKeyword(struct.args[i].name) + ": "), arg))
									), ", "
								)
							)
						),
						\union -> fail0("cannot construct union (" + struct_name + ") explicitly: " + pretFiType(tn) + "\n" + 
							"in exp:\n" + strIndent(pretFiExp(expr))
						),
						\__ -> fail0("unknown struct or union: " + tn.name)
					);
				};
				FcFieldPrim(field): {
					if (field == "structname") {
						switch (arg_type) {
							FiTypeName(name,__): {
								fi2nimJoinNimCode3(
									NimSnippet("rt_struct_name_wrapper("), 
									args[0], NimSnippet(", rt_utf8_to_string(\"" + name + "\"))")
								);
							}
							FiTypeFlow(): {
								fi2nimJoinNimCode3(
									NimSnippet("rt_flow_struct_name("), 
									args[0], NimSnippet(")")
								);
							}
							default: fail0("field may be get of a struct or a flow, got: " + pretFiExp(expr) + " of type: " + pretFiType(arg_type));
						}
					} else {
						switch (arg_type) {
							FiTypeName(__,__): {
								fi2nimJoinNimCode(args[0], NimSnippet("." + fiNimRenameKeyword(field)));
							}
							FiTypeFlow(): {
								fi2nimJoinNimCode3(
									NimSnippet("rt_get_flow_field("), 
									args[0], NimSnippet(", rt_utf8_to_string(\"" + field + "\"))")
								);
							}
							default: fail0("field may be get of a struct or a flow, got: " + pretFiExp(expr) + " of type: " + pretFiType(arg_type));
						}
					}
				}
				FcSetMutablePrim(field): {
					fi2nimBracketNimCode(fi2nimJoinNimCode3(
						args[0], NimSnippet("." + fiNimRenameKeyword(field) + " = "), args[1]
					))
				}
				FcCreateClosurePrim(__, __): fail0("TODO");
                FcQuote(): fail0("TODO");
                FcUnquote(): fail0("TODO");
			};
			wrap_ret(opt);
		}
		FiCast(e, from, to, __, __): {
			e_code = fiNimCompile(env, e, FiNimReturnValue(), locals);
			cast_code = if (fiEqualTypes(from, to)) e_code else {
				fiNimCompileCast(env, from, to, e_code, expr);
			}
			wrap_ret(cast_code);
		}
		default: {
			fail0("Not yet supported in Nim backend: " + pretFiExp(expr));
		}
	}
}

fiNimCompileComparison(x: NimCode, y: NimCode, op: string) -> NimCode {
	fi2nimJoinNimCodeMany([
		NimSnippet("(rt_compare("), x, NimSnippet(", "), y, NimSnippet(")"), NimSnippet(op), NimSnippet("0)")
	]);
}

fiNimCompileCast(env: FiNimEnv, from: FiType, to: FiType, ex: NimCode, expr: FiExp) -> NimCode {
	make_cast = \prefix -> fi2nimJoinNimCode(NimSnippet(prefix), fi2nimBracketNimCode(ex));
	def_cast = \-> make_cast("cast[" + fiNimCompileType(env, to) + "]");
	err_cast = \-> fail0("illegal cast from " + pretFiType(from) + " to " + pretFiType(to) + ", expression:\n" + pretFiExp(expr));
	if (from == FiTypeFlow()) {
		fi2nimJoinNimCode3(NimSnippet("rt_from_flow("), ex, NimSnippet(", " + fiNimCompileType(env, to) + ")"));
	} else {
		switch (to) {
			FiTypeName(to_name,__): {
				fiMapTypeName(to, env.prog.names, 
					\struct_to -> switch (from) {
						FiTypeName(from_name,__): 
							fiMapTypeName(from, env.prog.names, 
								\struct_from -> if (to_name == from_name) def_cast() else err_cast(),
								\union_from -> def_cast(),
								\err -> err_cast()
							);
						default: err_cast();
					},
					\union_to -> switch (from) {
						FiTypeName(__,__): def_cast();
						default: err_cast();
					}, 
					\err -> err_cast()
				);
			}
			FiTypeArray(__): switch (from) {
				FiTypeArray(__): def_cast();
				default: err_cast();
			}
			FiTypeRef(__): switch (from) {
				FiTypeRef(__): def_cast();
				default: err_cast();
			}
			FiTypeFunction(__,__): switch (from) {
				FiTypeFunction(__,__): def_cast();
				default: err_cast();
			}
			FiTypeFlow():   make_cast("rt_to_flow");
			FiTypeString(): make_cast("rt_to_string");
			FiTypeDouble(): make_cast("rt_to_double");
			FiTypeBool():   make_cast("rt_to_bool");
			FiTypeInt():    make_cast("rt_to_int");
			FiTypeNative(): make_cast("rt_to_native");
			FiTypeParameter(__): def_cast();
			FiTypeVoid(): err_cast();
		}
	}
}
