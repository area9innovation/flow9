import tools/flowc/backends/inca/lift_exp;
import tools/flowc/backends/inca/lift_graph;
import text/blueprint;

export {
	// Lift our code to lifted, mutable versions
	liftIncaFunctions(env : IncaEnv, graph : SimpleGraph<string, string>) -> string;
}

liftIncaFunctions(env : IncaEnv, graph : SimpleGraph<string, string>) -> string {
	perform = liftIncaGraph(env, graph);
	blueprint("
		import out/types;
		import inca/effects_lifted;

		export {
			performIncaEffect_lifted(v : IncaValue) -> void;

			%dec%
		}

		%perform%

		%def%
	", [
		"perform", perform,
		"dec", foldTree(env.functions, "", \name, fn, acc -> acc + (if (acc != "") "\n" else "") + name + "_lifted(" + superglue(fn.lambda.args, \a -> a.name + " : IncaValue", ", ") + ") -> IncaValue;"),
		"def", foldTree(env.functions, "", \name, fn, acc -> acc + liftIncaFunction(env, name, fn) + "\n")
	]);
}

liftIncaFunction(env : IncaEnv, name : string, fn : FiFunctionDec) -> string {
	name + "_lifted(" + superglue(fn.lambda.args, \a -> a.name + "_lifted : IncaValue", ", ") + ") -> IncaValue {\n"
	+ liftIncaExp(fn.lambda.body, 1) + ";\n"
	+ "}\n";
}
