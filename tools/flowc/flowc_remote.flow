import text/serialize;
import net/http;
import tools/common/config;
import tools/flowc/flowc_local;

export {
	// Run a task, described by a config, on a remote flowc server
	runConfigRemote(
		config : CompilerConfig, 
		onData: (flow) -> void, 
		onError : (string) -> void, 
		onRunComplete : (int) -> void,
		onServerError : (int, string) -> void
	) -> void;

	// Try to run config remote, in case it fails run locally.
	runConfig(config : CompilerConfig, globalEnv : FcTypeEnvGlobal, onRunComplete : (int) -> void) -> void;
}

runConfig(config : CompilerConfig, globalEnv : FcTypeEnvGlobal, onRunComplete : (int) -> void) -> void {
	if (isConfigParameterFalse(config.config, "server") || !java) {
		runConfigLocally(config, globalEnv, onRunComplete);
	} else {
		runConfigRemote(
			config, 
			\msg -> fcPrintln(msg, config.threadId), 
			\msg -> fcPrintln(msg, config.threadId), 
			onRunComplete, 
			\code, msg -> {
				if (isConfigParameterTrue(config.config, "client-mode")) {
					fcPrintln("Error " + i2s(code) + ": " + msg, config.threadId);
				} else {
					runConfigLocally(config, globalEnv, onRunComplete);
				}
			}
		)
	}
}

runConfigRemote(conf : CompilerConfig, onData: (flow) -> void, onError : (string) -> void, onRunComplete : (int) -> void, onServerError : (int, string) -> void) -> void {
	serverUrl = 
		"http://localhost:" +
		getConfigParameterDef(conf.config, "server-port", "10001");
	patchedConfig = patchRemoteConfig(conf);
	serialized_server_job = 
		isConfigParameterSet(conf.config, "compile-expression") || 
		isConfigParameterTrue(conf.config, "dump-program");
	httpCustomRequest(
		serverUrl,
		GET(), [], RequestPayload("config_" + serialize(patchedConfig)),
		\status, data, headers -> {
			code = status - 200;
			if (code == 0) {
				if (serialized_server_job) {
					value = deserialize(data, IllegalStruct());
					if (value == IllegalStruct()) {
						onError("WARNING: Illegal serialized data in response.");
					} else {
						onData(value);
					}
				} else {
					onData(data);
				}
				onRunComplete(0);
			} else if (0 < code && code < 64) {
				onError("Compilation error " + i2s(code) + "\n" + data);
				onRunComplete(code);
			} else {
				onServerError(status, data);
			}
		},
		false
	)
}

patchRemoteConfig(conf : CompilerConfig) -> CompilerConfig {
	cwd = strReplace(resolveRelativePath("."), "\\", "/");

	// Setup the 'output-dir' in case it is not set explicitly.
	conf1 = switch (lookupTree(conf.config, "output-dir")) {
		Some(__): conf.config;
		None():   setTree(conf.config, "output-dir", cwd);
	}
	// Add a flowfile as a config tree option.
	conf2 = setTree(conf1, "file", conf.flowfile);

	// Setup the 'working-dir' option
	conf3 = setTree(conf2, "working-dir", lookupTreeDef(conf2, "working-dir", cwd));

	// Resolve relative path for 'fontconfig-file', because server may run in a different place.
	conf4 = switch (lookupTree(conf3, "fontconfig-file")) {
		Some(file): setTree(conf3, "fontconfig-file", resolveRelativePath(file));
		None():     conf3;
	}
	CompilerConfig(conf with config = conf4);
}
