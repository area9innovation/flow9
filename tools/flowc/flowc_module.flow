import tools/flowc/flowc_timephases;
import tools/flowc/flow_parser;
import tools/flowc/incremental;
import tools/flowc/flowc_helpers;
import tools/flowc/desugar;
import tools/flowc/incremental/fi2fc;

export {
	dummyFcError = FcError("", []);

	// Parses a flow file (or grabs it from incremental cache)
	parseFlowFileOrLoadCached(
		config : CompilerConfig,
		globEnv : FcTypeEnvGlobal,
		// The flow path to a file to read
		file : string,
		// What file imports this, and where?
		fromfile : string, start : int, end : int
	) -> Pair<FcPreloadedModule, FcError>;

	parseFlowfile(config : CompilerConfig, fileinfo : FcFileInfo, fromfile : string) -> Pair<FcParsed, Maybe<FcError>>;
}

parseFlowFileOrLoadCached(config : CompilerConfig, globEnv : FcTypeEnvGlobal, file : string, fromfile : string, start : int, end : int) -> Pair<FcPreloadedModule, FcError> {
	switch (fcLoadFromCache(globEnv, file)) {
		Some(cached): {
			loaded = cached.loaded;
			module = loaded.module;
			needToReload = cached.outdated || fcModuleNeedReloading(module.fileinfo, module.stringIncludes, config);

			if ((!isConfigParameterTrue(config.config, "incremental-priority") && needToReload) || isConfigParameterFalse(config.config, "incremental")) {

				if (needToReload && config.verbose >= 1) {
					fcPrintln("Outdated cache entry: " + module.fileinfo.flowfile, config.threadId);
					fiFileInfoFileChangedMessage(module.fileinfo, config);
					iter(module.stringIncludes, \inc -> fiFileInfoFileChangedMessage(inc, config));
				}
				doParseFlowFileOrLoadCached(config, globEnv, file, fromfile, start, end);
			} else {
				if (config.verbose >= 1) fcPrintln("Reusing from cache module: " + file, config.threadId);
				Pair(FcLoaded(module, fcDependencyChangedCallback(config, globEnv, module)), dummyFcError);
			}
		}
		None(): {
			doParseFlowFileOrLoadCached(config, globEnv, file, fromfile, start, end);
		}
	}
}

doParseFlowFileOrLoadCached(
	config : CompilerConfig,
	globEnv : FcTypeEnvGlobal,
	file : string, 
	fromfile : string, 
	start : int, end : int
) -> Pair<FcPreloadedModule, FcError> {
	fileinfo = getFcFileInfo(file, config);
	if (fileExists(fileinfo.fullpath)) {

		// TODO: Inside this result, we have a list of old exported names, which
		// we should use for more fine-grained dependency tracking

		preloadResult = if (!isConfigParameterFalse(config.config, "incremental")) {
			preloadIncrementalModule(config, globEnv, fileinfo.flowfile)
		} else PreloadResult(None(), makeTree());

		switch (preloadResult.module : Maybe<FcLoaded>) {
			Some(loaded): {
				preloaded : FcPreloadedModule = loaded;
				Pair(preloaded, dummyFcError);
			}
			None(): {
				pair = parseFlowfile(config, fileinfo, fromfile);
				switch (pair.second) {
					None(): {
						parsed : FcPreloadedModule = FcParsed(pair.first with oldExports = preloadResult.oldExports);
						Pair(parsed, dummyFcError)
					}
					Some(err):{
						parsed : FcPreloadedModule = FcParsed(fileinfo, dummySyntaxTree, extractFcImports(fileinfo, pair.first.syntaxtree), [], makeTree());
						Pair(parsed, err)
					}
				}
			}
		}
	} else {
		parsed : FcPreloadedModule = FcParsed(fileinfo, dummySyntaxTree, [], [], makeTree());
		error = FcError("Import " + file + ": file not found. Check flow.config or use I=<includes>", [FcPosition(fromfile, start, end)]);
		Pair(parsed, error);
	}
}

parseFlowfile(config : CompilerConfig, fileinfo : FcFileInfo, fromfile : string) -> Pair<FcParsed, Maybe<FcError>> {
	if (config.verbose >= 1) {
		fcPrintln("Parsing '" + fileinfo.flowfile + "'" + (if (fromfile == "") "" else " from '" + fromfile + "'"), config.threadId);
	}
	// To avoid crashing things, we reject files larger than 2MB
	if (strlen(fileinfo.content) >= 2 * 1024 * 1024) {
		error = FcError("File too large " + i2s(strlen(fileinfo.content)), [FcPosition(fromfile, 0, 0)]);
		Pair(FcParsed(fileinfo, dummySyntaxTree, [], [], makeTree()), Some(error));
	} else {
		pr = parse_flow(fileinfo.content);
		st = pr.first;
		err = pr.second;
		errorM = if (st.end != strlen(fileinfo.content)) {
			// Check if fileinfo.content contains common imperative constructs
			whilePos = strIndexOf(fileinfo.content, "while (");
			forPos = strIndexOf(fileinfo.content, "for (");
			returnPos = strIndexOf(fileinfo.content, "return ");

			if (whilePos >= 0) {
				errorMsg = "Syntax error: Flow9 does not support 'while' loops. Flow is a functional language - use recursion, map, filter, or fold instead.";
				Some(FcError(errorMsg, [FcPosition(fileinfo.flowfile, whilePos, whilePos + 7)]));
			} else if (forPos >= 0) {
				errorMsg = "Syntax error: Flow9 does not support 'for' loops. Flow is a functional language - use recursion, map, filter, or fold instead.";
				Some(FcError(errorMsg, [FcPosition(fileinfo.flowfile, forPos, forPos + 5)]));
			} else if (returnPos >= 0) {
				errorMsg = "Syntax error: Flow9 does not have a 'return' keyword. In Flow, the last expression in a block is automatically the return value.";
				Some(FcError(errorMsg, [FcPosition(fileinfo.flowfile, returnPos, returnPos + 6)]));
			} else {
				// Fall back to the generic syntax error message
				Some(FcError("Syntax error: expected one of " + strGlue(err.expected, ", "), [FcPosition(fileinfo.flowfile, err.pos, err.pos)]));
			}
		} else None();
		Pair(FcParsed(fileinfo, st, extractFcImports(fileinfo, st), extractFcForbids(fileinfo, st), makeTree()), errorM);
	}
}

