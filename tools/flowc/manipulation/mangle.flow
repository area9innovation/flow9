import tools/flowc/incremental/fimodule;
import tools/flowc/incremental/fi_helpers;
import text/blueprint;

export {
	// Construct a type name encoding for a type, for use in mangling specialized
	// polymorphic types
	mangleSuffix(f : FiType) -> string;

    // unmangle type - restore type by mangled suffix
    unmangleType(suffix : string) -> FiType;

	specializeFiFunctionDec(
		f : FiFunctionDec,
		polymorphics : Tree<string, FiType>,
		specializations : Tree<string, Set<FiType>>,
		mangle : bool
	) -> [FiFunctionDec];

	specializeFiNativeDec(
		n : FiNativeDec,
		polymorphics : Tree<string, FiType>,
		specializations : Tree<string, Set<FiType>>,
		mangle : bool		
	) -> [FiNativeDec];

	specializationFiTypeStruct(
		s : FiTypeStruct,
		polymorphics : Tree<string, FiType>,
		specializations : Tree<string, Set<FiType>>,
		mangleFields : bool // whether mangle field types or not
	) -> [FiTypeStruct];

	specializationFiTypeUnion(
		s : FiTypeUnion,
		polymorphics : Tree<string, FiType>,
		specializations : Tree<string, Set<FiType>>,
		mangle : bool
	) -> [FiTypeUnion];

	// only mangles type in initializer - there should not be any unbound parameters
	specializeGlobalVar(
		v : FiGlobalVar,
		polymorphics : Tree<string, FiType>
	) -> FiGlobalVar;

    getTypeName(type : FiType) -> string;
    getTypars(type : FiType) -> [FiType];
}

mangleSuffix(f : FiType) -> string {
	suffix = switch (f) {
		FiTypeArray(t): "a" + mangleSuffix(t);
		FiTypeFunction(args, returnType): "c" + i2s(length(args)) + superglue(args, \a -> mangleSuffix(a.type), "") + mangleSuffix(returnType);
		FiTypeRef(t): "r" + mangleSuffix(t);
		FiTypeParameter(n): {
			"p" + i2s(strlen(n)); // Not supposed to happen
		}
		FiTypeBool(): "b";
		FiTypeInt(): "i";
		FiTypeDouble(): "d";
		FiTypeString(): "s";
		FiTypeFlow(): "f";
		FiTypeVoid(): "v";
		FiTypeNative(): "n";
		FiTypeName(name, typeparameters): "t" + i2s(length(typeparameters)) + superglue(typeparameters, mangleSuffix, "");
	}
	suffix;
}

unmangleType(suffix : string) -> FiType {
    doUnmangleType(suffix).first;
}

parseTypeSequence(suffix : string, start : int) -> Pair<[FiType], int> {
    // only support up to 9 arguments
    count = s2i(getCharAt(suffix, start));
    foldRange(0, count - 1, Pair([], start + 1), \acc, i -> {
        argType = doUnmangleType(strRight(suffix, acc.second));
        Pair(arrayPush(acc.first, argType.first), argType.second + acc.second);
    });
}

doUnmangleType(suffix : string) -> Pair<FiType, int> {
    char = getCharAt(suffix, 0);
    right = strRight(suffix, 1);
    if (char == "a") {
        inner = doUnmangleType(right);
        Pair(FiTypeArray(inner.first), inner.second);
    } else if (char == "c") {
        args : Pair<[FiType], int> = parseTypeSequence(suffix, 1);
        returnType = doUnmangleType(strRight(suffix, args.second));
        Pair(FiTypeFunction(map(args.first, \a -> FiFunArg("", a)), returnType.first), returnType.second);
    } else if (char == "r") {
        inner = doUnmangleType(right);
        Pair(FiTypeRef(inner.first), inner.second);
    } else if (char == "p") {
        num = s2i(getCharAt(suffix, 1));
        Pair(FiTypeParameter(strRepeat("?", num)), 2);
    } else if (char == "b") {
        Pair(FiTypeBool(), 1);
    } else if (char == "i") {
        Pair(FiTypeInt(), 1);
    } else if (char == "d") {
        Pair(FiTypeDouble(), 1);
    } else if (char == "s") {
        Pair(FiTypeString(), 1);
    } else if (char == "f") {
        Pair(FiTypeFlow(), 1);
    } else if (char == "v") {
        Pair(FiTypeVoid(), 1);
    } else if (char == "n") {
        Pair(FiTypeNative(), 1);
    } else if (char == "t") {
        params = parseTypeSequence(suffix, 1);
        Pair(FiTypeName("", params.first), params.second);
    } else {
        println("Invalid mangle string: " + suffix);
        Pair(FiTypeVoid(), 1);
    }
}

mangleName(name : string, t : FiType) -> string {
	/*if (!fiTypeFullyDefined(t)) {
		println("Incomplete type being mangled!");
		println(t);
		//printCallstack();
	}*/
	switch (t) {
		FiTypeName(n, tps): 
			if (n == name && length(tps) == 0) 
				name
			else	
				doMangleName(name, t);
		default:
			doMangleName(name, t);
	}
}

doMangleName(name : string, t : FiType) -> string {
	suffix = mangleSuffix(t);
/*	if (strContains(suffix, "fp")) {
		println(t);
		//printCallstack();
	}*/
	name + "___" + suffix;
}

getTypeName(type : FiType) -> string {
    switch (type) {
        FiTypeName(name, typeparameters): name;
        default: "";
    }
}

getTypars(type : FiType) -> [FiType] {
    switch (type) {
        FiTypeName(name, typeparameters): typeparameters;
        default: [];
    }
}

specializeEntity(
	entityName : string,
	polymorphics : Tree<string, FiType>,
	specializations : Tree<string, Set<FiType>>,
	constructSpecialized : (mangledName : string, typars : Tree<string, FiType>) -> ?
) -> [?] {
	mpoly = lookupTree(polymorphics, entityName);
	switch (mpoly) {
		None(): [constructSpecialized(entityName, makeTree())]; // no specialization occurs - but maybe it is needed further down
		Some(poly): {
			specs = lookupTreeDef(specializations, entityName, makeSet());
			// map of mangled name -> specialization - used to remove specializations resulting in the same mangled name
			// allows mangling to work as a merging transformation too
			uniqueSpecs = foldSet(specs, makeTree(), \tree, s -> setTree(tree, mangleName(entityName, s), s));
			convertTreeToArray(uniqueSpecs, \mangled, s : FiType -> {
				typars = fiMatchTypars(poly, s, makeTree());
				constructSpecialized(mangled, typars);
			});
		}
	}
}

// specializes function body and creates new FiFunctionDec
specializeFunctionBody(f : FiFunctionDec, mangledName : string, typars : Tree<string, FiType>, 
	polymorphics : Tree<string, FiType>, mangle : bool) 
{
	functionType = instantiateFiTypeFunctionTypars(f.type, typars, mangle);
	body = instantiateFiLambdaTypars(f.lambda, typars, polymorphics, makeSet(), mangle);
	FiFunctionDec(mangledName, body, functionType, f.declStart, f.defiStart);
}

// specializes function and deals with unbound type parameters inside
specializeFunction(f : FiFunctionDec, mangledName : string, typars : Tree<string, FiType>, 
	polymorphics : Tree<string, FiType>, mangle : bool) {

	// always instantiating without mangle first time to detect unbound parameters correctly
	draftType = instantiateFiTypeFunctionTypars(f.type, typars, false);
	// if function parameters are fully specialized, detect unbound type params inside function body
	// and replace with flow
	if (fiTypeFullyDefined(draftType)) {
		// always instantiating without mangle first time to detect unbound parameters correctly
		draftBody = instantiateFiLambdaTypars(f.lambda, typars, polymorphics, makeSet(), false);
		bodyParams = fiTypeGetUnboundParametersExp(draftBody);
		if (!isEmptySet(bodyParams)) {
			// if there are unbound parameters in function bodies, replace them with flow and do everything again
			extendedTypars = foldSet(bodyParams, typars, \acc, n -> setTree(acc, n, FiTypeFlow()));
			specializeFunction(f, mangledName, extendedTypars, polymorphics, mangle);
		} else {
			// otherwise, just proceed - if !mangle, use existing versions, or redo
			if (mangle)
				specializeFunctionBody(f, mangledName, typars, polymorphics, mangle)
			else
				FiFunctionDec(f with name=mangledName, lambda=draftBody, type=draftType)
		}
	} else { // otherwise, just do what we can - this is not the final step then
		specializeFunctionBody(f, mangledName, typars, polymorphics, mangle);
	}
}

// Construct all required specializations of a given function declaration
specializeFiFunctionDec(
	f : FiFunctionDec,
	polymorphics : Tree<string, FiType>,
	specializations : Tree<string, Set<FiType>>,
	mangle : bool
) -> [FiFunctionDec] {
	specializeEntity(
		f.name,
		polymorphics,
		specializations,
		\mangledName, typars -> {
			specializeFunction(f, mangledName, typars, polymorphics, mangle)
		});
}

// Construct all required specializations of a given native declaration
specializeFiNativeDec(
	n : FiNativeDec,
	polymorphics : Tree<string, FiType>,
	specializations : Tree<string, Set<FiType>>,
	mangle : bool
) -> [FiNativeDec] {
	specializeEntity(
		n.name,
		polymorphics,
		specializations,
		\mangledName, typars -> {
			FiNativeDec(mangledName, n.io, 
				instantiateFiTypeTypars(n.type, typars, mangle),
				n.nativeName,
				instantiateFiExpTypars(n.fallbackLambda, typars, polymorphics, makeSet(), mangle), 
				n.declStart, n.defiStart
			)
		});
}

specializationFiTypeStruct(
	struct_ : FiTypeStruct,
	polymorphics : Tree<string, FiType>,
	specializations : Tree<string, Set<FiType>>,
	mangleFields : bool
) -> [FiTypeStruct] {
	specializeEntity(
		struct_.name,
		polymorphics,
		specializations,
		\mangledName, typars -> {
			FiTypeStruct(
				if (mangleFields) mangledName else struct_.name, 
				[], // empty as all parameters are specialized
				map(struct_.args, \sa -> {
					FiStructArg(
						sa.name,
						instantiateFiTypeTypars(sa.type, typars, mangleFields),
						sa.ismutable
					)
				}),
				struct_.start
			)
		}
	);
}


specializationFiTypeUnion(
	union : FiTypeUnion,
	polymorphics : Tree<string, FiType>,
	specializations : Tree<string, Set<FiType>>,
	mangle : bool
) -> [FiTypeUnion] {
	specializeEntity(
		union.name,
		polymorphics,
		specializations,
		\mangledName, typars -> {
			FiTypeUnion(
				mangledName, 
				[], // empty as all type parameters are specialized
				map(union.typenames, \tn -> {
					// instantiating using union type parameters - i.e. List<T>(EmptyList, Cons<T>)
					// is in fact List<T>(EmptyList<T>, Cons<T>)
					cast(instantiateFiTypeTypars(FiTypeName(tn.name, union.typeparameters), typars, mangle) : FiType -> FiTypeName)
				}),
				union.start
			)
		},
	);
}

specializeGlobalVar(v : FiGlobalVar, polymorphics : Tree<string, FiType>) -> FiGlobalVar {
	FiGlobalVar(
		v.name,
		instantiateFiExpTypars(v.value, makeTree(), polymorphics, makeSet(), true),
		instantiateFiTypeTypars(v.type, makeTree(), true),
		v.declStart,
		v.defiStart
	);
}


instantiateFiExpTypars(f : FiExp, typars : Tree<string, FiType>, polymorphic : Tree<string, FiType>,
	bound : Set<string>, mangle : bool) -> FiExp {
	switch (f) {
		FiLambda(args, body, type, start):  // bound to be updated inside
			instantiateFiLambdaTypars(f, typars, polymorphic, bound, mangle);
		FiCall(fn, args, type, start): {
			FiCall(
				instantiateFiExpTypars(fn, typars, polymorphic, bound, mangle), 
				map(args, \a -> instantiateFiExpTypars(a, typars, polymorphic, bound, mangle)), 
				instantiateFiTypeTypars(type, typars, mangle), 
				start
			);
		}
		FiVar(name, type, start): 
			instantiateFiVarTypars(f, typars, polymorphic, bound, mangle);
		FiLet(name, type, e1, e2, type2, start): {
			FiLet(
				name,
				instantiateFiTypeTypars(type, typars, mangle), 
				instantiateFiExpTypars(e1, typars, polymorphic, bound, mangle), 
				instantiateFiExpTypars(e2, typars, polymorphic, insertSet(bound, name), mangle), 
				instantiateFiTypeTypars(type2, typars, mangle), 
				start
			);
		}
		FiIf(e1, e2, e3, type, start):  {
			FiIf(
				instantiateFiExpTypars(e1, typars, polymorphic, bound, mangle), 
				instantiateFiExpTypars(e2, typars, polymorphic, bound, mangle), 
				instantiateFiExpTypars(e3, typars, polymorphic, bound, mangle), 
				instantiateFiTypeTypars(type, typars, mangle), 
				start
			);
		}
		FiSwitch(x, switchType, cases, type, start):  {
			FiSwitch(
				instantiateFiVarTypars(x, typars, polymorphic, bound, mangle),
				instantiateFiTypeTypars(switchType, typars, mangle),
				map(cases, \c -> {
					structType = instantiateFiTypeTypars(FiTypeName(c.struct, getTypars(switchType)), typars, mangle);
					FiCase(getTypeName(structType), c.argNames, 
						instantiateFiExpTypars(c.body, typars, polymorphic, 
							fold(c.argNames, bound, \acc, a -> insertSet(acc, a)), mangle),
						c.start
					);
				}), 
				instantiateFiTypeTypars(type, typars, mangle), 
				start
			);
		}
		FiCast(e, tFrom, tTo, type, start):  {
			FiCast(
				instantiateFiExpTypars(e, typars, polymorphic, bound, mangle), 
				instantiateFiTypeTypars(tFrom, typars, mangle), 
				instantiateFiTypeTypars(tTo, typars, mangle), 
				instantiateFiTypeTypars(type, typars, mangle), 
				start
			);
		}
		FiSeq(es, type, start):  {
			FiSeq(
				map(es, \a -> instantiateFiExpTypars(a, typars, polymorphic, bound, mangle)), 
				instantiateFiTypeTypars(type, typars, mangle), 
				start
			);
		}
		FiCallPrim(op, es, type, start): {
			instantiatedType = instantiateFiTypeTypars(type, typars, mangle);
			instantiatedOp = switch (op) {
				FcStructPrim(name): 
					FcStructPrim(
						if (mangle && containsKeyTree(polymorphic, name)) {
							cast(instantiatedType : FiType -> FiTypeName).name;
						} else name
					);
				FcCreateClosurePrim(structName, lambdaName): {
					lambdaType = cast(type : FiType -> FiTypeFunction);
					instantiatedLambdaType = instantiateFiTypeFunctionTypars(lambdaType, typars, false);
					instantiatedClosureType = lastElement(instantiatedLambdaType.args, FiFunArg("", FiTypeVoid())).type;

					FcCreateClosurePrim(
						if (mangle && containsKeyTree(polymorphic, structName)) {
							cast(mangleType(instantiatedClosureType) : FiType -> FiTypeName).name
						} else structName,
						if (mangle && containsKeyTree(polymorphic, lambdaName)) {
							mangleName(lambdaName, instantiatedLambdaType);
						} else lambdaName
					);
				}
				default: op;
			}
			FiCallPrim(
				instantiatedOp,
				map(es, \a -> instantiateFiExpTypars(a, typars, polymorphic, bound, mangle)), 
				instantiatedType,
				start
			);
		}
		FiVoid(start): f;
		FiDouble(d, start): f;
		FiInt(i, start): f;
		FiString(s, start): f; 
		FiBool(b, start): f;
		FiRequire(flowfile, e, type, start): {
			FiRequire(flowfile, instantiateFiExpTypars(e, typars, polymorphic, bound, mangle), 
				instantiateFiTypeTypars(type, typars, mangle), start);
		}
		FiUnsafe(name, fallback, type, start): {
			FiUnsafe(name, instantiateFiExpTypars(fallback, typars, polymorphic, bound, mangle), 
				instantiateFiTypeTypars(type, typars, mangle), start);
		}
	}
}

instantiateFiVarTypars(v : FiVar, typars : Tree<string, FiType>, polymorphic : Tree<string, FiType>,
	bound : Set<string>, mangle : bool) -> FiVar {

	mangled = if (mangle && !containsSet(bound, v.name) && containsKeyTree(polymorphic, v.name)) {
		mangleName(v.name, instantiateFiTypeTypars(v.type, typars, false))
	} else 
		v.name;
	
	FiVar(mangled, instantiateFiTypeTypars(v.type, typars, mangle), v.start);
}

instantiateFiLambdaTypars(f : FiLambda, typars : Tree<string, FiType>, polymorphic : Tree<string, FiType>,
	bound : Set<string>, mangle : bool) -> FiLambda {
	FiLambda(
		map(f.args, \a -> FiFunArg(a.name, instantiateFiTypeTypars(a.type, typars, mangle))), 
		instantiateFiExpTypars(f.body, typars, polymorphic, 
			fold(f.args, bound, \acc, a -> insertSet(acc, a.name)),
			mangle), 
		instantiateFiTypeTypars(f.type, typars, mangle), 
		f.start
	);
}

mangleType(type : FiType) -> FiType {
	switch (type) {
		FiTypeArray(t): FiTypeArray(mangleType(t));
		FiTypeFunction(args, returnType): FiTypeFunction(map(args, \a -> FiFunArg(a.name, mangleType(a.type))),
			mangleType(returnType));
		FiTypeRef(t):  FiTypeRef(mangleType(t));
		FiTypeParameter(n): FiTypeParameter(n); // should not happen
		FiTypeBool(): type;
		FiTypeInt(): type;
		FiTypeDouble(): type;
		FiTypeString(): type;
		FiTypeFlow(): type;
		FiTypeVoid(): type;
		FiTypeNative(): type;
		FiTypeName(name, typeparameters): 
				FiTypeName(mangleName(name, type), []);
	}
}

instantiateFiTypeTypars(type : FiType, typars : Tree<string, FiType>, mangle : bool) -> FiType {
	switch (type) {
		FiTypeArray(t): FiTypeArray(instantiateFiTypeTypars(t, typars, mangle));
		FiTypeFunction(args, returnType): instantiateFiTypeFunctionTypars(type, typars, mangle);
		FiTypeRef(t):  FiTypeRef(instantiateFiTypeTypars(t, typars, mangle));
		FiTypeParameter(n): {
			resolved = lookupTreeDef(typars, n, type);
			if (mangle && resolved != type) 
				mangleType(resolved)
			else
				resolved;
		}
		FiTypeBool(): type;
		FiTypeInt(): type;
		FiTypeDouble(): type;
		FiTypeString(): type;
		FiTypeFlow(): type;
		FiTypeVoid(): type;
		FiTypeNative(): type;
		FiTypeName(name, typeparameters): {
			instance = instantiateFiTypeNameTypars(type, typars, mangle);
			if (mangle) mangleType(instance) else instance;
		}
	}
}

instantiateFiTypeFunctionTypars(fn : FiTypeFunction, typars : Tree<string, FiType>, mangle : bool) -> FiTypeFunction {
	FiTypeFunction(map(fn.args, \a -> {
			FiFunArg(a.name, instantiateFiTypeTypars(a.type, typars, mangle))
		}), instantiateFiTypeTypars(fn.returnType, typars, mangle));
}

instantiateFiTypeNameTypars(tn : FiTypeName, typars : Tree<string, FiType>, mangle : bool) -> FiTypeName {
	// when instantiating type parameters inside a type, mangling is always false
	// we need to mangle type name as a whole
	FiTypeName(tn.name, map(tn.typeparameters, \tp -> instantiateFiTypeTypars(tp, typars, false)));
}
