import utctime;
import fs/filesystem;
import tools/flowc/flowc_println;

export {

	FiFileInfo(
		flowfile : string, // The path as used by flow imports
		trampstamp : double, // The timestamp of the source file
		fullpath : string, // The full filesystem path
		md5sum : string,
		filesize : double,
		lastupdate : double, // The timestamp of the last successfull compilation
	);

	dummyFiFileInfo = FiFileInfo("", 0.0,"","", 0.0, 0.0);

	// Switching on md5 check may slow down a checking
	fiFileInfoFileChanged(file : FiFileInfo, checkMd5 : bool) -> bool;

	// Switching on md5 check may slow down a checking
	fiFileInfoFileChangedMessage(fileinfo : FiFileInfo, checkMd5 : bool) -> void;

	// Set 'lastupdate' to the current timestamp
	updateFiFileInfo(fileinfo : FiFileInfo) -> FiFileInfo;
}

fiFileInfoFileChanged(fileinfo : FiFileInfo, checkMd5 : bool) -> bool {
	timestamp_ = fileModified(fileinfo.fullpath);
	actualFilesize = fileSize(fileinfo.fullpath);
	actualMd5Sum = if (checkMd5) md5(getFileContent(fileinfo.fullpath)) else dummyFiFileInfo.md5sum;
	(fileinfo.trampstamp != timestamp_) || (fileinfo.filesize != actualFilesize) || checkMd5 && (fileinfo.md5sum != actualMd5Sum);

}

fiFileInfoFileChangedMessage(fileinfo : FiFileInfo, checkMd5 : bool) -> void {
	timestamp_ = fileModified(fileinfo.fullpath);
	actualFilesize = fileSize(fileinfo.fullpath);
	actualMd5Sum = if (checkMd5) md5(getFileContent(fileinfo.fullpath)) else dummyFiFileInfo.md5sum;
	if (fileinfo.trampstamp != timestamp_) {
		fcPrintln("Module '" + fileinfo.flowfile + "' is obsolete with timestamp '" + time2stringUtc(fileinfo.trampstamp) + "' while source has '" + time2stringUtc(timestamp_) + "'");
		fcPrintln("File at " + fileinfo.fullpath);
	}
	if (fileinfo.filesize != actualFilesize) {
		fcPrintln("Module '" + fileinfo.flowfile + "' is obsolete with file size " + d2s(fileinfo.filesize) + " while source size is " + d2s(actualFilesize));
		fcPrintln("File at " + fileinfo.fullpath);
	}
	if (checkMd5 && fileinfo.md5sum != actualMd5Sum) {
		fcPrintln("Module '" + fileinfo.flowfile + "' is obsolete with md5 sum " + fileinfo.md5sum + " while source md5 sum is " + actualMd5Sum);
		fcPrintln("File at " + fileinfo.fullpath);
	}
}

updateFiFileInfo(fileinfo : FiFileInfo) -> FiFileInfo {
	FiFileInfo(
		fileinfo.flowfile,
		fileinfo.trampstamp,
		fileinfo.fullpath,
		fileinfo.md5sum,
		fileinfo.filesize,
		timestamp()
	)
}
