import tools/flowc/incremental/fitype;
import ds/array;
import runtime;

export {
	isFiSubType(small : FiType, big : FiType, allowFlow : bool) -> bool;
}

isFiSubType(small : FiType, big : FiType, allowFlow : bool) -> bool {
    flowok = \ -> {
        (allowFlow && small == FiTypeFlow()) || small == big;
    }
	switch (big) {
		FiTypeVoid(): small == big;
		FiTypeBool(): flowok();
		FiTypeInt(): flowok();
		FiTypeDouble(): flowok();
		FiTypeString(): flowok();
		FiTypeArray(bt): switch (small) {
			FiTypeArray(st): isFiSubType(st, bt, allowFlow); // We could do false here
			default: flowok();
		}
		FiTypeFlow(): true;
		FiTypeNative(): small == big;
		FiTypeFunction(bargs, breturnType): {
			switch (small) {
				FiTypeFunction(sargs, sreturnType): {
					isFiSubType(sreturnType, breturnType, allowFlow)
					// Here, we reverse
					&& areFiSubTypes(map(bargs, \ba -> ba.type), map(sargs, \sa -> sa.type), allowFlow)
				}
				default: flowok();
			}
		}
		FiTypeRef(bt): switch (small) {
			FiTypeRef(st): {
				isFiSubType(st, bt, false)
			}
			default: false;
		}
		FiTypeParameter(bn): {
			switch (small) {
				// We just accept this
				FiTypeParameter(sn): true;
				default: flowok();
			}
		}
		FiTypeName(bname, btypeparameters): {
			switch (small) {
				FiTypeName(sname, stypeparameters): {
					if (sname == bname) {
                        // This is required for some cases, like switch, where we do not have typars
                        // as well as for implicit polymorphism due to polymorphic unions
                        stypeparameters == [] 
                        || btypeparameters == [] 
						|| areFiSubTypes(stypeparameters, btypeparameters, allowFlow)
					} else {
						//println("TODO: Check that " + sname + " <= " + bname);
						true;
					}
				}
				default: flowok();
			}
		} 
	}
}

areFiSubTypes(small : [FiType], big : [FiType], allowflow : bool) -> bool {
	length(small) == length(big)
	&& foldi(small, true, \i, acc, sa -> {
		acc && isFiSubType(sa, big[i], allowflow)
	})
}
