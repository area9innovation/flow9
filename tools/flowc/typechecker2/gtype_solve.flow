import tools/flowc/typechecker2/gtype_util;
import tools/flowc/typechecker2/gtype_resolve;
import tools/flowc/typechecker2/fctype2gtype;
import tools/flowc/typechecker2/gtype2fctype;
import tools/flowc/typechecker2/gsubtype_graph;
import tools/flowc/typechecker/combine_types;
import tools/flowc/fcexp_description;
import tools/flowc/debug;
import tools/flowc/type_helpers;
import tools/flowc/typechecker/type_expect_helpers;
import ds/easygraph_utils;

import tools/flowc/typechecker/solve_expectations;

export {
	// This resolves the constraints for the named function or var
	gtypeSolve(name : string, tyvarIdGroup : IdGroup, pos : FcPosition, env : FcTypeEnv, subtype : GSubtypeGraph) -> GEnv;
}

/*
This works using a few passes algorithm:
First, recursively deconstruct all relations based on the expectations and build a graph
of requirements for each tyvar.
Then, for each tyvar, figure out how to resolve it by taking all information about it
into account.


Run with something like

	flowcpp tools/flowc/flowc.flow -- test=1 incremental=0 gtype=1 >out.flow

and then

	mkdot

to produce the graphviz relations if you use verbose=2 or above.

TODO:
- Union vs union case

- Tyvars without any constraints do not enter the picture since they are not in the unresolved
  tyvar thing

- Get rid of GAnd, GOr

- Array vs flow. 107.
- Lower == upper, maybe except for typars. 108

- Check that we reconstruct struct defs with correct number of typars, and be smarter about
  implicit typars. (31, 37, 54, ..., 103, 114, 127)
*/

gtypeSolve(name : string, tyvarIdGroup : IdGroup, pos : FcPosition, env : FcTypeEnv, subtype : GSubtypeGraph) -> GEnv {
	if (false) {
		dot = simpleGraph2dot(subtype.supertypes, "", [], 
			\n -> [DotLabel(n)], 
			\e -> []
		);
		setFileContent(name + "-subtypes.dot", dot);

		dot2 = simpleGraph2dot(subtype.fieldSupertypes, "", [], 
			\n -> [DotLabel(n)], 
			\e -> []
		);
		setFileContent(name + "-fieldsubtypes.dot", dot2);
		{}
	}

	expects = ^(env.local.expects);

	// Build a map from tyvar to typar
	typars : Tree<int, GType> = foldTree(^(env.local.typars), makeTree(), \typar, tyvar, acc : Tree<int, GType> -> {
		setTree(acc, tyvar.id, GTypePar(typar));
	});

	ge = GEnv(
		env, 
		tyvarIdGroup, 
		// While debugging, let us do this
		if (^(env.local.debugTyping) > 0 || getConfigParameter(env.program.acc.config.config, "verbose") == name) 3 else env.program.acc.config.verbose, 
		typars, 
		makeSet(), 
		makeSet(),
		makeSimpleGraph(),
		subtype,
	);

	if (ge.verbose >= 2)  {
		fcPrintln("\nType " + name);
	}

	onError = \m, info, description -> {
		poss = FcPosition(pos.file, info.start, info.end);
		addFcTypeError(env.program, FcError(name + ": " + (if (description != "") description + ": " else "") + m, [poss]));
	};

	gunify = \genv : GEnv, output : FcType, input : FcType, info : FcInfo2, description : () -> string -> {
		ot = fctype2gtype(genv.env, tyvarIdGroup, output);
		it = fctype2gtype(genv.env, tyvarIdGroup, input);
		unifyGType(genv, ot, it, \m -> onError(m, info, description()));
	}

	ge1 = foldList(expects, ge, \acc, e -> {
		// If verbose is on, we keep track of what expects to debug
		solveDebug = (ge.verbose >= 1 && containsSet(^(env.local.debugExpects), e)) || ge.verbose >= 3;
		if (solveDebug) {
			env.local.debugTyping := 1;
		};
		if (solveDebug || ge.verbose >= 3) {
			fcPrintln("\n// " + strReplace(fcExpect2string(acc.env, e), "\n", " "));
		}

		re = switch (e : FcTypeExpect) {
			FcLessOrEqual(e1, e2, d, info, ex): {
				gunify(acc, e1, e2, info, \ -> d + ": " + fcexpDescription(ex));
			}
			FcVerifyType(e1, e2, info): {
				et1 = gunify(acc, e1, e2, info, \ -> "");
				gunify(et1, e2, e1, info, \ -> "");
			}
			FcExpectField(field, ftype, struct, info, ex): {
				st = fctype2gtype(env, tyvarIdGroup, struct);
				gfield = GField(field, fctype2gtype(env, tyvarIdGroup, ftype));
				if (false) {
					// This one breaks in test 101
					nacc = unifyGType(acc, gfield, st, \m -> onError(m, info, ""));
					unifyGType(nacc, st, gfield, \m -> onError(m, info, ""));
				} else {
					unifyGType(acc, gfield, st, \m -> onError(m, info, ""));
				}
			}
			FcSetMutableField(stype, field, ftype, info): {
				st = fctype2gtype(acc.env, tyvarIdGroup, stype);
				ft = fctype2gtype(acc.env, tyvarIdGroup, ftype);
				gfield = GField(field, ft);
				unifyGType(acc, st, gfield, \m -> onError(m, info, "set mutable"));
			}
		}
		if (solveDebug) {
			env.local.debugTyping := 0;
		}
		re;
	});


	if (ge1.verbose >= 3) {
		saveGDot(ge1, name + "_orig");
	}

	// Go ahead and resolve it
	ge2 = resolveGGraph(ge1, name, \e, l, u -> {
		unifyGType(e, l, u, \err -> {
			// TODO: We could keep a map from tyvar to position and use that for
			// smarter error reporting based on tyvars
			addFcTypeError(env.program, FcError(name + ": " + err, [pos]));
		})
	});

	// Now, set the tyvars in the environment
	foldTree(ge2.tyvars, env, \tv, type, acc -> {
		setTyvar(acc, tv, gtype2fctype(ge2, type));
		acc;
	});

	if (!isEmptySet(ge2.unresolvedTyvars)) {
		println("Error: Unresolved tyvars: " + toString(set2array(ge2.unresolvedTyvars)));
	}

	if (ge2.verbose >= 2) {
		saveGDot(ge2, name);

		batch = getFileContent("mkdot.bat");
		batch0 = batch + "dot "+ name + ".dot -Tsvg -o" + name + ".svg\n";
		setFileContent("mkdot.bat", batch0);
		{}
	}

	// Run the final checks
	onError2 = \m, is -> {
		addFcTypeError(ge2.env.program, FcError(name + ": " + m, map(is, \i -> {
				FcPosition(pos.file, i.start, i.end)
			})
		));
	};
	applyList(^(ge2.env.local.checks), \e -> {
		checkFinalTypeExpect(ge2.env, e, onError2);
	});

	// And we have resolved all expectations
	ge2.env.local.expects := makeList();
	ge2.env.local.checks := makeList();

	ge2;
}

saveGDot(env : GEnv, name : string) -> void {
	dot = simpleGraph2dot(env.relations, GTypeVar(0), [], 
		\n -> [DotLabel(gtype2string(env, n))], 
		\e -> []
	);
	setFileContent(name + ".dot", dot);
	{}
}

unifyGType(env : GEnv, output : GType, input : GType, onError : (string) -> void) -> GEnv {
	if (output == input) {
		env;
	} else {
		tr = Pair(output, input);
		if (containsSet(env.seen, tr)) {
			env;
		} else {
			// We set an entry in the cache to avoid infinite recursion
			seen = insertSet(env.seen, tr);
			if (env.verbose >= 3)  {
				pt = \t -> gtype2string(env, t);
				printIndent(env.env, "Unify " + gtype2string(env, output) + " c= " + gtype2string(env, input));
			}
			env2 = GEnv(env with seen = seen);
			env3 = unifyAndRecurseGType(env2, output, input, onError);

			if (env.verbose >= 3)  {
				printDedent(env.env, "Done " + gtype2string(env3, output) + " c= " + gtype2string(env3, input));
			}
			env3;
		}
	}
}

// Alright, these GTypes meet. We have to recursively spiral down on these guys
unifyAndRecurseGType(env : GEnv, left : GType, right : GType, onError : (string) -> void) -> GEnv {
	if (env.verbose >= 4)  {
		printIndent(env.env, "Unify  " + gtype2string(env, left) + " c= " + gtype2string(env, right));
	}

	todo = \ -> {
		printStay(env.env, "TODO: " + gtype2string(env, left) + " c= " + gtype2string(env, right));
		env;
	}

	error = \ -> {
		onError(gtype2string(env, left) + " != " + gtype2string(env, right));
		env;
	}

	rest = \ -> {
		switch (right) {
			GTypeVar(rid): {
				// OK, if this is bound, we have to recurse into the beast
				bound = lookupTree(env.tyvars, rid);
				nenv = switch (bound) {
					None(): env;
					Some(btyvar): unifyGType(env, left, btyvar, onError);
				}
				gunifyTyvar(nenv, left, right, onError);
			}
			GFlow(): {
				explicit_flow = isConfigParameterTrue(env.env.program.acc.config.config, "explicit-flow");
				switch (left) {
					GFlow(): env;
					GBasicType(bt): {
						if (explicit_flow) {
							switch (bt) {
								FcTypeNative(__): env;
								default: {
									// We require explicit casts to/from flow
									error();
								}
							}
						} else env;
					}
					GArray(__): if (explicit_flow) error() else env;
					GSubType(__): if (explicit_flow) error() else env;
					GTypePar(id): if (explicit_flow) error() else env;
					GFunction(args, rt): if (explicit_flow) error() else env;
					GRef(type): if (explicit_flow) error() else env;
					GTypeVar(id): todo();
				}
			}
			default: {
/*
				if (left == GFlow()) {
					// OK, we find all unbound tyvars and bind them to flow!
					tyvars = extractFTypeVars(env, right, makeSet());
					acc2 = foldSet(tyvars, env, \acc, tv -> {
						if (containsKeyTree(acc.tyvars, tv)) acc
						else setFTypeVar(acc, tv, left);
					});
					FEnvType(acc2, 
						if (kind == FUnifyLeft() || kind == FReduceLeft()) left else right
					);
				} else {
					onError(ftype2string(env, left) + "   and   " + ftype2string(env, right) + " are incompatible");
						// we do not support toString(FType) at the moment
						// + (if (env.verbose > 1) "\n   " + toString(left) + "\nvs " + toString(right) else ""));
					simplest = switch (left) {
						FBasicType(__): left;
						default: right;
					}
					FEnvType(env, simplest);
				}*/
				todo();
			}
		}
	}

	nenv = switch (left) {
		GBasicType(ltype): {
			switch (right) {
				GBasicType(rtype): {
					if (isSameFcType(env.env, ltype, rtype, false)) {
						env;
					} else {
						error();
					}
				}
				default: rest();
			}
		}
		GFlow(): {
			explicit_flow = isConfigParameterTrue(env.env.program.acc.config.config, "explicit-flow");
			switch (right) {
				GFlow(): env;
				GBasicType(bt): {
					if (explicit_flow) {
						switch (bt) {
							FcTypeNative(__): env;
							default: {
								// We require explicit casts to/from flow
								error();
							}
						}
					} else env;
				}
				GArray(__): if (explicit_flow) error() else env;
				GSubType(__): if (explicit_flow) error() else env;
				GTypePar(id): if (explicit_flow) error() else env;
				GFunction(args, rt): if (explicit_flow) error() else env;
				GRef(type): if (explicit_flow) error() else env;
				GTypeVar(id): rest();
			}
		}
		GTypePar(lid): {
			switch (right) {
				GTypePar(rid): {
					if (lid == rid) {
						env;
					} else {
						strict = getConfigParameter(env.env.program.acc.config.config, "strict");
						if (strict == "1") {
							onError(lid + " and " + rid + " are distinct");
						}
						env;
					}
				}
				default: {
					rest();
				}
			}
		}
		GFunction(largs, lrt): {
			switch (right) {
				GFunction(rargs, rrt): {
					nenv = unifyGTypes(env, rargs, largs, \i, e -> {
						if (i == -1) {
							onError(e + " function args")
						} else {
							onError("Arg #" + i2s(i) + ": " + e)
						}
					});
					unifyGType(nenv, lrt, rrt, onError);
				}
				default: rest();
			}
		}
		GArray(ltype): {
			switch (right) {
				GArray(rtype): {
					unifyGType(env, ltype, rtype, onError);
				}
				default: rest();
			}
		}
		GRef(ltype): {
			switch (right) {
				GRef(rtype): {
					unifyGType(env, ltype, rtype, onError);
				}
				default: rest();
			}
		}
		GTypeVar(lid): {
			bound = lookupTree(env.tyvars, lid);
			nenv = switch (bound) {
				None(): env;
				Some(btyvar): unifyGType(env, btyvar, right, onError);
			}
			gunifyTyvar(nenv, left, right, onError);
		}
		GSubType(): {
			switch (right) {
				GBasicType(t): error();
				GFlow(): rest();
				GTypePar(id): rest();
				GFunction(args, rt): error();
				GArray(type): error();
				GRef(type): error();
				GTypeVar(rid): rest();
				GSubType(): {
					gunifySubType(env, left, right, onError);
				}
			}
		}
	}

	if (env.verbose >= 4)  {
		printDedent(env.env, "Unify  " + gtype2string(env, left) + " c= " + gtype2string(env, right));
	}

	nenv;
}

unifyGTypes(env : GEnv, lefts : [GType], rights : [GType], onError : (int, string) -> void) -> GEnv {
	if (length(lefts) != length(rights)) {
		onError(-1, "Got " + i2s(length(lefts)) + ", but expected " + i2s(length(rights)));
		env;
	} else {
		foldi(lefts, env, \i, acc, l -> {
			unifyGType(acc, l, rights[i], \e -> onError(i + 1, e))
		})
	}
}

gunifyTyvar(env : GEnv, left : GType, right : GType, onError : (string) -> void) -> GEnv {
	addTyvars = \ut, t -> extractGTypeVars(env, t, ut);
	if (env.verbose >= 3) {
		printStay(env.env, "Adding edge between  " + gtype2string(env, left) + " and " + gtype2string(env, right));
	}
	GEnv(
		env with 
		unresolvedTyvars = addTyvars(addTyvars(env.unresolvedTyvars, left), right),
		relations = addSimpleEdge(env.relations, left, right, true, 0)
	)
}

gunifySubType(env : GEnv, left : GSubType, right : GSubType, onError : (string) -> void) -> GEnv {
	todo = \ -> {
		printStay(env.env, "TODO: Subtype " + gtype2string(env, left) + " c= " + gtype2string(env, right));
		env;
	}
	switch (left) {
		GNamed(lname, ltypars): {
			switch (right) {
				GNamed(rname, rtypars): {
					if (lname == rname) {
						// Do the typars
						// TODO: We could extend based on implicit polymorphism
						// See test1.
						unifyGTypes(env, ltypars, rtypars, \i, e -> {
							if (i == -1) {
								onError(e + " type parameters")
							} else {
								onError("Type par #" + i2s(i) + ": " + e)
							}
						});
					} else {
						gunifyNamedTypes(env, left, right, onError);
					}
				}
				GField(rname, rtype): {
					unifyGNamedAgainstField(env, left, right, onError);
				}
			}
		}
		GField(lname, ltype): {
			switch (right) {
				GNamed(rname, rtypars): {
					unifyGFieldAgainstName(env, left, right, onError);
				}
				default: {
					todo();
				}
			}
		}
	}
}

// We know the names are different, and that the right name is supposed to be a union.
gunifyNamedTypes(env : GEnv, left : GNamed, right : GNamed, onError : (string) -> void) -> GEnv {
	// From the name of a union to the structs, uninstantiated
	rtypenames : [FcTypeName] = union2typenames(env.env, right.name);
	mstruct = find(rtypenames, \tn -> tn.name == left.name);
	switch (mstruct) {
		None(): {
			// It could be that the left is a union.
			ltypenames : [FcTypeName] = union2typenames(env.env, left.name);
			if (ltypenames == []) {
				onError("No overlap between " + gtype2string(env, left) + " and " + gtype2string(env, right));
			}
			// The unify against each struct in it
			fold(ltypenames, env, \acc, tn -> {
				istruct = ginstantiateStructFromUnion(acc, right, tn, onError);
				unifyAndRecurseGType(acc, istruct, right, onError);
			});
		}
		Some(ustruct): {
			// The struct as it appears in the union. Let us remap typars from the union to the struct
			istruct = ginstantiateStructFromUnion(env, right, ustruct, onError);
		
			// And then subtype with our struct
			unifyAndRecurseGType(env, left, istruct, onError);
		}
	}
}

// We known that master is a union, and we want the instantiated struct which we know is part of it
ginstantiateStructFromUnion(env : GEnv, master : GNamed, ustruct : FcTypeName, onError : (string) -> void) -> GType {
	munion = lookupTree(env.env.program.acc.names.unions, master.name);
	switch (munion) {
		None(): {
			if (master.name == ustruct.name) {
				master
			} else {
				onError("Expected " + master.name + " to be a union with " + ustruct.name);
				GNamed(ustruct.name, []);
			}
		}
		Some(union): {
			// Take the union def and map the typars
			typars = foldi(union.typeparameters, makeTree(), \i, acc, tp -> {
				setTree(acc, strLeft("????????????", i + 1), tp)
			});
			// Now we make a struct with typar names that correspond to the unions
			instantiated = instantiateTyParsToTypes(env.env, typars, ustruct, onError);
			gtype0 = fctype2gtype(env.env, env.tyvarIdGroup, instantiated);
	
			// We might have to do implicit type-par promotion from the union
			gtype = switch (gtype0) {
				GNamed(gname, tps): {
					// Extend the type parameters to match the number of parameters
					ntps = concat(tps, generate(0, length(master.typars), \i -> {
						GTypePar(strLeft("????????????", i + 1))
					}));
					GNamed(gname, subrange(uniq(ntps), 0, length(master.typars)))
				}
				default: gtype0;
			}

			if (env.verbose >= 4) {
				printStay(env.env, "Constructed " + gtype2string(env, gtype) + " from " + gtype2string(env, master));
			}

			// Now, instantiate the typars in this gtype
			gtypars = foldi(master.typars, makeTree(), \i, acc, tp -> {
				setTree(acc, strLeft("????????????", i + 1), tp)
			});
			instantiateGTypePars(env, gtypars, gtype, onError);
		}
	}
}

unifyGFieldAgainstName(env : GEnv, left : GField, right : GNamed, onError : (string) -> void) -> GEnv {
	// printStay(env.env, "Subtyping field " + left.name + " against " + right.name);
	fieldType = getInstantiatedGNamedField(env, right, left.name, true, onError);
	// printStay(env.env, "Subtyping field " + left.name + " against " + right.name);
	unifyAndRecurseGType(env, left.type, fieldType, onError);
}

// Does this name have this field?
hasGNameField(env : GEnv, named : string, field : string) -> bool {
	mstruct = lookupTree(env.env.program.acc.names.structs, named);
	switch (mstruct) {
		None(): {
			munion = lookupTree(env.env.program.acc.names.unions, named);
			switch (munion) {
				None(): false;
				Some(union): {
					typenames : [FcTypeName] = union2typenames(env.env, named);
					forall(typenames, \tn -> hasGNameField(env, tn.name, field))
				}
			}
		}
		Some(struct): {
			exists(struct.args, \a : FcStructArg -> {
				a.name == field;
			});
		}
	}
}

unifyGNamedAgainstField(env : GEnv, left : GNamed, right : GField, onError : (string) -> void) -> GEnv {
	fieldType = getInstantiatedGNamedField(env, left, right.name, false, onError);
	unifyAndRecurseGType(env, fieldType, right.type, onError);
}

// Given a named structure, extract the instantiated type of a given field
getInstantiatedGNamedField(env : GEnv, named : GNamed, field : string, existEnough : bool, onError : (string) -> void) -> GType {
	error = \ -> {
		onError("Expected " + gtype2string(env, named) + " to have ." + field);
		GFlow();
	}
	mstruct = lookupTree(env.env.program.acc.names.structs, named.name);
	switch (mstruct) {
		None(): {
			munion = lookupTree(env.env.program.acc.names.unions, named.name);
			switch (munion) {
				None(): error();
				Some(union): {
					typenames : [FcTypeName] = union2typenames(env.env, named.name);
					withField = if (existEnough) filter(typenames, \tn -> hasGNameField(env, tn.name, field)) else typenames;
					instantiated = map(withField, \tn -> {
						ginstantiateStructFromUnion(env, named, tn, onError);
					});
					fields = filtermap(instantiated, \i : GType -> {
						switch (i) {
							GNamed(__, __): {
								Some(getInstantiatedGNamedField(env, i, field, existEnough, onError));
							}
							default: {
								error();
								None();
							}
						}
					});
					shared = uniq(fields);
					if ((existEnough && shared != []) || length(shared) == 1) {
						shared[0];
					} else {
						error();
					}
				}
			}
		}
		Some(struct): {
			sfield = find(struct.args, \a : FcStructArg -> {
				a.name == field;
			});
			switch (sfield) {
				None(): error();
				Some(stfield): {
					// OK, instantiate the struct typars from our named struct
					gtypars = foldi(named.typars, makeTree(), \i, acc, tp -> {
						setTree(acc, strLeft("????????????", i + 1), tp)
					});
					gfield = fctype2gtype(env.env, env.tyvarIdGroup, stfield.type);
					instantiateGTypePars(env, gtypars, gfield, onError);
				}
			}
		}
	}
}
