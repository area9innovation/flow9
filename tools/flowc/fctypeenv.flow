import math/hash;
import lingo/linecolumn;
import tools/flowc/flowcacc;
import tools/flowc/typechecker/type_expect;
import tools/flowc/dependencies;

import tools/flowc/typechecker2/gsubtype_graph_type;

export {
	// The typing environment for the entire compilation process
	FcTypeEnv(
		global : FcTypeEnvGlobal,
		program : FcTypeEnvProgram,
		local : FcTypeEnvLocal
	);

	FcCacheModule(
		loaded : FcLoaded,
		outdated : bool
	);

	// A global environment for a batch compilation of several programs 
	FcTypeEnvGlobal(
		moduleCache : ConcurHashMap<string, Maybe<FcCacheModule>>,
		nameCache : ConcurHashMap<string, Maybe<FcCacheNamed>>,
		sourceCache : ConcurHashMap<string, FcCachedSource> // Sources of files as strings
	);

	// A single program typing environment
	FcTypeEnvProgram(
		acc : FlowcAcc, 

		// The resolved type of functions and variables
		types : Tree<string, FcType>,

		// Function that prings an error, and increases the error count
		printError : (FcError) -> void,

		// Count of type errors
		errors : ref int,

		// Cache for fast resolving which structs constitute a union
		union2typenamesCache : ConcurHashMap<string, [FcTypeName]>,

		// A graph of subtypes
		subtypeGraph : GSubtypeGraph
	);

	// Typing environment for a single module
	FcTypeEnvLocal(
		// Requirements for types.
		expects : ref List<FcTypeExpect>,
		// Final checks after type checking
		checks : ref List<FcTypeCheck>,
		// The resolved type of tyvars
		tyvars : ref Tree<int, FcType>,
		// The current type of type parameters
		typars : ref Tree<string, FcTypeVar>,
		// The local stack variables info, used for duplication checking
		varinfo : Tree<string, FcInfo>,

		// Which expectations are collected during debug?
		debugExpects : ref Set<FcTypeExpect>,

		// Are we debug typing?
		debugTyping : ref int,

		// Indentation for debug
		debugIndentation : ref string,
	);

	initFcTypeEnvLocal() -> FcTypeEnvLocal;
	initFcTypeEnvGlobal() -> FcTypeEnvGlobal;

	fcLoadFromCache(env : FcTypeEnvGlobal, name : string) -> Maybe<FcCacheModule>;
	fcRemoveFromCache(env : FcTypeEnvGlobal, name : string) -> void;
	fcAddToCache(env : FcTypeEnvGlobal, name : string, loaded : FcLoaded) -> void;
	fcClearCache(env : FcTypeEnvGlobal) -> void;
	fcIterCache(env : FcTypeEnvGlobal, fn : (FiModule) -> void) -> void;
	fcFoldCache(env : FcTypeEnvGlobal, init : ?, fn : (?, FiModule) -> ?) -> ?;

	FiNamed ::= FiDeclaration, FiStructOrUnion;

	// Cached toplevel entities, including struct/union names
	FcCacheNamed(
		named : FiNamed,
		module : string, // name of a module of origin
		exported : bool,
	);

	// Lookup a cached entity: a struct, union, global var, function or native.
	fcLookupNameInCache(env : FcTypeEnvGlobal, name : string) -> Maybe<FcCacheNamed>;
	fcFilterNameCacheList(env : FcTypeEnvGlobal, fn : (FcCacheNamed) -> bool) -> List<FcCacheNamed>;
	fcFilterNameCacheArray(env : FcTypeEnvGlobal, fn : (FcCacheNamed) -> bool) -> [FcCacheNamed];

	// Lookups a cached file source, if not found - loads and stores.
	fcFileSource(env : FcTypeEnvGlobal, path : string) -> string;
	// Lookups a cached line resolver for a file, if not initialized - initializes it.
	fcFileLineResolver(env : FcTypeEnvGlobal, path : string) -> LineResolver;
	// Stores a certain string as a source for some path.
	fcSetFileSource(env : FcTypeEnvGlobal, path : string, src : string) -> void;
	// Loads file source from filesystem. May be used to undo fcSetFileSource function.
	fcLoadFileSource(env : FcTypeEnvGlobal, path : string) -> void;

	// Outputs the size of all caches in env.
	fcCacheSizeInfo(env : FcTypeEnvGlobal) -> string;
}

initFcTypeEnvLocal() -> FcTypeEnvLocal {
	FcTypeEnvLocal(ref makeList(), ref makeList(), ref makeTree(), ref makeTree(), makeTree(), ref makeSet(), ref 0, ref "")
}

initFcTypeEnvGlobal() -> FcTypeEnvGlobal {
	FcTypeEnvGlobal(makeConcurHashMap(hashString), makeConcurHashMap(hashString), makeConcurHashMap(hashString));
}

fcLoadFromCache(env : FcTypeEnvGlobal, name : string) -> Maybe<FcCacheModule> {
	getConcurHashMap(env.moduleCache, name, None());
}

fcRemoveFromCache(env : FcTypeEnvGlobal, name : string) -> void {
	switch (getConcurHashMap(env.moduleCache, name, None()) : Maybe<FcCacheModule>) {
		Some(cached): {
			setConcurHashMap(env.moduleCache, name, 
				Some(FcCacheModule(cached.loaded, true))
			);
		}
		None(): { }
	}
}

fcAddToCache(env : FcTypeEnvGlobal, name : string, loaded : FcLoaded) -> void {
	setConcurHashMap(env.moduleCache, name, Some(FcCacheModule(loaded, false)));
	module = loaded.module;
	exports = buildSet(module.exports);
	wrap = \n : FiNamed -> Some(FcCacheNamed(n, module.fileinfo.flowfile, containsSet(exports, n.name)));
	iter(module.structs, \s -> setConcurHashMap(env.nameCache, s.name, wrap(s)));
	iter(module.unions, \u -> setConcurHashMap(env.nameCache, u.name, wrap(u)));
	iter(module.functions, \f -> setConcurHashMap(env.nameCache, f.name, wrap(f)));
	iter(module.globalVars, \v -> setConcurHashMap(env.nameCache, v.name, wrap(v)));
	iter(module.natives, \n -> setConcurHashMap(env.nameCache, n.name, wrap(n)));
}

fcClearCache(env : FcTypeEnvGlobal) -> void {
	clearConcurHashMap(env.moduleCache);
	clearConcurHashMap(env.nameCache);
	clearConcurHashMap(env.sourceCache);
}

fcIterCache(env : FcTypeEnvGlobal, fn : (FiModule) -> void) -> void {
	iter(valuesConcurHashMap(env.moduleCache), \m -> 
		maybeApply(m, \cached -> 
			if (!cached.outdated) fn(cached.loaded.module)
		)
	);
}

fcFoldCache(env : FcTypeEnvGlobal, init : ?, fn : (?, FiModule) -> ?) -> ? {
	fold(valuesConcurHashMap(env.moduleCache), init, \acc, m -> 
		switch (m) {
			Some(cached): if (cached.outdated) acc else fn(acc, cached.loaded.module);
			None(): acc;
		}
	);
}

fcLookupNameInCache(env : FcTypeEnvGlobal, name : string) -> Maybe<FcCacheNamed> {
	getConcurHashMap(env.nameCache, name, None());
}

fcFilterNameCacheList(env : FcTypeEnvGlobal, fn : (FcCacheNamed) -> bool) -> List<FcCacheNamed> {
	filtermapList(array2list(valuesConcurHashMap(env.nameCache)), \n -> 
		maybeBind(n, \cached -> if (fn(cached)) Some(cached) else None())
	);
}

fcFilterNameCacheArray(env : FcTypeEnvGlobal, fn : (FcCacheNamed) -> bool) -> [FcCacheNamed] {
	filtermap(valuesConcurHashMap(env.nameCache), \n -> 
		maybeBind(n, \cached -> if (fn(cached)) Some(cached) else None())
	);
}

FcCachedSource(
	text : string,
	modified : double,
	resolver : Maybe<LineResolver>
);

fcFileSource(env : FcTypeEnvGlobal, path : string) -> string {
	if (!containsKeyConcurHashMap(env.sourceCache, path)) {
		setConcurHashMap(env.sourceCache, path, FcCachedSource(getFileContent(path), fileModified(path), None()));
	}
	cached = getConcurHashMap(env.sourceCache, path, FcCachedSource("", 0.0, None()));
	modified = fileModified(path);
	if (cached.modified != modified) {
		// Reload outdated source.
		text = getFileContent(path);
		setConcurHashMap(env.sourceCache, path, FcCachedSource(text, modified, None()));
		text;
	} else {
		cached.text;
	}
}

fcSetFileSource(env : FcTypeEnvGlobal, path : string, text : string) -> void {
	if (!containsKeyConcurHashMap(env.sourceCache, path)) {
		setConcurHashMap(env.sourceCache, path, FcCachedSource(text, fileModified(path), None()));
	} else {
		cached = getConcurHashMap(env.sourceCache, path, FcCachedSource("", 0.0, None()));
		setConcurHashMap(env.sourceCache, path, FcCachedSource(text, cached.modified, None()));
	}
}

fcLoadFileSource(env : FcTypeEnvGlobal, path : string) -> void {
	setConcurHashMap(env.sourceCache, path, FcCachedSource(getFileContent(path), fileModified(path), None()));
}

fcFileLineResolver(env : FcTypeEnvGlobal, path : string) -> LineResolver {
	if (!containsKeyConcurHashMap(env.sourceCache, path)) {
		text = getFileContent(path);
		setConcurHashMap(env.sourceCache, path, 
			FcCachedSource(text, fileModified(path), Some(makeLineResolver(text)))
		);
	}
	cached = getConcurHashMap(env.sourceCache, path, FcCachedSource("", 0.0, None()));
	modified = fileModified(path);
	if (cached.modified != modified) {
		// Reload outdated source.
		text = getFileContent(path);
		resolver = makeLineResolver(text);
		setConcurHashMap(env.sourceCache, path, FcCachedSource(text, modified, Some(resolver)));
		resolver;
	} else {
		switch (cached.resolver) {
			Some(resolver): {
				resolver;
			}
			None(): {
				// Init the source line resolver.
				resolver = makeLineResolver(cached.text);
				setConcurHashMap(env.sourceCache, path, FcCachedSource(cached with resolver = Some(resolver)));
				resolver;
			}
		}
	}
}

fcCacheSizeInfo(env : FcTypeEnvGlobal) -> string {
	"Module cache size: " + i2s(sizeConcurHashMap(env.moduleCache)) + ", " +
	"Name cache size: " + i2s(sizeConcurHashMap(env.nameCache)) + ", " +
	"Source cache size: " + i2s(sizeConcurHashMap(env.moduleCache))
}
