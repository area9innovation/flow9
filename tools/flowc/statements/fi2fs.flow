import tools/flowc/statements/fs_helpers;
import tools/flowc/incremental/fi_helpers;
import tools/flowc/manipulation/freevars;

export {
	FsReturnPolicy ::= FsReturnToOuter, FsReturnNowhere, FsReturnToLocal; 
		FsReturnToOuter();
		FsReturnNowhere();
		FsReturnToLocal(var: FsVar);

	FsEnv(
		args: [FiFunArg],
		locals:  Tree<string, FsVar>,
		effects: FiEffects,
		names: FiGlobalNames,
		mutable tmp: int, 
	);

	fiExp2FsStat(e : FiExp, returns: FsReturnPolicy, env: FsEnv, fn: string) -> FsStat;
	fiExp2FsExp(e : FiExp, env: FsEnv) -> Pair<FsExp, [FsStat]>;
}

fiExp2FsStat(e : FiExp, returns: FsReturnPolicy, env: FsEnv, fn: string) -> FsStat {
	wrap = \x -> switch (returns) {
		FsReturnToOuter():  FsRet(x);
		FsReturnNowhere():  FsWrapExp(x);
		FsReturnToLocal(v): FsAssign(v, x);
	}
	do_exp = \x -> fiExp2FsExp(x, env);
	switch (e) {
		FiSeq(es, __,__): {
			fsWrapSeq(concatA(mapi(es, \i, x -> 
				fsUnwrapSeq(fiExp2FsStat(x, if (i + 1 == length(es)) returns else FsReturnNowhere(), env, fn))
			)));
		}
		FiCast(ex, from, to,__,__): {
			pair = do_exp(ex);
			fsWrapSeq(concat(pair.second, [wrap(FsCast(pair.first, from, to))]));
		}
		FiCall(f, args, type, __): {
			args_pairs = map(args, do_exp);
			do_call = \ -> {
				f_pair = do_exp(f);
				fsWrapSeq(concat(
					concatA(concat(
						[f_pair.second], 
						map(args_pairs, \p -> p.second))
					), 
					[wrap(FsCall(f_pair.first, 
						map(args_pairs, \p -> p.first), type
					))]
				));
			}
			switch (f) {
				FiVar(nm,__,__): {
					if (nm != fn || returns != FsReturnToOuter()) do_call() else {
						switch (lookupTree(env.effects.names.toplevel, fn)) {
							Some(func): {
								func_args = cast(func: FiDeclaration -> FiFunctionDec).lambda.args;
								fsWrapSeq(concat(
									concatA(map(args_pairs, \p -> p.second)), 
									concatA([
										filtermapi(args_pairs, \i, p -> {
											make_let = \-> FsLet(FsVar(func_args[i].name + "__tmp__", func_args[i].type, FsVarLoc()), p.first);
											switch (p.first) {
												FsVar(name,__,__): 
													if (name == env.args[i].name) None() else Some(make_let());
												default:
													Some(make_let());
											}
										}),
										filtermapi(args_pairs, \i, p -> {
											make_assign = \-> FsAssign(
												FsVar(func_args[i].name, func_args[i].type, FsVarLoc()), 
												FsVar(func_args[i].name + "__tmp__", func_args[i].type, FsVarLoc())
											);
											switch (p.first) {
												FsVar(name,__,__): if (name == env.args[i].name) None() else Some(make_assign());
												default: Some(make_assign());
											}
										}),
										[FsTailCall()]
									])
								));
							}
							None(): fail0("function " + fn + " is not found");
						}
					}
				}
				default: do_call();
			}
		}
		FiSwitch(v, vtype, cs, type, __): {
			default_switch = \ -> FsSwitch(
				fiVar2FsVar(v.name, v.type, env), vtype,
				map(cs, \c -> {
					struct = lookupTreeDef(env.names.structs, c.struct, FiTypeStruct("", [], [], 0));
					struct_args = mapi(c.argNames, \i, arg_name -> FsVar(arg_name, struct.args[i].type, FsVarLoc()));
					case_env = FsEnv(env with
						locals = fold(struct_args, env.locals, \acc, arg -> if (arg.name == "__") acc else 
							setTree(acc, arg.name, FsVar(arg.name, arg.type, FsVarLoc()))
						)
					);
					FsCase(c.struct, struct_args, fiExp2FsStat(c.body, returns, case_env, fn))
				}), 
				type
			);
			switch (vtype) {
				FiTypeName(typename,__): {
					if (containsKeyTree(env.effects.names.unions, typename)) {
						default_switch();
					} else {
						switch (find(cs, \c -> c.struct == typename)) {
							Some(c): {
								struct = lookupTreeDef(env.names.structs, c.struct, FiTypeStruct("", [], [], 0));
								struct_args = mapi(c.argNames, \i, arg_name -> FiStructArg(arg_name, struct.args[i].type, struct.args[i].ismutable));
								case_env = FsEnv(env with
									locals = fold(struct_args, env.locals, \acc, arg -> if (arg.name == "__") acc else 
										setTree(acc, arg.name, FsVar(arg.name, arg.type, FsVarLoc()))
									)
								);
								fiExp2FsStat(c.body, returns, case_env, fn);
							}
							None(): {
								fail0("unmatched struct type '" + typename + "' in switch:\n" + prettyFiExp(dummyPretty, e));
							}
						}
					}
				}
				FiTypeFlow(): {
					default_switch();
				}
				default: {
					fail0("non-struct|union|flow type " + prettyFiType(dummyPretty, v.type, makeSet()) + " in switch:\n" + prettyFiExp(dummyPretty, e));
				}
			}
		}
		FiLambda(args, body, type,__): {
			free_vars = fifreevars(e);
			locals = buildSet(getTreeKeys(env.locals));
			closure = intersectSets(free_vars, locals);
			lambda_env = FsEnv(env with
				locals = foldSet(closure,
					fold(args, env.locals, \acc, arg -> 
						setTree(acc, arg.name, FsVar(arg.name, arg.type, FsVarArg()))
					),
					\acc, arg -> {
						v = lookupTreeDef(acc, arg, FsVar("", FiTypeVoid(), FsVarArg()));
						setTree(acc, arg, FsVar(arg, v.type, v.kind));
					}
				)
			);
			closure_vars = map(set2array(closure), \name -> 
				switch (lookupTree(env.locals, name)) {
					Some(tp): tp;
					None(): fail0("can't find type of a var: " + name);
				}
			);
			wrap(FsLambda(closure_vars, args, fiExp2FsStat(body, FsReturnToOuter(), lambda_env, fn), type));
		}
		FiLet(name, vtype, e1, e2, type, __): {
			if (name == "__") {
				fiExp2FsStat(e2, returns, env, fn);
			} else {
				let_var = FsVar(name, vtype, FsVarLoc());
				let_env = FsEnv(env with
					locals = setTree(env.locals, name, let_var)
				);
				if (fiExpIsFsExp(e1)) {
					e1_pair = do_exp(e1);
					fsWrapSeq(concat3(e1_pair.second,
						[FsLet(let_var, e1_pair.first)],
						fsUnwrapSeq(fiExp2FsStat(e2, returns, let_env, fn))
					));
				} else {
					
					fsWrapSeq(concat3(
						[FsVarDecl(let_var)],
						fsUnwrapSeq(fiExp2FsStat(e1, FsReturnToLocal(let_var), let_env, fn)),
						fsUnwrapSeq(fiExp2FsStat(e2, returns, let_env, fn))
					));
				}
			}
		}
		FiIf(e1, e2, e3, type, __): {
			e1_pair = do_exp(e1);
			fsWrapSeq(concat(e1_pair.second,
				[FsIf(
					e1_pair.first, 
					fiExp2FsStat(e2, returns, env, fn), 
					fiExp2FsStat(e3, returns, env, fn), 
					type
				)]
			));
		}
		FiCallPrim(op, es, type, __): {
			es_pairs = map(es, do_exp);
			fsWrapSeq(concat(
				concatA(map(es_pairs, \p -> p.second)), 
				[wrap(FsCallPrim(op, 
					map(es_pairs, \p -> p.first), type
				))]
			));
		}
		FiRequire(file, ex, type, __): {
			pair = do_exp(ex);
			fsWrapSeq(concat(pair.second, [wrap(FsRequire(file, pair.first, type))]));
		}
		FiUnsafe(name, ex, type, __): {
			pair = do_exp(ex);
			fsWrapSeq(concat(pair.second, [wrap(FsUnsafe(name, pair.first, type))]));
		}
		FiVar(name,type,__): wrap(fiVar2FsVar(name, type, env));
		FiVoid(__):        wrap(e);
		FiDouble(__, __):  wrap(e);
		FiInt(__, __):     wrap(e);
		FiString(__, __):  wrap(e);
		FiBool(__, __):    wrap(e);
	}
}

fiExp2FsExp(e : FiExp, env: FsEnv) -> Pair<FsExp, [FsStat]> {
	do_exps = \x -> fiExp2FsExp(x, env);
	switch (e) {
		FiSeq(es, type, start): {
			if (length(es) == 0) {
				Pair(FiVoid(0), []);
			} else if (length(es) == 1) {
				do_exps(es[0]);
			} else {
				last = lastElement(es, FiVoid(0));
				other = take(es, length(es) - 1);
				last_p = do_exps(last);
				other_s = fsUnwrapSeq(fiExp2FsStat(FiSeq(other, type, start), FsReturnNowhere(), env, ""));
				Pair(
					last_p.first,
					concat(other_s, last_p.second)
				);
			}
		}
		FiCast(ex, from, to,__,__): {
			pair = do_exps(ex);
			Pair(FsCast(pair.first, from, to), pair.second);
		}
		FiCall(f, args, type, call_start): {
			args_pairs = map(args, do_exps);
			f_pair = do_exps(f);
			Pair(
				FsCall(f_pair.first, 
					map(args_pairs, \p -> p.first), type
				),
				concatA(concat(
					[f_pair.second], 
					map(args_pairs, \p -> p.second)
				)), 
			);
		}
		FiSwitch(v, vtype, cases, type, start): {
			default_switch_stat = \return -> {
				cases_stats = map(cases, \c -> {
					struct = lookupTreeDef(env.names.structs, c.struct, FiTypeStruct("", [], [], 0));
					struct_args = mapi(c.argNames, \i, arg_name -> FsVar(arg_name, struct.args[i].type, FsVarLoc()));
					case_env = FsEnv(env with 
						locals = fold(struct_args, env.locals, \acc, arg -> if (arg.name == "__") acc else 
							setTree(acc, arg.name, FsVar(arg.name, arg.type, FsVarLoc()))
						)
					);
					case_stat = fiExp2FsStat(c.body, return, case_env, "");
					FsCase(c.struct, struct_args, case_stat);
				});
				FsSwitch(fiVar2FsVar(v.name, v.type, env), vtype, cases_stats, type);
			}
			if (type == FiTypeVoid()) {
				switch (vtype) {
					FiTypeName(typename,__): {
						switch_stat = if (containsKeyTree(env.effects.names.unions, typename)) {
							default_switch_stat(FsReturnNowhere());
						} else {
							switch (find(cases, \c -> c.struct == typename)) {
								Some(c): {
									struct = lookupTreeDef(env.names.structs, c.struct, FiTypeStruct("", [], [], 0));
									struct_args = mapi(c.argNames, \i, arg_name -> FiStructArg(arg_name, struct.args[i].type, struct.args[i].ismutable));
									case_env = FsEnv(env with
										locals = fold(struct_args, env.locals, \acc, arg -> if (arg.name == "__") acc else 
											setTree(acc, arg.name, FsVar(arg.name, arg.type, FsVarLoc()))
										)
									);
									fiExp2FsStat(c.body, FsReturnNowhere(), case_env, "");
								}
								None(): {
									fail0("unmatched struct type '" + typename + "' in switch:\n" + prettyFiExp(dummyPretty, e));
								}
							}
						}
						Pair(FiVoid(start), [switch_stat]);
					}
					FiTypeFlow(): {
						Pair(FiVoid(start), [default_switch_stat(FsReturnNowhere())]);
					}
					default: {
						fail0("non-struct|union|flow type " + prettyFiType(dummyPretty, v.type, makeSet()) + " in switch:\n" + prettyFiExp(dummyPretty, e));
					}
				}
			} else {
				tmp_var = FsVar("__switch_" + v.name + (if (env.tmp == 0) "" else "_" + i2s(env.tmp)), type, FsVarLoc());
				env.tmp ::= env.tmp + 1;
				switch (vtype) {
					FiTypeName(typename,__): {
						switch_stat = if (containsKeyTree(env.effects.names.unions, typename)) {
							default_switch_stat(FsReturnToLocal(tmp_var));
						} else {
							switch (find(cases, \c -> c.struct == typename)) {
								Some(c): {
									struct = lookupTreeDef(env.names.structs, c.struct, FiTypeStruct("", [], [], 0));
									struct_args = mapi(c.argNames, \i, arg_name -> FiStructArg(arg_name, struct.args[i].type, struct.args[i].ismutable));
									case_env = FsEnv(env with 
										locals = fold(struct_args, env.locals, \acc, arg -> if (arg.name == "__") acc else 
											setTree(acc, arg.name, FsVar(arg.name, arg.type, FsVarLoc()))
										)
									);
									fiExp2FsStat(c.body, FsReturnToLocal(tmp_var), case_env, "");
								}
								None(): {
									fail0("unmatched struct type '" + typename + "' in switch:\n" + prettyFiExp(dummyPretty, e));
								}
							}
						}
						Pair(tmp_var, [FsVarDecl(tmp_var), switch_stat]);
					}
					FiTypeFlow(): {
						Pair(tmp_var, [FsVarDecl(tmp_var), default_switch_stat(FsReturnToLocal(tmp_var))]);
					}
					default: {
						fail0("non-struct|union|flow type " + prettyFiType(dummyPretty, v.type, makeSet()) + " in switch:\n" + prettyFiExp(dummyPretty, e));
					}
				}
			}
		}
		FiLambda(args, body, type,__): {
			free_vars = fifreevars(e);
			locals = buildSet(getTreeKeys(env.locals));
			closure = intersectSets(free_vars, locals);
			lambda_env = FsEnv(env with
				locals = foldSet(
					closure,
					fold(args, env.locals, \acc, arg -> 
						setTree(acc, arg.name, FsVar(arg.name, arg.type, FsVarArg()))
					), \acc, arg -> {
						v = lookupTreeDef(acc, arg, FsVar("", FiTypeVoid(), FsVarArg()));
						setTree(acc, arg, FsVar(arg, v.type, v.kind));
					}
				)
			);
			closure_vars = map(set2array(closure), \name -> 
				switch (lookupTree(env.locals, name)) {
					Some(tp): tp;
					None(): fail0("can't find type of a var: " + name);
				}
			);
			Pair(FsLambda(closure_vars, args, fiExp2FsStat(body, FsReturnToOuter(), lambda_env, ""), type), []);
		}
		FiLet(name, vtype, e1, e2,__, __): {
			if (name == "__") {
				do_exps(e2);
			} else {
				e1_pair = do_exps(e1);
				let_var = FsVar(name, vtype, FsVarLoc());
				let_env = FsEnv(env with
					locals = setTree(env.locals, name, let_var)
				);
				e2_pair = fiExp2FsExp(e2, let_env);
				Pair(
					e2_pair.first,
					concat3(
						e1_pair.second, 
						[FsLet(let_var, e1_pair.first)],
						e2_pair.second
					)
				);
			}
		}
		FiIf(e1, e2, e3, type, __): {
			good_exp = \x -> fiExpIsFsExp(x) && !fiExpHasEffects(x, env.effects);
			e1_pair = do_exps(e1);
			e2_pair = do_exps(e2);
			e3_pair = do_exps(e3);
			if (good_exp(e1) && good_exp(e2) && good_exp(e3)) {
				Pair(
					FsTernaryIf( 
						e1_pair.first, 
						e2_pair.first, 
						e3_pair.first, type
					),
					concat3(e1_pair.second, e2_pair.second, e3_pair.second)
				);
			} else {
				tmp_var = FsVar("__if_" + (if (env.tmp == 0) "" else "_" + i2s(env.tmp)), type, FsVarLoc());
				env.tmp ::= env.tmp + 1;
				Pair(
					tmp_var,
					concat(
						e1_pair.second,
						[FsVarDecl(tmp_var), FsIf(
							e1_pair.first,
							FsSeq(
								concat(e2_pair.second, [FsAssign(tmp_var, e2_pair.first)]), 
								type
							),
							FsSeq(
								concat(e3_pair.second, [FsAssign(tmp_var, e3_pair.first)]), 
								type
							),
							type
						)]
					)
				);
			}
		}
		FiCallPrim(op, es, type, __): {
			es_pairs = map(es, do_exps);
			Pair(
				FsCallPrim(op,
					map(es_pairs, \p -> p.first), type
				),
				concatA(map(es_pairs, \p -> p.second))
			);
		}
		FiRequire(file, ex, type, __): {
			pair = do_exps(ex);
			Pair(FsRequire(file, pair.first, type), pair.second);
		}
		FiUnsafe(name, ex, type, __): {
			pair = do_exps(ex);
			Pair(FsUnsafe(name, pair.first, type), pair.second);
		}
		FiVar(name, type,__): Pair(fiVar2FsVar(name, type, env), []);
		FiVoid(__):        Pair(e, []);
		FiDouble(__, __):  Pair(e, []);
		FiInt(__, __):     Pair(e, []);
		FiString(__, __):  Pair(e, []);
		FiBool(__, __):    Pair(e, []);
	}
}

fiVar2FsVar(name: string, type: FiType, env: FsEnv) -> FsVar {
	switch (lookupTree(env.locals, name)) {
		Some(v): v;
		None(): {
			FsVar(name, type, FsVarGlobal());
		}
	}
}

fsVarIsInStat(v: string, s: FsStat) -> bool {
	switch(s) {
		FsLet(__, e): fsVarIsInExp(v, e);
		FsIf(e, s1, s2, __): fsVarIsInExp(v, e) || fsVarIsInStat(v, s1) || fsVarIsInStat(v, s2);
		FsSwitch(__,__, cases, __): exists(cases, \c -> fsVarIsInStat(v, c.body));
		FsSeq(ss, __): exists(ss, \s1 -> fsVarIsInStat(v, s1));
		FsWrapExp(e): fsVarIsInExp(v, e);
		FsRet(e):  fsVarIsInExp(v, e);
		FsTailCall(): false;
		FsVarDecl(var): false;
		FsAssign(var, e): fsVarIsInExp(v, e);
		FsMemory(op, e): fsVarIsInExp(v, e);
	}
}

fsVarIsInExp(v: string, e: FsExp) -> bool {
	switch(e) {
		FsLambda(__,__,body,__): fsVarIsInStat(v, body);
		FsCall(fn, args,__): fsVarIsInExp(v, fn) || exists(args, \arg -> fsVarIsInExp(v, arg));
		FsCallPrim(__,es,__): exists(es, \x -> fsVarIsInExp(v, x));
		FsRequire(__, ex,__): fsVarIsInExp(v, ex);
		FsUnsafe(__, ex,__): fsVarIsInExp(v, ex);
		FsCast(ex, from, to): fsVarIsInExp(v, ex);
		FsTernaryIf(cond, pos, neg, type1): fsVarIsInExp(v, cond) || fsVarIsInExp(v, pos) || fsVarIsInExp(v, neg);
		FsVar(name,__,__): v == name;
		FiConst(): false;
	}
}