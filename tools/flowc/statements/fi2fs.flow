import tools/flowc/statements/fs_helpers;
import tools/flowc/incremental/fi_helpers;
import tools/flowc/manipulation/freevars;

export {
	FsReturnPolicy ::= FsReturnToOuter, FsReturnNowhere, FsReturnToLocal; 
		FsReturnToOuter();
		FsReturnNowhere();
		FsReturnToLocal(var: FsVar);

	fiExp2FsStat(e : FiExp, returns: FsReturnPolicy, locals: Set<string>, names: FiGlobalNames) -> FsStat;
	fiExp2FsExp(e : FiExp, locals: Set<string>, names: FiGlobalNames) -> Pair<FsExp, [FsStat]>;
	fsWrapSeq(stats: [FsStat]) -> FsStat;
}

fsWrapSeq(stats: [FsStat]) -> FsStat {
	if (length(stats) == 0) FsSeq([], FiTypeVoid()) else 
	if (length(stats) == 1) stats[0] else 
	FsSeq(stats, fsStatType(stats[length(stats) - 1]));
}

fsUnwrapSeq(stat: FsStat) -> [FsStat] {
	switch (stat) {
		FsSeq(stats,__): stats; 
		default: [stat]; 
	}
}

fiExp2FsStat(e : FiExp, returns: FsReturnPolicy, locals: Set<string>, names: FiGlobalNames) -> FsStat {
	wrap = \x -> switch (returns) {
		FsReturnToOuter():  FsRet(x);
		FsReturnNowhere():  FsWrap(x);
		FsReturnToLocal(v): FsAssign(v, x);
	}
	do_exp = \x -> fiExp2FsExp(x, locals, names);
	switch (e) {
		FiSeq(es, __,__): {
			fsWrapSeq(concatA(mapi(es, \i, x -> 
				fsUnwrapSeq(fiExp2FsStat(x, if (i + 1 == length(es)) returns else FsReturnNowhere(), locals, names))
			)));
		}
		FiCast(ex, from, to,__,__): {
			pair = do_exp(ex);
			fsWrapSeq(concat(pair.second, [wrap(FsCast(pair.first, from, to))]));
		}
		FiCall(f, args, type, __): {
			f_pair = do_exp(f);
			args_pairs = map(args, do_exp);
			fsWrapSeq(concat(
				concatA(concat(
					[f_pair.second], 
					map(args_pairs, \p -> p.second))
				), 
				[wrap(FsCall(f_pair.first, 
					map(args_pairs, \p -> p.first), type
				))]
			));
		}
		FiSwitch(v, vtype, cs, type, __): {
			default_switch = \ -> FsSwitch(
				FsVar(v.name, v.type, containsSet(locals, v.name)), 
				map(cs, \c -> {
					case_locals = fold(c.argNames, locals, \acc, arg -> if (arg == "__") acc else insertSet(acc, arg));
					FsCase(c.struct, c.argNames, fiExp2FsStat(c.body, returns, case_locals, names))
				}), 
				type
			);
			switch (v.type) {
				FiTypeName(typename,__): {
					if (containsKeyTree(names.unions, typename)) {
						default_switch();
					} else {
						switch (find(cs, \c -> c.struct == typename)) {
							Some(c): {
								case_locals = fold(c.argNames, locals, \acc, arg -> if (arg == "__") acc else insertSet(acc, arg));
								fiExp2FsStat(c.body, returns, case_locals, names);
							}
							None(): {
								fail0("unmatched struct type '" + typename + "' in switch:\n" + prettyFiExp(dummyPretty, e));
							}
						}
					}
				}
				FiTypeFlow(): {
					default_switch();
				}
				default: {
					fail0("non-struct|union|flow type " + prettyFiType(dummyPretty, v.type, makeSet()) + " in switch:\n" + prettyFiExp(dummyPretty, e));
				}
			}
		}
		FiLambda(args, body, type,__): {
			lambda_locals = fold(args, locals, \acc, arg -> if (arg.name == "__") acc else insertSet(acc, arg.name));
			free_vars = fifreevars(e);
			closure = intersectSets(free_vars, locals);
			wrap(FsLambda(set2array(closure), args, fiExp2FsStat(body, FsReturnToOuter(), lambda_locals, names), type));
		}
		FiLet(name, vtype, e1, e2, type, __): {
			if (name == "__") {
				fiExp2FsStat(e2, returns, locals, names);
			} else {
				e1_pair = do_exp(e1);
				fsWrapSeq(concat3(e1_pair.second,
					[FsLet(name, vtype, e1_pair.first)],
					fsUnwrapSeq(fiExp2FsStat(e2, returns, insertSet(locals, name), names))
				));
			}
		}
		FiIf(e1, e2, e3, type, __): {
			e1_pair = do_exp(e1);
			fsWrapSeq(concat(e1_pair.second,
				[FsIf(
					e1_pair.first, 
					fiExp2FsStat(e2, returns, locals, names), 
					fiExp2FsStat(e3, returns, locals, names), 
					type
				)]
			));
		}
		FiCallPrim(op, es, type, __): {
			es_pairs = map(es, do_exp);
			fsWrapSeq(concat(
				concatA(map(es_pairs, \p -> p.second)), 
				[wrap(FsCallPrim(op, 
					map(es_pairs, \p -> p.first), type
				))]
			));
		}
		FiRequire(file, ex, type, __): {
			pair = do_exp(ex);
			fsWrapSeq(concat(pair.second, [wrap(FsRequire(file, pair.first, type))]));
		}
		FiUnsafe(name, ex, type, __): {
			pair = do_exp(ex);
			fsWrapSeq(concat(pair.second, [wrap(FsUnsafe(name, pair.first, type))]));
		}
		FiVar(name,type,__): wrap(FsVar(name, type, containsSet(locals, name)));
		FiVoid(__):        wrap(e);
		FiDouble(__, __):  wrap(e);
		FiInt(__, __):     wrap(e);
		FiString(__, __):  wrap(e);
		FiBool(__, __):    wrap(e);
	}
}

fiExp2FsExp(e : FiExp, locals: Set<string>, names: FiGlobalNames) -> Pair<FsExp, [FsStat]> {
	do_exps = \x -> fiExp2FsExp(x, locals, names);
	switch (e) {
		FiSeq(es, type, __): {
			if (length(es) == 0) {
				Pair(FiVoid(0), []);
			} else {
				pairs = map(es, do_exps);
				last = lastElement(pairs, Pair(FiVoid(0), []));
				Pair(
					last.first,
					concatA(map(pairs, \p -> p.second))
				);
			}
		}
		FiCast(ex, from, to,__,__): {
			pair = do_exps(ex);
			Pair(FsCast(pair.first, from, to), pair.second);
		}
		FiCall(f, args, type, call_start): {
			f_pair = do_exps(f);
			args_pairs = map(args, do_exps);
			Pair(
				FsCall(f_pair.first, 
					map(args_pairs, \p -> p.first), type
				),
				concatA(concat(
					[f_pair.second], 
					map(args_pairs, \p -> p.second))
				), 
			);
		}
		FiSwitch(v, vtype, cases, type, start): {
			tmp_var = FsVar("__switch_" + v.name, type, true);
			default_switch_stat = \-> {
				cases_stats = map(cases, \c -> {
					case_locals = fold(c.argNames, locals, \acc, arg -> if (arg == "__") acc else insertSet(acc, arg));
					case_stat = fiExp2FsStat(c.body, FsReturnToLocal(tmp_var), case_locals, names);
					FsCase(c.struct, c.argNames, case_stat);
				});
				FsSwitch(FsVar(v.name, v.type, containsSet(locals, v.name)), cases_stats, type);
			}
			switch (v.type) {
				FiTypeName(typename,__): {
					switch_stat = if (containsKeyTree(names.unions, typename)) {
						default_switch_stat();
					} else {
						switch (find(cases, \c -> c.struct == typename)) {
							Some(c): {
								case_locals = fold(c.argNames, locals, \acc, arg -> if (arg == "__") acc else insertSet(acc, arg));
								fiExp2FsStat(c.body, FsReturnToLocal(tmp_var), case_locals, names);
							}
							None(): {
								fail0("unmatched struct type '" + typename + "' in switch:\n" + prettyFiExp(dummyPretty, e));
							}
						}
					}
					Pair(tmp_var, [FsVarDecl(tmp_var), switch_stat]);
				}
				FiTypeFlow(): {
					Pair(tmp_var, [FsVarDecl(tmp_var), default_switch_stat()]);
				}
				default: {
					fail0("non-struct|union|flow type " + prettyFiType(dummyPretty, v.type, makeSet()) + " in switch:\n" + prettyFiExp(dummyPretty, e));
				}
			}
		}
		FiLambda(args, body, type,__): {
			free_vars = fifreevars(e);
			closure = intersectSets(free_vars, locals);
			lambda_locals = fold(args, locals, \acc, arg -> if (arg.name == "__") acc else insertSet(acc, arg.name));
			Pair(FsLambda(set2array(closure), args, fiExp2FsStat(body, FsReturnToOuter(), lambda_locals, names), type), []);
		}
		FiLet(name, vtype, e1, e2,__, __): {
			if (name == "__") {
				do_exps(e2);
			} else {
				e1_pair = do_exps(e1);
				e2_pair = fiExp2FsExp(e2, insertSet(locals, name), names);
				Pair(
					e2_pair.first,
					concat3(
						e1_pair.second, 
						[FsLet(name, vtype, e1_pair.first)],
						e2_pair.second
					)
				);
			}
		}
		FiIf(e1, e2, e3, type, __): {
			e1_pair = do_exps(e1);
			e2_pair = do_exps(e2);
			e3_pair = do_exps(e3);
			Pair(
				FsTernary( 
					e1_pair.first, 
					e2_pair.first, 
					e3_pair.first, type
				),
				concat3(e1_pair.second, e2_pair.second, e3_pair.second)
			);
		}
		FiCallPrim(op, es, type, __): {
			es_pairs = map(es, do_exps);
			Pair(
				FsCallPrim(op,
					map(es_pairs, \p -> p.first), type
				),
				concatA(map(es_pairs, \p -> p.second))
			);
		}
		FiRequire(file, ex, type, __): {
			pair = do_exps(ex);
			Pair(FsRequire(file, pair.first, type), pair.second);
		}
		FiUnsafe(name, ex, type, __): {
			pair = do_exps(ex);
			Pair(FsUnsafe(name, pair.first, type), pair.second);
		}
		FiVar(name, type,__): Pair(FsVar(name, type, containsSet(locals, name)), []);
		FiVoid(__):        Pair(e, []);
		FiDouble(__, __):  Pair(e, []);
		FiInt(__, __):     Pair(e, []);
		FiString(__, __):  Pair(e, []);
		FiBool(__, __):    Pair(e, []);
	}
}
