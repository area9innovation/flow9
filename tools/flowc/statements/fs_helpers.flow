//import ds/set;
import tools/flowc/incremental/fiprettyprint;
import tools/flowc/statements/fsexp;
import tools/flowc/manipulation/effects;
import string_utils;
//import tools/flowc/memory/fm_helpers;

export {

	fsExpType(e: FsExp) -> FiType;
	fsStatType(s: FsStat) -> FiType;

	fsExp2s(e: FsExp) -> string;
	fsStat2s(s: FsStat) -> string;

	fsSetExpType(e : FsExp, t: FiType) -> FsExp;

	fsHasTailCall(s: FsStat) -> bool;

	fiExpIsFsExp(e: FiExp) -> bool;

	fsWrapSeq(stats: [FsStat]) -> FsStat;
	fsUnwrapSeq(stat: FsStat) -> [FsStat];

	fsStatFreevars(s : FsStat) -> Set<string>;
	fsExpFreevars(e : FsExp) -> Set<string>;

	fsVarIsInStat(v: string, s: FsStat) -> bool;
	fsVarIsInExp(v: string, e: FsExp) -> bool;

	fsStatLambda(s: FsStat) -> FsLambda;

	fsVarIsGlobalFunc(t: FsVarTraits) -> bool;

	// If e has statements, wrap it in lambda so that it doesn't
	fiExpMakeNoStatements(e: FiExp, effects: FiEffects) -> FiExp;

	fiExpHasEffects(e: FiExp, effects: FiEffects) -> bool;
}

fsExpType(e: FsExp) -> FiType {
	switch (e) {
		FsLambda(__,__,__,__, type,__): type;
		FsCall(__,__, type,__):         type;
		FsVar(__,__,type,__):           type;
		FsCallPrim(__,__,type,__):      type;
		FiVoid(__):       FiTypeVoid();
		FiDouble(__, __): FiTypeDouble();
		FiInt(__, __):    FiTypeInt();
		FiString(__, __): FiTypeString();
		FiBool(__, __):   FiTypeBool();
	}
}

fsStatType(s: FsStat) -> FiType {
	switch (s) {
		FsLet(__,__,__,type,__):     type;
		FsIf(__,__,__,type,__):      type;
		FsSwitch(__,__,__,type,__):  type;
		FsSeq(__, type,__):          type;
		FsWrapExp(e, start):         fsExpType(e);
		FsRet(e, start):             fsExpType(e);
		FsTailCall(start):           FiTypeVoid();
		FsVarDecl(v,start):          v.type;
		FsAssign(__,__,__):          FiTypeVoid();
		FsMemory(op,__,__): {
			switch (op) {
				FsIsUnique(): FiTypeBool();
				default:      FiTypeVoid();
			}
		}
	}
}

fsStat2s(s: FsStat) -> string {
	t2s = \t -> prettyFiType(dummyPretty, t, makeSet());
	switch (s) {
		FsLet(v, e, s1,__,__): {
			v.name + " : " + t2s(v.type) + " = " + fsExp2s(e) + ";\n" + fsStat2s(s1);
		}
		FsIf(e, s1, s2, __,__): {
			"if (" + fsExp2s(e) + ")\n" + strIndent(fsStat2s(s1)) + "\nelse\n" + strIndent(fsStat2s(s2));
		}
		FsSwitch(v,__, cases, __,__): {
			"switch (" + fsExp2s(v) + ") {\n" +
				strIndent(superglue(cases, \c -> {
					body_s = fsStat2s(c.body);
					body_s1 = if (strContains(body_s, "\n") || startsWith(body_s, "{")) body_s else body_s + ";";
					c.struct + "(" + superglue(c.args, \arg -> arg.name, ", ") + "): " + body_s1;
				}, "\n")) +
			"\n}";
		}
		FsSeq(ss,__,__): {
			if (length(ss) == 0) "{}" else
			if (length(ss) == 1) fsStat2s(ss[0]) else
			"{\n" + strIndent(superglue(ss, fsStat2s, "\n")) + "\n}";
		}
		FsWrapExp(e,__): {
			fsExp2s(e) + ";";
		}
		FsRet(e,__): {
			"return " + fsExp2s(e) + ";";
		}
		FsTailCall(__): {
			"tailcall;";
		}
		FsVarDecl(var,__): {
			var.name + " : " + t2s(var.type) + ";";
		}
		FsAssign(to, what,__): {
			fsExp2s(to) + " := " +  fsExp2s(what) + ";";
		}
		FsMemory(op, e,__): {
			switch (op) {
				FsIncRC():    "incRef(" + fsExp2s(e) + ");";
				FsDecRC():    "decRef(" + fsExp2s(e) + ");";
				FsCheckRC():  "checkRef(" + fsExp2s(e) + ");";
				FsReuse():    "reuse(" + fsExp2s(e) + ");";
				FsDispose():  "dispose(" + fsExp2s(e) + ");";
				FsIsUnique(): "is_unique(" + fsExp2s(e) + ");";
				FsDecRef():   "dec_ref(" + fsExp2s(e) + ");";
				FsNull():     "nullptr";
			}
		}
	}
}

fsExp2s(e: FsExp) -> string {
	t2s = \t -> prettyFiType(dummyPretty, t, makeSet());
	switch (e) {
		FsLambda(__,closure, args, body,__,__): {
			body_s = fsStat2s(body);
			closure_s = "[" + superglue(closure, \v -> v.name, ", ") + "]";
			header = closure_s + "\\" + superglue(args, \arg -> arg.name + ":" + t2s(arg.type), ", ") + " -> ";
			if (!strContains(body_s, "\n") || startsWith(body_s, "{")) header + body_s else header + "{\n" + strIndent(body_s) + "\n}";
		}
		FsCall(f, args,__,__): {
			fsExp2s(f) + "(" + superglue(args, fsExp2s, ", ") + ")";
		}
		FsCallPrim(op, es, type,__): {
			fsPrim2s(op, map(es, fsExp2s), type);
		}
		FsVar(name, traits,__,__): {
			switch (traits.kind) {
				FsVarGlobal(func): name + if (func) "/*fn*/" else "/*gvar first: " + b2s(traits.firstUse) + " last: " + b2s(traits.lastUse) + "*/";
				default: name + "/* first: " + b2s(traits.firstUse) + " last: " + b2s(traits.lastUse) + "*/";
			}
		}
		FiVoid(__):      "{}";
		FiDouble(d, __): d2s(d);
		FiInt(i, __):    i2s(i)
		FiString(s, __): "\"" + escapeStr(s) + "\"";
		FiBool(b, __):   b2s(b);
	}
}

fsPrim2s(op: FsPrim, es: [string], type: FiType) -> string {
	t2s = \t -> prettyFiType(dummyPretty, t, makeSet());
	switch (op) {
		FsOrPrim():  "(" + es[0] + " || " + es[1] + ")";
		FsAndPrim(): "(" + es[0] + " && " + es[1] + ")";
		FsNotPrim(): "!" + es[0];
		FsEqPrim():  "(" + es[0] + " == " + es[1] + ")";
		FsNePrim():  "(" + es[0] + " != " + es[1] + ")";
		FsLePrim():  "(" + es[0] + " <= " + es[1] + ")";
		FsGePrim():  "(" + es[0] + " >= " + es[1] + ")";
		FsLtPrim():  "(" + es[0] + " < " + es[1] + ")";
		FsGtPrim():  "(" + es[0] + " > " + es[1] + ")";
		FsPlusPrim():  "(" + es[0] + " + " + es[1] + ")";
		FsMinusPrim(): "(" + es[0] + " - " + es[1] + ")"; 
		FsMulPrim():   "(" + es[0] + " * " + es[1] + ")";
		FsDivPrim():   "(" + es[0] + " / " + es[1] + ")";
		FsModPrim():   "(" + es[0] + " % " + es[1] + ")";
		FsNegPrim():   "-" + es[0];
		FsArrayPrim():   "[" + strGlue(es,  ", ") + "]";
		FsIndexPrim():   es[0] + "[" + es[1] + "]";
		FsDerefPrim():   "(*" +es[0] + ")";
		FsRefPrim():     "ref " + es[0];
		FsStructPrim(struct_name): {
			struct_type = cast(type : FiType -> FiTypeName);
			typars = if (struct_type.typeparameters == []) "" else 
				"<" + superglue(struct_type.typeparameters, \tp -> t2s(tp), ", ") + ">";
			struct_name + typars + "(" + strGlue(es, ", ") + ")";
		}
		FsFieldPrim(name): es[0] + "." + name;
		FsRequirePrim(file): {
			"require " + file + " " + es[0];
		}
		FsUnsafePrim(name): {
			"unsafe " + name + " " + es[0];
		}
		FsCastPrim(from, to): {
			"(" + es[0] + " : " + t2s(from) + " -> " + t2s(to) + ")";
		}
		FsIfPrim(): {
			"(" + es[0] + " ? " + es[1] + " : " + es[2] + ")";
		}
		FsReusePrim(name, reuse): {
			struct_type = cast(type : FiType -> FiTypeName);
			typars = if (struct_type.typeparameters == []) "" else 
				"<" + superglue(struct_type.typeparameters, \tp -> t2s(tp), ", ") + ">";
			name + typars + "@" + reuse + "(" + strGlue(es, ", ") + ")";
		}
	}
}

fsSetExpType(e : FsExp, t: FiType) -> FsExp {
	switch(e) {
		FsLambda(__,__,__,__,__,__): FsLambda(e with type = cast(t: FiType -> FiTypeFunction));
		FsCall(__,__,__,__):         FsCall(e with type = t);
		FsCallPrim(__,__,__,__):     FsCallPrim(e with type = t);
		FsVar(__,__,__,__):          FsVar(e with type = t);
		default: e;
	}
}

fsHasTailCall(s: FsStat) -> bool {
	switch (s) {
		FsIf(e, s1, s2,__,__): fsHasTailCall(s1) || fsHasTailCall(s2);
		FsSwitch(v,__, cases, __,__): exists(cases, \c -> fsHasTailCall(c.body));
		FsSeq(ss,__,__): if (length(ss) == 0) false else fsHasTailCall(ss[length(ss) - 1]);
		FsTailCall(__):  true;
		FsLet(v, e, s1, __,__): fsHasTailCall(s1);
		default: false;
	}
}

fiExpIsFsExp(e: FiExp) -> bool {
	switch (e) {
		FiSeq(es, type,__): 
			if (length(es) == 0) true else	
			if (length(es) == 1) {
				fiExpIsFsExp(es[0]) && type != FiTypeVoid();
			} else false;
		FiCast(ex,__,__,__,__): fiExpIsFsExp(ex);
		FiCall(f, args,__,__): fiExpIsFsExp(f) && forall(args, fiExpIsFsExp);
		FiSwitch(__,__,__,__,__): false;
		FiLambda(__,__,__,__): true;
		FiLet(__,__,__,__,__, __): false;
		FiIf(e1, e2, e3,__, __): fiExpIsFsExp(e1) && fiExpIsFsExp(e2) && fiExpIsFsExp(e3);   
		FiCallPrim(op, es,__, __): forall(es, fiExpIsFsExp);
		FiRequire(__,__,__,__): false;
		FiUnsafe(__,__,__,__): false;
		default: true;
	}
}

fsWrapSeq(stats: [FsStat]) -> FsStat {
	if (length(stats) == 0) FsSeq([], FiTypeVoid(), -1) else 
	if (length(stats) == 1) stats[0] else {
		// Filter out intermediate const values
		stats1 = filter(stats, \s ->
			switch (s) {
				FsWrapExp(e,__): {
					switch (e) {
						FiConst(): false;
						default: true;
					}
				}
				default: true;
			}
		);
		last = stats[length(stats1) - 1];
		FsSeq(stats1, fsStatType(last), last.start);
	}
}

fsUnwrapSeq(stat: FsStat) -> [FsStat] {
	switch (stat) {
		FsSeq(stats,__,__): stats; 
		default: [stat]; 
	}
}

fsStatFreevars(s : FsStat) -> Set<string> {
	fsStatFreevarsBoundFree(s, makeSet(), makeSet());
}

fsStatFreevarsBoundFree(s : FsStat, bound : Set<string>, free : Set<string>) -> Set<string> {
	switch (s) {
		FsLet(v, e, s1,__,__): {
			bound1 = insertSet(bound, v.name);
			free1 = fsExpFreevarsBoundFree(e, bound1, free);
			fsStatFreevarsBoundFree(s1, bound1, free1);
		}
		FsIf(e, s1, s2, __,__): {
			fsStatFreevarsBoundFree(s2, bound, 
				fsStatFreevarsBoundFree(s1, bound, 
					fsExpFreevarsBoundFree(e, bound, free)
				)
			);
		}
		FsSwitch(v,__, cases, __,__): {
			fold(cases, fsExpFreevarsBoundFree(v, bound, free), \acc, c -> 
				fsStatFreevarsBoundFree(c.body, bound, acc)
			);
		}
		FsSeq(ss,__,__): {
			fold(ss, free, \acc, x -> 
				fsStatFreevarsBoundFree(x, bound, acc)
			);
		}
		FsWrapExp(e,__): {
			fsExpFreevarsBoundFree(e, bound, free);
		}
		FsRet(e,__): {
			fsExpFreevarsBoundFree(e, bound, free);
		}
		FsAssign(to, what,__): {
			fsExpFreevarsBoundFree(to, bound, fsExpFreevarsBoundFree(what, bound, free));
		}
		FsMemory(__,__,__): free;
		FsTailCall(__): free;
		FsVarDecl(v,__): free;
	}
}

fsExpFreevars(e : FsExp) -> Set<string> {
	fsExpFreevarsBoundFree(e, makeSet(), makeSet());
}

fsExpFreevarsBoundFree(expr : FsExp, bound : Set<string>, free : Set<string>) -> Set<string> {
	switch (expr) {
		FsLambda(__,closure, args, body, __,__): {
			fsStatFreevarsBoundFree(body, 
				fold(args, bound, \acc, arg -> insertSet(acc, arg.name)),
				free
			);
		}
		FsCall(f, args,__,__): {
			fold(args, 
				fsExpFreevarsBoundFree(f, bound, free),
				\acc, arg -> fsExpFreevarsBoundFree(arg, bound, acc)
			);
		}
		FsCallPrim(__,es,__,__): {
			fold(es, bound, \acc, arg -> fsExpFreevarsBoundFree(arg, bound, acc));
		}
		FsVar(name,__,__,__): if (containsSet(bound, name)) free else insertSet(free, name);
		FiVoid(__):      free;
		FiDouble(d, __): free;
		FiInt(i, __):    free;
		FiString(s, __): free;
		FiBool(b, __):   free;
	}
}

fsVarIsInStat(v: string, s: FsStat) -> bool {
	switch(s) {
		FsLet(__, e, s1,__,__): fsVarIsInExp(v, e) || fsVarIsInStat(v, s1);
		FsIf(e, s1, s2, __,__): fsVarIsInExp(v, e) || fsVarIsInStat(v, s1) || fsVarIsInStat(v, s2);
		FsSwitch(__,__, cases, __,__): exists(cases, \c -> fsVarIsInStat(v, c.body));
		FsSeq(ss, __,__): exists(ss, \s1 -> fsVarIsInStat(v, s1));
		FsWrapExp(e,__): fsVarIsInExp(v, e);
		FsRet(e,__):  fsVarIsInExp(v, e);
		FsAssign(var, e,__): fsVarIsInExp(v, e);
		FsMemory(op, var,__): false;
		FsTailCall(__): false;
		FsVarDecl(var,__): false;
	}
}

fsVarIsInExp(v: string, e: FsExp) -> bool {
	switch(e) {
		FsLambda(__,__,__,body,__,__): fsVarIsInStat(v, body);
		FsCall(fn, args,__,__): fsVarIsInExp(v, fn) || exists(args, \arg -> fsVarIsInExp(v, arg));
		FsCallPrim(__,es,__,__): exists(es, \x -> fsVarIsInExp(v, x));
		FsVar(name,__,__,__): v == name;
		FiConst(): false;
	}
}

fsStatLambda(s: FsStat) -> FsLambda {
	do_ex = \ex -> switch (ex) {
		FsLambda(__,__,__,body,__,__): ex; 
		default: fail0("expected FsLambda got: " + fsExp2s(ex));
	}
	switch (s) {
		FsRet(ex,__): do_ex(ex);
		FsWrapExp(ex,__): do_ex(ex);
		default: fail0("expected FsRet or FsWrapExp got: " + fsStat2s(s));
	}
}

fsVarIsGlobalFunc(t: FsVarTraits) -> bool {
	switch (t.kind) {
		FsVarGlobal(func): func;
		default: false;
	}
}

fiExpHasNoStatements(e: FiExp, effects: FiEffects) -> bool {
	no_stats = \x -> fiExpHasNoStatements(x, effects);
	switch (e) {
		FiSeq(es, type,__): 
			if (length(es) == 0) true else	
			if (length(es) == 1) {
				no_stats(es[0]) && type != FiTypeVoid();
			} else false;
		FiCast(ex,__,__,__,__): no_stats(ex);
		FiCall(f, args,__,__): no_stats(f) && forall(args, no_stats);
		FiSwitch(__,__,__,__,__): false;
		FiLambda(__,__,__,__): true;
		FiLet(__,__,__,__,__, __): false;
		FiIf(e1, e2, e3,__, __): {
			no_stats1 = \x -> no_stats(x) && !fiExpHasEffects(x, effects);
			no_stats1(e1) && no_stats1(e2) && no_stats1(e3);
		}
		FiCallPrim(op, es,__, __): {
			switch (op) {
				FcAssignPrim(): false;
				FcSetMutablePrim(field): false;
				default: forall(es, no_stats);
			}
		}
		FiRequire(__,__,__,__): false;
		FiUnsafe(__,__,__,__): false;
		default: true;
	}
}

fiExpMakeNoStatements(e: FiExp, effects: FiEffects) -> FiExp {
	if (fiExpHasNoStatements(e, effects)) e else {
		tp = fiExpType(e);
		FiCall(
			FiLambda([], e, FiTypeFunction([], tp), e.start), [], tp, e.start
		);
	}
}

fiExpHasEffects(e: FiExp, effects: FiEffects) -> bool {
	switch (e) {
		FiSeq(es, type,__): exists(es, \x -> fiExpHasEffects(x, effects));
		FiCast(ex,__,__,__,__): fiExpHasEffects(ex, effects);
		FiCall(f, args,__,__): fiExpHasEffects(f, effects) || exists(args, \x -> fiExpHasEffects(x, effects));
		FiSwitch(__,__,cases,__,__): exists(cases, \c -> fiExpHasEffects(c.body, effects));
		FiLambda(__,body,__,__): fiExpHasEffects(body, effects);
		FiLet(__,__,e1,e2,__, __): fiExpHasEffects(e1, effects) || fiExpHasEffects(e2, effects);
		FiIf(e1, e2, e3,__, __): fiExpHasEffects(e1, effects) || fiExpHasEffects(e2, effects) || fiExpHasEffects(e3, effects);
		FiCallPrim(op, es,__, __): {
			exists(es, \x -> fiExpHasEffects(x, effects)) ||
			switch (op) {
				FcRefPrim(): true;
				default: false;
			}
		}
		FiRequire(__,ex,__,__): fiExpHasEffects(ex, effects);
		FiUnsafe(__,__,__,__): true;
		FiVar(name,__,__): containsSet(effects.hasEffects, name);
		default: false;
	}
}
