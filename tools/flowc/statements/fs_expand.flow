import tools/flowc/statements/fs_transform;

export {
	// Expands the AST with corresponding ref-counter operations
	fsExpandMem(s: FsStat, id2mem: [FsMem]) -> FsStat;
}

fsExpandMem(s: FsStat, id2mem: [FsMem]) -> FsStat {
	cast(fsMap(s, \x -> switch (x) {
		FsIf(cond, pos, neg, id,__):  {
			if (id == -1) x else 
			fsAppendBefore(
				fsExpandUnused(id2mem[id]),
				fsAppendBefore(
					fsExpandIncs(id2mem[id]),
					FsIf(x with
						s1 = fsAppendBefore(fsExpandDecs(id2mem[id]), pos),
						s2 = fsAppendBefore(fsExpandDecs(id2mem[id]), neg)
					)
				)
			);
		};
		FsSwitch(v, __, cs, id,__): {
			if (id == -1) x else 
			fsAppendBefore(
				fsExpandUnused(id2mem[id]),
				fsAppendBefore(
					fsExpandIncs(id2mem[id]),
					FsSwitch(x with
						cases = map(cs, \c -> 
							FsCase(c with
								body = fsAppendBefore(fsExpandDecs(id2mem[id]), c.body)
							)
						)
					)
				)
			);
		}
		FsRet(e, id,__): {
			if (id == -1) x else 
			fsAppendBefore(
				fsExpandUnused(id2mem[id]),
				fsAppendBefore(
					fsExpandIncs(id2mem[id]),
					fsAppendReturn(x, fsExpandDecs(id2mem[id]))
				)
			);
		}
		FsWrapExp(e, id,__): {
			if (id == -1) x else 
			fsAppendBefore(
				fsExpandUnused(id2mem[id]),
				fsAppendBefore(
					fsExpandIncs(id2mem[id]),
					fsAppendAfter(x, fsExpandDecs(id2mem[id]))
				)
			);
		}
		FsLet(__,e, s1, id,__): {
			if (id == -1) x else 
			fsAppendBefore(
				fsExpandUnused(id2mem[id]),
				fsAppendBefore(
					fsExpandIncs(id2mem[id]),
					FsLet(x with
						s = fsAppendBefore(fsExpandDecs(id2mem[id]), s1)
					)
				)
			);
		}
		FsAssign(to, what, id,__): {
			if (id == -1) x else 
			fsAppendBefore(
				fsExpandUnused(id2mem[id]),
				fsAppendBefore(
					fsExpandIncs(id2mem[id]),
					fsAppendAfter(x, fsExpandDecs(id2mem[id]))
				)
			);
		}
		default: x;
	}, true): FsAll -> FsStat);
}

fsExpandIncs(m: FsMem) -> Maybe<FsStat> {
	fold(tree2pairs(m.incs), None(), \acc, p ->
		Some(FsIncRc(p.first, p.second, -1, acc))
	);
}

fsExpandDecs(m: FsMem) -> Maybe<FsStat> {
	fold(set2array(m.decs), None(), \acc, v ->
		Some(FsDecRc(v, FsDecDirect(), -1, acc))
	);
}

fsExpandUnused(m: FsMem) -> Maybe<FsStat> {
	fold(tree2pairs(m.unused), None(), \acc, p ->
		Some(FsDecRc(p.first, p.second, -1, acc))
	);
}

fsAppendBefore(mops: Maybe<FsStat>, x: FsStat) -> FsStat {
	eitherMap(mops, \ops -> fsAppendLast(ops, x), x);
}

fsAppendAfter(x: FsStat, mops: Maybe<FsStat>) -> FsStat {
	eitherMap(mops, \ops -> fsSetNext(x, 
		eitherMap(x.next, \n -> fsAppendLast(ops, n), ops)), x
	);
}

fsAppendReturn(x: FsRet, mops: Maybe<FsStat>) -> FsStat {
	eitherMap(mops, \ops -> {
		ret_var = FsVar("tmp_return_var", fsType(x.e));
		FsLet(ret_var, x.e,
			fsAppendBefore(mops, FsRet(FsVarUse(ret_var, FsVarLocal(), -1), -1, None())),
			-1, None()
		);
	}, x);
}
