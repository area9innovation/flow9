import ds/treeutils;
import tools/flowc/statements/fs_transform;

export {
	fs_fast_prim_ops = true;
	fsStatPerceus(s: FsStat, debug: bool, name: string) -> FsStat;
}

fsStatPerceus(s: FsStat, debug0: bool, name: string) -> FsStat {
	id_counter = ref 0;
	new_id = \-> {
		id = ^id_counter;
		id_counter := ^id_counter + 1;
		id;
	}
	debug = ref debug0;
	s1 = cast(fsMap(s, fsIds(new_id), false): FsAll -> FsStat);
	if (^debug) {
		println("s1 with ids:\n" +
			//"s:\n" + fs2s(s) + "\n" +
			"s1:\n" + fs2s(s1) + "\n"
			//"s RAW:\n" + toStringFormatted(s1) + "\n"
		);
	}
	add_var = \x, acc, v -> {
		switch (lookupTree(acc, v.name)) {
			Some(w): fail(
				"Duplicate variable: " + v.name + " of type: " + pretFiType(v.type) + ", already is of type: " + pretFiType(w.type) + " in:\n" + 
				fs2s(x) + "\n" +
				"IN:\n" + fs2s(s1)
			);
			None(): { }
		}
		setTree(acc, v.name, v);
	}
	varName2type = fsFold(s1, makeTree(), \acc, x -> switch (x) {
		FsLet(v,__,__,__): add_var(x, acc, v);
		FsLambda(__,args,__,__,__): fold(args, acc, \ac, arg -> add_var(x, ac, arg));
		default: acc;
	}, true);

	//num_paths = ref makeTree();
	//fsNumControlFlowPaths(s1, num_paths);
	//total_paths = foldTree(^num_paths, 0.0, \__,n, acc -> acc + n);
	//println("\tnum paths:\n" + 
	//	superglue(tree2pairs(^num_paths), \p -> "\t\tid: " + i2s(p.first) + ", num paths: " + d2s(p.second), "\n")
	//);
	//println("\tstat perceus: " + name + ", ids: " + i2s(^id_counter) + ", num paths: " + d2s(total_paths));

	id2vars_last = fsFindLast(s1, ^id_counter, ^debug);
	crash = ref false;
/*
	id2node = generate(0, ^id_counter, \__ -> ref FsConst(FiVoid(-1), -1));
	fsIter(s1, \x -> if (x.id != -1) id2node[x.id] := x, true);


	if (false && total_paths < 1000000.0) {
		println("\t\tverifying Paths...");
		cmp_last_sets = \all_vars: Set<string>, last_vars: Set<string> -> if (sizeSet(all_vars) == sizeSet(last_vars)) true else {
			println(name + ": sizeSet(all_vars) != sizeSet(last_vars):\n" +
				i2s(sizeSet(all_vars)) + " != " + i2s(sizeSet(last_vars)) + "\n" +
				"all_vars: {" + strGlue(set2array(all_vars), ", ") + "}\n" +
				"last_vars: {" + strGlue(set2array(last_vars), ", ") + "}\n" +
				"missing: [" + strGlue(set2array(differenceSets(all_vars, last_vars)), ", ") + "]\n"
			);
			crash := true;
			debug := true;
			false;
		}
		cf_paths: ref Tree<int, [[int]]> = ref makeTree();
		fsControlFlowPaths(s1, cf_paths);
		traverseInOrder(^cf_paths, \id, paths -> {
			num = lookupTreeDef(^num_paths, id, -1.0);
			if (i2d(length(paths)) != num) {
				println(name + ": length(paths) != num: " + i2s(length(paths)) + " != " + d2s(num));
				crash := true;
				debug := true;
			}
			lambda = cast(^(id2node[id]): FsAll -> FsLambda);
			if (^debug) {
				println("Lambda: " + i2s(id) + "\n" + 
					"CF paths:\n" + superglue(paths, \path -> "\t[" + superglue(path, i2s, ", ") + "]" , "\n") + "\n"
				)
			}
			iter(paths, \path -> {
				vars1 = fold(lambda.args, 
					fold(lambda.closure, 
						makeSet(), 
						\ac, v -> if (fiTypeIsScalar(v.type)) ac else insertSet(ac, v.name)
					), 
					\ac, arg -> if (fiTypeIsScalar(arg.type)) ac else insertSet(ac, arg.name)
				);
				vars = fold(path, vars1, \acc, i -> switch (^(id2node[i])) {
					FsLet(v,__,__,__): if (fiTypeIsScalar(v.type)) acc else insertSet(acc, v.name);
					default: acc;
				});
				last = fold(path, makeSet(), \acc, i -> if (i == -1) acc else mergeSets(acc, id2vars_last[i]));
				if (!cmp_last_sets(vars, last)) {
					println(
						"id: " + i2s(id) + "\n" +
						"path: [" + superglue(path, i2s, ", ") + "]\n" + 
						"vars: {" + strGlue(set2array(vars), ", ") + "}\n" +
						"lasts: [\n" +
							superglue(path, \i -> "\t" +  i2s(i) + ": {" + strGlue(set2array(id2vars_last[i]), ", ") + "}", "\n") +
						"\n]\n" +
						"lambda:\n" + strIndent(fs2s(^(id2node[id])))
					);
				}
			});
		});
		println("\t\t... verified " + d2s(total_paths) + " paths");
	}
*/
	ret = cast(fsMap(s1, \x -> switch (x) {
		FsIf(cond, pos, neg, id):  {
			memops = fsGetMemops(cond, id2vars_last, ^debug);
			fsDecUnused(id,
				fsAddIncs(memops,
					FsIf(x with
						//s1 = fsDecUnused(pos.id, fsAddDecs2(memops, pos, id), id2vars_last, varName2type, ^debug),
						//s2 = fsDecUnused(neg.id, fsAddDecs2(memops, neg, id), id2vars_last, varName2type, ^debug)
						s1 = fsAddDecs2(memops, pos, id),
						s2 = fsAddDecs2(memops, neg, id)
					), id
				), id2vars_last, varName2type, ^debug
			);
		};
		FsSwitch(v,__, cs, id): {
			memops = fsGetMemops(v, id2vars_last, ^debug);
			fsDecUnused(id,
				fsAddIncs(memops,
					FsSwitch(x with
						cases = map(cs, \c ->
							FsCase(c with
								//body = fsDecUnused(c.body.id, fsAddDecs2(memops, c.body, id), id2vars_last, varName2type, ^debug)
								body = fsAddDecs2(memops, c.body, id)
							)
						)
					), id
				), id2vars_last, varName2type, ^debug
			);
		}
		FsSeq(ss, id): {
			fsDecUnused(id, x, id2vars_last, varName2type, ^debug);
		}
		FsRet(e, id): {
			memops = fsGetMemops(e, id2vars_last, ^debug);
			fsDecUnused(id, fsAddDecs1(memops, fsAddIncs(memops, x, id), id), id2vars_last, varName2type, ^debug);
		}
		FsWrapExp(e, id): {
			memops = fsGetMemops(e, id2vars_last, ^debug);
			x1 = if (fiTypeIsScalar(fsType(e))) x else {
				FsDecRc(e, -1);
			}
			fsDecUnused(id, fsAddIncs(memops, fsAddDecs(memops, x1, id), id), id2vars_last, varName2type, ^debug);
		}
		FsLet(__,e, s2, id): {
			memops = fsGetMemops(e, id2vars_last, ^debug);
			fsDecUnused(id,
				fsAddIncs(memops, FsLet(x with s = fsAddDecs2(memops, s2, s2.id)), id),
				id2vars_last, varName2type, ^debug
			);
		}
		FsAssign(to, what, id): {
			memops = fsGetMemops2(to, what, id2vars_last, ^debug);
			fsDecUnused(id,
				fsAddIncs(memops, fsAddDecs(memops, x, id), id),
				id2vars_last, varName2type, ^debug
			);
		}
		FsIncRc(__,__,__): fail0("FsIncRc met during memory operations markup");
		FsDecRc(__,__): fail0("FsDecRc met during memory operations markup");
		FsLambda(__,__, body,__,id): {
			//FsLambda(x with body = fsDecUnused(id, body, id2vars_last, varName2type, ^debug));
			x;
		}
		default: x;
	}, true): FsAll -> FsStat);
	if (^crash) {
		println("s1 with ids:\n" +
			//"s:\n" + fs2s(s) + "\n" +
			"s1:\n" + fs2s(s1) + "\n"
			//"s RAW:\n" + toStringFormatted(s1) + "\n"
		);
		 fsFindLast(s1, ^id_counter, ^debug);
		fail("CRAHS");
	}
	ret;
}

fsFindLast(s: FsStat, num: int, debug: bool) -> [Set<string>] {
	id2vars_pre = fsId2varsPre(s, num, debug);
	id2vars_post = fsId2varsPost(s, num, debug);
	id2vars_last = fsId2varsLast(s, num, id2vars_pre, id2vars_post, debug);
	if (debug) {
		set_tree2s = \st -> {
			superglue(tree2pairs(st), \p ->
				i2s(p.first) + ": " + "[" + strGlue(set2array(p.second), ", ") + "]", "\n"
			);
		}
		set_tree2s1 = \st -> {
			supergluei(st, \i, p ->
				i2s(i) + ": " + "[" + strGlue(set2array(p), ", ") + "]", "\n"
			);
		}
		println(
			"id2vars_pre:\n" + set_tree2s1(id2vars_pre) + "\n" +
			"id2vars_post:\n" + set_tree2s1(id2vars_post) + "\n" +
			"id2vars_last:\n" + set_tree2s1(id2vars_last) + "\n" +
			""
		);
	}
	id2vars_last;
}

fsLookupLast(x: FsAll, last: [Set<string>], id: int) -> Set<string> {
	if (id == -1) {
		makeSet();
	} else {
		last[id];
	}
}

fsContainsLast(x: FsAll, last: [Set<string>], id: int, var: string) -> bool {
	if (id == -1) {
		false;
	} else {
		containsSet(last[id], var);
	}
}

fsIsEmptyLast(x: FsAll, last: [Set<string>], id: int) -> bool {
	if (id == -1) {
		true;
	} else {
		isEmptySet(last[id]);
	}
}


fsDecUnused(id: int, x: FsStat, id2vars_last: [Set<string>], varName2type: Tree<string, FsVar>, debug: bool) -> FsStat {
	if (fsIsEmptyLast(x, id2vars_last, id)) {
		if (debug) {
			println("to dec <" + i2s(id) + "> unused: []");
		}
		x;
	} else {
		unused = map(set2array(fsLookupLast(x, id2vars_last, id)), \v_name ->
			switch (lookupTree(varName2type, v_name)) {
				Some(v): v;
				None(): fail0("Unknown variable: " + v_name);
			}
		);
		if (debug) {
			println("to dec <" + i2s(id) + "> unused: [" + superglue(unused, \v -> v.name, ", ") + "]");
		}
		FsSeq(concat(
			map(unused, \v -> FsDecRc(FsVarUse(v, FsVarLocal(), false, -1), -1)),
			fsUnwrapSeq(x)
		), -1);
	}
}

FsMemOps(
	var: FsVar,
	incs: int,  // How much we need to increment a RC of a var before evaluation of an expression
	dec: bool,  // Should we drop the var in the end of evaluation of an expression explicitly
);

fsGetMemops(e: FsExp, id2vars_last: [Set<string>], debug: bool) -> [Pair<FsVar, FsMemOps>] {
	memops = tree2pairs(fsExpVarIncs(e, makeTree(), id2vars_last, -1, FsOrPrim()));
	if (debug) {
		memops_str = superglue(memops, \p -> p.first.name + "=inc(" + i2s(p.second.incs) + ")" + if (p.second.dec) ", dec" else "", ", ");
		println("memops for <" + i2s(e.id) + ">: [" + memops_str + "]" +
			if (e.id != -1) "" else "EXP:\n" + strIndent(fs2s(e))
		);
	}
	memops;
}

fsGetMemops2(e1: FsExp, e2: FsExp, id2vars_last: [Set<string>], debug: bool) -> [Pair<FsVar, FsMemOps>] {
	memops = tree2pairs(fsExpVarIncs(e2, 
		fsExpVarIncs(e1, makeTree(), id2vars_last, -1, FsOrPrim()), 
		id2vars_last, -1, FsOrPrim()
	));
	if (debug) {
		memops_str = superglue(memops, \p -> p.first.name + "=inc(" + i2s(p.second.incs) + ")" + if (p.second.dec) ", dec" else "", ", ");
		println("memops for <" + i2s(e1.id) + ", " + i2s(e2.id) + ">: [" + memops_str + "]" +
			if (e1.id != -1 && e2.id != 1) "" else
				"EXP 1:\n" + strIndent(fs2s(e1)) + "\n" +
				"EXP 2:\n" + strIndent(fs2s(e2)) + "\n"
		);
	}
	memops;
}

fsAddIncs(memops: [Pair<FsVar, FsMemOps>], x: FsStat, id: int) -> FsStat {
	incs = filtermap(memops, \p ->
		if (p.second.incs > 0) {
			Some(FsIncRc(p.second.incs, FsVarUse(p.first, FsVarLocal(), false, -1), -1));
		} else {
			None();
		}
	);
	if (length(incs) == 0) x else {
		FsSeq(concat(incs, fsUnwrapSeq(x)), id);
	}
}

fsAddDecs(memops: [Pair<FsVar, FsMemOps>], x: FsStat, id: int) -> FsStat {
	decs = filtermap(memops, \p ->
		if (p.second.dec) {
			Some(FsDecRc(FsVarUse(p.first, FsVarLocal(), false, -1), -1));
		} else {
			None();
		}
	);
	if (length(decs) == 0) x else {
		FsSeq(concat(fsUnwrapSeq(x), decs), id);
	}
}

fsAddDecs2(memops: [Pair<FsVar, FsMemOps>], x: FsStat, id: int) -> FsStat {
	decs = filtermap(memops, \p ->
		if (p.second.dec) {
			Some(FsDecRc(FsVarUse(p.first, FsVarLocal(), false, -1), -1));
		} else {
			None();
		}
	);
	if (length(decs) == 0) x else {
		FsSeq(concat(decs, fsUnwrapSeq(x)), id);
	}
}

fsAddDecs1(memops: [Pair<FsVar, FsMemOps>], x: FsStat, id: int) -> FsStat {
	decs = filtermap(memops, \p ->
		if (p.second.dec) Some(FsDecRc(FsVarUse(p.first, FsVarLocal(), false, -1), -1)) else None()
	);
	if (length(decs) == 0) x else {
		seq1 = fsUnwrapSeq(x);
		ret = cast(seq1[length(seq1) - 1]: FsStat -> FsRet);
		switch (ret.e) {
			FsTailCall(__,__,__,__): {
				FsSeq(concat3(tail(seq1), decs, [ret]), id);
			}
			default: {
				ret_var = FsVar("tmp_return_var", fsType(ret));
				FsSeq(
					arrayPush(tail(seq1),
						FsLet(ret_var, ret.e,
							FsSeq(arrayPush(decs, FsRet(FsVarUse(ret_var, FsVarLocal(), false, -1), -1)), id),
							-1
						)
					), -1
				);
			}
		}
	}
}

fsExpVarIncs(e: FsExp, acc: Tree<FsVar, FsMemOps>, last: [Set<string>], ind: int, op: FsPrim) -> Tree<FsVar, FsMemOps> {
	switch (e) {
		FsClosure(closure, __,__): {
			fold(closure, acc, \ac, x -> fsExpVarIncs(x, ac, last, -1, FsOrPrim()));
		}
		FsCall(f, args,__,__): {
			fold(args, fsExpVarIncs(f, acc, last, -1, FsOrPrim()), \ac, x ->
				fsExpVarIncs(x, ac, last, -1, FsOrPrim())
			);
		}
		FsCallPrim(o, es,__,__): {
			foldi(es, acc, \i, ac, x -> fsExpVarIncs(x, ac, last, i, o));
		}
		FsTailCall(args, vals, type, id): {
			fold(vals, acc, \ac, x -> fsExpVarIncs(x, ac, last, -1, FsOrPrim()));
		}
		FsCast(e1,__,__,__): {
			fsExpVarIncs(e1, acc, last, -1, FsOrPrim());
		}
		FsVarUse(x, kind, last1, id): {
			call_case = \-> if (fsContainsLast(e, last, id, x.name)) acc else {
				switch (lookupTree(acc, x)) {
					Some(p): setTree(acc, x, FsMemOps(p with incs = p.incs + 1));
					None(): setTree(acc, x, FsMemOps(x, 1, false));
				}
			}
			prim_case = \-> if (!fsContainsLast(e, last, id, x.name)) acc else {
				switch (lookupTree(acc, x)) {
					Some(p): setTree(acc, x, FsMemOps(p with dec = true));
					None(): setTree(acc, x, FsMemOps(x, 0, true));
				}
			}
			if (fiTypeIsScalar(x.type)) acc else {
				switch (kind) {
					FsVarGlobalFunc(): acc;
					FsVarUninit(): acc;
					default: {
						if (fs_fast_prim_ops) {
							switch (op) {
								FsGePrim(): prim_case();
								FsLePrim(): prim_case();
								FsGtPrim(): prim_case();
								FsLtPrim(): prim_case();
								FsEqPrim(): prim_case();
								FsNePrim(): prim_case();
								FsFieldPrim(__,__): prim_case();
								FsIndexPrim(): prim_case();
								FsDerefPrim(): prim_case();
								default: call_case();
							}
						} else {
							call_case();
						}
					}
				}
			}
		}
		FsConst(c, id): acc;
	}
}

fsMergeSets(arr: [ref Set<string>], id: int, s: Set<string>) -> void {
	arr[id] := mergeSets(^(arr[id]), s);
}

fsId2varsPre(s1: FsStat, num: int, debug: bool) -> [Set<string>] {
	id2vars = generate(0, num, \__ -> ref makeSet());
	fsFold2(s1, Cons(makeSet(), EmptyList()),
		\acc, x: FsAll -> {
			add_v = \ac, v -> if (fiTypeIsScalar(v.type)) ac else insertSet(ac, v.name);
			switch (x) {
				FsLambda(closure, args,__,__,id): {
					new_vars = fold(args, fold(closure, makeSet(), \ac, v -> add_v(ac, v)), \ac, v -> add_v(ac, v));
					fsMergeSets(id2vars, id, new_vars);
					Cons(new_vars, acc);
				}
				FsLet(v, __,__,id): {
					new_vars = add_v(acc.head, v);
					fsMergeSets(id2vars, id, acc.head);
					Cons(new_vars, acc.tail);
				}
				default: {
					if (x.id != -1) {
						fsMergeSets(id2vars, x.id, acc.head);
					}
					acc;
				}
			}
		},
		\acc, x -> {
			switch (x) {
				FsLambda(__,__,__,__,id): {
					cast(acc.tail: List<Set<string>> -> Cons<Set<string>>);
				}
				default: acc;
			}
		},
		\args -> args,
		\p1, p2 -> Cons(mergeSets(p1.head, p2.head), p1.tail)
	);
	map(id2vars, \x -> ^x);
}

fsPerceusShowStack(stack: List<Set<string>>) -> string {
	"[" + strGlue(fsPerceusDoShowStack(stack, []), ", ") + "]";
}

fsPerceusDoShowStack(stack: List<Set<string>>, acc: [string]) -> [string] {
	switch (stack) {
		Cons(s, t): {
			fsPerceusDoShowStack(t, arrayPush(acc, "{" +  strGlue(set2array(s), ", ") + "}"));
		}
		EmptyList(): acc;
	}
}

fsId2varsPost(s1: FsStat, num: int, debug: bool) -> [Set<string>] {
	show_vars = \vs -> "[" + strGlue(set2array(vs), ", ") + "]";
	id2vars = generate(0, num, \__ -> ref makeSet());
	fsFold2(s1, Cons(makeSet(), EmptyList()),
		\acc, x -> {
			ret = switch (x) {
				FsLambda(__,__,__,__,id): {
					fsMergeSets(id2vars, id, makeSet());
					Cons(makeSet(), acc);
				}
				default: acc;
			}
			if (debug) {
				println(
					"(B) PRE id=" + i2s(x.id) + "\n" +
						"\tacc stack: " + fsPerceusShowStack(acc) + "\n" +
						"\tret stack: " + fsPerceusShowStack(ret) + "\n"
				);
			}
			ret;
		},
		\acc, x: FsAll -> {
			ret = switch (x) {
				FsLambda(__,__,__,__,id): {
					fsMergeSets(id2vars, id, acc.head);
					cast(acc.tail: List<Set<string>> -> Cons<Set<string>>);
				}
				FsVarUse(v, kind, last, id): {
					if (debug) {
						println("\t(B) id=" + i2s(id) + " var: " + toString(x) + "\n");
					}
					new_vars = switch (kind) {
						FsVarLocal(): {
							if (fiTypeIsScalar(v.type)) acc.head else insertSet(acc.head, v.name);
						}
						default: acc.head;
					}
					fsMergeSets(id2vars, id, acc.head);
					Cons(new_vars, acc.tail);
				}
				default: {
					if (x.id != -1) {
						fsMergeSets(id2vars, x.id, acc.head);
					}
					acc;
				}
			}
			if (debug) {
				println(
					"(B) POST id=" + i2s(x.id) + "\n" +
						"\tacc stack: " + fsPerceusShowStack(acc) + "\n" +
						"\tret stack: " + fsPerceusShowStack(ret) + "\n"
				);
			}
			ret;
		},
		\args -> reverseA(args),
		\p1, p2 -> Cons(mergeSets(p1.head, p2.head), p1.tail)
	);
	map(id2vars, \x -> ^x);
}

fsId2varsLast(s1: FsStat, num: int, id2vars_pre: [Set<string>], id2vars_post: [Set<string>], debug: bool) -> [Set<string>] {
	show_vars = \vs -> "[" + strGlue(set2array(vs), ", ") + "]";
	id2vars = generate(0, num, \__ -> ref makeSet());
	fsFold2(s1, Cons(makeSet(), makeList()),
		\acc: Cons<Set<string>>, x: FsAll -> {
			do_diff = \ -> {
				pre_vars = fsLookupLast(x, id2vars_pre, x.id);
				post_vars = fsLookupLast(x, id2vars_post, x.id);
				diff1 = differenceSets(pre_vars, post_vars);
				diff = differenceSets(diff1, acc.head);
				if (debug) {
					println(
						"(C) x.id: " + i2s(x.id) + "\n" +
						strIndent(
							"pre_vars: " + show_vars(pre_vars) + "\n" +
							"post_vars: " + show_vars(post_vars) + "\n" +
							"acc.head: " + show_vars(acc.head) + "\n" +
							"diff1: " + show_vars(diff1) + "\n" +
							"diff: " + show_vars(diff)
						)
					);
				}
				Pair(diff, diff1);
			}
			switch (x) {
				FsLambda(__,__,__,__,id): {
					if (debug) {
						println("(C) id=" + i2s(id) + " pushing stack: " + show_vars(acc.head));
					}
					p = do_diff();
					fsMergeSets(id2vars, id, p.first);
					Cons(makeSet(), acc);
				}
				default: {
					if (x.id == -1) acc else {
						p = do_diff();
						fsMergeSets(id2vars, x.id, p.first);
						Cons(mergeSets(acc.head, p.second), acc.tail);
					}
				}
			}
		},
		\acc, x -> {
			switch (x) {
				FsLambda(__,__,__,__,id): {
					cast(acc.tail: List<Set<string>> -> Cons<Set<string>>);
				}
				default: acc;
			}
		},
		\args -> args,
		\p1, p2 -> Cons(mergeSets(p1.head, p2.head), p1.tail)
	);
	map(id2vars, \x -> ^x);
}

fsIds(new_id: () -> int) -> (FsAll) -> FsAll {
	\s -> switch(s) {
		FsIf(c,__,__,__):         FsIf(s with id = new_id(), cond = fsExpSetId(c, new_id()));
		FsSwitch(__,__,__,__):    FsSwitch(s with id = new_id());
		FsSeq(__, __):            FsSeq(s with id = new_id());
		FsWrapExp(e,__):          FsWrapExp(s with id = new_id(), e = fsExpSetId(e, new_id()));
		FsRet(e,__):              FsRet(s with id = new_id(), e = fsExpSetId(e, new_id()));
		FsAssign(e1, e2,__):      FsAssign(s with id = new_id(), to = fsExpSetId(e1, new_id()), what = fsExpSetId(e2, new_id()));
		FsLet(__,e,__,__):        FsLet(s with id = new_id(), e = fsExpSetId(e, new_id()));
		FsLambda(__,__,__,__,__): FsLambda(s with id = new_id());
		FsVarUse(__,__,__,__):    FsVarUse(s with id = new_id());
		default: s;
	}
}

fsSetId(s: FsAll, x: int) -> FsAll {
	switch(s) {
		FsStat():  fsStatSetId(s, x);
		FsExp():   fsExpSetId(s, x);
		FsLambda(__,__,__,__,__): FsLambda(s with id = x);
	}
}

fsStatSetId(s: FsStat, x: int) -> FsStat {
	switch(s) {
		FsIf(__,__,__,__):     FsIf(s with id = x);
		FsSwitch(__,__,__,__): FsSwitch(s with id = x);
		FsSeq(__, __):         FsSeq(s with id = x);
		FsWrapExp(__,__):      FsWrapExp(s with id = x);
		FsRet(__,__):          FsRet(s with id = x);
		FsAssign(__,__,__):    FsAssign(s with id = x);
		FsLet(__,__,__,__):    FsLet(s with id = x);
		FsIncRc(__,__,__):     FsIncRc(s with id = x);
		FsDecRc(__,__):        FsDecRc(s with id = x);
	}
}

fsExpSetId(e: FsExp, x: int) -> FsExp {
	switch(e) {
		FsVarUse(__,__,__,__):    FsVarUse(e with id = x);
		FsClosure(__,__,__):      FsClosure(e with id = x);
		FsCall(__,__,__,__):      FsCall(e with id = x);
		FsCallPrim(__,__,__,__):  FsCallPrim(e with id = x);
		FsTailCall(__,__,__,__):  FsTailCall(e with id = x);
		FsCast(__,__,__,__):      FsCast(e with id = x);
		FsConst(__,__):           FsConst(e with id = x);
	}
}


fsControlFlowPaths(s: FsAll, acc: ref Tree<int, [[int]]>) -> [[int]] {
	check_single_branch = \branches -> {
		if (length(branches) > 1) {
			fail0("multy branches in:\n" + fs2s(s) + "\n" +
				"branches:\n" + superglue(branches, \branch -> "\t[" + superglue(branch, i2s, ", ") + "]", "\n")
			);
		} else if (length(branches) == 0) {
			fail0("no branches in:\n" + fs2s(s));
		} else {
			branches[0] 
		}
	}
	id_path = \id -> if (id == -1) [] else [id];
	switch (s) {
		FsIf(cond, s1, s2, id): {
			cond_cf = fsControlFlowPaths(cond, acc);
			s1_cf = fsControlFlowPaths(s1, acc);
			s2_cf = fsControlFlowPaths(s2, acc);
			mapConcat(cond_cf, \cond_path ->
				map(concat(s1_cf, s2_cf), \s_path ->
					concat3(id_path(id), cond_path, s_path),
				)
			);
		}
		FsSwitch(x,__, cs,id): {
			x_cf = fsControlFlowPaths(x, acc);
			forking = concatA(map(cs, \c ->
				map(fsControlFlowPaths(c.body, acc), \c_path ->
					concat(id_path(c.id), c_path)
				)
			));
			mapConcat(x_cf, \x_path ->
				map(forking, \c_path ->
					concat3(id_path(id), x_path, c_path)
				)
			);
		}
		FsSeq(ss, id): {
			fold(ss, [id_path(id)], \ac, x -> {
				x_cf = fsControlFlowPaths(x, acc);
				mapConcat(ac, \ac_path ->
					map(x_cf, \x_path ->
						concat(ac_path, x_path)
					)
				);
			});
		}
		FsLet(__, e1, s1, id): {
			e1_cf = fsControlFlowPaths(e1, acc);
			s1_cf = fsControlFlowPaths(s1, acc);
			mapConcat(e1_cf, \e1_path -> 
				map(s1_cf, \s1_path -> concat3(id_path(id), e1_path, s1_path))
			);
		}
		FsWrapExp(e1, id): {
			e1_cf = fsControlFlowPaths(e1, acc);
			map(e1_cf, \e1_path -> concat(id_path(id), e1_path));
		}
		FsRet(e1, id): {
			e1_cf = fsControlFlowPaths(e1, acc);
			map(e1_cf, \e1_path -> concat(id_path(id), e1_path));
		}
		FsAssign(to, what, id): {
			to_cf = fsControlFlowPaths(to, acc);
			what_cf = fsControlFlowPaths(what, acc);
			mapConcat(to_cf, \to_path -> 
				map(what_cf, \what_path -> concat3(id_path(id), to_path, what_path))
			);
		}
		FsIncRc(__,e1,id): {
			e1_cf = fsControlFlowPaths(e1, acc);
			map(e1_cf, \e1_path -> concat(id_path(id), e1_path));
		}
		FsDecRc(e1, id): {
			e1_cf = fsControlFlowPaths(e1, acc);
			map(e1_cf, \e1_path -> concat(id_path(id), e1_path));
		}
		FsClosure(vs, lam, id): {
			vs_cf = map(vs, \v ->
				check_single_branch(fsControlFlowPaths(v, acc))
			);
			fsControlFlowPaths(lam, acc);
			[concatA(concat([id_path(id)], vs_cf))];
		}
		FsLambda(__,__,e1,__,id): {
			e1_cf = fsControlFlowPaths(e1, acc);
			acc := setTree(^acc, id, e1_cf);
			[id_path(id)];
		}
		FsCall(fn, args, __,id): {
			fn_cf = check_single_branch(fsControlFlowPaths(fn, acc));
			args_cf = map(args, \arg ->
				check_single_branch(fsControlFlowPaths(arg, acc))
			);
			[concatA(concat([id_path(id), fn_cf], args_cf))];
		}
		FsCallPrim(__,es,__,id): {
			es_cf = map(es, \x ->
				check_single_branch(fsControlFlowPaths(x, acc))
			);
			[concatA(concat([id_path(id)], es_cf))];
		}
		FsTailCall(__,vs,__,id): {
			vs_cf = map(vs, \x ->
				check_single_branch(fsControlFlowPaths(x, acc))
			);
			[concatA(concat([id_path(id)], vs_cf))];
		}
		FsCast(e1,__,__,id): {
			e1_cf = fsControlFlowPaths(e1, acc);
			map(e1_cf, \e1_path -> concat(id_path(id), e1_path));
		}
		FsVarUse(__,__,__,id): [id_path(id)];
		FsConst(__,id): [id_path(id)];
	}
}

fsNumControlFlowPaths(s: FsAll, acc: ref Tree<int, double>) -> double {
	switch (s) {
		FsIf(cond, s1, s2, id): {
			fsNumControlFlowPaths(cond, acc) * (fsNumControlFlowPaths(s1, acc) + fsNumControlFlowPaths(s2, acc));
		}
		FsSwitch(x,__, cs,id): {
			fsNumControlFlowPaths(x, acc) * fold(cs, 0.0, \ac, c -> ac + fsNumControlFlowPaths(c.body, acc));
		}
		FsSeq(ss, id): {
			fold(ss, 1.0, \ac, x -> ac * fsNumControlFlowPaths(x, acc));
		}
		FsLet(__, e1, s1, id): {
			fsNumControlFlowPaths(e1, acc) * fsNumControlFlowPaths(s1, acc);
		}
		FsWrapExp(e1, id): {
			fsNumControlFlowPaths(e1, acc);
		}
		FsRet(e1, id): {
			fsNumControlFlowPaths(e1, acc);
		}
		FsAssign(to, what, id): {
			fsNumControlFlowPaths(to, acc) * fsNumControlFlowPaths(what, acc);
		}
		FsIncRc(__,e1,id): {
			fsNumControlFlowPaths(e1, acc);
		}
		FsDecRc(e1, id): {
			fsNumControlFlowPaths(e1, acc);
		}
		FsClosure(vs, lam, id): {
			fsNumControlFlowPaths(lam, acc);
			1.0;
		}
		FsLambda(__,__,e1,__,id): {
			e1_num = fsNumControlFlowPaths(e1, acc);
			acc := setTree(^acc, id, e1_num);
			1.0;
		}
		FsCall(fn, args, __,id): {
			fold(args, fsNumControlFlowPaths(fn, acc), \ac, arg ->
				ac * fsNumControlFlowPaths(arg, acc)
			);
		}
		FsCallPrim(__,es,__,id): {
			fold(es, 1.0, \ac, x ->
				ac * fsNumControlFlowPaths(x, acc)
			);
		}
		FsTailCall(__,vs,__,id): {
			fold(vs, 1.0, \ac, x ->
				ac * fsNumControlFlowPaths(x, acc)
			);
		}
		FsCast(e1,__,__,id): {
			fsNumControlFlowPaths(e1, acc);
		}
		FsVarUse(__,__,__,id): 1.0;
		FsConst(__,id): 1.0;
	}
}
