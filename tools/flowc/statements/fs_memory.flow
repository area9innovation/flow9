import ds/treeutils;
import tools/flowc/statements/fs_transform;

export {
	fsStatPerceus(s: FsStat, debug: bool) -> FsStat;
}

fsStatPerceus(s: FsStat, debug: bool) -> FsStat {
	id_counter = ref 0;
	new_id = \-> {
		id = ^id_counter;
		id_counter := ^id_counter + 1;
		id;
	}
	s1 = cast(fsMap(s, fsIds(new_id), false): FsAll -> FsStat);
	if (debug) {
		println("s1 with ids:\n" +
			//"s:\n" + fs2s(s) + "\n" +
			"s1:\n" + fs2s(s1) + "\n"
			//"s RAW:\n" + toStringFormatted(s1) + "\n"
		);
	}
	add_var = \x, acc, v -> {
		switch (lookupTree(acc, v.name)) {
			Some(w): fail(
				"Duplicate variable: " + v.name + " of type: " + pretFiType(v.type) + ", already is of type: " + pretFiType(w.type) + " in:\n" + 
				fs2s(x) + "\n" +
				"IN:\n" + fs2s(s1)
			);
			None(): { }
		}
		setTree(acc, v.name, v);
	}
	varName2type = fsFold(s1, makeTree(), \acc, x -> switch (x) {
		FsLet(v,__,__,__,__): add_var(x, acc, v);
		FsLambda(__,args,__,__,__): fold(args, acc, \ac, arg -> add_var(x, ac, arg));
		default: acc;
	}, true);
	id2vars_pre = fsId2varsPre(s1, debug);
	id2vars_post = fsId2varsPost(s1, debug);
	id2vars_last: Tree<int, Set<string>> = fsId2varsLast(s1, id2vars_pre, id2vars_post, debug);
	if (debug) {
		set_tree2s = \st -> {
			superglue(tree2pairs(st), \p ->
				i2s(p.first) + ": " + "[" + strGlue(set2array(p.second), ", ") + "]", "\n"
			);
		}
		println(
			"id2vars_pre:\n" + set_tree2s(id2vars_pre) + "\n" +
			"id2vars_post:\n" + set_tree2s(id2vars_post) + "\n" +
			"id2vars_last:\n" + set_tree2s(id2vars_last) + "\n" +
			""
		);
	}
	cast(fsMap(s1, \x -> switch (x) {
		FsIf(cond,__, pos, neg, id):  {
			memops = fsGetMemops(cond, id2vars_last, debug);
			fsAddIncs(memops,
				FsIf(x with
					s1 = fsDecUnused(pos.id, fsAddDecs2(memops, pos, id), id2vars_last, varName2type, debug),
					s2 = fsDecUnused(neg.id, fsAddDecs2(memops, neg, id), id2vars_last, varName2type, debug)
				), id
			);
		};
		FsSwitch(v,__,__, cs, id): {
			memops = fsGetMemops(v, id2vars_last, debug);
			fsAddIncs(memops,
				FsSwitch(x with
					cases = map(cs, \c ->
						FsCase(c with
							body = fsDecUnused(c.body.id, fsAddDecs2(memops, c.body, id), id2vars_last, varName2type, debug)
						)
					)
				), id
			);
		}
		FsRet(e,__,id): {
			memops = fsGetMemops(e, id2vars_last, debug);
			fsAddDecs1(memops, fsAddIncs(memops, x, id), id);
		}
		FsWrapExp(e,__,id): {
			memops = fsGetMemops(e, id2vars_last, debug);
			x1 = if (fiTypeIsScalar(fsType(e))) x else {
				FsDecRc(e, -1);
			}
			fsAddIncs(memops, fsAddDecs(memops, x1, id), id);
		}
		FsLet(__,e,__,s2, id): {
			memops = fsGetMemops(e, id2vars_last, debug);
			fsAddIncs(memops, FsLet(x with s = fsAddDecs2(memops, s2, s2.id)), id);
		}
		FsAssign(to, what,__,id): {
			memops = fsGetMemops2(to, what, id2vars_last, debug);
			fsAddIncs(memops, fsAddDecs(memops, x, id), id);
		}
		FsIncRc(__,__,__): fail0("FsIncRc met during memory operations markup");
		FsDecRc(__,__): fail0("FsDecRc met during memory operations markup");
		FsLambda(__,__, body,__,id): {
			FsLambda(x with body = fsDecUnused(id, body, id2vars_last, varName2type, debug));
		}
		default: x;
	}, true): FsAll -> FsStat);
}

fsDecUnused(id: int, x: FsStat, id2vars_last: Tree<int, Set<string>>, varName2type: Tree<string, FsVar>, debug: bool) -> FsStat {
	unused = map(set2array(lookupTreeDef(id2vars_last, id, makeSet())), \v_name ->
		switch (lookupTree(varName2type, v_name)) {
			Some(v): v;
			None(): fail0("Unknown variable: " + v_name);
		}
	);
	if (debug) {
		println("to dec <" + i2s(id) + "> unused: [" + superglue(unused, \v -> v.name, ", ") + "]");
	}
	if (length(unused) == 0) x else {
		FsSeq(concat(
			map(unused, \v -> FsDecRc(FsVarUse(v, FsVarLocal(), false, -1), -1)),
			fsUnwrapSeq(x)
		), -1);
	}
}

fsGetMemops(e: FsExp, id2vars_last: Tree<int, Set<string>>, debug: bool) -> [Pair<FsVar, FsMemOps>] {
	memops = tree2pairs(fsExpVarIncs(e, makeTree(), id2vars_last, -1, FsOrPrim()));
	if (debug) {
		memops_str = superglue(memops, \p -> p.first.name + "=inc(" + i2s(p.second.incs) + ")" + if (p.second.dec) ", dec" else "", ", ");
		println("memops for <" + i2s(e.id) + ">: [" + memops_str + "]");
	}
	memops;
}

fsGetMemops2(e1: FsExp, e2: FsExp, id2vars_last: Tree<int, Set<string>>, debug: bool) -> [Pair<FsVar, FsMemOps>] {
	memops = tree2pairs(fsExpVarIncs(e2, 
		fsExpVarIncs(e1, makeTree(), id2vars_last, -1, FsOrPrim()), 
		id2vars_last, -1, FsOrPrim()
	));
	if (debug) {
		memops_str = superglue(memops, \p -> p.first.name + "=inc(" + i2s(p.second.incs) + ")" + if (p.second.dec) ", dec" else "", ", ");
		println("memops for <" + i2s(e1.id) + ", " + i2s(e2.id) + ">: [" + memops_str + "]");
	}
	memops;
}

fsAddIncs(memops: [Pair<FsVar, FsMemOps>], x: FsStat, id: int) -> FsStat {
	incs = filtermap(memops, \p ->
		if (p.second.incs > 0) {
			Some(FsIncRc(p.second.incs, FsVarUse(p.first, FsVarLocal(), false, -1), -1));
		} else {
			None();
		}
	);
	if (length(incs) == 0) x else {
		FsSeq(concat(incs, fsUnwrapSeq(x)), id);
	}
}

fsAddDecs(memops: [Pair<FsVar, FsMemOps>], x: FsStat, id: int) -> FsStat {
	decs = filtermap(memops, \p ->
		if (p.second.dec) {
			Some(FsDecRc(FsVarUse(p.first, FsVarLocal(), false, -1), -1));
		} else {
			None();
		}
	);
	if (length(decs) == 0) x else {
		FsSeq(concat(fsUnwrapSeq(x), decs), id);
	}
}


fsAddDecs1(memops: [Pair<FsVar, FsMemOps>], x: FsStat, id: int) -> FsStat {
	decs = filtermap(memops, \p ->
		if (p.second.dec) Some(FsDecRc(FsVarUse(p.first, FsVarLocal(), false, -1), -1)) else None()
	);
	if (length(decs) == 0) x else {
		seq1 = fsUnwrapSeq(x);
		ret = cast(seq1[length(seq1) - 1]: FsStat -> FsRet);
		switch (ret.e) {
			FsTailCall(__,__,__,__): {
				FsSeq(concat3(tail(seq1), decs, [ret]), id);
			}
			default: {
				ret_var = FsVar("tmp_return_var", fsType(ret));
				FsSeq(
					arrayPush(tail(seq1),
						FsLet(ret_var, ret.e, [],
							FsSeq(arrayPush(decs, FsRet(FsVarUse(ret_var, FsVarLocal(), false, -1), [], -1)), id),
							-1
						)
					), -1
				);
			}
		}
	}
}

fsAddDecs2(memops: [Pair<FsVar, FsMemOps>], x: FsStat, id: int) -> FsStat {
	decs = filtermap(memops, \p ->
		if (p.second.dec) {
			Some(FsDecRc(FsVarUse(p.first, FsVarLocal(), false, -1), -1));
		} else {
			None();
		}
	);
	if (length(decs) == 0) x else {
		FsSeq(concat(decs, fsUnwrapSeq(x)), id);
	}
}

fsExpVarIncs(e: FsExp, acc: Tree<FsVar, FsMemOps>, last: Tree<int, Set<string>>, ind: int, op: FsPrim) -> Tree<FsVar, FsMemOps> {
	switch (e) {
		FsClosure(closure, __,__): {
			fold(closure, acc, \ac, x -> fsExpVarIncs(x, ac, last, -1, FsOrPrim()));
		}
		FsCall(f, args,__,__): {
			fold(args, fsExpVarIncs(f, acc, last, -1, FsOrPrim()), \ac, x ->
				fsExpVarIncs(x, ac, last, -1, FsOrPrim())
			);
		}
		FsCallPrim(o, es,__,__): {
			foldi(es, acc, \i, ac, x -> fsExpVarIncs(x, ac, last, i, o));
		}
		FsTailCall(args, vals, type, id): {
			fold(vals, acc, \ac, x -> fsExpVarIncs(x, ac, last, -1, FsOrPrim()));
		}
		FsCast(e1,__,__,__): {
			fsExpVarIncs(e1, acc, last, -1, FsOrPrim());
		}
		FsVarUse(x, kind, last1, id): {
			call_case = \-> if (containsSet(lookupTreeDef(last, id, makeSet()), x.name)) acc else {
				switch (lookupTree(acc, x)) {
					Some(p): setTree(acc, x, FsMemOps(p with incs = p.incs + 1));
					None(): setTree(acc, x, FsMemOps(x, 1, false));
				}
			}
			prim_case = \-> if (!containsSet(lookupTreeDef(last, id, makeSet()), x.name)) acc else {
				switch (lookupTree(acc, x)) {
					Some(p): setTree(acc, x, FsMemOps(p with dec = true));
					None(): setTree(acc, x, FsMemOps(x, 0, true));
				}
			}
			if (fiTypeIsScalar(x.type)) acc else {
				switch (kind) {
					FsVarGlobalFunc(): acc;
					FsVarUninit(): acc;
					default: {
						switch (op) {
							FsGePrim(): prim_case();
							FsLePrim(): prim_case();
							FsGtPrim(): prim_case();
							FsLtPrim(): prim_case();
							FsEqPrim(): prim_case();
							FsNePrim(): prim_case();
							FsFieldPrim(__,__): prim_case();
							FsIndexPrim(): prim_case();
							FsDerefPrim(): prim_case();
							default: call_case();
						}
					}
				}
			}
		}
		FsConst(c, id): acc;
	}
}

fsMergeStacks(l1: List<Set<string>>, l2: List<Set<string>>) -> List<Set<string>> {
	switch (l1) {
		Cons(s1, t1): {
			switch (l2) {
				Cons(s2, t2): {
					Cons(mergeSets(s1, s2), t1);
				}
				EmptyList(): l1;
			}
		}
		EmptyList(): l2;
	}
}

fsId2varsPre(s1: FsStat, debug: bool) -> Tree<int, Set<string>> {
	id2vars = ref makeTree();
	fsFold2(s1, Pair(makeSet(), makeList()),
		\acc, x: FsAll -> {
			add_v = \ac, v -> if (fiTypeIsScalar(v.type)) ac else insertSet(ac, v.name);
			switch (x) {
				FsLambda(closure, args,__,__,__): {
					new_vars = fold(args, fold(closure, makeSet(), \ac, v -> add_v(ac, v)), \ac, v -> add_v(ac, v));
					id2vars := insertMultipleMultimap(^id2vars, x.id, new_vars);
					Pair(new_vars, Cons(acc.first, acc.second));
				}
				FsLet(v, __,__,__,id): {
					new_vars = add_v(acc.first, v);
					id2vars := insertMultipleMultimap(^id2vars, x.id, acc.first);
					Pair(new_vars, acc.second);
				}
				default: {
					id2vars := insertMultipleMultimap(^id2vars, x.id, acc.first);
					Pair(acc.first, acc.second);
				}
			}
		},
		\acc, x -> {
			switch (x) {
				FsLambda(__,__,__,__,id): {
					switch (acc.second) {
						Cons(pop_vars, pop_stack): {
							if (debug) {
								println("(A) id=" + i2s(id) + " poping stack: " +
									"[" + strGlue(set2array(pop_vars), ", ") + "]"
								);
							}
							Pair(pop_vars, pop_stack);
						}
						default: acc;
					}
				}
				default: acc;
			}
		},
		\args -> args,
		\p1, p2 -> Pair(mergeSets(p1.first, p2.first), fsMergeStacks(p1.second, p2.second))
	);
	^id2vars;
}

fsId2varsPost(s1: FsStat, debug: bool) -> Tree<int, Set<string>> {
	show_vars = \vs -> "[" + strGlue(set2array(vs), ", ") + "]";
	id2vars = ref makeTree();
	fsFold2(s1, Pair(makeSet(), makeList()),
		\acc, x -> {
			switch (x) {
				FsLambda(__,__,__,__,id): {
					if (debug) {
						st = switch (acc.second) {
							Cons(vars,__): show_vars(vars);
							EmptyList(): ""
						}
						println(
							"(B) id=" + i2s(id) + " pushing stack: [" + show_vars(acc.first) + "]\n" +
							"\tacc.second: [" + st + "]\n"
						);
					}
					id2vars := insertMultipleMultimap(^id2vars, x.id, makeSet());
					Pair(makeSet(), Cons(acc.first, acc.second));
				}
				default: acc;
			}
		},
		\acc, x: FsAll -> {
			add_v = \ac, v -> if (fiTypeIsScalar(v.type)) ac else insertSet(ac, v.name);
			switch (x) {
				FsLambda(__,__,__,__,__): {
					switch (acc.second) {
						Cons(new_vars, new_stack): {
							if (debug) {
								println("(B) id=" + i2s(x.id) + " poping stack: " + show_vars(new_vars));
							}
							id2vars := insertMultipleMultimap(^id2vars, x.id, acc.first);
							Pair(new_vars, new_stack);
						}
						default: fail0("empty stack pop");
					}
				}
				FsVarUse(v, kind, last, id): {
					new_vars = switch (kind) {
						FsVarLocal(): add_v(acc.first, v);
						default: acc.first;
					}
					id2vars := insertMultipleMultimap(^id2vars, x.id, acc.first);
					Pair(new_vars, acc.second);
				}
				default: {
					id2vars := insertMultipleMultimap(^id2vars, x.id, acc.first);
					Pair(acc.first, acc.second);
				}
			}
		},
		\args -> reverseA(args),
		\p1, p2 -> Pair(mergeSets(p1.first, p2.first), fsMergeStacks(p1.second, p2.second))
	);
	^id2vars;
}

fsId2varsLast(s1: FsStat, id2vars_pre: Tree<int, Set<string>>, id2vars_post: Tree<int, Set<string>>, debug: bool) -> Tree<int, Set<string>> {
	show_vars = \vs -> "[" + strGlue(set2array(vs), ", ") + "]";
	id2vars = ref makeTree();
	fsFold2(s1, Pair(makeSet(), makeList()),
		\acc, x: FsAll -> {
			pre_vars = lookupTreeDef(id2vars_pre, x.id, makeSet());
			post_vars = lookupTreeDef(id2vars_post, x.id, makeSet());
			diff1 = differenceSets(pre_vars, post_vars);
			diff = differenceSets(diff1, acc.first);
			if (debug) {
				println(
					"(C) x.id: " + i2s(x.id) + "\n" +
					strIndent(
						"pre_vars: " + show_vars(pre_vars) + "\n" +
						"post_vars: " + show_vars(post_vars) + "\n" +
						"acc.first.vars: " + show_vars(acc.first) + "\n" +
						"diff1: " + show_vars(diff1) + "\n" +
						"diff: " + show_vars(diff)
					)
				);
			}
			switch (x) {
				FsLambda(__,__,__,__,id): {
					if (debug) {
						println("(C) id=" + i2s(id) + " pushing stack: " + show_vars(acc.first));
					}
					id2vars := insertMultipleMultimap(^id2vars, x.id, diff);
					Pair(makeSet(), Cons(acc.first, acc.second));
				}
				default: {
					id2vars := insertMultipleMultimap(^id2vars, x.id, diff);
					Pair(mergeSets(acc.first, diff1), acc.second);
				}
			}
		},
		\acc, x -> {
			switch (x) {
				FsLambda(__,__,__,__,id): {
					switch (acc.second) {
						Cons(vars, t): {
							if (debug) {
								println("(C) id=" + i2s(id) + " poping stack: " + show_vars(vars));
							}
							Pair(vars, t);
						}
						default: acc;
					}
				}
				default: acc;
			}
		},
		\args -> args,
		\p1, p2 -> Pair(mergeSets(p1.first, p2.first), fsMergeStacks(p1.second, p2.second))
	);
	^id2vars;
}

fsIds(new_id: () -> int) -> (FsAll) -> FsAll {
	\s -> switch(s) {
		FsIf(__,__,__,__,__):     FsIf(s with id = new_id());
		FsSwitch(__,__,__,__,__): FsSwitch(s with id = new_id());
		FsSeq(__, __):            FsSeq(s with id = new_id());
		FsWrapExp(__,__,__):      FsWrapExp(s with id = new_id());
		FsRet(__,__,__):          FsRet(s with id = new_id());
		FsAssign(__,__,__,__):    FsAssign(s with id = new_id());
		FsIncRc(__,__,__):        FsIncRc(s with id = new_id());
		FsDecRc(__,__):           FsDecRc(s with id = new_id());
		FsLet(__,__,__,__,__):    FsLet(s with id = new_id());
		FsClosure(__,__,__):      FsClosure(s with id = new_id());
		FsLambda(__,__,__,__,__): FsLambda(s with id = new_id());
		FsCall(__,__,__,__):      FsCall(s with id = new_id());
		FsCallPrim(__,__,__,__):  FsCallPrim(s with id = new_id());
		FsCast(__,__,__,__):      FsCast(s with id = new_id());
		FsConst(__,__):           FsConst(s with id = new_id());
		FsTailCall(__,__,__,__):  FsTailCall(s with id = new_id());
		FsVarUse(__,__,__,__):    FsVarUse(s with id = new_id());
	}
}
