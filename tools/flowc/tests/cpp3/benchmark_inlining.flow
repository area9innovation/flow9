import math/stringmath;
import net/url_parameter;

bench_fold(N: int, M: int) {
	t0 = timestamp();
	arr = map(enumFromTo(0, N), i2d);
	x = fold(enumFromTo(0, M), 0.0, \acc, x ->
		fold(arr, acc, \ac, y -> ac + i2d(x) + y)
	);
	t1 = timestamp() - t0;
	println("fold timing: " + d2st(t1 / 1000.0, 2) + " s., ret: " + d2s(x));
}

bench_map(N: int, M: int) {
	arr = map(enumFromTo(0, N), i2d);
	t0 = timestamp();
	a_x = map(enumFromTo(0, M), \x ->
		map(arr, \y -> i2d(x) + y)
	);
	t1 = timestamp() - t0;
	x = fold(a_x, 0.0, \acc, x -> acc + i2d(length(x)));
	println("map timing: " + d2st(t1 / 1000.0, 2) + " s., ret: " + d2s(x));
}

bench_iter(N: int, M: int) {
	t0 = timestamp();
	arr = map(enumFromTo(0, N), i2d);
	z = ref 0.0;
	iter(enumFromTo(0, M), \x ->
		iter(arr, \y -> z := ^z + y + i2d(x))
	);
	t1 = timestamp() - t0;
	println("iter timing: " + d2st(t1 / 1000.0, 2) + " s., ret: " + d2s(^z));
}

bench_suite(N: int, M: int) {
	println("Benching: N = " + i2s(N) + ", M = " + i2s(M));
	bench_fold(N, M);
	bench_map(N, M);
	bench_iter(N, M);
}

main() {
	t = timestamp();
	k_param = getUrlParameter("n");
	K = if (k_param == "") 3000 else s2i(k_param);
	bench_suite(K * 1, K * 100);
	bench_suite(K * 10, K * 10);
	bench_suite(K * 100, K * 1);
	println("benchmarking done in: " + d2st((timestamp() - t) / 1000.0, 2) + " s.");
	quit(0);
}
