// testing natives from array.flow

import ds/array;
import string;
import ds/list;
import expect;

// replace [func]
// concat [func]
// filter [func]


FooBar ::= Foo, Bar, Baz, Sbar;
	Foo(n : int);
	Bar(m : double);
	Baz(o : Foo);
	Sbar(s : string);


foo2bar(x : Foo) -> Bar {
	Bar(cast(x.n : int -> double));
}

testSubrange() {
	arr = [1, 2, 3, 4];

	res0 = subrange(arr, 1, 2);
	expect2(101, res0 == [2 , 3]);

	res1 = subrange(arr, 0, 4);
	expect2(102, res1 == arr);

	res2 = subrange(arr, 0, 0);
	expect2(103, res2 == []);

	res3 = subrange(arr, 3, 2);
	expect2(104, res3 == [4]);

	res4 = subrange(arr, 4, 1);
	expect2(105, res4 == []);

	arr1 = [1.0, 2.0, 3.0, 4.0];
	res5 = subrange(arr1, 1, 2);
	expect2(106, res5 == [2.0, 3.0]);

	res6 = subrange([MyStructA(111), MyStructB(222), MyStructC(333), MyStructD(444)], 1, 2);
	expect2(107, res6 == [MyStructB(222), MyStructC(333)]);

	res7 = subrange(["H", "e", "l", "l", "o"], 1, 3);
	expect2(108, res7 == ["e", "l", "l"]);
}

testReplace() {
	arr = [1, 2, 3, 4];
	arrd = [1.0, 2.0, 3.0, 4.0];

	res0 = replace(arr, 0, 2);
	expect2(201, res0 == [2, 2, 3, 4]);

	res1 = replace(arr, 4, 5);
	expect2(202, res1 == [1, 2, 3, 4, 5]);

	res2 = replace(arr, 0, arr[0]);
	expect2(203, res2 == arr);

	res3 = replace(arrd, 2, 2.0);
	expect2(204, res3 == [1.0, 2.0, 2.0, 4.0]);

	res4 = replace(arrd, 4, 5.0);
	expect2(205, res4 == [1.0, 2.0, 3.0, 4.0, 5.0]);

	res5 = replace(["H", "e", "k", "l", "o"], 2, "l");
	expect2(206, res5 == ["H", "e", "l", "l", "o"]);

	arr1 : [int] = [];
	res6 = replace(arr1, 0, 1);
	expect2(207, res6 == [1]);

	arr2 = [1,2,3];
	res7 = replace(arr2, 1, 5);
	expect2(208, res7 == [1,5,3]);

	arr3 = [1.2,2.3,3.4];
	res8 = replace(arr3, 1, 4.5);
	expect2(209, res8 == [1.2,4.5,3.4]);

	arr4 : [MyUnion] = [MyStructA(111), MyStructB(222), MyStructC(333)];
	res9 = replace(arr4, 1, MyStructD(444));
	expect2(210, res9 == [MyStructA(111), MyStructD(444), MyStructC(333)]);

	expect2(211, replace([[1], [2,1], [3,2,1], [4,3,2,1]], 3, [3,2,1]) == [[1], [2,1], [3,2,1], [3,2,1]]);
	expect2(212, replace([Foo(1), Foo(2), Foo(3), Foo(4)], 2, Foo(2)) == [Foo(1), Foo(2), Foo(2), Foo(4)]);
	// expect2(213, replace([\->"a1", \->"b2", \->"c3"], 0, \->"aa1") == [\->"aa1", \->"b2", \->"c3"]); // fails. Even when when trying: replace([\->"a1", \->"b2", \->"c3"], 0, \->"a1")
	expect2(214, replace([true, false, false, true], 2, true) == [true, false, true, true]);


}

testConcat() {
	a0 : [int] = [];
	expect2v(301, [1, 2, 3, 4, 5, 6], concat([1, 2, 3], [4, 5, 6]));
	expect2v(302, [4, 5, 6], concat(a0, [4, 5, 6]));
	expect2v(303, [1, 2, 3], concat([1, 2, 3], a0));
	expect2v(304, [], concat(a0, a0));
	expect2v(305, [4, 5, 6], concat([], [4, 5, 6]));
	expect2v(306, [1, 2, 3], concat([1, 2, 3], []));
	expect2v(308, [1,2,3,4,5,6], concat3([1,2],[3,4],[5,6]));

	// [double]
	res8 = concat([1.0, 2.0, 3.0], [4.0, 5.0, 6.0]);
	expect2(309, res8 == [1.0, 2.0, 3.0, 4.0, 5.0, 6.0]);

	// [string]
	res9 = concat(["1", "2", "3"], ["4", "5", "6"]);
	expect2(310, res9 == ["1", "2", "3", "4", "5", "6"]);
	
	// array of arrays
	expect2v(311, [[1],[2],[3]], concat([[1]], [[2],[3]]));

	expect2(312, concat([true, false], [false, true]) == [true, false, false, true]);
	expect2(312, concat([Foo(1), Foo(2)], [Foo(3), Foo(4)]) == [Foo(1), Foo(2), Foo(3), Foo(4)]);
	
	arru1 : [FooBar] = [Foo(1), Bar(2.0)];
	arru2 : [FooBar] = [Foo(3), Sbar("hello")];
	arru : [FooBar] = [Foo(1), Bar(2.0), Foo(3), Sbar("hello")];
	expect2v(312, concat(arru1, arru2), arru);

	// expect2(313, concat([\->"a1"], [\->"b2", \->"c3"]) == [\->"a1", \->"b2", \->"c3"]); //fails
}


testMap() {
	arr = [1, 2, 3, 4];

	res0 = map(arr, \a -> a * a);
	expect2(401, res0 == [1, 4, 9, 16]);

	res1 = map(arr, \a -> [a]);
	expect2(402, res1 == [[1], [2], [3], [4]]);

	res2 = map(["H", "e", "l", "l", "o"], \c -> getCharCodeAt(c, 0));
	expect2(403, res2 == [72, 101, 108, 108, 111]);

	res3 = map([1.0, 2.0, 3.0], \a -> a * a);
	expect2(404, res3 == [1.0, 4.0, 9.0]);

	res4 = map(arr, \a -> 1.0);
	expect2(405, res4 == [1.0, 1.0, 1.0, 1.0]);

	a0 : [int] = [];
	res5 = map(a0, \a -> a);
	expect2(406, res5 == []);

	res6 = map(arr, \a -> cast(a : int -> double));
	expect2(407, res6 == [1.0, 2.0, 3.0, 4.0]);

	res7 = map([1.0, 2.0, 3.0], \a -> a * a);
	expect2(408, res7 == [1.0, 4.0, 9.0]);

	res8 = map([Foo(1), Foo(2), Foo(3)], \a -> Bar(cast(a.n : int -> double)));	
	expect2(409, res8 == [Bar(1.0), Bar(2.0), Bar(3.0)]);
	
	// res8 = map([Foo(1), Foo(2), Foo(3)], foo2bar);			// Not working with first-class functions. TODO: fix it later!
	// expect2(409, res8 == [Bar(1.0), Bar(2.0), Bar(3.0)]);
	// res9 = map(arr, doubleInt);
	// expect2(410, res9 == [2, 4, 6, 8]);
	
	res9 = map([Foo(1), Foo(2), Foo(3)], \a -> Baz(a));
	expect2(410, res9 == [Baz(Foo(1)), Baz(Foo(2)), Baz(Foo(3))]);

	res10 = map(arr, \a -> cast(a : int -> flow));
	expect2(411, res10 == [cast(1 : int -> flow), cast(2 : int -> flow), cast(3 : int -> flow), cast(4 : int -> flow)]);

	res11 = map([cast(1 : int -> flow), cast(2 : int -> flow), cast(3 : int -> flow)], \a -> cast(a : flow -> int));
	expect2(412, res11 == [1,2,3]);

	res12 = map([1.0, 2.0, 3.0, 4.0], \a -> cast(a : double -> int));
	expect2(413, res12 == [1,2,3,4]);

	expect2(414, map([[1], [2,1], [3,2,1], [4,3,2,1]], \a -> map(a, \b -> b * 2)) == [[2], [4,2], [6,4,2], [8,6,4,2]]);

	arrUnion : [FooBar] = [Foo(1), Bar(2.0), Foo(3), Sbar("hello")];
	expect2(415, map(arrUnion, \a -> 
		switch (a) {
			Foo(n) : Foo(n*2);
			default : a;
		}) == [Foo(2), Bar(2.0), Foo(6), Sbar("hello")]);


	arrf = [\->"a1", \->"b2", \->"c3"];
	expect2(416, map(arrf, \f -> f()) == ["a1", "b2", "c3"]);

	expect2(417, map([true, false, false, true], \a -> !a) == [false, true, true, false]);

}

testMapi() {
	arr = [1, 2, 3, 4];

	expect2(501, mapi(arr, \i, a -> a + i) == [1, 3, 5, 7]);
	expect2(502, mapi(arr, \i, a -> i) == [0, 1, 2, 3]);
	expect2(503, mapi(["H", "e", "l", "l", "o"], \i, c -> getCharCodeAt(c, 0) + i) == [72, 102, 110, 111, 115]);
	expect2(504, mapi(arr, \i, a -> cast((a + i) : int -> double)) == [1.0, 3.0, 5.0, 7.0]);
	expect2(505, mapi([1.0, 2.0, 3.0], \i, a -> cast(i : int -> double) + a) == [1.0, 3.0, 5.0]);
	expect2(506, mapi([Foo(1), Foo(2), Foo(3)], \i, a -> Bar(cast((a.n + i) : int -> double))) == [Bar(1.0), Bar(3.0), Bar(5.0)]);
	expect2(507, mapi([true, false, false, true], \i, a -> i == 3 || !a) == [false, true, true, true]);

	arru : [FooBar] = [Foo(1), Bar(2.0), Foo(3), Sbar("hello")];
	expect2(508, mapi(arru, \i, a -> Foo(i)) == [Foo(0), Foo(1), Foo(2), Foo(3)]);
	expect2(509, mapi([\->"a1", \->"b2", \->"c3"], \i, f -> cast(i :int -> string) + f()) == ["0a1", "1b2", "2c3"]);


}

myConcat(a : [?], b : [?]) -> [?] { 
	concat(a, b); // workaround
}

MyUnion ::= MyStructA, MyStructB, MyStructC, MyStructD;
	MyStructA(a : int); MyStructB(b : int); MyStructC(c : int); MyStructD(d : int); 

testFold() {
	arrb = [true, true, false, false, true];
	arri = [1, 2, 3, 4];
	arrd = [1.0, 2.0, 3.0, 4.0];
	arrs = ["H", "e", "l", "l", "o"];
	arra = [[1], [2,1], [3,2,1], [4,3,2,1]];
	arrstruct = [Foo(1), Foo(2), Foo(3), Foo(4)];
	arrUnion : [FooBar] = [Foo(1), Bar(2.0), Foo(3), Sbar("hello")];
	arrf = [\->"a1", \->"b2", \->"c3"];

	expect2(601, fold(arrb, false, \a, b -> a == b) == false);
	expect2(602, fold(arrb, 0, \a, b -> if (b) a + 1 else a) == 3);
	expect2(603, fold(arrb, 0.0, \a, b -> if (b) a + 1.0 else a) == 3.0);
	expect2(604, fold(arrb, "", \a, b -> if (b) "true" else "false") == "true");
	expect2(605, fold(arrb, [], \a, b -> if (b) myConcat(a, [1]) else myConcat(a, [0])) == [1,1,0,0,1]);
	expect2(606, fold(arrb, [[]], \a, b -> [[b]]) == [[true]]);
	expect2(607, fold(arrb, Foo(2), \a, b -> if (b) Foo(a.n * 2) else a) == Foo(16));

	expect2(608, fold([1, 3, 5, 7, 8, 9], false, \a, b -> b % 2 == 0 || a) == true);
	expect2(609, fold(arri, 5, \a, b -> a + b) == 15);
	expect2(610, fold(arri, 5.0, \a, b -> cast(b : int -> double) + a) == 15.0);
	expect2(611, fold([101, 108, 108, 111], "H", \a, b -> list2string(Cons(fromCharCode(b), Cons(a, EmptyList())))) == "Hello");
	expect2(612, fold(arri, [0], \a, b -> myConcat(a, [b])) == [0,1,2,3,4]);
	expect2(613, fold(arri, [[0]], \a, b -> [myConcat(a[0], [b])]) == [[0,1,2,3,4]]);
	expect2(614 ,fold(arri, Foo(5), \a, b -> Foo(a.n + b)) == Foo(15));
	
	expect2(615, fold(arrd, false, \a, b -> b == 3.0 || a) == true);
	expect2(616, fold(arrd, 5, \a, b, -> a + cast(b : double -> int)) == 15);
	expect2(617, fold(arrd, 5.0, \a, b -> a + b) == 15.0);
	expect2(618, fold(arrd, "", \a, b -> if (b % 2.0 == 0.0) "even" else "odd") == "even");
	expect2(619, fold(arrd, [0.0], \a, b -> myConcat(a, [b])) == [0.0, 1.0, 2.0, 3.0, 4.0]);
	expect2(620, fold(arrd, [[]], \a, b -> [myConcat(a[0], [b])]) == [arrd]);
	expect2(621, fold(arrd, Bar(5.0), \a, b -> Bar(a.m + b)) == Bar(15.0));

	expect2(622, fold(arrs, false, \a, b -> a || b == "e") == true);
	expect2(623, fold(arrs, 0, \a, b -> a + getCharCodeAt(b, 0)) == 500);
	expect2(624, fold(arrs, 0.0, \a, b -> a + cast(getCharCodeAt(b, 0) : int -> double)) == 500.0);
	expect2(625, fold(arrs, "A", \a, b -> b) == "o");
	expect2(626, fold(arrs, [], \a, b -> myConcat(a, [b])) == arrs);
	expect2(627, fold(arrs, [[]], \a, b -> [myConcat(a[0], [b])]) == [arrs]);
	expect2(628, fold(arrs, Sbar(""), \a, b -> Sbar(b)) == Sbar("o"));

	expect2(629, fold(arra, false, \a, b -> a || myLength(b) == 3) == true);
	expect2(630, fold(arra, 0, \a, b -> a + myLength(b)) == 10);
	expect2(631, fold(arra, 5.0, \a, b -> a + cast(b[0] : int -> double)) == 15.0);
	expect2(632, fold(arra, "", \a, b -> if (b[0] % 2 == 0) "even" else "odd") == "even");
	expect2(633, fold(arra, [], \a, b -> myConcat(a, b)) == [1,2,1,3,2,1,4,3,2,1]);
	expect2(634, fold(arra, [[]], \a, b -> [myConcat(a[0], b)]) == [[1,2,1,3,2,1,4,3,2,1]]);
	expect2(635, fold(arra, Foo(0), \a, b -> Foo(a.n + fold(b, a.n, \c, d -> c + d))) == Foo(42));

	expect2(636, fold(arrstruct, false, \a, b -> b.n % 2 == 0) == true);
	expect2(637, fold(arrstruct, 5, \a, b -> a + b.n) == 15);
	expect2(638, fold(arrstruct, 5.0, \a, b -> a + cast(b.n : int -> double)) == 15.0);
	expect2(639, fold(arrstruct, "", \a, b -> if (b.n % 2 == 0) "even" else "odd") == "even");
	expect2(640, fold(arrstruct, [], \a, b -> myConcat(a, [b])) == arrstruct);
	expect2(641, fold(arrstruct, [[]], \a, b -> [myConcat(a[0], [b])]) == [arrstruct]);
	expect2(642, fold(arrstruct, Baz(Foo(0)), \a, b -> Baz(b)) == Baz(Foo(4)));
	
	expect2(643, fold(arrUnion, false, \a, b -> 
		switch (b) {
			Baz(f) : true;
			default : a;
			}) == false );

	expect2(644, fold(arrUnion, 0, \a, b -> 
		switch (b) {
			Foo(k) : a + k;
			default : a;
		}) == 4);

	expect2(645, fold(arrUnion, 0.0, \a, b -> 
		switch (b) {
			Bar(k) : a + k;
			default : a;
		}) == 2.0);

	expect2(646, fold(arrUnion, "", \a, b -> 
		switch (b) {
			Sbar(s) : a + s;
			default : a;
		}) == "hello");

	expect2(647, fold(arrUnion, [], \a, b -> myConcat(a, [b])) == arrUnion);
	expect2(648, fold(arrUnion, [[]], \a, b -> myConcat(a, [[b]])) == [[], [Foo(1)], [Bar(2.0)], [Foo(3)], [Sbar("hello")]]);
	expect2(649, fold(arrUnion, Foo(0), \a, b -> 
		switch (b) {
			Foo(k) : Foo(a.n + k);
			default : a;
			}) == Foo(4));

	expect2(650, fold(arrf, false, \a, f -> a || f() == "b2") == true);
	expect2(651, fold(arrf, 0, \a, f -> if (f() == "b2") a + 1 else a) == 1);
	expect2(652, fold(arrf, 0.0, \a, f -> if (f() == "b2") a + 3.14 else a) == 3.14);
	expect2(653, fold(arrf, "", \a, f -> a + f()) == "a1b2c3");
	expect2(654, fold(arrf, [], \a, f -> myConcat(a, [f()])) == ["a1", "b2", "c3"]);
	expect2(655, fold(arrf, [[]], \a, f -> myConcat(a, [[f()]])) == [[], ["a1"], ["b2"], ["c3"]]);
	expect2(656, fold(arrf, Sbar(""), \a, f -> Sbar(a.s + f())) == Sbar("a1b2c3"));

	a : [MyUnion] = [MyStructA(111), MyStructB(222), MyStructC(333)];
	s = fold(a, "", \acc, u : MyUnion -> {
		acc + switch (u) {
			MyStructA(__): "a" + cast(u.a : int -> string);
			MyStructB(__): "b" + cast(u.b : int -> string);
			MyStructC(__): "c" + cast(u.c : int -> string);
			MyStructD(__): "d" + cast(u.d : int -> string);
		}
	});
	expect2v(657, "a111b222c333", s);
}


testFoldi() {
	arrb = [true, true, false, false, true];
	arri = [1, 2, 3, 4];
	arrd = [1.0, 2.0, 3.0, 4.0];
	arrs = ["H", "e", "l", "l", "o"];
	arra = [[1], [2,1], [3,2,1], [4,3,2,1]];
	arrstruct = [Foo(1), Foo(2), Foo(3), Foo(4)];
	arrUnion : [FooBar] = [Foo(1), Bar(2.0), Foo(3), Sbar("hello")];
	arrf = [\->"a1", \->"b2", \->"c3"];

	expect2(701, foldi(arrb, false, \i, a, b -> i == 2 || !a) == true);
	expect2(702, foldi(arrb, 0, \i, a, b -> if (b) a + i + 1 else a + i) == 13);
	expect2(703, foldi(arrb, 2.0, \i, a, b -> if (b) a + cast(i : int -> double) else a * cast(i : int -> double)) == 22.0);
	expect2(704, foldi(arrb, "", \i, a, b -> if (i > 3) "long array" else if (b) "true last" else "false last") == "long array");
	expect2(705, foldi(arrb, [], \i, a, b -> if (i %2 == 0) myConcat(a, [b]) else a) == [true, false, true]);
	expect2(706, foldi(arrb, [[]], \i, a, b -> if (i % 2 != 0) [myConcat(a[0], [b])] else a) == [[true, false]]);
	expect2(707, foldi(arrb, Foo(0), \i, a, b -> if (b) Foo(a.n + i) else a) == Foo(5));

	expect2(708, foldi(arri, false, \i, a, b -> if ((i * b) % 3 == 0) !a else a) == true);
	expect2(709, foldi(arri, 0, \i, a, b -> a + i + b) == 16);
	expect2(710, foldi(arri, 2.0, \i, a, b -> cast(i + b : int -> double) + a) == 18.0);
	expect2(711, foldi(arri, "", \i, a, b -> if (b % 2 == 0) "even" else "odd") == "even");
	expect2(712, foldi(arri, [], \i, a, b -> myConcat(a, [i,b])) == [0,1,1,2,2,3,3,4]);
	expect2(713, foldi(arri, [[]], \i, a, b -> myConcat(a, [[i,b]])) == [[],[0,1],[1,2],[2,3],[3,4]]);
	expect2(714, foldi(arri, Foo(9), \i, a, b -> Foo(a.n + i + b)) == Foo(25));

	expect2(715, foldi(arrd, true, \i, a, b -> if (i % 2 == 0) b % 2.0 == 0.0 else a) == false);
	expect2(716, foldi(arrd, 0, \i, a, b -> i + a + cast(b : double -> int)) == 16);
	expect2(717, foldi(arrd, 2.0, \i, a, b -> if (i % 2 == 0) a * b else a + b) == 16.0);
	expect2(718, foldi(arrd, "", \i, a, b -> if (b % 3.0 == 1.0) "1bmod3" else if (i % 2 == 0) "even" else "odd") == "1bmod3");
	expect2(719, foldi(arrd, [], \i, a, b -> if (i % 2 == 0) myConcat(a, [b]) else a) == [1.0, 3.0]);
	expect2(720, foldi(arrd, [[]], \i, a, b -> if (i % 2 == 0) myConcat(a, [[b]]) else a) == [[],[1.0],[3.0]]);
	expect2(721, foldi(arrd, Bar(5.0), \i, a, b -> if (i % 2 == 0) Bar(a.m + b) else a) == Bar(9.0));

	expect2(722, foldi(arrs, false, \i, a, b -> a || b == "e") == true);
	expect2(723, foldi(arrs, 0, \i, a, b -> if (b == "l") a + 1 else a) == 2);
	expect2(724, foldi(arrs, 0.0, \i, a, b -> if (b == "l" && i == 2) a + 0.5 else a) == 0.5);
	expect2(725, foldi(arrs, "A", \i, a, b -> if (i % 3 == 1) b else a) == "o");
	// expect2(726, foldi(arrs, [], \i, a, b -> myConcat(a, [Pair(i, b)])) == [Pair(0, "H"), Pair(1, "e"), Pair(2, "l"), Pair(3, "l"), Pair(4, "o")]);
	expect2(727, foldi(arrs, [[]], \i, a, b -> myConcat(a, [[b]])) == [[],["H"],["e"],["l"],["l"],["o"]]);
	expect2(728, foldi(arrs, Sbar("A"), \i, a, b -> if (i % 3 == 1) Sbar(b) else a) == Sbar("o"));

	expect2(729, foldi(arra, false, \i, a, b -> a || length(b) == 3) == true);
	expect2(730, foldi(arra, 0, \i, a, b -> foldi(b, a, \j, c, d -> c + d + j)) == 30);
	expect2(731, foldi(arra, 0.0, \i, a, b -> foldi(b, a, \j, c, d -> c + cast(d + j : int -> double))) == 30.0);
	expect2(732, foldi(arra, "", \i, a, b -> if ((myLength(b) * i) % 2 == 0) "even" else "odd") == "even");
	expect2(733, foldi(arra, [], \i, a, b -> myConcat(a, myConcat([i], b))) == [0,1,1,2,1,2,3,2,1,3,4,3,2,1]);
	expect2(734, foldi(arra, [[]], \i, a, b -> myConcat(a, [myConcat([i],b)])) == [[],[0,1],[1,2,1],[2,3,2,1],[3,4,3,2,1]]);
	expect2(735, foldi(arra, Foo(0), \i, a, b -> if (myLength(b) > 0) Foo(b[myLength(b)-1-i]) else a) == Foo(4));


	expect2(736, foldi(arrstruct, false, \i, a, b -> a || b.n > 2 || i > 5 ) == true);
	expect2(737, foldi(arrstruct, 0, \i, a, b -> a + i + b.n) == 16);
	expect2(738, foldi(arrstruct, 0.0, \i, a, b -> a + cast((i + b.n) : int -> double)) == 16.0);
	expect2(739, foldi(arrstruct, "", \i, a, b -> if (b.n + i % 2 == 0) "even" else "odd") == "odd");
	expect2(740, foldi(arrstruct, [], \i, a, b -> myConcat(a, [Foo(i), b])) == [Foo(0), Foo(1), Foo(1), Foo(2), Foo(2), Foo(3), Foo(3), Foo(4)]);
	expect2(741, foldi(arrstruct, [[]], \i, a, b -> myConcat(a, [[Foo(i), b]])) == [[], [Foo(0), Foo(1)], [Foo(1), Foo(2)], [Foo(2), Foo(3)], [Foo(3), Foo(4)]]);
	expect2(742, foldi(arrstruct, Foo(0), \i, a, b -> Foo(a.n + i + b.n)) == Foo(16));


	expect2(743, foldi(arrUnion, false,\i, a, b -> 
		switch (b) {
			Sbar(s) : true;
			default : a;
		}) == true);

	expect2(744, foldi(arrUnion, 0, \i, a, b -> 
		switch (b) {
			Foo(k) : a + k + i;
			default : a; 
		}) == 6);

	expect2(745, foldi(arrUnion, 0.0, \i, a, b -> 
		switch (b) {
			Bar(k) : a + k + cast(i : int -> double);
			default : a; 
		}) == 3.0);

	expect2(746, foldi(arrUnion, "", \i, a, b -> 
		switch(b) {
			Sbar(s) : a + s;
			default: a;
		}) == "hello");

	expect2(747, foldi(arrUnion, [], \i, a, b -> myConcat(a, [Foo(i), b])) == [Foo(0), Foo(1), Foo(1), Bar(2.0), Foo(2), Foo(3), Foo(3), Sbar("hello")]);
	expect2(748, foldi(arrUnion, [[]], \i, a, b -> myConcat(a, [[Foo(i), b]])) == [[], [Foo(0), Foo(1)], [Foo(1), Bar(2.0)], [Foo(2), Foo(3)], [Foo(3), Sbar("hello")]]);

	expect2(749, foldi(arrUnion, Foo(0), \i, a, b -> 
		switch (b) {
			Foo(k) : Foo(a.n + i + k);
			default : a;
		}) == Foo(6));

	expect2(750, foldi(arrf, false, \i, a, f -> a || f() == "b2") == true);
	expect2(751, foldi(arrf, 0, \i, a, f -> if (f() == "c3") a + i else a) == 2);
	expect2(752, foldi(arrf, 0.0, \i, a, f -> if (f() == "c3") a + cast(i : int -> double) else a) == 2.0);
	expect2(753, foldi(arrf, "", \i, a, f -> if (i % 2 == 0) a + f() else a) == "a1c3");
	expect2(754, foldi(arrf, [], \i, a, f -> myConcat(a, [f()])) == ["a1", "b2", "c3"]);
	expect2(755, foldi(arrf, [[]], \i, a, f -> myConcat(a, [[f()]])) == [[], ["a1"], ["b2"], ["c3"]]);
	expect2(756, foldi(arrf, Sbar(""), \i, a, f -> if (i % 2 == 0) Sbar(a.s + f()) else a) == Sbar("a1c3"));
}

testFilter() {
	arru : [FooBar] = [Foo(1), Bar(2.0), Foo(3), Sbar("hello")];


	expect2(801, filter([true, false, false, true], \a -> a) == [true, true]);
	expect2(802, filter([1, 2, 3, 4], \a -> a % 2 == 0) == [2, 4]);
	expect2(803, filter([1.0, 2.0, 3.0, 4.0], \a -> a % 2.0 == 1.0) == [1.0, 3.0]);
	expect2(804, filter(["H", "e", "l", "l", "o"], \c -> getCharCodeAt(c, 0) == 108) == ["l", "l"]);
	expect2(805, filter([[1], [2,1], [3,2,1], [4,3,2,1]], \a -> myLength(a) > 2) == [[3,2,1], [4,3,2,1]]);
	expect2(806, filter([Foo(1), Foo(2), Foo(3), Foo(4)], \a -> a.n % 2 == 0) == [Foo(2), Foo(4)]);
	expect2(807, filter(arru, \a -> 
		switch (a) {
			Foo(n) : true;
			default : false;
		}) == [Foo(1), Foo(3)]);
	// expect2(808, filter([\->"a1", \->"b2", \->"c3"], \f -> f() == "b2") == [\->"b2"]); // fails
}

testIteriUntil() {
	res0 = iteriUntil([1, 2, 3, 4], \i, a -> a > 2);
	expect2(901, res0 == 2);

	res1 = iteriUntil([1.0, 2.0, 3.0, 4.0], \i, a -> a > 2.7);
	expect2(902, res1 == 2);

	res2 = iteriUntil(["H", "e", "l", "l", "o"], \i, c -> c == "l" || i == 80);
	expect2(903, res2 == 2);

	res3 = iteriUntil([1, 2, 3, 4], \i, a -> a > 4 || i == 2);
	expect2(904, res3 == 2);

	res4 = iteriUntil([1, 2, 3, 4], \i, a -> a - i == 0);
	expect2(905, res4 == 4);

	res5 = iteriUntil([Foo(1), Foo(2), Foo(3), Foo(4)], \i, a -> a.n > 2);
	expect2(906, res5 == 2);

	res6 = iteriUntil([[0], [1,2], [3,4,5], [6,7,8,9]], \i, a -> a[i] == 5);
	expect2(907, res6 == 2);

	res7 = iteriUntil([cast(1 : int -> flow), cast(2 : int -> flow), cast(3 : int -> flow)], \i, a -> cast(a : flow -> int) == 2);
	expect2(908, res7 == 1);

	res8 = iteriUntil([true, false, false, true, true], \i, a -> a && i != 0);
	expect2(909, res8 == 3);

	arru : [FooBar] = [Foo(1), Bar(2.0), Foo(3), Sbar("hello")];
	expect2(910, iteriUntil(arru, \i, a -> a == Foo(3)) == 2);

	expect2(911, iteriUntil([\->"a1", \->"b2", \->"c3"], \i, f -> f() == "b2") == 1);

}

S10i(x : int);

testElemIndex() {
	ai = [1,2,3];
	expect2(1001, elemIndex(ai, 2, -1) == 1);
	expect2(1002, elemIndex(ai, 4, 666) == 666);

	af = [1.0,2.0,3.0];
	expect2(1003, elemIndex(af, 2.0, -1) == 1);
	expect2(1004, elemIndex(af, 4.0, 666) == 666);

	as = [S10i(1),S10i(2),S10i(3)];
	expect2(1005, elemIndex(as, S10i(2), -1) == 1);
	expect2(1006, elemIndex(as, S10i(4), 666) == 666);

	astr = ["1","2","3"];
	expect2(1007, elemIndex(astr, "2", -1) == 1);
	expect2(1008, elemIndex(astr, "4", 666) == 666);

	expect2(1009, elemIndex([[1], [2,1], [3,2,1], [4,3,2,1]], [3,2,1], 666) == 2);
	expect2(1010, elemIndex([false, false, false, true, false, false], true, 666) == 3);

	arru : [FooBar] = [Foo(1), Bar(2.0), Foo(3), Sbar("hello")];
	expect2(1011, elemIndex(arru, Foo(3), 666) == 2);
}

testCallbacksApply(a : ?, cb : (?) -> ??) -> ?? {
	cb(a)
}

myLength(a : [?]) -> int {
	length(a)
}

testCallbacks(base : int) {
	a = [1,2,3];
	expect2(base + 0, 3 == myLength(a)); 
	expect2(base + 1, 3 == testCallbacksApply(a, myLength));
	// expect2(base + 2, 3 == testCallbacksApply(a, length)); // TODO: Cannot resolve variable in WasmVar: length___c1aii yet
	
	myLength2 = \aa : [int] -> { myLength(aa) };
	expect2(base + 3, 3 == testCallbacksApply(a, myLength2)); 
	
	// myLength3 = \aa : [int] -> { length(aa) };	// TODO: Cannot resolve variable in WasmVar: length___c1aii yet
	// expect2(base + 4, 3 == myLength3(a)); 
	// expect2(base + 5, 3 == testCallbacksApply(a, myLength2)); 
	
	s = "123";
	expect2(base + 10, 3 == testCallbacksApply(s, strlen));
	
	myStrlen = \ss -> strlen(s);
	expect2(base + 11, 3 == testCallbacksApply(s, myStrlen));
}

testList2array() {
	l0 = EmptyList();
	l1 = Cons(111, l0);
	li = Cons(l1, Cons(l0, EmptyList()));
	a = list2array(li);
	expect2(1201, isSameObj(a[0], l0));
	expect2(1202, isSameObj(a[1], l1));
}

main() {
	// expectVerbose();
	testSubrange();
	testReplace();
	testConcat();
	testMap();
	testMapi();
	testFold();
	testFoldi();
	testFilter();
	testIteriUntil();
	testElemIndex();
	testCallbacks(1100);
	testList2array();
	
	expectFinish();
}