//import ds/tree;
//import tools/flowc/fctypeenv;
//import tools/flowc/incremental/fimodule;
import tools/flowc/incremental/fi_helpers;
//import tools/flowc/incremental/fiprettyprint;
import tools/flowc/incremental/fisubtype;
//import tools/flowc/incremental/fi_verify_struct_type;
import tools/flowc/flowc_helpers;
//import tools/flowc/flowc_module;
import tools/flowc/incremental/fc2fi;
//import tools/flowc/incremental/fisubtype;

export {
	moduleWhereNameIsDefined(globalNames : FcFiGlobalNames, name : string) -> string;
	
	// Unions of both variants of global names
	FcFiGlobalNames ::= FcGlobalNames, FiGlobalNames;
	FcFiTypeStruct  ::= FcTypeStruct, FiTypeStruct;
	FcFiTypeUnion   ::= FcTypeUnion, FiTypeUnion;

	GetFieldTypeParams(
		globalNames : FcFiGlobalNames,
		moduleTypes : Tree<string, FiStructOrUnion>,
		field_name : string,
		type_name : string,
		onError : (FcError) -> void,
		moduleName : string,
		start : int,
	);

	getFieldType(params : GetFieldTypeParams) -> Maybe<FiType>;
}

getFieldType(params : GetFieldTypeParams) -> Maybe<FiType> {
	failCallback = \pos -> {
		params.onError(FcError(
			"There is no field '" + params.field_name + "' in struct '" + params.type_name + "'",
			[
				FcPosition(params.moduleName, params.start, params.start),
				FcPosition(moduleWhereNameIsDefined(params.globalNames, params.type_name), pos, pos),
			]
		));
		None();
	};
	if (params.field_name == "structname") Some(FiTypeString()) else {
		switch (lookupStruct(params.globalNames, params.type_name)) {
			Some(struct): getStructFieldType(struct, params.field_name, failCallback);
			None(): {
				switch (lookupUnion(params.globalNames, params.type_name)) {
					Some(union): getUnionFieldType(union, params, failCallback);
					None(): {
						switch (lookupTree(params.moduleTypes, params.type_name)) {
							Some(type): {
								switch (type) {
									FiTypeStruct(__,__,__,__): getStructFieldType(type, params.field_name, failCallback);
									FiTypeUnion(__, __, typenames, __): getUnionFieldType(type, params, failCallback);
								}
							}
							None(): {
								// This branch must be impossible
								None();
							}
						}
					}
				}
			}
		}
	}
}

lookupStruct(globalNames : FcFiGlobalNames, type_name : string) -> Maybe<FcFiTypeStruct> {
	switch (globalNames) {
		FcGlobalNames(__,__,__,__,__,__,__,__,__,__,__,__): lookupTree(globalNames.structs, type_name);
		FiGlobalNames(__,__,__,__,__,__,__,__): lookupTree(globalNames.structs, type_name);
	}
}

lookupUnion(globalNames : FcFiGlobalNames, type_name : string) -> Maybe<FcFiTypeUnion> {
	switch (globalNames) {
		FcGlobalNames(__,__,__,__,__,__,__,__,__,__,__,__): lookupTree(globalNames.unions, type_name);
		FiGlobalNames(__,__,__,__,__,__,__,__): lookupTree(globalNames.unions, type_name);
	}
}

getStructFieldType(struct : FcFiTypeStruct, field_name : string, failCallback : (int) -> Maybe<FiType>) -> Maybe<FiType> {
	switch (struct) {
		FiTypeStruct(__,__,__,__):
			switch (find(struct.args, \arg -> arg.name == field_name)) {
				Some(arg): Some(arg.type);
				None(): failCallback(struct.start);
			}
		FcTypeStruct(__,__,__,__):
			switch (find(struct.args, \arg -> arg.name == field_name)) {
				Some(arg): Some(fctype2fiEasy(arg.type));
				None(): failCallback(struct.info.start);
			}
	}
}

getUnionFieldType(union : FcFiTypeUnion, params : GetFieldTypeParams, failCallback : (int) -> Maybe<FiType>) -> Maybe<FiType>  {
	switch (union) {
		FiTypeUnion(__,__,__,__): {
			field_types = map(union.typenames, \tp -> getFieldType(GetFieldTypeParams(params with type_name = tp.name)));
			mostGeneralType(field_types, params.globalNames)
		}
		FcTypeUnion(__,__,__,__): {
			field_types = map(union.typenames, \tp -> getFieldType(GetFieldTypeParams(params with type_name = tp.name)));
			mostGeneralType(field_types, params.globalNames)
		}
	}
};

mostGeneralType(types : [Maybe<FiType>], names : FcGlobalNames) -> Maybe<FiType> {
	pair = fold(types, Pair(None(), true), 
		\acc, mtp -> {
			switch (mtp) {
				Some(t1): {
					switch (acc.first) {
						Some(t2): {
							if (isFiSubType(names, t1, t2, true, true)) {
								Pair(Some(t2), true)
							} else if (isFiSubType(names, t2, t1, true, true)) {
								Pair(Some(t1), true)
							} else {
								Pair(None(), false)
							}
						}
						None(): Pair(Some(t1), true)
					}
				}
				None(): Pair(None(), false)
			}
		}
	);
	if (pair.second) {
		pair.first
	} else {
		None()
	}
};

moduleWhereNameIsDefined(globalNames : FcFiGlobalNames, name : string) -> string {
	switch (globalNames) {
		FcGlobalNames(__,__,__,__,__,__,__,__,__,__,__,__): {
			lookupTreeDef(globalNames.function2module, name,
				lookupTreeDef(globalNames.globalVar2module, name,
					lookupTreeDef(globalNames.struct2module, name,
						lookupTreeDef(globalNames.union2module, name,
							lookupTreeDef(globalNames.native2module, name, "")
						)
					)
				)
			);
		}
		FiGlobalNames(__,__,__,__,__,__,__,__): {
			lookupTreeDef(globalNames.function2module, name,
				lookupTreeDef(globalNames.globalVar2module, name,
					lookupTreeDef(globalNames.struct2module, name,
						lookupTreeDef(globalNames.union2module, name,
							lookupTreeDef(globalNames.native2module, name, "")
						)
					)
				)
			);
		}
	}
}
