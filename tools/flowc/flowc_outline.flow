import tools/flowc/flowc_typecheck;

export {
	fcPrintOutline(config : CompilerConfig, globEnv : FcTypeEnvGlobal, showStopper : (int) -> void) -> void;
}

fcPrintOutline(config : CompilerConfig, globEnv : FcTypeEnvGlobal, showStopper : (int) -> void) -> void {
	parsed = parseFlowFileOrLoadCached(config, globEnv, config.flowfile, "", 0, 0);
	fcacc = prepareInitialFlowcAcc(config);
	if (parsed.second == dummyFcError) {
		switch (parsed.first) {
			FcParsed(fileinfo, syntaxtree, __, __, __): {
				fcmodule = desugarFcModule(fcacc, initFcTypeEnvLocal(), fileinfo, syntaxtree);
				fcPrintOutlineOfFcModule(config, fcmodule);
			}
			FcLoaded(fimodule, __): {
				fcPrintOutlineOfFiModule(config, fimodule);
			}
		}
	} else {
		printFcError(fcacc, parsed.second);
	}
	showStopper(0)
}

fcPrintOutlineOfFiModule(config : CompilerConfig, module : FiModule) -> void {
	fileinfo = module.fileinfo;
	declStarts =
	fold(module.functions,
		fold(module.globalVars,
			fold(module.natives,
				makeTree(),
				\acc, nat -> setTree(acc, nat.name, nat.declStart)
			),
			\acc, var -> setTree(acc, var.name, var.declStart)
		),
		\acc, fun -> setTree(acc, fun.name, fun.declStart)
	);
	iter(module.imports, \imp -> printLocation(config, fileinfo, imp.start, -1, "import " + imp.flowfile));
	iter(module.forbids, \fbd -> printLocation(config, fileinfo, fbd.start, -1, "forbid " + fbd.flowfile));
	iter(module.exports, \ex -> printLocation(config, fileinfo, lookupTreeDef(declStarts, ex, -1), -1, "export " + ex));
	iter(module.structs, \str -> printLocation(config, fileinfo, str.start, -1, "struct " + str.name));
	iter(module.unions, \uni -> printLocation(config, fileinfo, uni.start, -1, "union " + uni.name));

	iter(module.functions, \fun -> {
		printLocation(config, fileinfo, fun.defiStart, -1, "fundef " + fun.name);
		if (fun.declStart != fun.defiStart) {
			printLocation(config, fileinfo, fun.declStart, -1, "fundecl " + fun.name);
		}
	});

	iter(module.globalVars, \var -> {
		printLocation(config, fileinfo, var.defiStart, -1, "vardef " + var.name);
		if (var.declStart != var.defiStart) {
			printLocation(config, fileinfo, var.declStart, -1, "vardecl " + var.name);
		}
	});

	iter(module.natives, \nat -> {
		printLocation(config, fileinfo, nat.defiStart, -1, "natdef " + nat.name);
		if (nat.declStart != nat.defiStart) {
			printLocation(config, fileinfo, nat.declStart, -1, "natdecl " + nat.name);
		}
	});
}

fcPrintOutlineOfFcModule(config : CompilerConfig, module : FcModule) -> void {
	fileinfo = fcfileinfo2fi(module.fileinfo);
	declStarts =
	foldTree(module.functions,
		foldTree(module.globalVars,
			foldTree(module.natives,
				makeTree(),
				\name, nat, acc -> setTree(acc, name, nat.info.start)
			),
			\name, var, acc -> setTree(acc, name, var.declInfo.start)
		),
		\name, fun, acc -> setTree(acc, name, fun.declInfo.start)
	);
	iter(module.imports, \imp -> printLocation(config, fileinfo, imp.start, -1, "import " + imp.flowfile));
	iter(module.forbids, \fbd -> printLocation(config, fileinfo, fbd.start, -1, "forbid " + fbd.flowfile));
	iterSet(module.exports, \ex -> printLocation(config, fileinfo, lookupTreeDef(declStarts, ex, -1), -1, "export " + ex));
	traverseInOrder(module.structs, \name, str -> printLocation(config, fileinfo, str.info.start, -1, "struct " + name));
	traverseInOrder(module.unions, \name, uni -> printLocation(config, fileinfo, uni.info.start, -1, "union " + name));

	traverseInOrder(module.functions, \name, fun -> {
		printLocation(config, fileinfo, fun.defiInfo.start, -1, "fundef " + name);
		if (fun.declInfo.start != fun.defiInfo.start) {
			printLocation(config, fileinfo, fun.declInfo.start, -1, "fundecl " + name);
		}
	});

	traverseInOrder(module.globalVars, \name, var -> {
		printLocation(config, fileinfo, var.bodyInfo.start, -1, "vardef " + name);
		if (var.declInfo.start != var.bodyInfo.start) {
			printLocation(config, fileinfo, var.declInfo.start, -1, "vardecl " + name);
		}
	});

	traverseInOrder(module.natives, \name, nat -> {
		printLocation(config, fileinfo, nat.info.start, -1, "natdecl " + name);
		switch (nat.flowfallback) {
			Some(func): {
				printLocation(config, fileinfo, func.defiInfo.start, -1, "natdef " + name);
			}
			None(): { }
		}
	});
}
