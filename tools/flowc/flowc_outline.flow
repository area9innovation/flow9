import tools/flowc/flowc_typecheck;

export {
	fcPrintOutline(config : CompilerConfig, globEnv : FcTypeEnvGlobal, showStopper : (int) -> void) -> void;
}

fcPrintOutline(config : CompilerConfig, globEnv : FcTypeEnvGlobal, showStopper : (int) -> void) -> void {

	prog = parseAndTypecheckFlowFile(config, globEnv, config.flowfile).first;
	module = lookupTreeDef(prog.modules, config.flowfile, dummyFiModule);

	exportedPos = \exported : string ->
		switch (lookupTree(prog.names.toplevel, exported)) {
		Some(decl):
			switch(decl) {
				FiFunctionDec(__,__,__,declStart,__): declStart;
				FiGlobalVar(__,__,__,declStart,__): declStart;
				FiNativeDec(__,__,__,__,__,declStart,__): declStart;
			}
		None(): -1;
	};

	iter(module.imports, \imp -> printLocation(config, module.fileinfo.fullpath, imp.start, "import " + imp.flowfile));
	iter(module.forbids, \fbd -> printLocation(config, module.fileinfo.fullpath, fbd.start, "forbid " + fbd.flowfile));
	iter(module.exports, \ex -> printLocation(config, module.fileinfo.fullpath, exportedPos(ex), "export " + ex));
	iter(module.structs, \str -> printLocation(config, module.fileinfo.fullpath, str.start, "struct " + str.name));
	iter(module.unions, \uni -> printLocation(config, module.fileinfo.fullpath, uni.start, "union " + uni.name));

	iter(module.functions, \fun -> {
		printLocation(config, module.fileinfo.fullpath, fun.defiStart, "fundef " + fun.name);
		if (fun.declStart != fun.defiStart) {
			printLocation(config, module.fileinfo.fullpath, fun.declStart, "fundecl " + fun.name);
		}
	});

	iter(module.globalVars, \var -> {
		printLocation(config, module.fileinfo.fullpath, var.defiStart, "vardef " + var.name);
		if (var.declStart != var.defiStart) {
			printLocation(config, module.fileinfo.fullpath, var.declStart, "vardecl " + var.name);
		}
	});

	iter(module.natives, \nat -> {
		printLocation(config, module.fileinfo.fullpath, nat.defiStart, "natdef " + nat.name);
		if (nat.declStart != nat.defiStart) {
			printLocation(config, module.fileinfo.fullpath, nat.declStart, "natdecl " + nat.name);
		}
	});
	showStopper(0)
}
