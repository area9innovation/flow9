import formats/json/json;
import formats/uri2/uri2;

/*
 This module describes basic structures and encoding/decoding functions for the language Server Protocol (LSP)
 The detailed description and specification of LSP may be found at https://microsoft.github.io/language-server-protocol/
*/

export {

LspPosition : (line : int, character : int);
LspRange : (start : LspPosition, end : LspPosition);
LspLocation : (uri : URI, range : LspRange);
LspCommand : (title : string, command : string, arguments : [Json]);
LspTextEdit : (range: LspRange, newText: string);
LspTextDocumentIdentifier : (uri : URI);
LspTextDocumentItem : (uri : URI, languageId : string, version : string, text : string);
LspTextDocumentPositionParams : (textDocument : LspTextDocumentIdentifier, position : LspPosition);
LspTextDocumentSymbol : (name : string, detail : string, kind : int, deprecated : bool, range : LspRange, selectionRange : LspRange, children : [LspTextDocumentSymbol]);
LspTextDocumentSymbolInfo : (name : string, detail : string, kind : int, deprecated : bool, location : LspLocation, containerName : string);
LspHover : (contents : [LspMarkedString], range : Maybe<LspRange>);
LspMarkedString : (language : string, value : string);
LspRename : (textDocument : LspTextDocumentIdentifier, position : LspPosition, newName : string);
LspExecuteCommandParams : (command : string, arguments: [Json]);
LspCompletionContext : (
	triggerKind : int,  // Values: Invoked=1, triggerCharacter=2, TriggerIncompleteCompletions=3
	triggerCharacter : string
);
LspCompletionParams : (textDocument : LspTextDocumentIdentifier, position : LspPosition, context : LspCompletionContext);
LspCompletionList : (isIncomplete : bool, items : [LspCompletionItem]);
LspCompletionItem : (
	label: string,
	kind: int,
	tags: [int], // Values: Deprecated=1
	detail: string,
	documentation: string,
	deprecated: bool,
	preselect: bool,
	sortText: string,
	filterText: string,
	insertText: string,
	insertTextFormat: int, // Value: PlainText=1, Snippet=2
	textEdit: Maybe<LspTextEdit>,
	additionalTextEdits: [LspTextEdit],
	commitCharacters: [string],
	command: Maybe<LspCommand>,
	data: Json
);

lspPosition2Json(pos : LspPosition) -> Json;
lspRange2Json(range : LspRange) -> Json;
lspLocation2Json(loc : LspLocation) -> Json;
lspCommand2Json(com : LspCommand) -> Json;
lspTextDocumentIdentifier2Json(id : LspTextDocumentIdentifier) -> Json;
lspTextDocumentItem2Json(it : LspTextDocumentItem) -> Json;
lspTextDocumentPositionParams2Json(params : LspTextDocumentPositionParams) -> Json;
lspTextDocumentSymbol2Json(symbol : LspTextDocumentSymbol) -> Json;
lspMarkedString2Json(str : LspMarkedString) -> Json;
lspHover2Json(hover : LspHover) -> Json;
lspTextEdit2Json(edit : LspTextEdit) -> Json;
lspCompletionList2Json(list : LspCompletionList) -> Json;

json2LspPosition(json : Json) -> Maybe<LspPosition>;
json2LspRange(json : Json) -> Maybe<LspRange>;
json2LspLocation(json : Json) -> Maybe<LspLocation>;
json2LspCommand(json : Json) -> Maybe<LspCommand>;
json2LspTextDocumentIdentifier(json : Json) -> Maybe<LspTextDocumentIdentifier>;
json2LspTextDocumentItem(json : Json) -> Maybe<LspTextDocumentItem>;
json2LspTextDocumentPositionParams(json : Json) -> Maybe<LspTextDocumentPositionParams>;
json2LspRename(json : Json) -> Maybe<LspRename>;
json2LspExecuteCommandParams(json : Json) -> Maybe<LspExecuteCommandParams>;
json2LspCompletionParams(json : Json) -> Maybe<LspCompletionParams>;

// 0 stands for no kin
fcLspSymbolKindName2id = pairs2tree([
	Pair("File", 1),       Pair("Module", 2),      Pair("Namespace", 3), Pair("Package", 4),     Pair("Class", 5),
	Pair("Method", 6),     Pair("Property", 7),    Pair("Field", 8),     Pair("Constructor", 9), Pair("Enum", 10),
	Pair("Interface", 11), Pair("Function", 12),   Pair("Variable", 13), Pair("Constant", 14),   Pair("String", 15),
	Pair("Number", 16),    Pair("Boolean", 17),    Pair("Array", 18),    Pair("Object", 19),     Pair("Key", 20),
	Pair("Null", 21),      Pair("EnumMember", 22), Pair("Struct", 23),   Pair("Event", 24),      Pair("Operator", 25),
	Pair("TypeParameter", 26),
]);

// 0 stands for no kind
fcLspCompletionItemKind2id = pairs2tree([
	Pair("Text", 1),       Pair("Method", 2),      Pair("Function", 3),   Pair("Constructor", 4), Pair("Field", 5),
	Pair("Variable", 6),   Pair("Class", 7),       Pair("Interface", 8),  Pair("Module", 9),      Pair("Property", 10),
	Pair("Unit", 11),      Pair("Value", 12),      Pair("Enum", 13),      Pair("Keyword", 14),    Pair("Snippet", 15),
	Pair("Color", 16),     Pair("File", 17),       Pair("Reference", 18), Pair("Folder", 19),     Pair("EnumMember", 20),
	Pair("Constant", 21),  Pair("Struct", 22),     Pair("Event", 23),     Pair("Operator", 24),   Pair("TypeParameter", 25)
]);

fcLspSymbolKindId2Name = ["", 
	"File",      "Module",     "Namespace", "Package",     "Class",
	"Method",    "Property",   "Field",     "Constructor", "Enum", 
	"Interface", "Function",   "Variable",  "Constant",    "String",
	"Number",    "Boolean",    "Array",     "Object",      "Key", 
	"Null",      "EnumMember", "Struct",    "Event",       "Operator", 
	"TypeParameter"
];

}

lspPosition2Json(pos : LspPosition) -> Json {
	JsonObject([Pair("line", JsonDouble(i2d(pos.line))), Pair("character", JsonDouble(i2d(pos.character)))]);
}

lspRange2Json(range : LspRange) -> Json {
	JsonObject([Pair("start", lspPosition2Json(range.start)), Pair("end", lspPosition2Json(range.end))]);
}

lspLocation2Json(loc : LspLocation) -> Json {
	JsonObject([Pair("uri", JsonString(uri2string(loc.uri))), Pair("range", lspRange2Json(loc.range))]);
}

lspCommand2Json(com : LspCommand) -> Json {
	JsonObject([
		Pair("title", JsonString(com.title)), 
		Pair("command", JsonString(com.command)), 
		Pair("arguments", JsonArray(com.arguments))
	]);
}

lspTextDocumentIdentifier2Json(id : LspTextDocumentIdentifier) -> Json {
	JsonObject([Pair("uri", JsonString(uri2string(id.uri)))]);
}

lspTextDocumentItem2Json(it : LspTextDocumentItem) -> Json {
	JsonObject([
		Pair("uri", JsonString(uri2string(it.uri))),
		Pair("languageId", JsonString(it.languageId)),
		Pair("version", JsonString(it.version)),
		Pair("text", JsonString(it.text))
	]);
}

lspTextDocumentPositionParams2Json(params : LspTextDocumentPositionParams) -> Json {
	JsonObject([
		Pair("textDocument", lspTextDocumentIdentifier2Json(params.textDocument)),
		Pair("position", lspPosition2Json(params.position))
	]);
}

lspTextDocumentSymbol2Json(symbol : LspTextDocumentSymbol) -> Json {
	JsonObject([
		Pair("name", JsonString(symbol.name)),
		Pair("detail", JsonString(symbol.detail)),
		Pair("kind", JsonDouble(i2d(symbol.kind))),
		Pair("deprecated", JsonBool(symbol.deprecated)),
		Pair("range", lspRange2Json(symbol.range)),
		Pair("selectionRange", lspRange2Json(symbol.selectionRange)),
		Pair("children", JsonArray(map(symbol.children, lspTextDocumentSymbol2Json)))
	]);
}

lspMarkedString2Json(str : LspMarkedString) -> Json {
	if (str.language == "") {
		JsonString(str.value);
	} else {
		JsonObject([Pair("language", JsonString(str.language)), Pair("value", JsonString(str.value))]);
	}
}

lspHover2Json(hover : LspHover) -> Json {
	properties0 = if (length(hover.contents) == 1) {
		[Pair("contents", lspMarkedString2Json(hover.contents[0]))];
	} else {
		[Pair("contents", JsonArray(map(hover.contents, lspMarkedString2Json)))];
	}
	properties1 = eitherMap(hover.range, \range -> concat(properties0, [Pair("range", lspRange2Json(range))]), properties0);
	JsonObject(properties1);
}

lspTextEdit2Json(edit : LspTextEdit) -> Json {
	JsonObject([
		Pair("range", lspRange2Json(edit.range)),
		Pair("newText", JsonString(edit.newText)),
	]);
}

lspCompletionItem2Json(item : LspCompletionItem) -> Json {
	JsonObject(filtermap([
		Some(Pair("label", JsonString(item.label))),
		if (item.kind == 0) None() else Some(Pair("kind", JsonString(i2s(item.kind)))),
		Some(Pair("tags", JsonArray(map(item.tags, \tag -> JsonString(i2s(tag)))))),
		Some(Pair("detail", JsonString(item.detail))),
		Some(Pair("documentation", JsonString(item.documentation))),
		Some(Pair("deprecated", JsonBool(item.deprecated))),
		Some(Pair("preselect", JsonBool(item.preselect))),
		Some(Pair("sortText", JsonString(item.sortText))),
		Some(Pair("filterText", JsonString(item.filterText))),
		Some(Pair("insertText", JsonString(item.insertText))),
		Some(Pair("insertTextFormat", JsonString(i2s(item.insertTextFormat)))),
		maybeMap(item.textEdit, \edit -> Pair("textEdit", lspTextEdit2Json(edit))),
		Some(Pair("additionalTextEdits", JsonArray(map(item.additionalTextEdits, lspTextEdit2Json)))),
		Some(Pair("commitCharacters", JsonArray(map(item.commitCharacters, \ch -> JsonString(ch))))),
		maybeMap(item.command, \command -> Pair("command", lspCommand2Json(command))),
		if (item.data == JsonNull()) None() else Some(Pair("data", item.data))
	], idfn));
}

lspCompletionList2Json(list : LspCompletionList) -> Json {
	JsonObject([
		Pair("isIncomplete", JsonBool(list.isIncomplete)),
		Pair("items", JsonArray(map(list.items, lspCompletionItem2Json)))
	]);
}

json2LspPosition(json : Json) -> Maybe<LspPosition> {
	line = getJsonIntField(json, "line", -1);
	char = getJsonIntField(json, "character", -1);
	if (line != -1 && char != -1) Some(LspPosition(line, char)) else None();
}

json2LspRange(json : Json) -> Maybe<LspRange> {
	maybeBind(json2LspPosition(getJsonObjectField(json, "start")), \start ->
		maybeMap(json2LspPosition(getJsonObjectField(json, "end")), \end ->
			LspRange(start, end)
		)
	);
}

json2LspLocation(json : Json) -> Maybe<LspLocation> {
	maybeBind(json2LspRange(getJsonObjectField(json, "range")), \range -> {
		uri = getJsonStringField(json, "uri", "");
		if (uri == "") None() else Some(LspLocation(parseURI(uri), range));
	});
}

json2LspCommand(json : Json) -> Maybe<LspCommand> {
	title = getJsonStringField(json, "title", "");
	command = getJsonStringField(json, "command", "");
	args = getJsonArrayField(json, "arguments");
	if (title == "" || command == "") None() else {
		Some(LspCommand(title, command, args));
	}
}

json2LspTextDocumentIdentifier(json : Json) -> Maybe<LspTextDocumentIdentifier> {
	uri = getJsonStringField(json, "uri", "");
	if (uri == "") None() else Some(LspTextDocumentIdentifier(parseURI(uri)));
}

json2LspTextDocumentItem(json : Json) -> Maybe<LspTextDocumentItem> {
	uri = getJsonStringField(json, "uri", "");
	languageId = getJsonStringField(json, "languageId", "");
	version = getJsonStringField(json, "version", "");
	text = getJsonStringField(json, "text", "");
	if (uri == "" || languageId == "" || version == "" || text == "") None() else {
		Some(LspTextDocumentItem(parseURI(uri), languageId, version, text));
	}
}

json2LspTextDocumentPositionParams(json : Json) -> Maybe<LspTextDocumentPositionParams> {
	maybeBind(json2LspTextDocumentIdentifier(getJsonObjectField(json, "textDocument")), \doc ->
		maybeMap(json2LspPosition(getJsonObjectField(json, "position")), \position ->
			LspTextDocumentPositionParams(doc, position)
		)
	)
}

json2LspRename(json : Json) -> Maybe<LspRename> {
	maybeBind(json2LspTextDocumentIdentifier(getJsonObjectField(json, "textDocument")), \doc ->
		maybeBind(json2LspPosition(getJsonObjectField(json, "position")), \position -> {
			newName = getJsonStringField(json, "newName", "");
			if (newName == "") None() else Some(LspRename(doc, position, newName));
		})
	)
}

json2LspExecuteCommandParams(json : Json) -> Maybe<LspExecuteCommandParams> {
	command = getJsonStringField(json, "command", "");
	args = getJsonArrayField(json, "arguments");
	if (command == "") None() else {
		Some(LspExecuteCommandParams(command, args));
	}
}

json2LspCompletionParams(json : Json) -> Maybe<LspCompletionParams> {
	maybeMap(json2LspTextDocumentPositionParams(json), \pos -> {
			context = getJsonObjectField(json, "context");
			LspCompletionParams(
				pos.textDocument, 
				pos.position, 
				LspCompletionContext(
					getJsonIntField(context, "triggerKind", 0), 
					getJsonStringField(context, "triggerCharacter", "")
				)
			);
		}
	);
}

