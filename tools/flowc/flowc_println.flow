import sys/concurrent;
import net/url_parameter;

export {
	// In server mode this function transfers argument to the http
	// response buffer as well as to the console output. Id is a thread id.
	fcPrintln(str : string, id : int) -> void;

	// In server mode this function transfers argument to the http
	// response buffer as well ass to the console output. Id is a thread id.
	fcPrintBinary(str : string, id : int) -> void;

	// Returns accumulated output string (buffer). Id is a thread id.
	fcOutput(id : int) -> string;

	// Returns accumulated binary buffer. Id is a thread id.
	fcBinary(id : int) -> string;

	// Clears the output buffers, accociated with a thread id.
	clearFcOutput(id : int) -> void;

	// Set the skip printing flag for a given thread
	fcSetSkipPrinting(id : int, val : bool) -> void;
}

fcOutputBuffer = initConcurrentHashMap();
fcBinaryBuffer = initConcurrentHashMap();
fcSkipPrinting = initConcurrentHashMap();

fcGetSkipPrinting(id : int) -> bool {
	if (!containsConcurrentHashMap(fcSkipPrinting, id)) {
		false 
	} else {
		getConcurrentHashMap(fcSkipPrinting, id);
	}
}

fcSetSkipPrinting(id : int, val : bool) -> void {
	setConcurrentHashMap(fcSkipPrinting, id, val);
}

fcPrintln(str : string, id : int) -> void {
	if (!fcGetSkipPrinting(id)) {
		if (getUrlParameterDef("server-mode", "") != "") {
			if (!containsConcurrentHashMap(fcOutputBuffer, id)) {
				setConcurrentHashMap(fcOutputBuffer, id, ref []);
			}
			refArrayPush(getConcurrentHashMap(fcOutputBuffer, id), str);
		}
		println(str);
	}
}

fcPrintBinary(str : string, id : int) -> void {
	if (getUrlParameterDef("server-mode", "") != "") {
		if (!containsConcurrentHashMap(fcBinaryBuffer, id)) {
			setConcurrentHashMap(fcBinaryBuffer, id, ref []);
		}
		refArrayPush(getConcurrentHashMap(fcBinaryBuffer, id), str);
	}
}

fcOutput(id : int) -> string {
	if (!containsConcurrentHashMap(fcOutputBuffer, id)) "" else
	strGlue(^(getConcurrentHashMap(fcOutputBuffer, id)), "\n")
}

fcBinary(id : int) -> string {
	if (!containsConcurrentHashMap(fcBinaryBuffer, id)) "" else
	strGlue(^(getConcurrentHashMap(fcBinaryBuffer, id)), "\n")
}

clearFcOutput(id : int) -> void {
	setConcurrentHashMap(fcOutputBuffer, id, ref []);
	setConcurrentHashMap(fcBinaryBuffer, id, ref []);
}
