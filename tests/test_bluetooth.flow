import sys/bluetooth;

import material/material2tropic;

main() {
    makeErrorHandler = \prefix -> \error -> println("Error: " + prefix + ": " + error);
    
    processBluetoothConnect = \-> {
        if (checkBluetoothSupported()) {
            requestBluetoothDevice(
                BluetoothFilterParameters(
                    true,
                    None(),
                    Some(["battery_service"])
                ),
                \device -> {
                    println("Got device");

                    connectedCallback = \ -> {
                        println("Device GATT connected");

                        device.gatt.getPrimaryService(
                            "battery_service",
                            \service -> {
                                println("Got service");

                                service.getCharacteristic(
                                    "battery_level",
                                    \levelCharacteristic -> {
                                        valueCallback = \value -> {
                                            println("Got battery level value: " + i2s(value[0]) + "%");
                                        }
                                        levelCharacteristic.readValue(
                                            valueCallback,
                                            makeErrorHandler("Read battery level value")
                                        );

                                        removeListener = levelCharacteristic.addValueListener(valueCallback);
                                    },
                                    makeErrorHandler("Get battery level characteristic")
                                );
                            },
                            makeErrorHandler("Get battery service")
                        )
                    }

                    if (!device.gatt.connected) {
                        device.gatt.connect(connectedCallback, makeErrorHandler("Device GATT connect"));
                    } else {
                        connectedCallback();
                    }
                },
                makeErrorHandler("Get device")
            )
        }
    }

    mrender(
        makeMaterialManager([]),
        true,
        MTextButton("Discover", processBluetoothConnect, [], [])
    )
}