import material/material;
import material/material_dialog;
import material/material2tropic;

main() {
	setRendererType("html");
	manager = makeMaterialManager([]);

	videoUrl = "http://localhost/images/material_test/big_buck_bunny.mp4";
	playerWH = make(WidthHeight(640.0, 480.0));

	videoPlayerReadyB = make(false);
	videoPlayer = MVideoPlayer(videoUrl, playerWH, []);

	pngDataB = make("");
	pngFnM = make(None());

	printerDialog = \ -> {
		close = make(false);
		ShowMDialog(manager, close,
			[
				MDialogNoAnimation()
			],
			MConstruct(
				[
					makeSubscribe(pngFnM, \fnM -> maybeApply(fnM,
						\fn -> fn(\png -> {
							timer(100, \ -> {
								next(close, true);
								next(videoPlayerReadyB, false);
							});
							next(pngDataB, png);
						})
					)),
					\ -> {
						timer(1000, \ -> next(videoPlayerReadyB, true));
						nop
					}
				],
				MExtractPNG2(
					videoPlayer,
					videoPlayerReadyB,
					\fnM -> next(pngFnM, fnM)
				)
			)
		);
	}

	mrender(manager, false, MLines([
		MTextButton("PRINT", \ -> printerDialog(), [MContained()], []) |> MBorderBottom(24.0),
		TSelect(pngDataB, \img -> TPicture(img, [])) |> MBorderBottom(24.0),
	]))
}
