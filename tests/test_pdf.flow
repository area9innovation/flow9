import material/material2tropic;

// This is test for pdf.js library, that added to flow in https://github.com/area9innovation/flow9/commit/edd9bd0e411f5b1d9ca87171e6e6cd70c85faf06
// This client side PDF library works only in JS target and can produce CORS errors.
main() {
	mManager = makeMaterialManager([]);
	urlB = make("https://github.com/area9innovation/flow9/raw/master/www/test_pwa/php/file.pdf");
	pdfMB = make(None());

	pageB = make(1.0);
	pageT = fselectLift(pageB, round);
	whB = make(WidthHeight(200.0, 200.0));

	view = MBorder4(16.0, MLines([
		MTextInput(urlB, [], []),
		MTextButton("Load", \ -> {
			next(pdfMB, None());
			loadFPdfDocument(
				getValue(urlB),
				[/*headers*/]
				\pdf -> {
					println("Loaded " + i2s(pdf.pageCount) + " pages");
					next(pdfMB, Some(pdf));
				},
				[OnError(println)]
			);
		}, [], []),
		MFixedY(32.0),
		MSelect(pdfMB, \pdfM -> {
			eitherMap(
				pdfM,
				\pdf -> {
					MLines([
						MBaselineCols([
							MSelect(pageB, \page -> {
								MText ("Page: " + d2s(page) + " of " + i2s(pdf.pageCount), []);
							}),
							MFixedX(16.0),
							MSlider(pageB, [
								MSliderRange(1.0, i2d(pdf.pageCount)),
								MSliderStep(1.0),
								MSliderShowValue(true),
							]),
						]),
						MBorder4(
							8.0,
							MShadow(
								const(3.0),
								MInspect([IAvailable(whB)],
									TFForm(
										pdf.getPageView(pageT, [], fselectLift(whB, \wh -> WidthHeight(wh.width, wh.height))),
										TFormMetrics(fselectLift(whB, \wh -> wh.width), fselectLift(whB, \wh -> wh.height), const(0.0))
									)
								)
							)
						),
					])
				},
				MCenter(MText(_("Nothing loaded"), []))
			);
		}),
	]));

	mrender(mManager, true, view);
	{}
}
